import Carleson.GridStructure
import Carleson.Psi
import Carleson.ToMathlib.BoundedCompactSupport

open Set MeasureTheory Metric Function Complex Bornology
open scoped NNReal ENNReal ComplexConjugate
noncomputable section

section Generic
universe u
variable {ğ•œ : Type*} [_root_.RCLike ğ•œ]
variable {X : Type u} {A : â„â‰¥0} [PseudoMetricSpace X] [DoublingMeasure X A]

/- The data in a tile structure, and some basic properties.
This is mostly separated out so that we can nicely define the notation `d_ğ”­`.
Note: compose `ğ“˜` with `Grid` to get the `ğ“˜` of the paper. -/
class PreTileStructure {A : outParam â„â‰¥0} [PseudoMetricSpace X] [DoublingMeasure X A]
  [FunctionDistances ğ•œ X] (Q : outParam (SimpleFunc X (Î˜ X)))
  (D : outParam â„•) (Îº : outParam â„) (S : outParam â„•) (o : outParam X)
  extends GridStructure X D Îº S o where
  protected ğ”“ : Type u
  fintype_ğ”“ : Fintype ğ”“
  protected ğ“˜ : ğ”“ â†’ Grid
  surjective_ğ“˜ : Surjective ğ“˜
  ğ’¬ : ğ”“ â†’ Î˜ X
  range_ğ’¬ : range ğ’¬ âŠ† range Q

export PreTileStructure (ğ’¬ range_ğ’¬)

variable {D : â„•} {Îº : â„} {S : â„•} {o : X}
variable [FunctionDistances ğ•œ X]  {Q : SimpleFunc X (Î˜ X)} [PreTileStructure Q D Îº S o]

variable (X) in
def ğ”“ := PreTileStructure.ğ”“ ğ•œ X
instance : Fintype (ğ”“ X) := PreTileStructure.fintype_ğ”“
def ğ“˜ : ğ”“ X â†’ Grid X := PreTileStructure.ğ“˜
lemma surjective_ğ“˜ : Surjective (ğ“˜ : ğ”“ X â†’ Grid X) := PreTileStructure.surjective_ğ“˜
instance : Inhabited (ğ”“ X) := âŸ¨(surjective_ğ“˜ default).chooseâŸ©
def ğ”  (p : ğ”“ X) : X := c (ğ“˜ p)
def ğ”° (p : ğ”“ X) : â„¤ := s (ğ“˜ p)

local notation "ball_(" D "," ğ”­ ")" => @ball (WithFunctionDistance (ğ”  ğ”­) (D ^ ğ”° ğ”­ / 4)) _

/-- A tile structure. -/
-- note: we don't explicitly include injectivity of `Î©` on `ğ”“(I)`, since it follows from these
-- axioms: see `toTileLike_injective`
class TileStructure {A : outParam â„â‰¥0} [PseudoMetricSpace X] [DoublingMeasure X A]
    [FunctionDistances â„ X] (Q : outParam (SimpleFunc X (Î˜ X)))
    (D : outParam â„•) (Îº : outParam â„) (S : outParam â„•) (o : outParam X)
    extends PreTileStructure Q D Îº S o where
  Î© : ğ”“ â†’ Set (Î˜ X)
  biUnion_Î© {i} : range Q âŠ† â‹ƒ p âˆˆ ğ“˜ â»Â¹' {i}, Î© p -- 2.0.13, union contains `Q`
  disjoint_Î© {p p'} (h : p â‰  p') (hp : ğ“˜ p = ğ“˜ p') : -- 2.0.13, union is disjoint
    Disjoint (Î© p) (Î© p')
  relative_fundamental_dyadic {p p'} (h : ğ“˜ p â‰¤ ğ“˜ p') : -- 2.0.14
    Disjoint (Î© p) (Î© p') âˆ¨ Î© p' âŠ† Î© p
  cball_subset {p} : ball_(D, p) (ğ’¬ p) 5â»Â¹ âŠ† Î© p -- 2.0.15, first inclusion
  subset_cball {p} : Î© p âŠ† ball_(D, p) (ğ’¬ p) 1 -- 2.0.15, second inclusion

export TileStructure (Î© biUnion_Î© disjoint_Î© relative_fundamental_dyadic)

end Generic


open scoped ShortVariables
variable {X : Type*} [PseudoMetricSpace X] {a : â„•} {q : â„} {K : X â†’ X â†’ â„‚}
  {Ïƒâ‚ Ïƒâ‚‚ : X â†’ â„¤} {F G : Set X} [ProofData a q K Ïƒâ‚ Ïƒâ‚‚ F G]

section

variable [TileStructure Q D Îº S o] {p p' : ğ”“ X} {f g : Î˜ X}

-- maybe we should delete the following three notations, and use `dist_{ğ“˜ p}` instead?
notation "dist_(" ğ”­ ")" => @dist (WithFunctionDistance (ğ”  ğ”­) (D ^ ğ”° ğ”­ / 4)) _
notation "nndist_(" ğ”­ ")" => @nndist (WithFunctionDistance (ğ”  ğ”­) (D ^ ğ”° ğ”­ / 4)) _
notation "ball_(" ğ”­ ")" => @ball (WithFunctionDistance (ğ”  ğ”­) (D ^ ğ”° ğ”­ / 4)) _


@[simp] lemma dist_ğ“˜ (p : ğ”“ X) : dist_{ğ“˜ p} f g = dist_(p) f g := rfl
@[simp] lemma nndist_ğ“˜ (p : ğ”“ X) : nndist_{ğ“˜ p} f g = nndist_(p) f g := rfl
@[simp] lemma ball_ğ“˜ (p : ğ”“ X) {r : â„} : ball_{ğ“˜ p} f r = ball_(p) f r := rfl

@[simp] lemma cball_subset {p : ğ”“ X} : ball_(p) (ğ’¬ p) 5â»Â¹ âŠ† Î© p := TileStructure.cball_subset
@[simp] lemma subset_cball {p : ğ”“ X} : Î© p âŠ† ball_(p) (ğ’¬ p) 1 := TileStructure.subset_cball

lemma ball_eq_of_grid_eq {p q : ğ”“ X} {Ï‘ : Î˜ X} {r : â„} (h : ğ“˜ p = ğ“˜ q) :
    ball_(p) Ï‘ r = ball_(q) Ï‘ r := by rw [â† ball_ğ“˜, h]

lemma cball_disjoint {p p' : ğ”“ X} (h : p â‰  p') (hp : ğ“˜ p = ğ“˜ p') :
    Disjoint (ball_(p) (ğ’¬ p) 5â»Â¹) (ball_(p') (ğ’¬ p') 5â»Â¹) :=
  disjoint_of_subset cball_subset cball_subset (disjoint_Î© h hp)

/-- The set `E` defined in Proposition 2.0.2. -/
def E (p : ğ”“ X) : Set X :=
  { x âˆˆ ğ“˜ p | Q x âˆˆ Î© p âˆ§ ğ”° p âˆˆ Icc (Ïƒâ‚ x) (Ïƒâ‚‚ x) }

lemma E_subset_ğ“˜ {p : ğ”“ X} : E p âŠ† ğ“˜ p := fun _ â†¦ mem_of_mem_inter_left

lemma measurableSet_E {p : ğ”“ X} : MeasurableSet (E p) := by
  refine (Measurable.and ?_ (Measurable.and ?_ ?_)).setOf
  Â· rw [â† measurableSet_setOf]; exact coeGrid_measurable
  Â· simp_rw [â† mem_preimage, â† measurableSet_setOf]; exact SimpleFunc.measurableSet_preimage ..
  Â· apply (measurable_set_mem _).comp
    apply Measurable.comp (f := fun x â†¦ (Ïƒâ‚ x, Ïƒâ‚‚ x)) (g := fun p â†¦ Icc p.1 p.2)
    Â· exact measurable_from_prod_countable fun _ _ _ â†¦ trivial
    Â· exact measurable_Ïƒâ‚.prod_mk measurable_Ïƒâ‚‚

lemma volume_E_lt_top : volume (E p) < âŠ¤ := trans (measure_mono E_subset_ğ“˜) volume_coeGrid_lt_top

section T

/-- The operator `T_ğ”­` defined in Proposition 2.0.2, considered on the set `F`.
It is the map `T âˆ˜ (1_F * Â·) : f â†¦ T (1_F * f)`, also denoted `T1_F`
The operator `T` in Proposition 2.0.2 is therefore applied to `(F := Set.univ)`. -/
def carlesonOn (p : ğ”“ X) (f : X â†’ â„‚) : X â†’ â„‚ :=
  indicator (E p)
    fun x â†¦ âˆ« y, exp (I * (Q x y - Q x x)) * K x y * Ïˆ (D ^ (- ğ”° p) * dist x y) * f y

-- not used anywhere and deprecated for `AEStronglyMeasurable.carlesonOn`
lemma measurable_carlesonOn {p : ğ”“ X} {f : X â†’ â„‚} (measf : Measurable f) :
    Measurable (carlesonOn p f) := by
  refine (StronglyMeasurable.integral_prod_right ?_).measurable.indicator measurableSet_E
  refine (((Measurable.mul ?_ measurable_K).mul ?_).mul ?_).stronglyMeasurable
  Â· have : Measurable fun (p : X Ã— X) â†¦ (p.1, p.1) := by fun_prop
    refine ((Measurable.sub ?_ ?_).const_mul I).cexp <;> apply measurable_ofReal.comp
    Â· exact measurable_Qâ‚‚
    Â· exact measurable_Qâ‚‚.comp this
  Â· apply measurable_ofReal.comp
    apply Measurable.comp (f := fun x : X Ã— X â†¦ D ^ (-ğ”° p) * dist x.1 x.2) (g := Ïˆ)
    Â· exact measurable_const.max (measurable_const.min (Measurable.min (by fun_prop) (by fun_prop)))
    Â· exact measurable_dist.const_mul _
  Â· exact measf.comp measurable_snd

open Classical in
/-- The operator `T_â„­ f` defined at the bottom of Section 7.4.
We will use this in other places of the formalization as well. -/
def carlesonSum (â„­ : Set (ğ”“ X)) (f : X â†’ â„‚) (x : X) : â„‚ :=
  âˆ‘ p âˆˆ {p | p âˆˆ â„­}, carlesonOn p f x

-- not used anywhere and deprecated for `AEStronglyMeasurable.carlesonSum`
@[fun_prop]
lemma measurable_carlesonSum {â„­ : Set (ğ”“ X)} {f : X â†’ â„‚} (measf : Measurable f) :
    Measurable (carlesonSum â„­ f) :=
  Finset.measurable_sum _ fun _ _ â†¦ measurable_carlesonOn measf

lemma _root_.MeasureTheory.AEStronglyMeasurable.carlesonOn {p : ğ”“ X} {f : X â†’ â„‚}
    (hf : AEStronglyMeasurable f) : AEStronglyMeasurable (carlesonOn p f) := by
  refine .indicator ?_ measurableSet_E
  refine .integral_prod_right'
    (f := fun z â†¦ exp (Complex.I * (Q z.1 z.2 - Q z.1 z.1)) * K z.1 z.2 *
      Ïˆ (D ^ (- ğ”° p) * dist z.1 z.2) * f z.2) ?_
  refine (((AEStronglyMeasurable.mul ?_ aestronglyMeasurable_K).mul ?_).mul ?_)
  Â· apply Measurable.aestronglyMeasurable
    have : Measurable fun (p : X Ã— X) â†¦ (p.1, p.1) := by fun_prop
    refine ((Measurable.sub ?_ ?_).const_mul I).cexp <;> apply measurable_ofReal.comp
    Â· exact measurable_Qâ‚‚
    Â· exact measurable_Qâ‚‚.comp this
  Â· apply Measurable.aestronglyMeasurable
    apply measurable_ofReal.comp
    apply Measurable.comp (f := fun x : X Ã— X â†¦ D ^ (-ğ”° p) * dist x.1 x.2) (g := Ïˆ)
    Â· exact measurable_const.max (measurable_const.min (Measurable.min (by fun_prop) (by fun_prop)))
    Â· exact measurable_dist.const_mul _
  Â· exact hf.snd

lemma _root_.MeasureTheory.AEStronglyMeasurable.carlesonSum {â„­ : Set (ğ”“ X)}
    {f : X â†’ â„‚} (hf : AEStronglyMeasurable f) : AEStronglyMeasurable (carlesonSum â„­ f) :=
  Finset.aestronglyMeasurable_sum _ fun _ _ â†¦ hf.carlesonOn

lemma carlesonOn_def' (p : ğ”“ X) (f : X â†’ â„‚) : carlesonOn p f =
    indicator (E p) fun x â†¦ âˆ« y, Ks (ğ”° p) x y * f y * exp (I * (Q x y - Q x x)) := by
  unfold carlesonOn Ks
  exact congr_arg _ (funext fun x â†¦ (congr_arg _ (funext fun y â†¦ by ring)))

lemma support_carlesonOn_subset_E {f : X â†’ â„‚} : support (carlesonOn p f) âŠ† E p :=
  fun _ hx â†¦ mem_of_indicator_ne_zero hx

lemma support_carlesonSum_subset {â„­ : Set (ğ”“ X)} {f : X â†’ â„‚} :
    support (carlesonSum â„­ f) âŠ† (â‹ƒ p âˆˆ â„­, ğ“˜ p) := by
  intro x hx
  rw [mem_support] at hx
  contrapose! hx
  refine Finset.sum_eq_zero (fun p hp â†¦ nmem_support.mp (fun hxp â†¦ hx ?_))
  simp only [Finset.mem_filter] at hp
  exact Set.mem_biUnion hp.2 <| E_subset_ğ“˜ (support_carlesonOn_subset_E hxp)

theorem _root_.MeasureTheory.BoundedCompactSupport.carlesonOn {f : X â†’ â„‚}
    (hf : BoundedCompactSupport f) : BoundedCompactSupport (carlesonOn p f) where
  stronglyMeasurable :=
    (measurable_carlesonOn hf.stronglyMeasurable.measurable).stronglyMeasurable
  isBounded := by
    let xâ‚€ : X := Classical.choice inferInstance
    obtain âŸ¨râ‚€, hrâ‚€, hfrâ‚€âŸ© := hf.isBoundedSupport.subset_closedBall_lt 0 xâ‚€
    let râ‚ := (â†‘D ^ ğ”° p / 2) + râ‚€
    have hcf : support (_root_.carlesonOn p f) âŠ† closedBall xâ‚€ râ‚ := by
      simp_rw [carlesonOn_def']
      intro x hx
      simp only [mem_support] at hx
      apply indicator_apply_ne_zero.mp at hx
      replace hx := hx.2
      simp only [mem_support] at hx
      have : âˆƒ y, Ks (ğ”° p) x y * f y * cexp (I * (â†‘((Q x) y) - â†‘((Q x) x))) â‰  0 := by
        -- mathlib lemma: if integral ne zero, then integrand ne zero at a point
        by_contra hc
        simp only [not_exists, ne_eq, not_not] at hc
        refine hx ?_
        refine integral_eq_zero_of_ae ?_
        simp_all only [support_subset_iff, ne_eq,
          mem_closedBall, integral_zero, not_true_eq_false, xâ‚€]
      obtain âŸ¨y, hyâŸ© := this
      simp only [ne_eq, mul_eq_zero, exp_ne_zero, or_false, not_or] at hy
      have := dist_mem_Icc_of_Ks_ne_zero hy.1
      apply (dist_triangle _ y _).trans
      unfold râ‚
      gcongr
      Â· exact (dist_mem_Icc_of_Ks_ne_zero hy.1).2
      Â· exact hfrâ‚€ hy.2
    obtain âŸ¨CK, hCK, hCKâŸ© :=
      IsBounded.exists_bound_of_norm_Ks (Metric.isBounded_closedBall (x := xâ‚€) (r := râ‚)) (ğ”° p)
    let C := volume.real (closedBall xâ‚€ râ‚€) * (CK * (eLpNorm f âŠ¤).toReal)
    apply isBounded_range_iff_forall_norm_le.2 âŸ¨C, fun x â†¦ ?_âŸ©
    wlog hx : x âˆˆ support (_root_.carlesonOn p f)
    Â· simp only [mem_support, ne_eq, not_not] at hx
      rw [hx, norm_zero]
      positivity
    Â· simp_rw [carlesonOn_def']
      refine trans (norm_indicator_le_norm_self _ _) ?_
      let g := (closedBall xâ‚€ râ‚€).indicator (fun _ â†¦ CK * (eLpNorm f âŠ¤).toReal)
      have hK : âˆ€áµ y, â€–Ks (ğ”° p) x y * f y * cexp (I * (â†‘((Q x) y) - â†‘((Q x) x)))â€– â‰¤ g y := by
        filter_upwards [hf.ae_le] with y hy
        by_cases hy' : y âˆˆ support f
        Â· have := hfrâ‚€ hy'
          calc
            _ â‰¤ â€–Ks (ğ”° p) x y * f yâ€– * â€–cexp (I * (â†‘((Q x) y) - â†‘((Q x) x)))â€– := norm_mul_le ..
            _ = â€–Ks (ğ”° p) x y * f yâ€– := by rw [norm_exp_I_mul_sub_ofReal, mul_one]
            _ â‰¤ â€–Ks (ğ”° p) x yâ€– * â€–f yâ€– := norm_mul_le ..
            _ â‰¤ CK * (eLpNorm f âŠ¤).toReal := by gcongr; exact hCK x y (hcf hx)
            _ = g y := by simp_all only [indicator_of_mem, g]
        Â· simp only [mem_support, ne_eq, not_not] at hy'
          rw [hy']
          simp only [mul_zero, zero_mul, norm_zero, g]
          unfold indicator
          split_ifs <;> positivity
      calc
        _ â‰¤ âˆ« y, g y := by
          refine norm_integral_le_of_norm_le ?_ hK
          exact Integrable.indicator_const measurableSet_closedBall measure_closedBall_lt_top
        _ = volume.real (closedBall xâ‚€ râ‚€) * (CK * (eLpNorm f âŠ¤ volume).toReal) :=
          integral_indicator_const _ measurableSet_closedBall
  hasCompactSupport := by
    suffices support (_root_.carlesonOn p f) âŠ† ğ“˜ p by
      refine HasCompactSupport.of_support_subset_isBounded ?_ this
      exact Metric.isBounded_ball.subset Grid_subset_ball
    exact Trans.trans support_carlesonOn_subset_E E_subset_ğ“˜

theorem _root_.MeasureTheory.BoundedCompactSupport.carlesonSum {â„­ : Set (ğ”“ X)} {f : X â†’ â„‚}
    (hf : BoundedCompactSupport f) : BoundedCompactSupport (carlesonSum â„­ f) :=
  .finset_sum (fun _ _ â†¦ hf.carlesonOn)

lemma carlesonSum_inter_add_inter_compl {f : X â†’ â„‚} {x : X} (A B : Set (ğ”“ X)) :
    carlesonSum (A âˆ© B) f x + carlesonSum (A âˆ© Bá¶œ) f x = carlesonSum A f x := by
  classical
  simp only [carlesonSum]
  conv_rhs => rw [â† Finset.sum_filter_add_sum_filter_not _ (fun p â†¦ p âˆˆ B)]
  congr 2
  Â· ext; simp
  Â· ext; simp

lemma sum_carlesonSum_of_pairwiseDisjoint {Î¹ : Type*} {f : X â†’ â„‚} {x : X} {A : Î¹ â†’ Set (ğ”“ X)}
    {s : Finset Î¹} (hs : (s : Set Î¹).PairwiseDisjoint A) :
    âˆ‘ i âˆˆ s, carlesonSum (A i) f x = carlesonSum (â‹ƒ i âˆˆ s, A i) f x := by
  classical
  simp only [carlesonSum]
  rw [â† Finset.sum_biUnion]
  Â· congr
    ext p
    simp
  Â· convert hs
    refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
    Â· intro i hi j hj hij
      convert Finset.disjoint_coe.2 (h hi hj hij)
      Â· ext; simp
      Â· ext; simp
    Â· intro i hi j hj hij
      apply Finset.disjoint_coe.1
      convert h hi hj hij
      Â· ext; simp
      Â· ext; simp

end T

variable (X) in
def TileLike : Type _ := Grid X Ã— OrderDual (Set (Î˜ X))

def TileLike.fst (x : TileLike X) : Grid X := x.1
def TileLike.snd (x : TileLike X) : Set (Î˜ X) := x.2

@[simp] lemma TileLike.fst_mk (x : Grid X) (y : Set (Î˜ X)) : TileLike.fst (x, y) = x := by rfl
@[simp] lemma TileLike.snd_mk (x : Grid X) (y : Set (Î˜ X)) : TileLike.snd (x, y) = y := by rfl

instance : PartialOrder (TileLike X) := by dsimp [TileLike]; infer_instance

lemma TileLike.le_def (x y : TileLike X) : x â‰¤ y â†” x.fst â‰¤ y.fst âˆ§ y.snd âŠ† x.snd := by rfl

def toTileLike (p : ğ”“ X) : TileLike X := (ğ“˜ p, Î© p)

@[simp] lemma toTileLike_fst (p : ğ”“ X) : (toTileLike p).fst = ğ“˜ p := by rfl
@[simp] lemma toTileLike_snd (p : ğ”“ X) : (toTileLike p).snd = Î© p := by rfl

/-- This is not defined as such in the blueprint, but `Î»p â‰² Î»'p'` can be written using
`smul l p â‰¤ smul l' p'`.
Beware: `smul 1 p` is very different from `toTileLike p`. -/
def smul (l : â„) (p : ğ”“ X) : TileLike X :=
  (ğ“˜ p, ball_(p) (ğ’¬ p) l)

@[simp] lemma smul_fst (l : â„) (p : ğ”“ X) : (smul l p).fst = ğ“˜ p := by rfl
@[simp] lemma smul_snd (l : â„) (p : ğ”“ X) : (smul l p).snd = ball_(p) (ğ’¬ p) l := by rfl

lemma smul_mono_left {l l' : â„} {p : ğ”“ X} (h : l â‰¤ l') : smul l' p â‰¤ smul l p := by
  simp [TileLike.le_def, h, ball_subset_ball]

lemma smul_le_toTileLike : smul 1 p â‰¤ toTileLike p := by
  simp [TileLike.le_def, subset_cball (p := p)]

lemma toTileLike_le_smul : toTileLike p â‰¤ smul 5â»Â¹ p := by
  simp [TileLike.le_def, cball_subset (p := p)]

lemma ğ’¬_mem_Î© : ğ’¬ p âˆˆ Î© p := cball_subset <| mem_ball_self <| by norm_num

lemma ğ’¬_inj {p' : ğ”“ X} (h : ğ’¬ p = ğ’¬ p') (hğ“˜ : ğ“˜ p = ğ“˜ p') : p = p' := by
  contrapose! h
  exact fun hğ’¬ â†¦ (not_disjoint_iff.2 âŸ¨ğ’¬ p, ğ’¬_mem_Î©, hğ’¬ â–¸ ğ’¬_mem_Î©âŸ©) (disjoint_Î© h hğ“˜)

lemma toTileLike_injective : Injective (fun p : ğ”“ X â†¦ toTileLike p) := by
  intros p p' h
  simp_rw [toTileLike, TileLike, Prod.ext_iff] at h
  by_contra h2
  have : Disjoint (Î© p) (Î© p') := disjoint_Î© h2 h.1
  have : Î© p = âˆ… := by simpa [â† h.2]
  exact not_mem_empty _ (by rw [â† this]; exact ğ’¬_mem_Î©)

instance : PartialOrder (ğ”“ X) := PartialOrder.lift toTileLike toTileLike_injective

lemma ğ”“.le_def {p q : ğ”“ X} : p â‰¤ q â†” toTileLike p â‰¤ toTileLike q := by rfl
lemma ğ”“.le_def' {p q : ğ”“ X} : p â‰¤ q â†” ğ“˜ p â‰¤ ğ“˜ q âˆ§ Î© q âŠ† Î© p := by rfl

lemma dist_ğ’¬_lt_one_of_le {p q : ğ”“ X} (h : p â‰¤ q) : dist_(p) (ğ’¬ q) (ğ’¬ p) < 1 :=
  ((cball_subset.trans h.2).trans subset_cball) (mem_ball_self (by norm_num))

lemma dist_ğ’¬_lt_one_of_le' {p q : ğ”“ X} (h : p â‰¤ q) : dist_(p) (ğ’¬ p) (ğ’¬ q) < 1 :=
  mem_ball'.mp (dist_ğ’¬_lt_one_of_le h)

lemma ğ“˜_strictMono : StrictMono (ğ“˜ (X := X)) := fun _ _ h â†¦ h.le.1.lt_of_ne <|
  fun h' â†¦ disjoint_left.mp (disjoint_Î© h.ne h') (h.le.2 ğ’¬_mem_Î©) ğ’¬_mem_Î©

/-- Lemma 5.3.1 -/
lemma smul_mono {m m' n n' : â„} (hp : smul n p â‰¤ smul m p') (hm : m' â‰¤ m) (hn : n â‰¤ n') :
    smul n' p â‰¤ smul m' p' :=
  smul_mono_left hn |>.trans hp |>.trans <| smul_mono_left hm

/-- Lemma 5.3.2 (generalizing `1` to `k > 0`) -/
lemma smul_C2_1_2 (m : â„) {n k : â„} (hk : 0 < k) (hp : ğ“˜ p â‰  ğ“˜ p') (hl : smul n p â‰¤ smul k p') :
    smul (n + C2_1_2 a * m) p â‰¤ smul m p' := by
  replace hp : ğ“˜ p < ğ“˜ p' := hl.1.lt_of_ne hp
  have : ball_(p') (ğ’¬ p') m âŠ† ball_(p) (ğ’¬ p) (n + C2_1_2 a * m) := fun x hx â†¦ by
    rw [@mem_ball] at hx âŠ¢
    calc
      _ â‰¤ dist_(p) x (ğ’¬ p') + dist_(p) (ğ’¬ p') (ğ’¬ p) := dist_triangle ..
      _ â‰¤ C2_1_2 a * dist_(p') x (ğ’¬ p') + dist_(p) (ğ’¬ p') (ğ’¬ p) := by
        gcongr; exact Grid.dist_strictMono hp
      _ < C2_1_2 a * m + dist_(p) (ğ’¬ p') (ğ’¬ p) := by gcongr; rw [C2_1_2]; positivity
      _ < _ := by
        rw [add_comm]; gcongr
        exact mem_ball.mp <| mem_of_mem_of_subset (by convert mem_ball_self hk) hl.2
  exact âŸ¨hl.1, thisâŸ©

lemma dist_LTSeries {n : â„•} {u : Set (ğ”“ X)} {s : LTSeries u} (hs : s.length = n) {f g : Î˜ X} :
    dist_(s.head.1) f g â‰¤ C2_1_2 a ^ n * dist_(s.last.1) f g := by
  induction n generalizing s with
  | zero => rw [pow_zero, one_mul]; apply Grid.dist_mono s.head_le_last.1
  | succ n ih =>
    let s' : LTSeries u := s.eraseLast
    specialize ih (show s'.length = n by simp [s', hs])
    have link : dist_(s'.last.1) f g â‰¤ C2_1_2 a * dist_(s.last.1) f g :=
      Grid.dist_strictMono <| ğ“˜_strictMono <| s.eraseLast_last_rel_last (by omega)
    apply ih.trans; rw [pow_succ, mul_assoc]; gcongr; unfold C2_1_2; positivity

end

/-- The constraint on `Î»` in the first part of Lemma 5.3.3. -/
def C5_3_3 (a : â„•) : â„ := (1 - C2_1_2 a)â»Â¹

include q K Ïƒâ‚ Ïƒâ‚‚ F G in
lemma C5_3_3_le : C5_3_3 a â‰¤ 11 / 10 := by
  rw [C5_3_3, inv_le_commâ‚€ (sub_pos.mpr <| C2_1_2_lt_one X) (by norm_num), le_sub_comm]
  exact C2_1_2_le_inv_512 X |>.trans <| by norm_num

variable [TileStructure Q D Îº S o] {p p' : ğ”“ X} {f g : Î˜ X}

/-- Lemma 5.3.3, Equation (5.3.3) -/
lemma wiggle_order_11_10 {n : â„} (hp : p â‰¤ p') (hn : C5_3_3 a â‰¤ n) : smul n p â‰¤ smul n p' := by
  rcases eq_or_ne (ğ“˜ p) (ğ“˜ p') with h | h
  Â· rcases eq_or_ne p p' with rfl | h2
    Â· rfl
    Â· exact absurd h (ğ“˜_strictMono (lt_of_le_of_ne hp h2)).ne
  Â· calc
      _ â‰¤ smul (1 + C2_1_2 a * n) p := by
        apply smul_mono_left
        rwa [â† le_sub_iff_add_le, â† one_sub_mul, â† inv_le_iff_one_le_mulâ‚€']
        linarith [C2_1_2_le_inv_512 (X := X)]
      _ â‰¤ smul n p' := smul_C2_1_2 (k := 5â»Â¹) n (by norm_num) h
        (smul_le_toTileLike.trans <| ğ”“.le_def.mp hp |>.trans toTileLike_le_smul)

/-- Lemma 5.3.3, Equation (5.3.4) -/
lemma wiggle_order_100 (hp : smul 10 p â‰¤ smul 1 p') (hn : ğ“˜ p â‰  ğ“˜ p') :
    smul 100 p â‰¤ smul 100 p' :=
  calc
    _ â‰¤ smul (10 + C2_1_2 a * 100) p :=
      smul_mono_left (by linarith [C2_1_2_le_inv_512 (X := X)])
    _ â‰¤ _ := smul_C2_1_2 100 zero_lt_one hn hp

/-- Lemma 5.3.3, Equation (5.3.5) -/
lemma wiggle_order_500 (hp : smul 2 p â‰¤ smul 1 p') (hn : ğ“˜ p â‰  ğ“˜ p') :
    smul 4 p â‰¤ smul 500 p' :=
  calc
    _ â‰¤ smul (2 + C2_1_2 a * 500) p :=
      smul_mono_left (by linarith [C2_1_2_le_inv_512 (X := X)])
    _ â‰¤ _ := smul_C2_1_2 500 zero_lt_one hn hp

def C5_3_2 (a : â„•) : â„ := 2 ^ (-95 * (a : â„))

def TileLike.toTile (t : TileLike X) : Set (X Ã— Î˜ X) :=
  (t.fst : Set X) Ã—Ë¢ t.snd

/-- From a TileLike, we can construct a set. This is used in the definitions `Eâ‚` and `Eâ‚‚`. -/
def TileLike.toSet (t : TileLike X) : Set X :=
  t.fst âˆ© G âˆ© Q â»Â¹' t.snd

def Eâ‚ (p : ğ”“ X) : Set X :=
  (toTileLike p).toSet

def Eâ‚‚ (l : â„) (p : ğ”“ X) : Set X :=
  (smul l p).toSet

lemma Eâ‚_subset (p : ğ”“ X) : Eâ‚ p âŠ† ğ“˜ p := by
  change â†‘(ğ“˜ p) âˆ© G âˆ© (Q â»Â¹' Î© p) âŠ† â†‘(ğ“˜ p)
  rw [inter_assoc]
  exact inter_subset_left

lemma Eâ‚‚_subset (l : â„) (p : ğ”“ X) : Eâ‚‚ l p âŠ† ğ“˜ p := by
  change â†‘(ğ“˜ p) âˆ© G âˆ© (Q â»Â¹' (ball_(p) (ğ’¬ p) l)) âŠ† â†‘(ğ“˜ p)
  rw [inter_assoc]
  exact inter_subset_left

/-! `ğ”“(ğ”“')` in the blueprint is `lowerClosure ğ”“'` in Lean. -/

/-- This density is defined to live in `â„â‰¥0âˆ`. Use `ENNReal.toReal` to get a real number. -/
def densâ‚ (ğ”“' : Set (ğ”“ X)) : â„â‰¥0âˆ :=
  â¨† (p' âˆˆ ğ”“') (l â‰¥ (2 : â„â‰¥0)), l ^ (-a : â„) *
  â¨† (p âˆˆ lowerClosure ğ”“') (_h2 : smul l p' â‰¤ smul l p),
  volume (Eâ‚‚ l p) / volume (ğ“˜ p : Set X)

lemma densâ‚_mono {ğ”“â‚ ğ”“â‚‚ : Set (ğ”“ X)} (h : ğ”“â‚ âŠ† ğ”“â‚‚) :
    densâ‚ ğ”“â‚ â‰¤ densâ‚ ğ”“â‚‚ := by
  simp only [densâ‚, iSup_le_iff]
  intro p hp r hr
  refine le_iSupâ‚‚_of_le p (h hp) ?_
  apply ENNReal.mul_le_of_le_div'
  simp only [iSup_le_iff]
  intro q hq hqr
  rw [ENNReal.le_div_iff_mul_le (by left; simp)]
  Â· refine le_iSupâ‚‚_of_le r hr ?_
    rw [mul_comm]
    gcongr
    exact le_iSupâ‚‚_of_le q (lowerClosure_mono h hq) (le_iSup_iff.mpr fun b a â†¦ a hqr)
  Â· left
    have hr0 : r â‰  0 := by positivity
    simp [hr0]

/-- This density is defined to live in `â„â‰¥0âˆ`. Use `ENNReal.toReal` to get a real number. -/
def densâ‚‚ (ğ”“' : Set (ğ”“ X)) : â„â‰¥0âˆ :=
  â¨† (p âˆˆ ğ”“') (r â‰¥ 4 * (D ^ ğ”° p : â„)),
  volume (F âˆ© ball (ğ”  p) r) / volume (ball (ğ”  p) r)

lemma densâ‚‚_eq_biSup_densâ‚‚ (ğ”“' : Set (ğ”“ X)) :
    densâ‚‚ (ğ”“') = â¨† (p âˆˆ ğ”“'), densâ‚‚ ({p}) := by
  simp [densâ‚‚]

-- a small characterization that might be useful
lemma isAntichain_iff_disjoint (ğ”„ : Set (ğ”“ X)) :
    IsAntichain (Â·â‰¤Â·) (toTileLike (X := X) '' ğ”„) â†”
    âˆ€ p p', p âˆˆ ğ”„ â†’ p' âˆˆ ğ”„ â†’ p â‰  p' â†’
    Disjoint (toTileLike (X := X) p).toTile (toTileLike p').toTile := sorry

lemma ENNReal.rpow_le_rpow_of_nonpos {x y : â„â‰¥0âˆ} {z : â„} (hz : z â‰¤ 0) (h : x â‰¤ y) :
    y ^ z â‰¤ x ^ z := by
  rw [â† neg_neg z, rpow_neg y, rpow_neg x, â† inv_rpow, â† inv_rpow]
  exact rpow_le_rpow (ENNReal.inv_le_inv.mpr h) (neg_nonneg.mpr hz)

/- A rough estimate. It's also less than 2 ^ (-a) -/
def densâ‚_le_one {ğ”“' : Set (ğ”“ X)} : densâ‚ ğ”“' â‰¤ 1 := by
  conv_rhs => rw [â† mul_one 1]
  simp only [densâ‚, mem_lowerClosure, iSup_exists, iSup_le_iff]
  intros i _ j hj
  gcongr
  Â· calc
    (j : â„â‰¥0âˆ) ^ (-(a : â„)) â‰¤ 2 ^ (-(a : â„)) := by
      apply ENNReal.rpow_le_rpow_of_nonpos
      Â· simp_rw [neg_nonpos, Nat.cast_nonneg']
      exact_mod_cast hj
    _ â‰¤ 2 ^ (0 : â„) :=
      ENNReal.rpow_le_rpow_of_exponent_le (by norm_num) (neg_nonpos.mpr (Nat.cast_nonneg' _))
    _ = 1 := by norm_num
  simp only [iSup_le_iff, and_imp]
  intros i' _ _ _ _
  calc
  volume (Eâ‚‚ j i') / volume (ğ“˜ i' : Set X) â‰¤ volume (ğ“˜ i' : Set X) / volume (ğ“˜ i' : Set X) := by
    gcongr
    apply Eâ‚‚_subset
  _ â‰¤ 1 := ENNReal.div_self_le_one

/-! ### Stack sizes -/

variable {C C' : Set (ğ”“ X)} {x x' : X}
open scoped Classical

/-- The number of tiles `p` in `s` whose underlying cube `ğ“˜ p` contains `x`. -/
def stackSize (C : Set (ğ”“ X)) (x : X) : â„• :=
  âˆ‘ p âˆˆ { p | p âˆˆ C }, (ğ“˜ p : Set X).indicator 1 x

lemma stackSize_setOf_add_stackSize_setOf_not {P : ğ”“ X â†’ Prop} :
    stackSize {p âˆˆ C | P p} x + stackSize {p âˆˆ C | Â¬ P p} x = stackSize C x := by
  simp_rw [stackSize]
  conv_rhs => rw [â† Finset.sum_filter_add_sum_filter_not _ P]
  simp_rw [Finset.filter_filter]
  congr

lemma stackSize_inter_add_stackSize_sdiff :
    stackSize (C âˆ© C') x + stackSize (C \ C') x = stackSize C x :=
  stackSize_setOf_add_stackSize_setOf_not

lemma stackSize_sdiff_eq (x : X) :
  stackSize (C \ C') x = stackSize C x - stackSize (C âˆ© C') x := by
  exact Nat.eq_sub_of_add_eq' stackSize_inter_add_stackSize_sdiff

lemma stackSize_congr (h : âˆ€ p âˆˆ C, x âˆˆ (ğ“˜ p : Set X) â†” x' âˆˆ (ğ“˜ p : Set X)) :
    stackSize C x = stackSize C x' := by
  refine Finset.sum_congr rfl fun p hp â†¦ ?_
  simp_rw [Finset.mem_filter, Finset.mem_univ, true_and] at hp
  simp_rw [indicator, h p hp, Pi.one_apply]

lemma stackSize_mono (h : C âŠ† C') : stackSize C x â‰¤ stackSize C' x := by
  apply Finset.sum_le_sum_of_subset (fun x â†¦ ?_)
  simp [iff_true_intro (@h x)]

-- Simplify the cast of `stackSize C x` from `â„•` to `â„`
lemma stackSize_real (C : Set (ğ”“ X)) (x : X) : (stackSize C x : â„) =
    âˆ‘ p âˆˆ { p | p âˆˆ C }, (ğ“˜ p : Set X).indicator (1 : X â†’ â„) x := by
  rw [stackSize, Nat.cast_sum]
  refine Finset.sum_congr rfl (fun u _ â†¦ ?_)
  by_cases hx : x âˆˆ (ğ“˜ u : Set X) <;> simp [hx]

lemma stackSize_measurable : Measurable fun x â†¦ (stackSize C x : â„â‰¥0âˆ) := by
  simp_rw [stackSize, Nat.cast_sum, indicator, Nat.cast_ite]
  refine Finset.measurable_sum _ fun _ _ â†¦ Measurable.ite coeGrid_measurable ?_ ?_ <;> simp

lemma stackSize_le_one_of_pairwiseDisjoint {C : Set (ğ”“ X)} {x : X}
    (h : C.PairwiseDisjoint (fun p â†¦ (ğ“˜ p : Set X))) : stackSize C x â‰¤ 1 := by
  by_cases hx : âˆƒ p âˆˆ C, x âˆˆ (ğ“˜ p : Set X)
  Â· rcases hx with âŸ¨p, pC, hpâŸ©
    rw [stackSize, Finset.sum_eq_single_of_mem p]; rotate_left
    Â· simp [pC]
    Â· intro b hb hbp
      simp only [indicator_apply_eq_zero, Pi.one_apply, one_ne_zero, imp_false]
      simp only [Finset.mem_filter, Finset.mem_univ, true_and] at hb
      exact disjoint_left.1 (h pC hb hbp.symm) hp
    simp [hp]
  Â· have : stackSize C x = 0 := by
      apply Finset.sum_eq_zero (fun p hp â†¦ ?_)
      simp only [Finset.mem_filter, Finset.mem_univ, true_and, not_exists, not_and,
        indicator_apply_eq_zero, Pi.one_apply, one_ne_zero, imp_false] at hp hx âŠ¢
      exact hx _ hp
    linarith

/-! ### Decomposing a set of tiles into disjoint subfamilies -/

/-- Given any family of tiles, one can extract a maximal disjoint subfamily, covering everything. -/
lemma exists_maximal_disjoint_covering_subfamily (A : Set (ğ”“ X)) :
    âˆƒ (B : Set (ğ”“ X)), B.PairwiseDisjoint (fun p â†¦ (ğ“˜ p : Set X)) âˆ§
      B âŠ† A âˆ§ (âˆ€ a âˆˆ A, âˆƒ b âˆˆ B, (ğ“˜ a : Set X) âŠ† ğ“˜ b) := by
  -- consider the pairwise disjoint families in `A` such that any element of `A` is disjoint from
  -- every member of the family, or contained in one of them.
  let M : Set (Set (ğ”“ X)) := {B | B.PairwiseDisjoint (fun p â†¦ (ğ“˜ p : Set X)) âˆ§ B âŠ† A âˆ§ âˆ€ a âˆˆ A,
    (âˆƒ b âˆˆ B, (ğ“˜ a : Set X) âŠ† ğ“˜ b) âˆ¨ (âˆ€ b âˆˆ B, Disjoint (ğ“˜ a : Set X) (ğ“˜ b))}
  -- let `B` be a maximal such family. It satisfies the properties of the lemma.
  obtain âŸ¨B, BM, hBâŸ© : âˆƒ B âˆˆ M, âˆ€ B' âˆˆ M, B âŠ† B' â†’ B = B' :=
    Finite.exists_maximal_wrt id _ (toFinite M) âŸ¨âˆ…, by simp [M]âŸ©
  refine âŸ¨B, BM.1, BM.2.1, fun a ha â†¦ ?_âŸ©
  rcases BM.2.2 a ha with h'a | h'a
  Â· exact h'a
  exfalso
  let F := {a' âˆˆ A | (ğ“˜ a : Set X) âŠ† ğ“˜ a' âˆ§ âˆ€ b âˆˆ B, Disjoint (ğ“˜ a' : Set X) (ğ“˜ b)}
  obtain âŸ¨a', a'F, ha'âŸ© : âˆƒ a' âˆˆ F, âˆ€ p âˆˆ F, (ğ“˜ a' : Set X) âŠ† ğ“˜ p â†’ (ğ“˜ a' : Set X) = ğ“˜ p := by
    apply Finite.exists_maximal_wrt _ _ (toFinite F)
    exact âŸ¨a, by simpa [F, ha] using h'aâŸ©
  have : insert a' B âˆˆ M := by
    refine âŸ¨?_, ?_, fun p hp â†¦ ?_âŸ©
    Â· apply PairwiseDisjoint.insert BM.1 (fun b hb h'b â†¦ a'F.2.2 b hb)
    Â· apply insert_subset a'F.1 BM.2.1
    rcases BM.2.2 p hp with âŸ¨b, hbâŸ© | h'p
    Â· exact Or.inl âŸ¨b, mem_insert_of_mem _ hb.1, hb.2âŸ©
    by_cases Hp : Disjoint (ğ“˜ p : Set X) (ğ“˜ a')
    Â· right
      simpa [Hp] using h'p
    refine Or.inl âŸ¨a', mem_insert a' B, ?_âŸ©
    rcases le_or_ge_or_disjoint (i := ğ“˜ p) (j := ğ“˜ a') with hij | hij |hij
    Â· exact (Grid.le_def.1 hij).1
    Â· have : p âˆˆ F := âŸ¨hp, a'F.2.1.trans (Grid.le_def.1 hij).1, h'pâŸ©
      rw [ha' p this (Grid.le_def.1 hij).1]
    Â· exact (Hp hij).elim
  have : B = insert a' B := hB _ this (subset_insert a' B)
  have : a' âˆˆ B := by rw [this]; exact mem_insert a' B
  have : Disjoint (ğ“˜ a' : Set X) (ğ“˜ a' : Set X) := a'F.2.2 _ this
  exact disjoint_left.1 this Grid.c_mem_Grid Grid.c_mem_Grid

/-- A disjoint subfamily of `A` covering everything. -/
def maximalSubfamily (A : Set (ğ”“ X)) : Set (ğ”“ X) :=
  (exists_maximal_disjoint_covering_subfamily A).choose

/-- Iterating `maximalSubfamily` to obtain disjoint subfamilies of `A`. -/
def iteratedMaximalSubfamily (A : Set (ğ”“ X)) (n : â„•) : Set (ğ”“ X) :=
  maximalSubfamily (A \ (â‹ƒ (i : {i | i < n}), have : i < n := i.2; iteratedMaximalSubfamily A i))

lemma pairwiseDisjoint_iteratedMaximalSubfamily_image (A : Set (ğ”“ X)) (n : â„•) :
    (iteratedMaximalSubfamily A n).PairwiseDisjoint (fun p â†¦ (ğ“˜ p : Set X)) := by
  rw [iteratedMaximalSubfamily]
  exact (exists_maximal_disjoint_covering_subfamily (X := X) _).choose_spec.1

lemma iteratedMaximalSubfamily_subset (A : Set (ğ”“ X)) (n : â„•) :
    iteratedMaximalSubfamily A n âŠ† A := by
  rw [iteratedMaximalSubfamily]
  exact (exists_maximal_disjoint_covering_subfamily (X := X) _).choose_spec.2.1.trans diff_subset

lemma pairwiseDisjoint_iteratedMaximalSubfamily (A : Set (ğ”“ X)) :
    univ.PairwiseDisjoint (iteratedMaximalSubfamily A) := by
  intro m hm n hn hmn
  wlog h'mn : m < n generalizing m n with H
  Â· exact (H (mem_univ n) (mem_univ m) hmn.symm (by omega)).symm
  have : iteratedMaximalSubfamily A n
      âŠ† A \ â‹ƒ (i : {i | i < n}), iteratedMaximalSubfamily A i := by
    conv_lhs => rw [iteratedMaximalSubfamily]
    apply (exists_maximal_disjoint_covering_subfamily _).choose_spec.2.1
  apply subset_compl_iff_disjoint_left.1
  rw [compl_eq_univ_diff]
  apply this.trans
  apply diff_subset_diff (subset_univ _)
  apply subset_iUnion_of_subset âŸ¨m, h'mnâŸ©
  simp

/-- Any set of tiles can be written as the union of disjoint subfamilies, their number being
controlled by the maximal stack size. -/
lemma eq_biUnion_iteratedMaximalSubfamily (A : Set (ğ”“ X)) {N : â„•} (hN : âˆ€ x, stackSize A x â‰¤ N) :
    A = â‹ƒ n < N, iteratedMaximalSubfamily A n := by
  apply Subset.antisymm; swap
  Â· simp [iUnion_subset_iff, iteratedMaximalSubfamily_subset]
  -- we show that after `N` steps the maximal subfamilies cover everything. Otherwise, say some
  -- `p` is left. Then `ğ“˜ p` is contained in an element of each of the previous subfamilies.
  -- This gives `N+1` different elements containing any element of `ğ“˜ p`, a contradiction with
  -- the maximal stack size.
  intro p hp
  contrapose! hN
  simp only [mem_iUnion, exists_prop, not_exists, not_and] at hN
  have E n (hn : n < N) : âˆƒ u âˆˆ iteratedMaximalSubfamily A n, (ğ“˜ p : Set X) âŠ† (ğ“˜ u : Set X) := by
    rw [iteratedMaximalSubfamily]
    apply (exists_maximal_disjoint_covering_subfamily _).choose_spec.2.2
    simp only [coe_setOf, mem_setOf_eq, mem_diff, hp,
      mem_iUnion, Subtype.exists, exists_prop, not_exists, not_and, true_and]
    intro i hi
    exact hN i (hi.trans hn)
  choose! u hu h'u using E
  obtain âŸ¨x, hxpâŸ© : âˆƒ x, x âˆˆ (ğ“˜ p : Set X) := âŸ¨_, Grid.c_mem_GridâŸ©
  use x
  have : stackSize {q âˆˆ A | q = p} x + stackSize {q âˆˆ A | q â‰  p} x = stackSize A x :=
    stackSize_setOf_add_stackSize_setOf_not
  have : 1 = stackSize {q âˆˆ A | q = p} x := by
    have : 1 = âˆ‘ q âˆˆ {p}, (ğ“˜ q : Set X).indicator 1 x := by simp [hxp]
    rw [this]
    congr
    ext
    simp (config := {contextual := true}) [hp]
  have : âˆ‘ p âˆˆ {p | p âˆˆ u '' (Iio N)}, (ğ“˜ p : Set X).indicator 1 x
      â‰¤ stackSize {q | q âˆˆ A âˆ§ q â‰  p} x := by
    apply Finset.sum_le_sum_of_subset
    rintro p hp
    simp only [mem_image, mem_Iio, Finset.mem_filter, Finset.mem_univ, true_and] at hp
    rcases hp with âŸ¨n, hn, rflâŸ©
    simp only [ne_eq, mem_setOf_eq, Finset.mem_filter,
      Finset.mem_univ, iteratedMaximalSubfamily_subset _ _ (hu n hn), true_and]
    rintro rfl
    exact hN n hn (hu n hn)
  have : âˆ‘ p âˆˆ {p | p âˆˆ u '' (Iio N)}, (ğ“˜ p : Set X).indicator 1 x
      = âˆ‘ p âˆˆ {p | p âˆˆ u '' (Iio N)}, 1 := by
    apply Finset.sum_congr rfl (fun p hp â†¦ ?_)
    simp only [mem_image, mem_Iio, Finset.mem_filter, Finset.mem_univ, true_and] at hp
    rcases hp with âŸ¨n, hn, rflâŸ©
    have : x âˆˆ (ğ“˜ (u n) : Set X) := h'u n hn hxp
    simp [this]
  have : âˆ‘ p âˆˆ {p | p âˆˆ u '' (Iio N)}, 1 = N := by
    have : Finset.filter (fun p â†¦ p âˆˆ u '' Iio N) Finset.univ = Finset.image u (Finset.Iio N) := by
      ext p; simp
    simp only [Finset.sum_const, smul_eq_mul, mul_one, this]
    rw [Finset.card_image_of_injOn, Nat.card_Iio N]
    intro a ha b hb hab
    contrapose! hab
    simp only [Finset.coe_Iio, mem_Iio] at ha hb
    have := pairwiseDisjoint_iteratedMaximalSubfamily A (mem_univ a) (mem_univ b) hab
    exact disjoint_iff_forall_ne.1 this (hu a ha) (hu b hb)
  omega
