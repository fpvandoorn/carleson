import Carleson.FinitaryCarleson

open scoped NNReal
open MeasureTheory Set ENNReal Filter Topology ShortVariables Bornology Metric Complex

noncomputable section

variable {X : Type*} {a : â„•} [MetricSpace X] {q q' : â„â‰¥0} {F G : Set X} {K : X â†’ X â†’ â„‚}
variable [KernelProofData a K] {Q : SimpleFunc X (Î˜ X)} {f : X â†’ â„‚} {Ïƒâ‚ Ïƒâ‚‚ : X â†’ â„¤}

/-- The operator T_{sâ‚, sâ‚‚} introduced in Lemma 3.0.3. -/
def T_S (Q : SimpleFunc X (Î˜ X)) (sâ‚ sâ‚‚ : â„¤) (f : X â†’ â„‚) (x : X) : â„‚ :=
  âˆ‘ s âˆˆ Finset.Icc sâ‚ sâ‚‚, âˆ« y, Ks s x y * f y * exp (I * Q x y)

lemma measurable_T_inner_integral {s : â„¤} (mf : Measurable f) :
    Measurable fun x â†¦ âˆ« y, Ks s x y * f y * exp (I * Q x y) := by
  rw [â† stronglyMeasurable_iff_measurable]
  apply StronglyMeasurable.integral_prod_right
  rw [stronglyMeasurable_iff_measurable]
  apply (measurable_Ks.mul (mf.comp measurable_snd)).mul
  refine ((Complex.measurable_ofReal.comp measurable_Qâ‚‚).const_mul I).cexp

lemma measurable_T_S {sâ‚ sâ‚‚} (mf : Measurable f) : Measurable (T_S Q sâ‚ sâ‚‚ f Â·) :=
  Finset.measurable_sum _ fun _ _ â†¦ measurable_T_inner_integral mf

/-- The operator T_{2, Ïƒâ‚, Ïƒâ‚‚} introduced in Lemma 3.0.4. -/
def T_lin (Q : SimpleFunc X (Î˜ X)) (Ïƒâ‚ Ïƒâ‚‚ : X â†’ â„¤) (f : X â†’ â„‚) (x : X) : â„‚ :=
  T_S Q (Ïƒâ‚ x) (Ïƒâ‚‚ x) f x

lemma measurable_T_lin (mf : Measurable f) (mÏƒâ‚ : Measurable Ïƒâ‚) (mÏƒâ‚‚ : Measurable Ïƒâ‚‚)
    (rÏƒâ‚ : (range Ïƒâ‚).Finite) (rÏƒâ‚‚ : (range Ïƒâ‚‚).Finite) : Measurable (T_lin Q Ïƒâ‚ Ïƒâ‚‚ f Â·) := by
  obtain âŸ¨lb, hlbâŸ© := bddBelow_def.mp rÏƒâ‚.bddBelow
  obtain âŸ¨ub, hubâŸ© := bddAbove_def.mp rÏƒâ‚‚.bddAbove
  simp_rw [mem_range, forall_exists_index, forall_apply_eq_imp_iff] at hlb hub
  have rearr : T_lin Q Ïƒâ‚ Ïƒâ‚‚ f = fun x â†¦ âˆ‘ s âˆˆ Finset.Icc lb ub,
      {x' | s âˆˆ Icc (Ïƒâ‚ x') (Ïƒâ‚‚ x')}.indicator
        (fun z â†¦ âˆ« y, Ks s z y * f y * exp (I * Q z y)) x := by
    ext x; unfold T_lin T_S
    calc
      _ = âˆ‘ s âˆˆ Finset.Icc (Ïƒâ‚ x) (Ïƒâ‚‚ x), {x' | s âˆˆ Icc (Ïƒâ‚ x') (Ïƒâ‚‚ x')}.indicator
          (fun z â†¦ âˆ« y, Ks s z y * f y * exp (I * Q z y)) x := by
        congr! with s ms; rw [indicator_of_mem (by simpa using ms)]
      _ = _ := by
        refine Finset.sum_subset (Finset.Icc_subset_Icc (hlb x) (hub x)) fun s ms ns â†¦ ?_
        apply indicator_of_notMem; rwa [mem_setOf_eq, mem_Icc, â† Finset.mem_Icc]
  rw [rearr]
  refine Finset.measurable_sum _ fun _ _ â†¦ (measurable_T_inner_integral mf).indicator ?_
  rw [measurableSet_setOf]; apply (measurable_set_mem _).comp
  apply Measurable.comp (f := fun x â†¦ (Ïƒâ‚ x, Ïƒâ‚‚ x)) (g := fun p â†¦ Icc p.1 p.2)
  Â· exact measurable_from_prod_countable_left fun _ _ _ â†¦ trivial
  Â· exact mÏƒâ‚.prodMk mÏƒâ‚‚

section Linearised

variable [IsCancellative X (defaultÏ„ a)]

variable (q q' F f Ïƒâ‚ Ïƒâ‚‚) in
/-- Convenience structure for the parameters that stay constant throughout the recursive calls to
finitary Carleson in the proof of Lemma 3.0.4. -/
structure CP304 where
  /-- `Q` is the only non-`Prop` field of `CP304`. -/
  Q : SimpleFunc X (Î˜ X)
  BST_T_Q (Î¸ : Î˜ X) : HasBoundedStrongType (linearizedNontangentialOperator Q Î¸ K Â· Â·)
    2 2 volume volume (C_Ts a)
  hq : q âˆˆ Ioc 1 2
  hqq' : q.HolderConjugate q'
  bF : IsBounded F
  mF : MeasurableSet F
  mf : Measurable f
  nf : (â€–f Â·â€–) â‰¤ F.indicator 1
  mÏƒâ‚ : Measurable Ïƒâ‚
  mÏƒâ‚‚ : Measurable Ïƒâ‚‚
  rÏƒâ‚ : (range Ïƒâ‚).Finite
  rÏƒâ‚‚ : (range Ïƒâ‚‚).Finite
  lÏƒ : Ïƒâ‚ â‰¤ Ïƒâ‚‚

theorem finitary_carleson_step
    (CP : CP304 q q' F f Ïƒâ‚ Ïƒâ‚‚) (bG : IsBounded G) (mG : MeasurableSet G) :
    âˆƒ G' âŠ† G, IsBounded G' âˆ§ MeasurableSet G' âˆ§ 2 * volume G' â‰¤ volume G âˆ§
    âˆ«â» x in G \ G', â€–T_lin CP.Q Ïƒâ‚ Ïƒâ‚‚ f xâ€–â‚‘ â‰¤
    C2_0_1 a q * (volume G) ^ (q' : â„)â»Â¹ * (volume F) ^ (q : â„)â»Â¹ := by
  obtain âŸ¨Q, BST_T_Q, hq, hqq', bF, mF, mf, nf, mÏƒâ‚, mÏƒâ‚‚, rÏƒâ‚, rÏƒâ‚‚, lÏƒâŸ© := CP
  let PD : ProofData a q K Ïƒâ‚ Ïƒâ‚‚ F G :=
    âŸ¨â€¹_â€º, hq, bF, bG, mF, mG, mÏƒâ‚, mÏƒâ‚‚, rÏƒâ‚, rÏƒâ‚‚, lÏƒ, Q, BST_T_QâŸ©
  obtain âŸ¨Gâ‚, mGâ‚, vGâ‚, hGâ‚âŸ© := finitary_carleson X
  refine âŸ¨G âˆ© Gâ‚, inter_subset_left, bG.subset inter_subset_left, mG.inter mGâ‚, ?_, ?_âŸ©
  Â· refine le_trans ?_ vGâ‚; gcongr; exact inter_subset_right
  Â· simp_rw [diff_self_inter]; simp_rw [toFinset_Icc, show nnq = q by rfl] at hGâ‚
    convert hGâ‚ f mf nf using 4; rw [eq_sub_iff_add_eq]; norm_cast
    exact hqq'.symm.inv_add_inv_eq_one

variable (q q' F f Ïƒâ‚ Ïƒâ‚‚) in
/-- All the parameters needed to apply the recursion of Lemma 3.0.4. -/
structure P304 where
  /-- `CP` holds all constant parameters. -/
  CP : CP304 q q' F f Ïƒâ‚ Ïƒâ‚‚
  /-- `G` is the set being recursed on. -/
  G : Set X
  bG : IsBounded G
  mG : MeasurableSet G

/-- Construct `G_{n+1}` given `G_n`. -/
def P304.succ (P : P304 q q' F f Ïƒâ‚ Ïƒâ‚‚) : P304 q q' F f Ïƒâ‚ Ïƒâ‚‚ where
  CP := P.CP
  G := (finitary_carleson_step P.CP P.bG P.mG).choose
  bG := (finitary_carleson_step P.CP P.bG P.mG).choose_spec.2.1
  mG := (finitary_carleson_step P.CP P.bG P.mG).choose_spec.2.2.1

variable (CP : CP304 q q' F f Ïƒâ‚ Ïƒâ‚‚) (bG : IsBounded G) (mG : MeasurableSet G)

/-- `slice CP bG mG n` contains `G_n` and its associated data. -/
def slice (n : â„•) : P304 q q' F f Ïƒâ‚ Ïƒâ‚‚ := P304.succ^[n] âŸ¨CP, G, bG, mGâŸ©

variable {CP bG mG n}

@[simp]
lemma slice_CP : (slice CP bG mG n).CP = CP := by
  induction n with
  | zero => simp [slice]
  | succ n ih => rw [slice] at ih âŠ¢; rwa [Function.iterate_succ_apply']

lemma slice_G_subset : (slice CP bG mG (n + 1)).G âŠ† (slice CP bG mG n).G := by
  rw [slice, Function.iterate_succ_apply']
  set P := slice CP bG mG n
  exact (finitary_carleson_step P.CP P.bG P.mG).choose_spec.1

lemma antitone_slice_G : Antitone fun n â†¦ (slice CP bG mG n).G :=
  antitone_nat_of_succ_le fun _ â†¦ slice_G_subset

lemma volume_slice : 2 * volume (slice CP bG mG (n + 1)).G â‰¤ volume (slice CP bG mG n).G := by
  rw [slice, Function.iterate_succ_apply']
  set P := slice CP bG mG n
  exact (finitary_carleson_step P.CP P.bG P.mG).choose_spec.2.2.2.1

lemma volume_slice_le_inv_two_pow_mul : volume (slice CP bG mG n).G â‰¤ 2â»Â¹ ^ n * volume G := by
  induction n with
  | zero => simp [slice]
  | succ n ih =>
    replace ih := volume_slice.trans ih
    rwa [mul_le_iff_le_inv two_ne_zero ofNat_ne_top, â† mul_assoc, â† pow_succ'] at ih

/-- The sets `G_n` become arbitrarily small. -/
lemma exists_volume_slice_lt_eps {Îµ : â„â‰¥0âˆ} (Îµpos : 0 < Îµ) :
    âˆƒ n, volume (slice CP bG mG (n + 1)).G < Îµ := by
  suffices âˆƒ n, 2â»Â¹ ^ (n + 1) * volume G < Îµ by
    obtain âŸ¨n, hnâŸ© := this
    exact âŸ¨n, volume_slice_le_inv_two_pow_mul.trans_lt hnâŸ©
  rcases eq_zero_or_pos (volume G) with vG | vG; Â· simpa [vG]
  have vGnt : volume G â‰  âŠ¤ := bG.measure_lt_top.ne
  conv =>
    enter [1, n]
    rw [â† ENNReal.lt_div_iff_mul_lt (.inl vG.ne') (.inl vGnt), pow_succ', â† ENNReal.div_eq_inv_mul,
      ENNReal.div_lt_iff (.inl two_ne_zero) (.inl ofNat_ne_top)]
  have Îµdvn0 : Îµ / volume G * 2 â‰  0 :=
    mul_ne_zero (ENNReal.div_ne_zero.mpr âŸ¨Îµpos.ne', vGntâŸ©) two_ne_zero
  exact exists_inv_two_pow_lt Îµdvn0

lemma slice_integral_bound :
    âˆ«â» x in (slice CP bG mG n).G \ (slice CP bG mG (n + 1)).G, â€–T_lin CP.Q Ïƒâ‚ Ïƒâ‚‚ f xâ€–â‚‘ â‰¤
    C2_0_1 a q * volume (slice CP bG mG n).G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹ := by
  set P := slice CP bG mG n
  convert (finitary_carleson_step P.CP P.bG P.mG).choose_spec.2.2.2.2
  Â· rw [slice, Function.iterate_succ_apply']; rfl
  Â· simp [P]

/-- The slightly unusual way of writing the integrand is to facilitate applying the
monotone convergence theorem. -/
lemma slice_integral_bound_sum :
    âˆ«â» x, (G \ (slice CP bG mG (n + 1)).G).indicator (â€–T_lin CP.Q Ïƒâ‚ Ïƒâ‚‚ f Â·â€–â‚‘) x â‰¤
    C2_0_1 a q * (âˆ‘ i âˆˆ Finset.range (n + 1), (2â»Â¹ ^ i) ^ (q' : â„)â»Â¹) *
    volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹ := by
  rw [lintegral_indicator (mG.diff (slice CP bG mG _).mG)]
  induction n with
  | zero =>
    rw [zero_add, Finset.range_one, Finset.sum_singleton, pow_zero, one_rpow, mul_one]
    convert slice_integral_bound; simp [slice]
  | succ n ih =>
    rw [â† diff_union_diff_cancel _ slice_G_subset]; swap
    Â· exact antitone_slice_G (zero_le _)
    rw [lintegral_union ((slice CP bG mG _).mG.diff (slice CP bG mG _).mG)]; swap
    Â· exact disjoint_of_subset_right diff_subset disjoint_sdiff_left
    rw [Finset.sum_range_succ, mul_add, add_mul, add_mul]; gcongr
    rw [mul_assoc _ _ (volume G ^ _), â† ENNReal.mul_rpow_of_nonneg _ _ (by positivity)]
    apply slice_integral_bound.trans; gcongr; exact volume_slice_le_inv_two_pow_mul

end Linearised

lemma sum_le_four_div_q_sub_one (hq : q âˆˆ Ioc 1 2) (hqq' : q.HolderConjugate q') {n : â„•} :
    âˆ‘ i âˆˆ Finset.range n, ((2 : â„â‰¥0âˆ)â»Â¹ ^ i) ^ (q' : â„)â»Â¹ â‰¤ (2 ^ 2 / (q - 1) : â„â‰¥0) := by
  have ltq' : 1 < q' := (NNReal.holderConjugate_iff.mp hqq'.symm).1
  calc
    _ = âˆ‘ i âˆˆ Finset.range n, ((2 : â„â‰¥0âˆ) ^ (-(q' : â„)â»Â¹)) ^ i := by
      congr! with i mi
      simp_rw [â† ENNReal.rpow_natCast, ENNReal.inv_rpow, â† ENNReal.rpow_neg, â† ENNReal.rpow_mul]
      rw [mul_comm]
    _ â‰¤ _ := ENNReal.sum_le_tsum _
    _ = (1 - 2 ^ (-(q' : â„)â»Â¹))â»Â¹ := ENNReal.tsum_geometric _
    _ â‰¤ 2 * (ENNReal.ofReal (q' : â„)â»Â¹)â»Â¹ := by
      refine near_1_geometric_bound âŸ¨by positivity, ?_âŸ©
      norm_cast; rw [inv_le_one_iffâ‚€]; exact .inr ltq'.le
    _ = ENNReal.ofNNReal (2 * q / (q - 1)) := by
      rw [ENNReal.ofReal_inv_of_pos (by positivity), inv_inv, ofReal_coe_nnreal,
        show (2 : â„â‰¥0âˆ) = (2 : â„â‰¥0) by rfl, â† ENNReal.coe_mul]
      rw [NNReal.holderConjugate_iff_eq_conjExponent hq.1] at hqq'
      rw [hqq', mul_div_assoc]
    _ â‰¤ _ := by rw [sq]; gcongr; exact hq.2

/-- The constant used in `linearized_truncation` and `S_truncation`.
Has value `2 ^ (442 * a ^ 3 + 2)` in the blueprint. -/
def C3_0_4 (a : â„•) (q : â„â‰¥0) : â„â‰¥0 :=
  2 ^ ((3 * ğ•” + 17 + 5 * (ğ•” / 4)) * a ^ 3 + 2) / (q - 1) ^ 6

lemma eq_C3_0_4 : C2_0_1 a q * (2 ^ 2 / (q - 1)) = C3_0_4 a q := by
  rw [C2_0_1, C2_0_2]
  simp only [C3_0_4, div_mul_div_comm, â† pow_add]
  congr

/-- Lemma 3.0.4. -/
lemma linearized_truncation
    [IsCancellative X (defaultÏ„ a)] (hq : q âˆˆ Ioc 1 2) (hqq' : q.HolderConjugate q')
    (bF : IsBounded F) (bG : IsBounded G) (mF : MeasurableSet F) (mG : MeasurableSet G)
    (mf : Measurable f) (nf : (â€–f Â·â€–) â‰¤ F.indicator 1) (mÏƒâ‚ : Measurable Ïƒâ‚) (mÏƒâ‚‚ : Measurable Ïƒâ‚‚)
    (rÏƒâ‚ : (range Ïƒâ‚).Finite) (rÏƒâ‚‚ : (range Ïƒâ‚‚).Finite) (lÏƒ : Ïƒâ‚ â‰¤ Ïƒâ‚‚)
    (BST_T_Q : âˆ€ Î¸ : Î˜ X, HasBoundedStrongType (linearizedNontangentialOperator Q Î¸ K Â· Â·)
      2 2 volume volume (C_Ts a)) :
    âˆ«â» x in G, â€–T_lin Q Ïƒâ‚ Ïƒâ‚‚ f xâ€–â‚‘ â‰¤
    C3_0_4 a q * volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹ := by
  let CP : CP304 q q' F f Ïƒâ‚ Ïƒâ‚‚ := âŸ¨Q, BST_T_Q, hq, hqq', bF, mF, mf, nf, mÏƒâ‚, mÏƒâ‚‚, rÏƒâ‚, rÏƒâ‚‚, lÏƒâŸ©
  calc
    _ = âˆ«â» x in â‹ƒ n, G \ (slice CP bG mG (n + 1)).G, â€–T_lin CP.Q Ïƒâ‚ Ïƒâ‚‚ f xâ€–â‚‘ := by
      apply setLIntegral_congr; rw [â† diff_iInter]; refine (diff_null_ae_eq_self ?_).symm
      rw [Antitone.measure_iInter]; rotate_left
      Â· exact fun _ _ _ â†¦ antitone_slice_G (by omega)
      Â· exact fun n â†¦ (slice CP bG mG (n + 1)).mG.nullMeasurableSet
      Â· use 0; rw [â† lt_top_iff_ne_top]
        exact (measure_mono slice_G_subset).trans_lt bG.measure_lt_top
      rw [show (0 : â„â‰¥0âˆ) = âŠ¥ by rfl, iInf_eq_bot]
      exact fun _ â†¦ exists_volume_slice_lt_eps
    _ = âˆ«â» x, â¨† n, (G \ (slice CP bG mG (n + 1)).G).indicator (â€–T_lin CP.Q Ïƒâ‚ Ïƒâ‚‚ f Â·â€–â‚‘) x := by
      rw [â† lintegral_indicator (MeasurableSet.iUnion fun n â†¦ mG.diff (slice CP bG mG (n + 1)).mG)]
      congr! with x
      rw [â† iSup_apply, iSup_indicator rfl monotone_const]; swap
      Â· exact fun _ _ _ â†¦ sdiff_le_sdiff_left (antitone_slice_G (by omega))
      rw [iSup_const]
    _ = â¨† n, âˆ«â» x, (G \ (slice CP bG mG (n + 1)).G).indicator (â€–T_lin CP.Q Ïƒâ‚ Ïƒâ‚‚ f Â·â€–â‚‘) x := by
      refine lintegral_iSup (fun n â†¦ ?_) (fun i j hl â†¦ ?_)
      Â· exact (measurable_T_lin mf mÏƒâ‚ mÏƒâ‚‚ rÏƒâ‚ rÏƒâ‚‚).enorm.indicator
          (mG.diff (slice CP bG mG (n + 1)).mG)
      Â· exact indicator_le_indicator_of_subset (sdiff_le_sdiff_left (antitone_slice_G (by omega)))
          (zero_le _)
    _ â‰¤ C2_0_1 a q * (2 ^ 2 / (q - 1) : â„â‰¥0) * volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹ := by
      refine iSup_le fun n â†¦ slice_integral_bound_sum.trans ?_
      gcongr; exact sum_le_four_div_q_sub_one hq hqq'
    _ = _ := by rw [â† ENNReal.coe_mul, eq_C3_0_4]

/-- Lemma 3.0.3. `B` is the blueprint's `S`. -/
lemma S_truncation
    [IsCancellative X (defaultÏ„ a)] {B : â„•} (hq : q âˆˆ Ioc 1 2) (hqq' : q.HolderConjugate q')
    (bF : IsBounded F) (bG : IsBounded G) (mF : MeasurableSet F) (mG : MeasurableSet G)
    (mf : Measurable f) (nf : (â€–f Â·â€–) â‰¤ F.indicator 1)
    (BST_T_Q : âˆ€ Î¸ : Î˜ X, HasBoundedStrongType (linearizedNontangentialOperator Q Î¸ K Â· Â·)
      2 2 volume volume (C_Ts a)) :
    âˆ«â» x in G, â¨† sâ‚ âˆˆ Finset.Icc (-B : â„¤) B, â¨† sâ‚‚ âˆˆ Finset.Icc sâ‚ B, â€–T_S Q sâ‚ sâ‚‚ f xâ€–â‚‘ â‰¤
    C3_0_4 a q * volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹ := by
  -- Define `T1'` and `T1` and prove their measurability
  let T1' (x : X) (s' : â„¤) := â¨† sâ‚‚ âˆˆ Finset.Icc s' B, â€–T_S Q s' sâ‚‚ f xâ€–â‚‘
  have mT1' {n : â„¤} : Measurable (T1' Â· n) :=
    Measurable.iSup fun _ â†¦ Measurable.iSup fun _ â†¦ (measurable_T_S mf).enorm
  let T1 (x : X) := â¨† sâ‚ âˆˆ Finset.Icc (-B : â„¤) B, T1' x sâ‚
  have mT1 : Measurable T1 := Measurable.iSup fun _ â†¦ Measurable.iSup fun _ â†¦ mT1'
  -- For each `x` define a candidate set of values for `Ïƒâ‚ x`;
  -- the final value is the minimum in this set. Also prove measurability of membership
  let candÏƒâ‚ (x : X) := (Finset.Icc (-B : â„¤) B).filter (T1 x = T1' x Â·)
  have necÏƒâ‚ (x : X) : (candÏƒâ‚ x).Nonempty := by
    rw [Finset.filter_nonempty_iff]
    obtain âŸ¨s', ms', hs'âŸ© := (Finset.Icc (-B : â„¤) B).exists_max_image (T1' x) âŸ¨0, by simpâŸ©
    use s', ms'; apply le_antisymm
    Â· exact iSupâ‚‚_le hs'
    Â· apply le_biSup _ ms'
  have scÏƒâ‚ (x : X) : candÏƒâ‚ x âŠ† Finset.Icc (-B) B := by simp [candÏƒâ‚]
  have mcÏƒâ‚ {n : â„¤} : Measurable (n âˆˆ candÏƒâ‚ Â·) := by
    simp_rw [candÏƒâ‚, Finset.mem_filter, Finset.mem_Icc]
    apply measurable_const.and; rw [â† measurableSet_setOf]; exact measurableSet_eq_fun' mT1 mT1'
  -- Define `Ïƒâ‚` and prove its measurability and finite range
  let Ïƒâ‚ (x : X) := (candÏƒâ‚ x).min' (necÏƒâ‚ x)
  have eÏƒâ‚ (x : X) : Ïƒâ‚ x âˆˆ candÏƒâ‚ x := (candÏƒâ‚ x).min'_mem (necÏƒâ‚ x)
  have minÏƒâ‚ (x : X) {n : â„¤} (hn : n âˆˆ candÏƒâ‚ x) : Ïƒâ‚ x â‰¤ n := (candÏƒâ‚ x).min'_le _ hn
  have mÏƒâ‚ : Measurable Ïƒâ‚ := by
    classical
    refine measurable_to_countable' fun n â†¦ ?_
    have eqv : Ïƒâ‚ â»Â¹' {n} =
        candÏƒâ‚ â»Â¹' ((Finset.Icc (-B : â„¤) B).powerset.filter fun c â†¦ n âˆˆ c âˆ§ âˆ€ m âˆˆ c, n â‰¤ m) := by
      ext x
      simp_rw [mem_preimage, mem_singleton_iff, Finset.coe_filter, Finset.mem_powerset,
        mem_setOf_eq, scÏƒâ‚, true_and]
      constructor <;> intro h
      Â· rw [â† h]; exact âŸ¨eÏƒâ‚ x, fun m â†¦ minÏƒâ‚ xâŸ©
      Â· rw [â† (candÏƒâ‚ x).le_min'_iff (necÏƒâ‚ x)] at h; obtain âŸ¨hâ‚, hâ‚‚ : n â‰¤ Ïƒâ‚ xâŸ© := h
        exact le_antisymm ((candÏƒâ‚ x).min'_le _ hâ‚) hâ‚‚
    simp_rw [eqv, Finset.coe_filter, Finset.mem_powerset, preimage_setOf_eq, measurableSet_setOf]
    refine Measurable.and ?_ (mcÏƒâ‚.and (Measurable.forall fun m â†¦ mcÏƒâ‚.imp measurable_const))
    simp [scÏƒâ‚]
  have rÏƒâ‚ : (range Ïƒâ‚).Finite := by
    suffices range Ïƒâ‚ âŠ† Set.Icc (-B) B by exact (finite_Icc (-B : â„¤) B).subset this
    simp_rw [range_subset_iff, mem_Icc, â† Finset.mem_Icc]; exact fun x â†¦ scÏƒâ‚ x (eÏƒâ‚ x)
  -- Incorporate `Ïƒâ‚` into the main integral
  simp_rw [candÏƒâ‚, Finset.mem_filter, Finset.mem_Icc] at eÏƒâ‚
  change âˆ«â» x in G, T1 x â‰¤ _
  conv_lhs => enter [2, x]; rw [(eÏƒâ‚ x).2]
  -- Work analogously to define `Ïƒâ‚‚`
  let candÏƒâ‚‚ (x : X) :=
    (Finset.Icc (Ïƒâ‚ x) B).filter (fun s'' : â„¤ â†¦ T1' x (Ïƒâ‚ x) = â€–T_S Q (Ïƒâ‚ x) s'' f xâ€–â‚‘)
  have necÏƒâ‚‚ (x : X) : (candÏƒâ‚‚ x).Nonempty := by
    rw [Finset.filter_nonempty_iff]
    obtain âŸ¨s', ms', hs'âŸ© := (Finset.Icc (Ïƒâ‚ x) B).exists_max_image
      (â€–T_S Q (Ïƒâ‚ x) Â· f xâ€–â‚‘) âŸ¨Ïƒâ‚ x, by simpa using (eÏƒâ‚ x).1.2âŸ©
    use s', ms'; apply le_antisymm
    Â· exact iSupâ‚‚_le hs'
    Â· apply le_biSup _ ms'
  have scÏƒâ‚‚ (x : X) : candÏƒâ‚‚ x âŠ† Finset.Icc (-B : â„¤) B :=
    subset_trans (by simp [candÏƒâ‚‚]) (Finset.Icc_subset_Icc_left (eÏƒâ‚ x).1.1)
  have mcÏƒâ‚‚ {n : â„¤} : Measurable (n âˆˆ candÏƒâ‚‚ Â·) := by
    simp_rw [candÏƒâ‚‚, Finset.mem_filter, Finset.mem_Icc]
    apply Measurable.and
    Â· apply Measurable.and ?_ measurable_const
      rw [â† measurableSet_setOf]; exact measurableSet_le mÏƒâ‚ measurable_const
    Â· rw [â† measurableSet_setOf]; apply measurableSet_eq_fun'
      Â· apply Measurable.comp (f := fun x â†¦ (x, Ïƒâ‚ x)) (g := fun p â†¦ T1' p.1 p.2)
        Â· exact measurable_from_prod_countable_left fun _ â†¦ mT1'
        Â· exact measurable_id.prodMk mÏƒâ‚
      Â· apply Measurable.enorm
        apply (Measurable.comp (f := fun x â†¦ (x, Ïƒâ‚ x)) (g := fun p â†¦ T_S Q p.2 n f p.1))
        Â· exact measurable_from_prod_countable_left fun _ â†¦ measurable_T_S mf
        Â· exact measurable_id.prodMk mÏƒâ‚
  -- Work analogously to prove `Ïƒâ‚‚`'s properties
  let Ïƒâ‚‚ (x : X) := (candÏƒâ‚‚ x).min' (necÏƒâ‚‚ x)
  have eÏƒâ‚‚ (x : X) : Ïƒâ‚‚ x âˆˆ candÏƒâ‚‚ x := (candÏƒâ‚‚ x).min'_mem (necÏƒâ‚‚ x)
  have minÏƒâ‚‚ (x : X) {n : â„¤} (hn : n âˆˆ candÏƒâ‚‚ x) : Ïƒâ‚‚ x â‰¤ n := (candÏƒâ‚‚ x).min'_le _ hn
  have mÏƒâ‚‚ : Measurable Ïƒâ‚‚ := by
    classical
    refine measurable_to_countable' fun n â†¦ ?_
    have eqv : Ïƒâ‚‚ â»Â¹' {n} =
        candÏƒâ‚‚ â»Â¹' ((Finset.Icc (-B : â„¤) B).powerset.filter fun c â†¦ n âˆˆ c âˆ§ âˆ€ m âˆˆ c, n â‰¤ m) := by
      ext x
      simp_rw [mem_preimage, mem_singleton_iff, Finset.coe_filter, Finset.mem_powerset,
        mem_setOf_eq, scÏƒâ‚‚, true_and]
      constructor <;> intro h
      Â· rw [â† h]; exact âŸ¨eÏƒâ‚‚ x, fun m â†¦ minÏƒâ‚‚ xâŸ©
      Â· rw [â† (candÏƒâ‚‚ x).le_min'_iff (necÏƒâ‚‚ x)] at h; obtain âŸ¨hâ‚, hâ‚‚ : n â‰¤ Ïƒâ‚‚ xâŸ© := h
        exact le_antisymm ((candÏƒâ‚‚ x).min'_le _ hâ‚) hâ‚‚
    simp_rw [eqv, Finset.coe_filter, Finset.mem_powerset, preimage_setOf_eq, measurableSet_setOf]
    refine Measurable.and ?_ (mcÏƒâ‚‚.and (Measurable.forall fun m â†¦ mcÏƒâ‚‚.imp measurable_const))
    simp [scÏƒâ‚‚]
  have rÏƒâ‚‚ : (range Ïƒâ‚‚).Finite := by
    suffices range Ïƒâ‚‚ âŠ† Set.Icc (-B) B by exact (finite_Icc (-B : â„¤) B).subset this
    simp_rw [range_subset_iff, mem_Icc, â† Finset.mem_Icc]; exact fun x â†¦ scÏƒâ‚‚ x (eÏƒâ‚‚ x)
  simp_rw [candÏƒâ‚‚, Finset.mem_filter, Finset.mem_Icc] at eÏƒâ‚‚
  have lÏƒ : Ïƒâ‚ â‰¤ Ïƒâ‚‚ := by intro x; exact (eÏƒâ‚‚ x).1.1
  -- Complete the reduction
  conv_lhs => enter [2, x]; rw [(eÏƒâ‚‚ x).2]
  exact linearized_truncation hq hqq' bF bG mF mG mf nf mÏƒâ‚ mÏƒâ‚‚ rÏƒâ‚ rÏƒâ‚‚ lÏƒ BST_T_Q

section RTruncation

/-- The largest integer `sâ‚` satisfying `D ^ (-(sâ‚ - 2)) * Râ‚ > 1 / 2`. -/
def L302 (a : â„•) (Râ‚ : â„) : â„¤ := âŒŠReal.logb D (2 * Râ‚)âŒ‹ + 3

/-- The smallest integer `sâ‚‚` satisfying `D ^ (-(sâ‚‚ + 2)) * Râ‚‚ < 1 / (4 * D)`. -/
def U302 (a : â„•) (Râ‚‚ : â„) : â„¤ := âŒˆReal.logb D (4 * Râ‚‚)âŒ‰ - 2

include K in
lemma Râ‚_le_D_zpow_div_four {Râ‚ : â„} : Râ‚ â‰¤ D ^ (L302 a Râ‚ - 1) / 4 := by
  rcases le_or_gt Râ‚ 0 with hRâ‚ | hRâ‚; Â· exact hRâ‚.trans (by positivity)
  rw [le_div_iffâ‚€' zero_lt_four, L302, add_sub_assoc, show (3 - 1 : â„¤) = 2 by norm_num]
  have Dg1 := one_lt_realD X
  calc
    _ = (2 : â„) * D ^ Real.logb D (2 * Râ‚) := by
      rw [Real.rpow_logb (realD_pos a) Dg1.ne' (by linarith only [hRâ‚])]; ring
    _ â‰¤ D * D ^ (âŒŠReal.logb D (2 * Râ‚)âŒ‹ + 1) := by
      rw [â† Real.rpow_intCast]; gcongr
      Â· linarith only [four_le_realD X]
      Â· exact Dg1.le
      Â· push_cast; exact (Int.lt_floor_add_one _).le
    _ = _ := by rw [â† zpow_one_addâ‚€ (realD_pos a).ne']; congr 1; omega

include K in
lemma D_zpow_div_two_le_Râ‚‚ {Râ‚‚ : â„} (hRâ‚‚ : 0 < Râ‚‚) : D ^ (U302 a Râ‚‚) / 2 â‰¤ Râ‚‚ := by
  rw [div_le_iffâ‚€' zero_lt_two, U302]
  have Dg1 := one_lt_realD X
  calc
    _ = (D : â„)â»Â¹ * D ^ (âŒˆReal.logb D (4 * Râ‚‚)âŒ‰ - 1) := by
      conv_rhs => rw [mul_comm, â† zpow_sub_oneâ‚€ (realD_pos a).ne']
      congr 1; omega
    _ â‰¤ 2â»Â¹ * (4 * Râ‚‚) := by
      gcongr; Â· linarith only [four_le_realD X]
      have : 0 < 4 * Râ‚‚ := by positivity
      nth_rw 2 [â† Real.rpow_logb (realD_pos a) Dg1.ne' this]
      rw [â† Real.rpow_intCast]; gcongr
      Â· exact Dg1.le
      Â· push_cast; rw [sub_le_iff_le_add]; exact (Int.ceil_lt_add_one _).le
    _ = _ := by ring

include K in
lemma monotoneOn_L302 : MonotoneOn (L302 a) {r | 0 < r} := fun râ‚ prâ‚ râ‚‚ prâ‚‚ hle â†¦ by
  unfold L302; gcongr
  Â· exact one_lt_realD X
  Â· rw [mem_setOf_eq] at prâ‚; positivity

include K in
lemma monotoneOn_U302 : MonotoneOn (U302 a) {r | 0 < r} := fun râ‚ prâ‚ râ‚‚ prâ‚‚ hle â†¦ by
  unfold U302; gcongr
  Â· exact one_lt_realD X
  Â· rw [mem_setOf_eq] at prâ‚; positivity

include K in
/-- There exists a uniform bound for all possible values of `L302` and `U302` over the annulus in
`R_truncation`. -/
lemma exists_uniform_annulus_bound {R : â„} (hR : 0 < R) :
    âˆƒ B : â„•, âˆ€ Râ‚ âˆˆ Ioo Râ»Â¹ R, âˆ€ Râ‚‚ âˆˆ Ioo Râ‚ R,
      L302 a Râ‚ âˆˆ Finset.Icc (-B : â„¤) B âˆ§ U302 a Râ‚‚ âˆˆ Finset.Icc (-B : â„¤) B := by
  have iRpos : 0 < Râ»Â¹ := by positivity
  let Bâ‚ := (L302 a Râ»Â¹).natAbs
  let Bâ‚‚ := (L302 a R).natAbs
  let Bâ‚ƒ := (U302 a Râ»Â¹).natAbs
  let Bâ‚„ := (U302 a R).natAbs
  use max (max Bâ‚ Bâ‚‚) (max Bâ‚ƒ Bâ‚„); intro Râ‚ mRâ‚ Râ‚‚ mRâ‚‚
  have Râ‚pos : 0 < Râ‚ := iRpos.trans mRâ‚.1
  have Râ‚‚pos : 0 < Râ‚‚ := Râ‚pos.trans mRâ‚‚.1
  constructor
  Â· suffices L302 a Râ‚ âˆˆ Finset.Icc (-Bâ‚ : â„¤) Bâ‚‚ by rw [Finset.mem_Icc] at this âŠ¢; omega
    simp_rw [Finset.mem_Icc, Bâ‚, Bâ‚‚]
    have hâ‚ : L302 a Râ»Â¹ â‰¤ L302 a Râ‚ := monotoneOn_L302 (X := X) iRpos Râ‚pos mRâ‚.1.le
    have hâ‚‚ : L302 a Râ‚ â‰¤ L302 a R := monotoneOn_L302 (X := X) Râ‚pos hR mRâ‚.2.le
    omega
  Â· suffices U302 a Râ‚‚ âˆˆ Finset.Icc (-Bâ‚ƒ : â„¤) Bâ‚„ by rw [Finset.mem_Icc] at this âŠ¢; omega
    simp_rw [Finset.mem_Icc, Bâ‚ƒ, Bâ‚„]
    have hâ‚ƒ : U302 a Râ»Â¹ â‰¤ U302 a Râ‚‚ := monotoneOn_U302 (X := X) iRpos Râ‚‚pos (mRâ‚.1.trans mRâ‚‚.1).le
    have hâ‚„ : U302 a Râ‚‚ â‰¤ U302 a R := monotoneOn_U302 (X := X) Râ‚‚pos hR mRâ‚‚.2.le
    omega

lemma enorm_setIntegral_annulus_le {x : X} {Râ‚ Râ‚‚ : â„} {s : â„¤} (nf : (â€–f Â·â€–) â‰¤ F.indicator 1) :
    â€–âˆ« y in Annulus.oo x Râ‚ Râ‚‚, Ks s x y * f y * exp (I * Q x y)â€–â‚‘ â‰¤
    C2_1_3 a * globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„)) x := by
  calc
    _ â‰¤ âˆ«â» y in Annulus.oo x Râ‚ Râ‚‚, â€–Ks s x yâ€–â‚‘ * â€–f yâ€–â‚‘ * â€–exp (I * Q x y)â€–â‚‘ := by
      simp_rw [â† enorm_mul]; exact enorm_integral_le_lintegral_enorm _
    _ = âˆ«â» y in Annulus.oo x Râ‚ Râ‚‚ âˆ© ball x (D ^ s), â€–Ks s x yâ€–â‚‘ * â€–f yâ€–â‚‘ := by
      simp_rw [enorm_exp_I_mul_ofReal, mul_one]
      rw [â† lintegral_inter_add_diff (B := ball x (D ^ s)) _
        (Annulus.oo x Râ‚ Râ‚‚) measurableSet_ball]
      conv_rhs => rw [â† add_zero (lintegral ..)]
      congr 1
      refine setLIntegral_eq_zero (Annulus.measurableSet_oo.diff measurableSet_ball) fun y my â†¦ ?_
      suffices Ks s x y = 0 by rw [this, enorm_zero, zero_mul, Pi.zero_apply]
      contrapose! my; replace my := dist_mem_Ioo_of_Ks_ne_zero my
      rw [mem_diff, not_and_or, not_not]; right
      rw [mem_Ioo, â† mem_ball'] at my; exact (ball_subset_ball (half_le_self (by positivity))) my.2
    _ â‰¤ âˆ«â» y in ball x (D ^ s), â€–Ks s x yâ€–â‚‘ * â€–f yâ€–â‚‘ := lintegral_mono_set inter_subset_right
    _ â‰¤ âˆ«â» y in ball x (D ^ s),
        C2_1_3 a / volume (ball x (D ^ s)) * â€–F.indicator (1 : X â†’ â„) yâ€–â‚‘ := by
      gcongr with y
      Â· exact enorm_Ks_le
      Â· simp_rw [enorm_le_iff_norm_le, norm_indicator_eq_indicator_norm, Pi.one_apply, norm_one]
        exact nf y
    _ = C2_1_3 a * â¨â» y in ball x (D ^ s), â€–F.indicator (1 : X â†’ â„) yâ€–â‚‘ âˆ‚volume := by
      rw [lintegral_const_mul']; swap
      Â· exact div_ne_top coe_ne_top (measure_ball_pos _ x (defaultD_pow_pos a _)).ne'
      rw [setLAverage_eq, div_eq_mul_inv, ENNReal.div_eq_inv_mul, mul_assoc]
    _ â‰¤ _ := by
      gcongr; exact laverage_le_globalMaximalFunction (by rw [dist_self, defaultD]; positivity)

/-- The fringes of the scale interval in Lemma 3.0.2's proof that require separate handling. -/
def edgeScales (a : â„•) (Râ‚ Râ‚‚ : â„) : Finset â„¤ :=
  {L302 a Râ‚ - 1, L302 a Râ‚ - 2, U302 a Râ‚‚ + 1, U302 a Râ‚‚ + 2}

lemma diff_subset_edgeScales {a : â„•} {Râ‚ Râ‚‚ : â„} :
    Finset.Icc (L302 a Râ‚ - 2) (U302 a Râ‚‚ + 2) \ Finset.Icc (L302 a Râ‚) (U302 a Râ‚‚) âŠ†
    edgeScales a Râ‚ Râ‚‚ := fun s ms â†¦ by
  simp_rw [Finset.mem_sdiff, Finset.mem_Icc, not_and_or, not_le] at ms
  simp only [edgeScales, Finset.mem_insert, Finset.mem_singleton]
  omega

lemma enorm_carlesonOperatorIntegrand_le_T_S {Râ‚ Râ‚‚ : â„} (hRâ‚ : 0 < Râ‚) (hRâ‚‚ : Râ‚ < Râ‚‚)
    (mf : Measurable f) (nf : (â€–f Â·â€–) â‰¤ F.indicator 1) {x : X} :
    â€–carlesonOperatorIntegrand K (Q x) Râ‚ Râ‚‚ f xâ€–â‚‘ â‰¤
    â€–T_S Q (L302 a Râ‚) (U302 a Râ‚‚) f xâ€–â‚‘ +
    4 * C2_1_3 a * globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„)) x := by
  have Dg1 := one_lt_realD X
  let BR := Finset.Icc (L302 a Râ‚ - 2) (U302 a Râ‚‚ + 2) -- big range
  let SR := Finset.Icc (L302 a Râ‚) (U302 a Râ‚‚) -- small range
  calc
    _ = â€–âˆ« y in Annulus.oo x Râ‚ Râ‚‚, âˆ‘ s âˆˆ BR, Ks s x y * f y * exp (I * Q x y)â€–â‚‘ := by
      unfold carlesonOperatorIntegrand; congr 1
      refine setIntegral_congr_fun Annulus.measurableSet_oo fun y my â†¦ ?_
      have dpos : 0 < dist x y := by rw [Annulus.oo, mem_setOf_eq] at my; exact hRâ‚.trans my.1
      simp_rw [â† Finset.sum_mul]; congr
      refine (sum_Ks (Finset.Icc_subset_Icc ?_ ?_) Dg1 dpos).symm
      Â· rw [L302, add_sub_assoc, show (3 : â„¤) - 2 = 1 by norm_num, add_comm 1, Int.floor_add_one]
        gcongr; exact my.1.le
      Â· rw [U302, sub_add_cancel]; gcongr; exact my.2.le
    _ = â€–âˆ‘ s âˆˆ BR, âˆ« y in Annulus.oo x Râ‚ Râ‚‚, Ks s x y * f y * exp (I * Q x y)â€–â‚‘ := by
      congr 1; refine integral_finset_sum _ fun s ms â†¦ ?_
      simp_rw [mul_rotate _ (f _)]; refine ((integrable_Ks_x Dg1).bdd_mul ?_ ?_).restrict
      Â· rw [aestronglyMeasurable_iff_aemeasurable]
        exact (mf.mul ((Complex.measurable_ofReal.comp (measurable_Qâ‚ _))
          |>.const_mul I).cexp).aemeasurable
      Â· simp_rw [norm_mul, norm_exp_I_mul_ofReal, mul_one]
        use 1; exact fun y â†¦ (nf y).trans (indicator_one_le_one y)
    _ â‰¤ â€–âˆ‘ s âˆˆ SR, âˆ« y in Annulus.oo x Râ‚ Râ‚‚, Ks s x y * f y * exp (I * Q x y)â€–â‚‘ +
        â€–âˆ‘ s âˆˆ BR \ SR, âˆ« y in Annulus.oo x Râ‚ Râ‚‚, Ks s x y * f y * exp (I * Q x y)â€–â‚‘ := by
      have : SR âŠ† BR := Finset.Icc_subset_Icc (by omega) (by omega)
      rw [â† Finset.inter_eq_right] at this
      rw [â† Finset.sum_inter_add_sum_diff BR SR, this]
      exact enorm_add_le _ _
    _ = â€–T_S Q (L302 a Râ‚) (U302 a Râ‚‚) f xâ€–â‚‘ +
        â€–âˆ‘ s âˆˆ BR \ SR, âˆ« y in Annulus.oo x Râ‚ Râ‚‚, Ks s x y * f y * exp (I * Q x y)â€–â‚‘ := by
      unfold SR T_S; congr! 3 with s ms; rw [Finset.mem_Icc] at ms
      refine setIntegral_eq_integral_of_forall_compl_eq_zero fun y ny â†¦ ?_
      suffices Ks s x y = 0 by simp_rw [this, zero_mul]
      contrapose! ny; replace ny := dist_mem_Ioo_of_Ks_ne_zero ny
      rw [Annulus.oo, mem_setOf_eq]; refine mem_of_mem_of_subset ny (Ioo_subset_Ioo ?_ ?_)
      Â· apply Râ‚_le_D_zpow_div_four (X := X).trans; gcongr; exacts [Dg1.le, ms.1]
      Â· refine le_trans ?_ (D_zpow_div_two_le_Râ‚‚ (X := X) (hRâ‚.trans hRâ‚‚))
        gcongr; exacts [Dg1.le, ms.2]
    _ â‰¤ â€–T_S Q (L302 a Râ‚) (U302 a Râ‚‚) f xâ€–â‚‘ +
        âˆ‘ s âˆˆ BR \ SR, â€–âˆ« y in Annulus.oo x Râ‚ Râ‚‚, Ks s x y * f y * exp (I * Q x y)â€–â‚‘ := by
      gcongr; exact enorm_sum_le _ _
    _ â‰¤ â€–T_S Q (L302 a Râ‚) (U302 a Râ‚‚) f xâ€–â‚‘ + âˆ‘ s âˆˆ edgeScales a Râ‚ Râ‚‚,
        â€–âˆ« y in Annulus.oo x Râ‚ Râ‚‚, Ks s x y * f y * exp (I * Q x y)â€–â‚‘ := by
      gcongr; exact diff_subset_edgeScales
    _ â‰¤ â€–T_S Q (L302 a Râ‚) (U302 a Râ‚‚) f xâ€–â‚‘ + âˆ‘ s âˆˆ edgeScales a Râ‚ Râ‚‚,
        C2_1_3 a * globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„)) x := by
      gcongr with s ms; rw [â† Finset.mem_coe] at ms; exact enorm_setIntegral_annulus_le nf
    _ â‰¤ _ := by
      rw [Finset.sum_const, nsmul_eq_mul, â† mul_assoc]; gcongr; exact_mod_cast Finset.card_le_four

lemma lintegral_globalMaximalFunction_le (hq : q âˆˆ Ioc 1 2) (hqq' : q.HolderConjugate q')
    (bF : IsBounded F) (mF : MeasurableSet F) (mG : MeasurableSet G) :
    âˆ«â» x in G, globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„)) x â‰¤
    C2_0_6' (defaultA a) 1 q * volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹ := by
  calc
    _ = âˆ«â» x, G.indicator 1 x * globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„)) x := by
      simp_rw [â† indicator_eq_indicator_one_mul]; rw [lintegral_indicator mG]
    _ â‰¤ eLpNorm (G.indicator (1 : X â†’ â„â‰¥0âˆ)) q' *
        eLpNorm (globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„))) q := by
      apply lintegral_mul_le_eLpNorm_mul_eLqNorm
      Â· rw [holderConjugate_coe_iff]; exact hqq'.symm
      Â· exact aemeasurable_const.indicator mG
      Â· exact AEStronglyMeasurable.globalMaximalFunction.aemeasurable
    _ â‰¤ volume G ^ (q' : â„)â»Â¹ *
        (C2_0_6' (defaultA a) 1 q * eLpNorm (F.indicator (1 : X â†’ â„)) q) := by
      gcongr
      Â· rw [Pi.one_def]; convert eLpNorm_indicator_const_le (1 : â„â‰¥0âˆ) q'
        rw [enorm_eq_self, coe_toReal, one_div, one_mul]
      Â· refine (hasStrongType_globalMaximalFunction zero_lt_one hq.1 _ ?_).2
        rw [Pi.one_def]; exact memLp_indicator_const _ mF _ (.inr bF.measure_lt_top.ne)
    _ â‰¤ _ := by
      rw [â† mul_assoc, mul_comm (_ ^ _)]; gcongr
      rw [Pi.one_def]; convert eLpNorm_indicator_const_le (1 : â„) q
      rw [enorm_one, coe_toReal, one_div, one_mul]

/-- The operator T_{Râ‚, Râ‚‚, R} introduced in Lemma 3.0.2. -/
def T_R (K : X â†’ X â†’ â„‚) (Q : SimpleFunc X (Î˜ X)) (Râ‚ Râ‚‚ R : â„) (f : X â†’ â„‚) (x : X) : â„‚ :=
  (ball o R).indicator (fun x â†¦ carlesonOperatorIntegrand K (Q x) Râ‚ Râ‚‚ f x) x

lemma C1_0_2_pos {a : â„•} {q : â„â‰¥0} (hq : 1 < q) : 0 < C1_0_2 a q := by
  rw [C1_0_2]
  apply div_pos
  Â· positivity
  Â· exact pow_pos (tsub_pos_of_lt hq) _

lemma le_C1_0_2 (a4 : 4 â‰¤ a) (hq : q âˆˆ Ioc 1 2) :
    C3_0_4 a q + 4 * C2_1_3 a * C2_0_6' (defaultA a) 1 q â‰¤ C1_0_2 a q := by
  have : (1 : â„â‰¥0) â‰¤ 2 := by norm_num
  grw [C2_0_6'_defaultA_one_le hq.1 (a := a)]
  have : q / (q - 1) â‰¤ 2 / (q - 1) ^ 6 := by
    gcongr
    Â· have : 0 < q - 1 := tsub_pos_iff_lt.2 hq.1
      positivity
    Â· exact hq.2
    Â· have : q - 1 = (q - 1) ^ 1 := by simp
      conv_rhs => rw [this]
      apply pow_le_pow_of_le_one (by simp) ?_ (by norm_num)
      rw [tsub_le_iff_left]
      exact hq.2.trans_eq (by norm_num)
  grw [this]
  simp only [C3_0_4, C2_1_3, C1_0_2, div_eq_mul_inv, â† mul_assoc, â† add_mul]
  gcongr
  rw [show (4 : â„â‰¥0) = 2 ^ 2 by norm_num]
  simp only [â† pow_add, â† pow_succ]
  apply add_le_pow_two le_rfl ?_ ?_
  Â· ring_nf
    suffices 2 + 4 * a â‰¤ a ^ 3 by omega
    linarith [sixteen_times_le_cube a4]
  Â· linarith [sixteen_times_le_cube a4]

variable [IsCancellative X (defaultÏ„ a)]

lemma R_truncation' (hq : q âˆˆ Ioc 1 2) (hqq' : q.HolderConjugate q')
    (mF : MeasurableSet F) (mG : MeasurableSet G) (mf : Measurable f) (nf : (â€–f Â·â€–) â‰¤ F.indicator 1)
    {n : â„•} {R : â„} (hR : R = 2 ^ n) (sF : F âŠ† ball o (2 * R)) (sG : G âŠ† ball o R)
    (BST_T_Q : âˆ€ Î¸ : Î˜ X, HasBoundedStrongType (linearizedNontangentialOperator Q Î¸ K Â· Â·)
      2 2 volume volume (C_Ts a)) :
    âˆ«â» x in G, â¨† Râ‚ âˆˆ Ioo Râ»Â¹ R, â¨† Râ‚‚ âˆˆ Ioo Râ‚ R, â€–T_R K Q Râ‚ Râ‚‚ R f xâ€–â‚‘ â‰¤
    C1_0_2 a q * volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹ := by
  have Rpos : 0 < R := by rw [hR]; positivity
  have iRpos : 0 < Râ»Â¹ := by rw [hR]; positivity
  obtain âŸ¨B, hBâŸ© := exists_uniform_annulus_bound (X := X) Rpos
  calc
    _ = âˆ«â» x in G, â¨† Râ‚ âˆˆ Ioo Râ»Â¹ R, â¨† Râ‚‚ âˆˆ Ioo Râ‚ R,
        â€–carlesonOperatorIntegrand K (Q x) Râ‚ Râ‚‚ f xâ€–â‚‘ := by
      refine setLIntegral_congr_fun mG fun x mx â†¦ ?_
      unfold T_R; congr! 7 with Râ‚ mRâ‚ Râ‚‚ mRâ‚‚; rw [indicator_of_mem (sG mx)]
    _ â‰¤ âˆ«â» x in G, â¨† Râ‚ âˆˆ Ioo Râ»Â¹ R, â¨† Râ‚‚ âˆˆ Ioo Râ‚ R,
        â€–T_S Q (L302 a Râ‚) (U302 a Râ‚‚) f xâ€–â‚‘ +
        4 * C2_1_3 a * globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„)) x := by
      gcongr with x Râ‚ mRâ‚ Râ‚‚ mRâ‚‚
      exact enorm_carlesonOperatorIntegrand_le_T_S (iRpos.trans mRâ‚.1) mRâ‚‚.1 mf nf
    _ â‰¤ âˆ«â» x in G, â¨† Râ‚ âˆˆ Ioo Râ»Â¹ R, (â¨† Râ‚‚ âˆˆ Ioo Râ‚ R, â€–T_S Q (L302 a Râ‚) (U302 a Râ‚‚) f xâ€–â‚‘) +
        â¨† Râ‚‚ âˆˆ Ioo Râ‚ R, 4 * C2_1_3 a *
          globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„)) x := by
      gcongr with x Râ‚ mRâ‚; exact biSup_add_le_add_biSup
    _ â‰¤ âˆ«â» x in G, (â¨† Râ‚ âˆˆ Ioo Râ»Â¹ R, â¨† Râ‚‚ âˆˆ Ioo Râ‚ R, â€–T_S Q (L302 a Râ‚) (U302 a Râ‚‚) f xâ€–â‚‘) +
        â¨† Râ‚ âˆˆ Ioo Râ»Â¹ R, â¨† Râ‚‚ âˆˆ Ioo Râ‚ R, 4 * C2_1_3 a *
          globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„)) x := by
      gcongr with x; exact biSup_add_le_add_biSup
    _ â‰¤ âˆ«â» x in G, (â¨† sâ‚ âˆˆ Finset.Icc (-B : â„¤) B, â¨† sâ‚‚ âˆˆ Finset.Icc sâ‚ B, â€–T_S Q sâ‚ sâ‚‚ f xâ€–â‚‘) +
        4 * C2_1_3 a * globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„)) x := by
      gcongr with x
      all_goals refine iSupâ‚‚_le fun Râ‚ mRâ‚ â†¦ iSupâ‚‚_le fun Râ‚‚ mRâ‚‚ â†¦ ?_
      Â· specialize hB Râ‚ mRâ‚ Râ‚‚ mRâ‚‚
        rcases lt_or_ge (U302 a Râ‚‚) (L302 a Râ‚) with hul | hul
        Â· rw [T_S, Finset.Icc_eq_empty (not_le.mpr hul), Finset.sum_empty, enorm_zero]
          exact zero_le _
        Â· trans â¨† sâ‚‚ âˆˆ Finset.Icc (L302 a Râ‚) B, â€–T_S Q (L302 a Râ‚) sâ‚‚ f xâ€–â‚‘
          Â· have : U302 a Râ‚‚ âˆˆ Finset.Icc (L302 a Râ‚) B :=
              Finset.mem_Icc.mpr âŸ¨hul, (Finset.mem_Icc.mp hB.2).2âŸ©
            exact le_biSup (Î± := â„â‰¥0âˆ) _ this
          Â· exact le_biSup (Î± := â„â‰¥0âˆ) _ hB.1
      Â· rfl
    _ = (âˆ«â» x in G, â¨† sâ‚ âˆˆ Finset.Icc (-B : â„¤) B, â¨† sâ‚‚ âˆˆ Finset.Icc sâ‚ B, â€–T_S Q sâ‚ sâ‚‚ f xâ€–â‚‘) +
        4 * C2_1_3 a * âˆ«â» x in G, globalMaximalFunction volume 1 (F.indicator (1 : X â†’ â„)) x := by
      rw [â† lintegral_const_mul' _ _ (by finiteness)]; apply lintegral_add_left
      simp_rw [â† Finset.mem_coe]
      refine Measurable.biSup _ (Finset.countable_toSet _) fun sâ‚ msâ‚ â†¦ ?_
      exact Measurable.biSup _ (Finset.countable_toSet _) fun sâ‚‚ msâ‚‚ â†¦ (measurable_T_S mf).enorm
    _ â‰¤ C3_0_4 a q * volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹ + 4 * C2_1_3 a *
        (C2_0_6' (defaultA a) 1 q * volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹) := by
      have bF := isBounded_ball.subset sF
      have bG := isBounded_ball.subset sG
      gcongr
      Â· exact S_truncation hq hqq' bF bG mF mG mf nf BST_T_Q
      Â· exact lintegral_globalMaximalFunction_le hq hqq' bF mF mG
    _ â‰¤ _ := by
      simp_rw [â† mul_assoc, â† add_mul]; gcongr; norm_cast; exact le_C1_0_2 (four_le_a X) hq

/-- Lemma 3.0.2. -/
lemma R_truncation (hq : q âˆˆ Ioc 1 2) (hqq' : q.HolderConjugate q')
    (mF : MeasurableSet F) (mG : MeasurableSet G) (mf : Measurable f) (nf : (â€–f Â·â€–) â‰¤ F.indicator 1)
    {n : â„•} {R : â„} (hR : R = 2 ^ n)
    (BST_T_Q : âˆ€ Î¸ : Î˜ X, HasBoundedStrongType (linearizedNontangentialOperator Q Î¸ K Â· Â·)
      2 2 volume volume (C_Ts a)) :
    âˆ«â» x in G, â¨† Râ‚ âˆˆ Ioo Râ»Â¹ R, â¨† Râ‚‚ âˆˆ Ioo Râ‚ R, â€–T_R K Q Râ‚ Râ‚‚ R f xâ€–â‚‘ â‰¤
    C1_0_2 a q * volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹ := by
  wlog sG : G âŠ† ball o R generalizing G
  Â· calc
      _ = _ := by
        rw [â† inter_comm, â† setLIntegral_indicator measurableSet_ball]
        refine lintegral_congr fun x â†¦ ?_
        symm; rw [indicator_apply_eq_self]; intro nx
        simp_rw [T_R, indicator_of_notMem nx, enorm_zero, iSup_zero]
      _ â‰¤ _ := @this (G âˆ© ball o R) (mG.inter measurableSet_ball) inter_subset_right
      _ â‰¤ _ := by gcongr; exact inter_subset_left
  wlog sF : F âŠ† ball o (2 * R) generalizing F f
  Â· have nf' : (â€–(ball o (2 * R)).indicator f Â·â€–) â‰¤ (F âˆ© ball o (2 * R)).indicator 1 := fun x â†¦ by
      rw [inter_comm, â† indicator_indicator]
      by_cases hx : x âˆˆ ball o (2 * R)
      Â· simp_rw [indicator_of_mem hx]; exact nf x
      Â· simp_rw [indicator_of_notMem hx]; simp
    calc
      _ = _ := by
        refine setLIntegral_congr_fun mG fun x mx â†¦ ?_
        unfold T_R carlesonOperatorIntegrand; congr! 7 with Râ‚ mRâ‚ Râ‚‚ mRâ‚‚ x'
        simp_rw [indicator_of_mem (sG mx)]
        refine setIntegral_congr_fun Annulus.measurableSet_oo fun y my â†¦ ?_
        congr 2; symm; rw [indicator_apply_eq_self]; apply absurd
        specialize sG mx; rw [Annulus.oo, mem_setOf_eq] at my; rw [mem_ball] at sG âŠ¢
        calc
          _ â‰¤ dist x y + dist x o := dist_triangle_left ..
          _ < Râ‚‚ + R := add_lt_add my.2 sG
          _ < _ := by rw [two_mul]; exact add_lt_add_right mRâ‚‚.2 _
      _ â‰¤ _ :=
        this (mF.inter measurableSet_ball) (mf.indicator measurableSet_ball) nf' inter_subset_right
      _ â‰¤ _ := by gcongr; exact inter_subset_left
  exact R_truncation' hq hqq' mF mG mf nf hR sF sG BST_T_Q

end RTruncation
