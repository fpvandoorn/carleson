import Carleson.Defs
import Carleson.ToMathlib.Data.ENNReal

open MeasureTheory Measure Metric Complex Set Bornology Function
open ENNReal hiding one_lt_two
open scoped NNReal

noncomputable section

/-! # Basic definitions and lemmas

This file contains definitions from Section 2 of the blueprint and used throughout the proof
of metric Carleson, as well as results about structures defined in sections 1 and 2
(`DoublingMeasure` `KernelProofData`, `IsCancellative`, `FunctionDistances`, ...).
The constants `D`, `Îº`, `Z` from the blueprint are introduce here, and useful inequalities are
provided for them (as well as for the constants `a` and `tau`).

-/

lemma seven_le_c : 7 â‰¤ ğ•” := by simp [ğ•”]

lemma c_le_100 : ğ•” â‰¤ 100 := by simp [ğ•”]

/- To check that the value of `c` is irrelevant, you can take `ğ•” = 7` above, or you can comment
the previous lines and uncomment the next ones.

lemma exists_c : âˆƒ (c : â„•), 7 â‰¤ c âˆ§ c â‰¤ 100 := âŸ¨7, le_rfl, by norm_numâŸ©
def ğ•” : â„• := exists_c.choose
lemma seven_le_c : 7 â‰¤ ğ•” := exists_c.choose_spec.1
lemma c_le_100 : ğ•” â‰¤ 100 := exists_c.choose_spec.2
-/

/-- The constant `D` from (2.0.1). -/
@[simp] def defaultD (a : â„•) : â„• := 2 ^ (ğ•” * a ^ 2)

/-- `D` as an element of `â„â‰¥0`. -/
def nnD (a : â„•) : â„â‰¥0 := âŸ¨defaultD a, by simpâŸ©

/-- The constant `Îº` from (2.0.2). -/
@[simp] def defaultÎº (a : â„•) : â„ := 2 ^ (-10 * (a : â„))

/-- The constant `Z` from (2.0.3). -/
@[simp] def defaultZ (a : â„•) : â„• := 2 ^ (12 * a)

section ConstantBounds

variable (a : â„•)

lemma defaultD_pos : 0 < defaultD a := by rw [defaultD]; positivity

lemma realD_pos : 0 < (defaultD a : â„) := mod_cast defaultD_pos a

lemma realD_nonneg : 0 â‰¤ (defaultD a : â„) := le_of_lt (realD_pos a)

lemma one_le_realD : 1 â‰¤ (defaultD a : â„) := by
  rw [defaultD, Nat.cast_pow, Nat.cast_ofNat, â† pow_zero 2]
  exact pow_le_pow_rightâ‚€ (one_le_two) (by omega)

lemma defaultD_pow_pos (z : â„¤) : 0 < (defaultD a : â„) ^ z :=
  zpow_pos (realD_pos _) _

lemma mul_defaultD_pow_pos {r : â„} (hr : 0 < r) (z : â„¤) : 0 < r * (defaultD a : â„) ^ z :=
  mul_pos hr (defaultD_pow_pos a z)

section KernelProofData

variable (X) [PseudoMetricSpace X] {a} {K : X â†’ X â†’ â„‚} [hK : KernelProofData a K]

include hK

-- used in 7.5.6 (`limited_scale_impact`)
lemma hundred_lt_D : 100 < defaultD a := by
  have : 100 < 2 ^ 7 := by norm_num
  apply this.trans_le
  have : 16 â‰¤ a ^ 2 := by nlinarith [four_le_a X]
  simp only [defaultD]
  gcongr
  Â· norm_num
  Â· nlinarith [seven_le_c]

-- used in 7.5.6 (`limited_scale_impact`)
lemma hundred_lt_realD : (100 : â„) < defaultD a := mod_cast hundred_lt_D X

-- used in 4.1.7 (`small_boundary`)
lemma twentyfive_le_realD : (25 : â„) â‰¤ defaultD a := by linarith [hundred_lt_realD X]

-- used in 4.1.3 (`I3_prop_3_1`)
lemma eight_le_realD : (8 : â„) â‰¤ defaultD a := by linarith [twentyfive_le_realD X]

-- used in 4.1.6 (`transitive_boundary`)
lemma five_le_realD : (5 : â„) â‰¤ defaultD a := by linarith [twentyfive_le_realD X]

-- used in various places in `Carleson.TileExistence`
lemma four_le_realD : (4 : â„) â‰¤ defaultD a := by linarith [twentyfive_le_realD X]

lemma one_lt_realD : 1 < (defaultD a : â„) := by linarith [twentyfive_le_realD X]

lemma a_pos : 0 < a := by linarith [four_le_a X]

lemma cast_a_pos : 0 < (a : â„) := by norm_cast; exact a_pos X

lemma Ï„_pos : 0 < defaultÏ„ a := inv_pos.mpr (cast_a_pos X)

lemma Ï„_nonneg : 0 â‰¤ defaultÏ„ a := (Ï„_pos X).le

lemma Ï„_le_one : defaultÏ„ a â‰¤ 1 := by

  rw [defaultÏ„, inv_le_one_iffâ‚€]; right; norm_cast; linarith [four_le_a X]

/-- `Ï„` as an element of `â„â‰¥0`. -/
def nnÏ„ : â„â‰¥0 := âŸ¨defaultÏ„ a, Ï„_nonneg XâŸ©
lemma nnÏ„_pos : 0 < nnÏ„ X := Ï„_pos X

end KernelProofData

namespace ShortVariables
-- open this section to get shorter 1-letter names for a bunch of variables

set_option hygiene false
scoped notation "D" => defaultD a
scoped notation "Îº" => defaultÎº a
scoped notation "Z" => defaultZ a
scoped notation "Ï„" => defaultÏ„ a
scoped notation "o" => cancelPt X
scoped notation "S" => defaultS X
scoped notation "nnÏ„" => nnÏ„ X
scoped notation "nnD" => nnD a

end ShortVariables

open scoped ShortVariables

variable {a : â„•}

lemma Îº_nonneg : 0 â‰¤ Îº :=
  Real.rpow_nonneg (by norm_num) _

lemma Îº_le_one : Îº â‰¤ 1 :=
  Real.rpow_le_one_of_one_le_of_nonpos one_le_two (by linarith)

end ConstantBounds

section DoublingMeasure

universe u

namespace MeasureTheory

/- # Doubling metric measure spaces -/

variable {X : Type*} {A : â„â‰¥0} [PseudoMetricSpace X] [DoublingMeasure X A]

instance : ProperSpace X := by
  constructor
  intro x r
  refine TotallyBounded.isCompact_of_isClosed ?_ isClosed_closedBall
  obtain âŸ¨r', hr'âŸ© := exists_gt r
  apply TotallyBounded.subset (closedBall_subset_ball hr')
  refine Metric.totallyBounded_iff.mpr fun Îµ hÎµ â†¦ ?_
  obtain âŸ¨s, _, h2sâŸ© := IsDoubling.allBallsCoverBalls volume |>.ballsCoverBalls (by norm_num) hÎµ x
  use s, s.finite_toSet, by simpa using h2s

instance : IsOpenPosMeasure (volume : Measure X) := isOpenPosMeasure_of_isDoubling _

/-- Monotonicity of doubling measure metric spaces in `A`. -/
@[reducible]
def DoublingMeasure.mono {A'} (h : A â‰¤ A') : DoublingMeasure X A' where
  toIsDoubling := IsDoubling.mono h

open Module

instance InnerProductSpace.DoublingMeasure
    {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]
    [MeasurableSpace E] [BorelSpace E] [FiniteDimensional â„ E] :
    DoublingMeasure E (2 ^ finrank â„ E) where

end MeasureTheory

variable {ğ•œ X : Type*} {A : â„•} [_root_.RCLike ğ•œ] [PseudoMetricSpace X]

lemma fact_isCompact_ball (x : X) (r : â„) : Fact (IsBounded (ball x r)) :=
  âŸ¨isBounded_ballâŸ©
attribute [local instance] fact_isCompact_ball

section FunctionDistances

variable [FunctionDistances ğ•œ X]

instance : ContinuousMapClass (Î˜ X) X ğ•œ := âŸ¨fun f â†¦ (f : C(X, ğ•œ)).2âŸ©

def toWithFunctionDistance {x : X} {r : â„} [FunctionDistances ğ•œ X] :
    Î˜ X â‰ƒ WithFunctionDistance x r := .refl _

end FunctionDistances

/-- The `â„â‰¥0`-valued distance function on `WithFunctionDistance x r`. Preferably use `edist` -/
notation3 "nndist_{" x ", " r "}" => @nndist (WithFunctionDistance x r) _
/-- The ball with center `x` and radius `r` in `WithFunctionDistance x r`. -/
notation3 "ball_{" x ", " r "}" => @ball (WithFunctionDistance x r) _ in

instance nonempty_Space [CompatibleFunctions ğ•œ X A] : Nonempty X := by
  obtain âŸ¨x,_âŸ© := â€¹CompatibleFunctions ğ•œ X Aâ€º.eq_zero
  use x

instance inhabited_Space [CompatibleFunctions ğ•œ X A] : Inhabited X :=
  âŸ¨nonempty_Space.someâŸ©

lemma le_localOscillation [CompatibleFunctions ğ•œ X A] (x : X) (r : â„) (f g : Î˜ X) {y z : X}
    (hy : y âˆˆ ball x r) (hz : z âˆˆ ball x r) : â€–coeÎ˜ f y - coeÎ˜ g y - coeÎ˜ f z + coeÎ˜ g zâ€– â‰¤
    ENNReal.toReal (localOscillation (ball x r) (coeÎ˜ f) (coeÎ˜ g)) := by
  rw [(ENNReal.toReal_ofReal (norm_nonneg _)).symm]
  let f (z) := â¨† (_ : z âˆˆ ball x r Ã—Ë¢ ball x r), ENNReal.ofReal â€–f z.1 - g z.1 - f z.2 + g z.2â€–
  apply ENNReal.toReal_mono
  Â· exact lt_of_le_of_lt CompatibleFunctions.localOscillation_le_cdist ENNReal.ofReal_lt_top |>.ne
  Â· exact le_of_eq_of_le (Eq.symm (iSup_pos âŸ¨hy, hzâŸ©)) (le_iSup f âŸ¨y, zâŸ©)

lemma oscillation_le_cdist [CompatibleFunctions ğ•œ X A] (x : X) (r : â„) (f g : Î˜ X) {y z : X}
    (hy : y âˆˆ ball x r) (hz : z âˆˆ ball x r) :
    â€–coeÎ˜ f y - coeÎ˜ g y - coeÎ˜ f z + coeÎ˜ g zâ€– â‰¤ dist_{x, r} f g := by
  apply le_trans <| le_localOscillation x r f g hy hz
  rw [â† ENNReal.toReal_ofReal dist_nonneg]
  exact ENNReal.toReal_mono ENNReal.ofReal_ne_top CompatibleFunctions.localOscillation_le_cdist

export CompatibleFunctions (localOscillation_le_cdist cdist_mono cdist_le le_cdist)

lemma dist_congr [FunctionDistances ğ•œ X] {xâ‚ xâ‚‚ : X} {râ‚ râ‚‚ : â„} {f g : Î˜ X}
    (eâ‚ : xâ‚ = xâ‚‚) (eâ‚‚ : râ‚ = râ‚‚) : dist_{xâ‚, râ‚} f g = dist_{xâ‚‚, râ‚‚} f g := by congr

variable (X) in
/-- The point `o` in the blueprint -/
def cancelPt [CompatibleFunctions ğ•œ X A] : X :=
  CompatibleFunctions.eq_zero (ğ•œ := ğ•œ) |>.choose

lemma cancelPt_eq_zero [CompatibleFunctions ğ•œ X A] {f : Î˜ X} : f (cancelPt X) = 0 :=
  CompatibleFunctions.eq_zero (ğ•œ := ğ•œ) |>.choose_spec f

/-- The `NNReal` version of the inhomogeneous Lipschitz norm on a ball, `iLipENorm`. -/
def iLipNNNorm {ğ•œ} [NormedField ğ•œ] (Ï• : X â†’ ğ•œ) (xâ‚€ : X) (R : â„) : â„â‰¥0 :=
  (iLipENorm Ï• xâ‚€ R).toNNReal

variable [hXA : DoublingMeasure X A]

lemma enorm_integral_exp_le [CompatibleFunctions â„ X A] {Ï„ : â„} [IsCancellative X Ï„]
    {x : X} {r : â„} {Ï• : X â†’ â„‚} (h2 : support Ï• âŠ† ball x r) {f g : Î˜ X} :
    â€–âˆ« x, exp (I * (f x - g x)) * Ï• xâ€–â‚‘ â‰¤
    (A : â„â‰¥0âˆ) * volume (ball x r) * iLipENorm Ï• x r * (1 + edist_{x, r} f g) ^ (- Ï„) := by
  rcases le_or_gt r 0 with hr | hr
  Â· simp only [ball_eq_empty.2 hr, subset_empty_iff, support_eq_empty_iff] at h2
    simp [h2]
  rcases eq_or_ne A 0 with rfl | hA
  Â· have : (volume : Measure X) = 0 := by
      have := hXA.toIsDoubling
      simp at this
      apply eq_zero_of_isDoubling_zero
    simp [this]
  rcases eq_or_ne (iLipENorm Ï• x r) âˆ with h1 | h1
  Â· apply le_top.trans_eq
    symm
    simp [h1, edist_ne_top, hA, (measure_ball_pos volume x hr).ne']
  exact IsCancellative.enorm_integral_exp_le' hr h1 h2

/-- Constructor of `IsCancellative` in terms of real norms instead of extended reals. -/
lemma isCancellative_of_norm_integral_exp_le (Ï„ : â„) [CompatibleFunctions â„ X A]
    (h : âˆ€ {x : X} {r : â„} {Ï• : X â†’ â„‚} (_hr : 0 < r) (_h1 : iLipENorm Ï• x r â‰  âˆ)
    (_h2 : support Ï• âŠ† ball x r) {f g : Î˜ X},
      â€–âˆ« x in ball x r, exp (I * (f x - g x)) * Ï• xâ€– â‰¤
      A * volume.real (ball x r) * iLipNNNorm Ï• x r * (1 + dist_{x, r} f g) ^ (-Ï„)) :
    IsCancellative X Ï„ := by
  constructor
  intro x r Ï• hr h1 h2 f g
  convert ENNReal.ofReal_le_ofReal (h (x := x) (r := r) (Ï• := Ï•) hr h1 h2 (f := f) (g := g))
  Â· rw [ofReal_norm_eq_enorm]
    congr 1
    rw [setIntegral_eq_integral_of_forall_compl_eq_zero (fun y hy â†¦ ?_)]
    have : Ï• y = 0 := by
      apply notMem_support.1
      contrapose! hy
      exact h2 hy
    simp [this]
  Â· rw [ENNReal.ofReal_mul (by positivity), ENNReal.ofReal_mul (by positivity),
      ENNReal.ofReal_mul (by positivity)]
    congr
    Â· simp
    Â· simp only [Measure.real, ofReal_toReal measure_ball_ne_top]
    Â· simp [iLipNNNorm, coe_toNNReal h1]
    Â· rw [â† ENNReal.ofReal_rpow_of_pos (by positivity)]
      congr
      rw [ENNReal.ofReal_add zero_le_one dist_nonneg]
      simp [edist_dist]

/-- The "volume function" `V`. We will need to assume
`IsFiniteMeasureOnCompacts` and `ProperSpace` to actually know that this volume is finite. -/
def vol {X : Type*} [PseudoMetricSpace X] [MeasureSpace X] (x y : X) : â„â‰¥0âˆ :=
  volume (ball x (dist x y))

@[fun_prop]
lemma measurable_vol {X : Type*} [PseudoMetricSpace X] [SecondCountableTopology X]
    [MeasureSpace X] [OpensMeasurableSpace X] [SFinite (volume : Measure X)] :
    Measurable (uncurry vol : X Ã— X â†’ â„â‰¥0âˆ) := by
  let f : X Ã— X â†’ X Ã— â„ := fun (x, y) â†¦ (x, dist x y)
  let g : X Ã— â„ â†’ â„â‰¥0âˆ := fun (x, a) â†¦ volume (ball x a)
  apply Measurable.comp (f := f) (g := g)
  Â· apply measurable_measure_ball
  Â· fun_prop

@[fun_prop]
lemma measurable_volâ‚ {X : Type*} [PseudoMetricSpace X] [SecondCountableTopology X]
    [MeasureSpace X] [OpensMeasurableSpace X] [SFinite (volume : Measure X)] {y : X} :
    Measurable (vol Â· y) := by
  change Measurable (uncurry vol âˆ˜ fun x : X â†¦ (x, y))
  apply Measurable.comp <;> fun_prop

lemma Real.vol_def {X : Type*} [PseudoMetricSpace X] [MeasureSpace X] {x y : X} :
  Real.vol x y = (vol x y).toReal := rfl

lemma ofReal_vol {X : Type*} [PseudoMetricSpace X] [MeasureSpace X] [ProperSpace X]
  [IsFiniteMeasureOnCompacts (volume : Measure X)] {x y : X} :
    ENNReal.ofReal (Real.vol x y) = vol x y := by
  simp_rw [Real.vol_def, ENNReal.ofReal_toReal_eq_iff, vol]
  apply measure_ball_ne_top

lemma le_upperRadius [FunctionDistances â„ X] {Q : X â†’ Î˜ X} {Î¸ : Î˜ X} {x : X} {r : â„}
    (hr : dist_{x, r} Î¸ (Q x) < 1) : ENNReal.ofReal r â‰¤ upperRadius Q Î¸ x := by
  apply le_iSupâ‚‚ (f := fun r _ â†¦ ENNReal.ofReal r) r hr

private lemma carlesonOperatorIntegrand_const_smul [FunctionDistances â„ X] (K : X â†’ X â†’ â„‚)
    (Î¸ : Î˜ X) (Râ‚ Râ‚‚ : â„) (f : X â†’ â„‚) (z : â„‚) :
    carlesonOperatorIntegrand (z â€¢ K) Î¸ Râ‚ Râ‚‚ f = z â€¢ carlesonOperatorIntegrand K Î¸ Râ‚ Râ‚‚ f := by
  unfold carlesonOperatorIntegrand
  ext x
  simp_rw [Pi.smul_apply, smul_eq_mul, â† integral_const_mul]
  congr with y
  ring

private lemma linearizedCarlesonOperator_const_smul [FunctionDistances â„ X] (Q : X â†’ Î˜ X)
    (K : X â†’ X â†’ â„‚) (f : X â†’ â„‚) (z : â„‚) :
    linearizedCarlesonOperator Q (z â€¢ K) f = â€–zâ€–â‚‘ â€¢ linearizedCarlesonOperator Q K f := by
  unfold linearizedCarlesonOperator
  simp_rw [carlesonOperatorIntegrand_const_smul, Pi.smul_apply, smul_eq_mul, enorm_mul, â† mul_iSup]
  rfl

lemma carlesonOperator_const_smul [FunctionDistances â„ X] (K : X â†’ X â†’ â„‚) (f : X â†’ â„‚) (z : â„‚) :
    carlesonOperator (z â€¢ K) f = â€–zâ€–â‚‘ â€¢ carlesonOperator K f := by
  unfold carlesonOperator
  simp_rw [linearizedCarlesonOperator_const_smul, Pi.smul_apply, â† smul_iSup]
  rfl

lemma nontangentialOperator_const_smul (z : â„‚) {K : X â†’ X â†’ â„‚} :
    nontangentialOperator (z â€¢ K) = â€–zâ€–â‚‘ â€¢ nontangentialOperator K := by
  unfold nontangentialOperator
  simp_rw [Pi.smul_apply, smul_eq_mul, mul_assoc, integral_const_mul, enorm_mul, â† ENNReal.mul_iSup]
  rfl

private lemma carlesonOperatorIntegrand_const_smul' [FunctionDistances â„ X] (K : X â†’ X â†’ â„‚)
    (Î¸ : Î˜ X) (Râ‚ Râ‚‚ : â„) (f : X â†’ â„‚) (z : â„‚) :
    carlesonOperatorIntegrand K Î¸ Râ‚ Râ‚‚ (z â€¢ f) = z â€¢ carlesonOperatorIntegrand K Î¸ Râ‚ Râ‚‚ f := by
  unfold carlesonOperatorIntegrand
  ext x
  simp_rw [Pi.smul_apply, smul_eq_mul, â† integral_const_mul]
  congr with y
  ring

private lemma linearizedCarlesonOperator_const_smul' [FunctionDistances â„ X] (Q : X â†’ Î˜ X)
    (K : X â†’ X â†’ â„‚) (f : X â†’ â„‚) (z : â„‚) :
    linearizedCarlesonOperator Q K (z â€¢ f) = â€–zâ€–â‚‘ â€¢ linearizedCarlesonOperator Q K f := by
  unfold linearizedCarlesonOperator
  simp_rw [carlesonOperatorIntegrand_const_smul', Pi.smul_apply, smul_eq_mul, enorm_mul, â† mul_iSup]
  rfl

lemma carlesonOperator_const_smul' [FunctionDistances â„ X] (K : X â†’ X â†’ â„‚) (f : X â†’ â„‚) (z : â„‚) :
    carlesonOperator K (z â€¢ f) = â€–zâ€–â‚‘ â€¢ carlesonOperator K f := by
  unfold carlesonOperator
  simp_rw [linearizedCarlesonOperator_const_smul', Pi.smul_apply, â† smul_iSup]
  rfl


end DoublingMeasure

section Kernel

variable {X : Type*} {a : â„•} {K : X â†’ X â†’ â„‚} [PseudoMetricSpace X] [MeasureSpace X]

open Function

lemma C_K_pos {a : â„} : 0 < C_K a := NNReal.rpow_pos (by norm_num)
lemma C_K_pos_real {a : â„} : 0 < (C_K a : â„) := C_K_pos

export IsOneSidedKernel (measurable_K norm_K_le_vol_inv norm_K_sub_le)

lemma isOneSidedKernel_const_smul {a : â„•} {K : X â†’ X â†’ â„‚} [IsOneSidedKernel a K] {r : â„}
    (hr : |r| â‰¤ 1) :
    IsOneSidedKernel a (r â€¢ K) where
  measurable_K := measurable_K.const_smul r
  norm_K_le_vol_inv x y := by
    convert mul_le_mul hr (norm_K_le_vol_inv (K := K) x y) (norm_nonneg _) (zero_le_one' â„) using 1
    all_goals simp
  norm_K_sub_le h := by
    simp only [Pi.smul_apply, real_smul]
    rw [â† one_mul (_ ^ _ * _), â† mul_sub, Complex.norm_mul, norm_real, Real.norm_eq_abs]
    gcongr
    exact norm_K_sub_le h

lemma MeasureTheory.stronglyMeasurable_K [IsOneSidedKernel a K] :
    StronglyMeasurable (uncurry K) :=
  measurable_K.stronglyMeasurable

lemma MeasureTheory.aestronglyMeasurable_K [IsOneSidedKernel a K] :
    AEStronglyMeasurable (uncurry K) :=
  measurable_K.aestronglyMeasurable

lemma measurable_K_left [IsOneSidedKernel a K] (y : X) : Measurable (K Â· y) :=
  measurable_K.of_uncurry_right

lemma measurable_K_right [IsOneSidedKernel a K] (x : X) : Measurable (K x) :=
  measurable_K.of_uncurry_left

lemma enorm_K_le_vol_inv [ProperSpace X] [IsFiniteMeasureOnCompacts (volume : Measure X)]
    [IsOneSidedKernel a K] (x y : X) : â€–K x yâ€–â‚‘ â‰¤ (C_K a : â„â‰¥0âˆ) / vol x y := by
  rw [â† ofReal_norm, â† ofReal_vol, â† ofReal_coe_nnreal]
  refine le_trans ?_ (ofReal_div_le measureReal_nonneg)
  gcongr
  apply norm_K_le_vol_inv

lemma enorm_K_le_ball_complement [ProperSpace X] [IsFiniteMeasureOnCompacts (volume : Measure X)]
    [IsOneSidedKernel a K] {r : â„} {x : X} {y : X} (hy : y âˆˆ (ball x r)á¶œ) :
    â€–K x yâ€–â‚‘ â‰¤ C_K a / volume (ball x r) := by
  apply le_trans (enorm_K_le_vol_inv x y)
  apply ENNReal.div_le_div_left (measure_mono (ball_subset_ball (by simpa [dist_comm] using hy)))

lemma enorm_K_le_ball_complement' [ProperSpace X] [IsFiniteMeasureOnCompacts (volume : Measure X)]
    [IsOpenPosMeasure (volume : Measure X)] [IsOneSidedKernel a K] {r : â„} (hr : 0 < r)
    {x : X} {y : X} (hy : y âˆˆ (ball x r)á¶œ) :
    â€–K x yâ€–â‚‘ â‰¤ (C_K a / volume (ball x r)).toNNReal := by
  rw [ENNReal.coe_toNNReal ?ne_top]
  case ne_top =>
    rw [Ne, ENNReal.div_eq_top]
    push_neg
    simp [ne_of_gt (measure_ball_pos volume x hr)]
  exact enorm_K_le_ball_complement hy

lemma enorm_K_sub_le [ProperSpace X] [IsFiniteMeasureOnCompacts (volume : Measure X)]
    [IsOneSidedKernel a K] {x y y' : X} (h : 2 * dist y y' â‰¤ dist x y) :
    â€–K x y - K x y'â€–â‚‘ â‰¤ (edist y y' / edist x y) ^ (a : â„)â»Â¹ * (C_K a / vol x y) := by
  simp_rw [â† ofReal_norm, â† ofReal_vol, â† ofReal_coe_nnreal, edist_dist]
  calc
    _ â‰¤ ENNReal.ofReal ((dist y y' / dist x y) ^ (a : â„)â»Â¹ * (C_K a / Real.vol x y)) := by
      gcongr; apply norm_K_sub_le h
    _ â‰¤ _ := by
      rw [ENNReal.ofReal_mul']; swap
      Â· exact div_nonneg NNReal.zero_le_coe measureReal_nonneg
      gcongr
      Â· rw [â† ENNReal.ofReal_rpow_of_nonneg (by positivity) (by positivity)]
        gcongr
        apply ofReal_div_le (by positivity)
      Â· exact ofReal_div_le measureReal_nonneg

lemma integrableOn_K_mul [IsOpenPosMeasure (volume : Measure X)]
    [IsFiniteMeasureOnCompacts (volume : Measure X)] [ProperSpace X] [IsOneSidedKernel a K]
    {f : X â†’ â„‚} {s : Set X} (hf : IntegrableOn f s) (x : X) {r : â„} (hr : 0 < r)
    (hs : s âŠ† (ball x r)á¶œ) : IntegrableOn (K x * f) s := by
  use (measurable_K_right x).aemeasurable.restrict.mul hf.aemeasurable |>.aestronglyMeasurable
  exact (hasFiniteIntegral_def _ _).mpr <| calc
    _ = âˆ«â» y in s, â€–K x yâ€–â‚‘ * â€–f yâ€–â‚‘ := by simp
    _ â‰¤ âˆ«â» y in s, C_K a / volume (ball x r) * â€–f yâ€–â‚‘ := by
      exact setLIntegral_mono_ae (hf.aemeasurable.enorm.const_mul _) <| Filter.Eventually.of_forall
        fun y hy â†¦ mul_le_mul_right' (enorm_K_le_ball_complement (hs hy)) _
    _ = _ * âˆ«â» y in s, â€–f yâ€–â‚‘ := by exact lintegral_const_mul'' _ hf.aemeasurable.enorm
    _ < âˆ := ENNReal.mul_lt_top (ENNReal.div_lt_top coe_ne_top (measure_ball_pos _ x hr).ne') hf.2

lemma integrableOn_K_Icc [IsOpenPosMeasure (volume : Measure X)]
    [IsFiniteMeasureOnCompacts (volume : Measure X)] [ProperSpace X]
    [IsOneSidedKernel a K] {x : X} {r R : â„} (hr : r > 0) :
    IntegrableOn (K x) {y | dist x y âˆˆ Icc r R} volume := by
  rw [â† mul_one (K x)]
  refine integrableOn_K_mul ?_ x hr ?_
  Â· have : {y | dist x y âˆˆ Icc r R} âŠ† closedBall x R := Annulus.cc_subset_closedBall
    exact integrableOn_const ((measure_mono this).trans_lt measure_closedBall_lt_top).ne
  Â· intro y hy; simp [hy.1, dist_comm y x]

end Kernel


section PseudoMetricSpace

variable {X : Type*} {a : â„•} {q : â„} {K : X â†’ X â†’ â„‚} {Ïƒâ‚ Ïƒâ‚‚ : X â†’ â„¤} {F G : Set X}
  [PseudoMetricSpace X]

section Iterate

variable [CompatibleFunctions â„ X (defaultA a)]

lemma le_cdist_iterate {x : X} {r : â„} (hr : 0 â‰¤ r) (f g : Î˜ X) (k : â„•) :
    2 ^ k * dist_{x, r} f g â‰¤ dist_{x, (defaultA a) ^ k * r} f g := by
  induction k with
  | zero => rw [pow_zero, one_mul]; congr! <;> simp
  | succ k ih =>
    trans 2 * dist_{x, (defaultA a) ^ k * r} f g
    Â· rw [pow_succ', mul_assoc]
      exact (mul_le_mul_iff_rightâ‚€ zero_lt_two).mpr ih
    Â· convert le_cdist (ball_subset_ball _) using 1
      Â· exact dist_congr rfl (by rw [â† mul_assoc, pow_succ'])
      Â· nth_rw 1 [â† one_mul ((defaultA a) ^ k * r)]; gcongr
        rw [â† Nat.cast_one, Nat.cast_le]; exact Nat.one_le_two_pow

lemma cdist_le_iterate {x : X} {r : â„} (hr : 0 < r) (f g : Î˜ X) (k : â„•) :
    dist_{x, 2 ^ k * r} f g â‰¤ (defaultA a) ^ k * dist_{x, r} f g := by
  induction k with
  | zero => simp_rw [pow_zero, one_mul]; congr! <;> simp
  | succ k ih =>
    trans defaultA a * dist_{x, 2 ^ k * r} f g
    Â· convert cdist_le _ using 1
      Â· exact dist_congr rfl (by ring)
      Â· rw [dist_self]; positivity
    Â· replace ih := (mul_le_mul_iff_rightâ‚€ (show 0 < (defaultA a : â„) by positivity)).mpr ih
      rwa [â† mul_assoc, â† pow_succ'] at ih

lemma cdist_le_mul_cdist {x x' : X} {r r' : â„} (hr : 0 < r) (hr' : 0 < r') (f g : Î˜ X) :
    dist_{x', r'} f g â‰¤ As (defaultA a) ((r' + dist x' x) / r) * dist_{x, r} f g := by
  calc
    dist_{x', r'} f g â‰¤ dist_{x, 2 ^ _ * r} f g := ?e
    _ â‰¤ _ := cdist_le_iterate hr f g _
  case e =>
    apply cdist_mono
    apply ball_subset_ball'
    calc
      r' + dist x' x = (r' + dist x' x) / r * r := div_mul_cancelâ‚€ _ hr.ne' |>.symm
      _ â‰¤ 2 ^ âŒˆReal.logb 2 ((r' + dist x' x) / r)âŒ‰â‚Š * r := by
        gcongr
        apply Real.le_pow_natCeil_logb (by norm_num) (by positivity)

lemma ballsCoverBalls_iterate_nat {x : X} {d r : â„} {n : â„•} :
    BallsCoverBalls (WithFunctionDistance x d) (2 ^ n * r) r (defaultA a ^ n) :=
  CompatibleFunctions.allBallsCoverBalls.pow r

lemma ballsCoverBalls_iterate {x : X} {d R r : â„} (hr : 0 < r) :
    BallsCoverBalls (WithFunctionDistance x d) R r (defaultA a ^ âŒˆReal.logb 2 (R / r)âŒ‰â‚Š) :=
  CompatibleFunctions.allBallsCoverBalls.ballsCoverBalls one_lt_two hr

end Iterate

section MeasQ

variable [KernelProofData a K] {Q : SimpleFunc X (Î˜ X)}

@[fun_prop]
lemma measurable_Qâ‚‚ : Measurable fun p : X Ã— X â†¦ Q p.1 p.2 := fun s meass â†¦ by
  have : (fun p : X Ã— X â†¦ (Q p.1) p.2) â»Â¹' s = â‹ƒ Î¸ âˆˆ Q.range, (Q â»Â¹' {Î¸}) Ã—Ë¢ (Î¸ â»Â¹' s) := by
    ext âŸ¨x, yâŸ©
    simp only [mem_preimage, SimpleFunc.mem_range, mem_range, iUnion_exists, iUnion_iUnion_eq',
      mem_iUnion, mem_prod, mem_singleton_iff]
    constructor <;> intro h
    Â· use x
    Â· obtain âŸ¨j, hjâŸ© := h; exact congr($(hj.1) y).symm â–¸ hj.2
  rw [this]
  exact Q.range.measurableSet_biUnion fun Î¸ _ â†¦
    (Q.measurableSet_fiber Î¸).prod (meass.preimage (map_continuous Î¸).measurable)

lemma stronglyMeasurable_Qâ‚‚ : StronglyMeasurable fun p : X Ã— X â†¦ Q p.1 p.2 :=
  measurable_Qâ‚‚.stronglyMeasurable

@[fun_prop]
lemma aestronglyMeasurable_Qâ‚‚ : AEStronglyMeasurable fun p : X Ã— X â†¦ Q p.1 p.2 :=
  measurable_Qâ‚‚.aestronglyMeasurable

@[fun_prop]
lemma measurable_Qâ‚ (x : X) : Measurable (Q x) :=
  let Q' : X â†’ X â†’ â„ := fun x' y â†¦ Q x' y
  have : (fun y â†¦ Q' x y) = Q x := rfl
  this â–¸ measurable_Qâ‚‚.of_uncurry_left

end MeasQ

end PseudoMetricSpace
