import Carleson.Classical.Approximation
import Carleson.Classical.ControlApproximationEffect

/- This file contains the proof of the classical Carleson theorem from Section 11.1. -/

open MeasureTheory Real Filter Topology

noncomputable section

local notation "S_" => partialFourierSum

/- Theorem 1.1 (Classical Carleson) -/
theorem exceptional_set_carleson {f : ‚Ñù ‚Üí ‚ÑÇ}
    (cont_f : Continuous f) (periodic_f : f.Periodic (2 * œÄ))
    {Œµ : ‚Ñù} (Œµpos : 0 < Œµ) :
    ‚àÉ E ‚äÜ Set.Icc 0 (2 * œÄ), MeasurableSet E ‚àß volume.real E ‚â§ Œµ ‚àß
    ‚àÉ N‚ÇÄ, ‚àÄ x ‚àà (Set.Icc 0 (2 * œÄ)) \ E, ‚àÄ N > N‚ÇÄ,
    ‚Äñf x - S_ N f x‚Äñ ‚â§ Œµ := by
  set Œµ' := Œµ / 4 / C_control_approximation_effect Œµ with Œµ'def
  have Œµ'pos : Œµ' > 0 := div_pos (div_pos Œµpos (by norm_num))
    (C_control_approximation_effect_pos Œµpos)

  /- Approximate f by a smooth f‚ÇÄ. -/
  have unicont_f : UniformContinuous f := periodic_f.uniformContinuous_of_continuous
    Real.two_pi_pos cont_f.continuousOn
  obtain ‚ü®f‚ÇÄ, contDiff_f‚ÇÄ, periodic_f‚ÇÄ, hf‚ÇÄ‚ü© := close_smooth_approx_periodic unicont_f periodic_f Œµ'pos
  have Œµ4pos : Œµ / 4 > 0 := by linarith
  obtain ‚ü®N‚ÇÄ, hN‚ÇÄ‚ü© := fourierConv_ofTwiceDifferentiable periodic_f‚ÇÄ
    ((contDiff_infty.mp (contDiff_f‚ÇÄ)) 2) Œµ4pos
  set h := f‚ÇÄ - f with hdef
  have h_measurable : Measurable h := (Continuous.sub contDiff_f‚ÇÄ.continuous cont_f).measurable
  have h_periodic : h.Periodic (2 * œÄ) := periodic_f‚ÇÄ.sub periodic_f
  have h_bound : ‚àÄ x, ‚Äñh x‚Äñ ‚â§ Œµ' := by
    intro x
    simpa only [hdef, Pi.sub_apply, norm_sub_rev] using hf‚ÇÄ x

  /- Control approximation effect: Get a bound on the partial Fourier sums of h. -/
  obtain ‚ü®E, Esubset, Emeasurable, Evolume, hE‚ü© := control_approximation_effect Œµpos Œµ'pos
    h_measurable h_periodic h_bound

  /- This is a classical "epsilon third" argument. -/
  use E, Esubset, Emeasurable, Evolume, N‚ÇÄ
  intro x hx N NgtN‚ÇÄ
  calc ‚Äñf x - S_ N f x‚Äñ
  _ = ‚Äñ(f x - f‚ÇÄ x) + (f‚ÇÄ x - S_ N f‚ÇÄ x) + (S_ N f‚ÇÄ x - S_ N f x)‚Äñ := by ring_nf
  _ ‚â§ ‚Äñ(f x - f‚ÇÄ x) + (f‚ÇÄ x - S_ N f‚ÇÄ x)‚Äñ + ‚ÄñS_ N f‚ÇÄ x - S_ N f x‚Äñ := norm_add_le ..
  _ ‚â§ ‚Äñf x - f‚ÇÄ x‚Äñ + ‚Äñf‚ÇÄ x - S_ N f‚ÇÄ x‚Äñ + ‚ÄñS_ N f‚ÇÄ x - S_ N f x‚Äñ :=
    add_le_add_right (norm_add_le ..) _
  _ ‚â§ Œµ' + (Œµ / 4) + (Œµ / 4) := by
    gcongr
    ¬∑ exact hf‚ÇÄ x
    ¬∑ exact hN‚ÇÄ N NgtN‚ÇÄ x hx.1
    ¬∑ have := hE x hx N
      rw [hdef, partialFourierSum_sub (contDiff_f‚ÇÄ.continuous.intervalIntegrable 0 (2 * œÄ))
        (cont_f.intervalIntegrable 0 (2 * œÄ))] at this
      apply le_trans this
      rw [Œµ'def, mul_div_cancel‚ÇÄ _ (C_control_approximation_effect_pos Œµpos).ne.symm]
  _ ‚â§ (Œµ / 2) + (Œµ / 4) + (Œµ / 4) := by
    gcongr
    rw [Œµ'def, div_div]
    apply div_le_div_of_nonneg_left Œµpos.le (by norm_num)
    rw [‚Üê div_le_iff‚ÇÄ' (by norm_num)]
    exact le_trans' (lt_C_control_approximation_effect Œµpos).le (by linarith [Real.two_le_pi])
  _ ‚â§ Œµ := by linarith

theorem carleson_interval {f : ‚Ñù ‚Üí ‚ÑÇ} (cont_f : Continuous f) (periodic_f : f.Periodic (2 * œÄ)) :
    ‚àÄ·µê x ‚àÇvolume.restrict (Set.Icc 0 (2 * œÄ)),
      Filter.Tendsto (S_ ¬∑ f x) Filter.atTop (nhds (f x)) := by

  let Œ¥ (k : ‚Ñï) : ‚Ñù := 1 / (k + 1) --arbitrary sequence tending to zero
  have Œ¥conv : Filter.Tendsto Œ¥ Filter.atTop (nhds 0) := tendsto_one_div_add_atTop_nhds_zero_nat
  have Œ¥pos (k : ‚Ñï) : 0 < Œ¥ k := by apply div_pos zero_lt_one (by linarith)

  -- ENNReal version to be comparable to volumes
  let Œ¥' (k : ‚Ñï) := ENNReal.ofReal (Œ¥ k)
  have Œ¥'conv : Filter.Tendsto Œ¥' Filter.atTop (nhds 0) := by
    rw [‚Üê ENNReal.ofReal_zero]
    exact ENNReal.tendsto_ofReal Œ¥conv

  set Œµ := fun k n ‚Ü¶ (1 / 2) ^ n * 2‚Åª¬π * Œ¥ k with Œµdef
  have Œµpos (k n : ‚Ñï) : 0 < Œµ k n := by positivity
  have Œµsmall (k : ‚Ñï) {e : ‚Ñù} (epos : 0 < e) : ‚àÉ n, Œµ k n < e := by
    have : Filter.Tendsto (Œµ k) Filter.atTop (nhds 0) := by
      rw [Œµdef]
      simp_rw [mul_assoc]
      rw [‚Üê zero_mul (2‚Åª¬π * Œ¥ k)]
      apply Filter.Tendsto.mul_const
      exact tendsto_pow_atTop_nhds_zero_of_lt_one (by linarith) (by linarith)
    rw [Metric.tendsto_atTop] at this
    rcases (this e epos) with ‚ü®n, hn‚ü©
    use n
    convert (hn n (by simp))
    simp_rw [dist_zero_right, Real.norm_eq_abs, abs_of_nonneg (Œµpos k n).le]

  have Œ¥'_eq {k : ‚Ñï} : Œ¥' k = ‚àë' n, ENNReal.ofReal (Œµ k n) := by
    rw [Œµdef]
    conv => rhs; pattern ENNReal.ofReal _; rw [ENNReal.ofReal_mul' (Œ¥pos k).le,
      ENNReal.ofReal_mul' (by norm_num), ENNReal.ofReal_pow (by norm_num)]
    rw [ENNReal.tsum_mul_right, ENNReal.tsum_mul_right, ENNReal.tsum_geometric,
      ‚Üê ENNReal.ofReal_one, ‚Üê ENNReal.ofReal_sub, ‚Üê ENNReal.ofReal_inv_of_pos (by norm_num),
      ‚Üê ENNReal.ofReal_mul' (by norm_num)]
    conv => pattern ENNReal.ofReal _; ring_nf; rw [ENNReal.ofReal_one]
    rw [one_mul]
    norm_num

  -- Main step: Apply exceptional_set_carleson to get a family of exceptional sets parameterized by Œµ.
  choose EŒµ hEŒµ_subset _ hEŒµ_measure hEŒµ using (@exceptional_set_carleson f cont_f periodic_f)

  have EŒµmeasure {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) : volume (EŒµ hŒµ) ‚â§ ENNReal.ofReal Œµ := by
    rw [ENNReal.le_ofReal_iff_toReal_le _ hŒµ.le]
    ¬∑ exact hEŒµ_measure hŒµ
    ¬∑ rw [‚Üê lt_top_iff_ne_top]
      apply lt_of_le_of_lt (measure_mono (hEŒµ_subset hŒµ)) measure_Icc_lt_top

  -- Define exceptional sets parameterized by Œ¥.
  let EŒ¥ (k : ‚Ñï) := ‚ãÉ (n : ‚Ñï), EŒµ (Œµpos k n)
  have EŒ¥measure (k : ‚Ñï) : volume (EŒ¥ k) ‚â§ Œ¥' k := by
    apply le_trans (measure_iUnion_le _)
    rw [Œ¥'_eq]
    exact ENNReal.tsum_le_tsum (fun n ‚Ü¶ EŒµmeasure (Œµpos k n))

  -- Define final exceptional set.
  let E := ‚ãÇ (k : ‚Ñï), EŒ¥ k

  -- Show that it has the desired property.
  have hE : ‚àÄ x ‚àà (Set.Icc 0 (2 * œÄ)) \ E, Filter.Tendsto (S_ ¬∑ f x) Filter.atTop (nhds (f x)) := by
    intro x hx
    rw [Set.diff_iInter, Set.mem_iUnion] at hx
    rcases hx with ‚ü®k,hk‚ü©
    rw [Set.diff_iUnion, Set.mem_iInter] at hk

    rw [Metric.tendsto_atTop']
    intro e epos
    rcases (Œµsmall k epos) with ‚ü®n, lt_e‚ü©

    rcases (hEŒµ (Œµpos k n)) with ‚ü®N‚ÇÄ,hN‚ÇÄ‚ü©
    use N‚ÇÄ
    intro N hN
    rw [dist_comm, dist_eq_norm]
    exact (hN‚ÇÄ x (hk n) N hN).trans_lt lt_e

  -- Show that is has measure zero.
  have Emeasure : volume E ‚â§ 0 := by
    have : ‚àÄ k, volume E ‚â§ Œ¥' k := by
      intro k
      apply le_trans' (EŒ¥measure k)
      apply measure_mono
      apply Set.iInter_subset
    exact ge_of_tendsto' Œ¥'conv this

  -- Use results to prove the statement.
  rw [ae_restrict_iff' measurableSet_Icc]
  apply le_antisymm _ (zero_le _)
  apply le_trans' Emeasure
  apply measure_mono
  intro x hx
  simp only [Set.mem_compl_iff, Set.mem_setOf_eq, Classical.not_imp] at hx
  by_contra h
  exact hx.2 (hE x ‚ü®hx.1, h‚ü©)

section
open Pointwise

--TODO: might be generalized
lemma Function.Periodic.ae_of_ae_restrict {T : ‚Ñù} (hT : 0 < T) {a : ‚Ñù} {P : (x : ‚Ñù) ‚Üí Prop}
    (hP : Function.Periodic P T)
    (h : ‚àÄ·µê x ‚àÇvolume.restrict (Set.Ico a (a + T)), P x) : ‚àÄ·µê x, P x := by
  rw [ae_restrict_iff' measurableSet_Ico, ae_iff] at h
  set E_interval := {x | ¬¨(x ‚àà Set.Ico a (a + T) ‚Üí P x)} with E_interval_def
  -- Define exceptional set as countable union of translations of the exceptional set on the interval
  set E := ‚ãÉ (k : ‚Ñ§), k ‚Ä¢ T +·µ• E_interval with Edef
  have hE : E = {a | ¬¨P a} := by
    ext x
    rw [Set.mem_iUnion]
    constructor
    ¬∑ intro h
      rcases h with ‚ü®k, hk‚ü©
      rw [Set.mem_vadd_set_iff_neg_vadd_mem, vadd_eq_add, ‚Üê sub_eq_neg_add, E_interval_def] at hk
      simp only [Classical.not_imp, Set.mem_setOf_eq, hP.sub_zsmul_eq k] at hk
      exact hk.2
    ¬∑ dsimp
      rcases (hP.exists_mem_Ico' hT x a) with ‚ü®n, hn, hxn‚ü©
      rw [hxn]
      refine fun h ‚Ü¶ ‚ü®n, ?_‚ü©
      rw [Set.mem_vadd_set_iff_neg_vadd_mem, vadd_eq_add, ‚Üê sub_eq_neg_add, E_interval_def]
      simp only [Classical.not_imp, Set.mem_setOf_eq]
      exact ‚ü®hn, h‚ü©
  -- The union still has measure zero
  have Emeasure : volume E = 0 := by
    rw [Edef, measure_iUnion_null]
    refine fun k ‚Ü¶ measure_vadd_null h ..
  rw [ae_iff, ‚Üê hE]
  exact Emeasure

end

/- Carleson's theorem asserting a.e. convergence of the
partial Fourier sums for continous functions. -/
theorem classical_carleson {f : ‚Ñù ‚Üí ‚ÑÇ} (cont_f : Continuous f)
    (periodic_f : f.Periodic (2 * œÄ)) :
    ‚àÄ·µê x, Tendsto (S_ ¬∑ f x) atTop (ùìù (f x)) := by
  -- Reduce to a.e. convergence on [0,2œÄ]
  apply @Function.Periodic.ae_of_ae_restrict _ Real.two_pi_pos 0
  ¬∑ rw [Function.Periodic]
    intro x
    conv => pattern S_ _ _ _; rw [partialFourierSum_periodic]
    conv => pattern f _; rw [periodic_f]
  apply ae_restrict_of_ae_eq_of_ae_restrict Ico_ae_eq_Icc.symm
  rw [zero_add]
  -- Show a.e. convergence on [0,2œÄ]
  exact carleson_interval cont_f periodic_f


end
