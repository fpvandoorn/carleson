import Carleson.Antichain.AntichainOperator

macro_rules | `(tactic |gcongr_discharger) => `(tactic | with_reducible assumption)

macro_rules | `(tactic |gcongr_discharger) => `(tactic | with_reducible assumption)

noncomputable section

open scoped ENNReal NNReal ShortVariables

open MeasureTheory Metric Set

namespace Antichain

variable {X : Type*} {a : â„•} {q : â„} {K : X â†’ X â†’ â„‚} {Ïƒâ‚ Ïƒâ‚‚ : X â†’ â„¤} {F G : Set X}
  [MetricSpace X] [ProofData a q K Ïƒâ‚ Ïƒâ‚‚ F G] [TileStructure Q D Îº S o]

-- Lemma 6.3.1
-- hp is eq. 6.3.1, hp' is eq. 6.3.2.
lemma tile_reach (ha : 4 â‰¤ a) {Ï‘ : Î˜ X} {N : â„•} {p p' : ğ”“ X} (hp : dist_(p) (ğ’¬ p) Ï‘ â‰¤ 2^N)
    (hp' : dist_(p') (ğ’¬ p') Ï‘ â‰¤ 2^N) (hI : ğ“˜ p â‰¤ ğ“˜ p') (hs : ğ”° p < ğ”° p') :
    smul (2^(N + 2)) p â‰¤ smul (2^(N + 2)) p' := by
  -- 6.3.4
  have hp2 : dist_(p) Ï‘ (ğ’¬ p') â‰¤ 2^N := by
    rw [dist_comm]
    exact le_trans (Grid.dist_mono hI) hp'
  -- 6.3.5
  have hp'2 : dist_(p) (ğ’¬ p) (ğ’¬ p') â‰¤ 2^(N + 1) :=
    calc dist_(p) (ğ’¬ p) (ğ’¬ p')
      _ â‰¤ dist_(p) (ğ’¬ p) Ï‘ + dist_(p) Ï‘ (ğ’¬ p') := dist_triangle _ _ _
      _ â‰¤ 2^N + 2^N := add_le_add hp hp2
      _ = 2^(N + 1) := by ring
  -- Start proof of 6.3.3.
  simp only [TileLike.le_def, smul_fst, smul_snd]
  refine âŸ¨hI, fun o' ho' â†¦ ?_âŸ© -- o' is Ï‘' in blueprint, ho' is 6.3.6.
  -- 6.3.7
  have hlt : dist_{ğ”  p', 8 * D^ğ”° p'} (ğ’¬ p') o' < 2^(5*a + N + 2) := by
    have hle : dist_{ğ”  p', 8 * D^ğ”° p'} (ğ’¬ p') o' â‰¤ (defaultA a) ^ 5 * dist_(p') (ğ’¬ p') o' := by
      have hpos : (0 : â„) < D^ğ”° p'/4 := by
        rw [div_eq_mul_one_div, mul_comm]
        apply mul_defaultD_pow_pos _ (by linarith)
      have h8 : (8 : â„) * D^ğ”° p' = 2^5 * (D^ğ”° p'/4) := by ring
      rw [h8]
      exact cdist_le_iterate hpos (ğ’¬ p') o' 5
    apply lt_of_le_of_lt hle
    simp only [defaultA, add_assoc]
    rw [pow_add, Nat.cast_pow, Nat.cast_ofNat, â† pow_mul, mul_comm a, dist_comm]
    gcongr
    exact ho'
  -- 6.3.8
  have hin : ğ”  p âˆˆ ball (ğ”  p') (4 * D^ğ”° p') := Grid_subset_ball (hI.1 Grid.c_mem_Grid)
  -- 6.3.9
  have hball_le : ball (ğ”  p) (4 * D^ğ”° p') âŠ† ball (ğ”  p') (8 * D^ğ”° p') := by
    intro x hx
    rw [mem_ball] at hx hin âŠ¢
    calc dist x (ğ”  p')
      _ â‰¤ dist x (ğ”  p)  + dist (ğ”  p) (ğ”  p') := dist_triangle _ _ _
      _ < 4 * â†‘D ^ ğ”° p' + 4 * â†‘D ^ ğ”° p' := add_lt_add hx hin
      _ = 8 * â†‘D ^ ğ”° p' := by ring
  -- 6.3.10
  have hlt2 : dist_{ğ”  p, 4 * D^ğ”° p'} (ğ’¬ p') o' < 2^(5*a + N + 2) :=
    lt_of_le_of_lt (cdist_mono hball_le) hlt
  -- 6.3.11
  have hlt3 : dist_{ğ”  p, 2^((2 : â„¤) - 5*a^2 - 2*a) * D^ğ”° p'} (ğ’¬ p') o' < 2^N := by
    have hle : 2 ^ ((5 : â„¤)*a + 2) * dist_{ğ”  p, 2^((2 : â„¤) - 5*a^2 - 2*a) * D^ğ”° p'} (ğ’¬ p') o' â‰¤
        dist_{ğ”  p, 4 * D^ğ”° p'} (ğ’¬ p') o' := by
      have heq : (defaultA a : â„) ^ ((5 : â„¤)*a + 2) * 2^((2 : â„¤) - 5*a^2 - 2*a) = 4 := by
        simp only [defaultA, Nat.cast_pow, Nat.cast_ofNat, â† zpow_natCast, â† zpow_mul]
        rw [â† zpow_addâ‚€ two_ne_zero]
        ring_nf
      rw [â† heq, mul_assoc]
      exact le_cdist_iterate (by positivity) (ğ’¬ p') o' (5*a + 2)
    rw [â† le_div_iffâ‚€' (by positivity), div_eq_mul_inv, â† zpow_neg, neg_add, â† neg_mul,
      â† sub_eq_add_neg, mul_comm _ ((2 : â„) ^ _)] at hle
    calc dist_{ğ”  p, 2^((2 : â„¤) - 5*a^2 - 2*a) * D^ğ”° p'} (ğ’¬ p') o'
      _ â‰¤ 2^(-(5 : â„¤)*a - 2) * dist_{ğ”  p, 4 * D^ğ”° p'} (ğ’¬ p') o' := hle
      _ < 2^(-(5 : â„¤)*a - 2) * 2^(5*a + N + 2) := (mul_lt_mul_left (by positivity)).mpr hlt2
      _ = 2^N := by
        rw [â† zpow_natCast, â† zpow_addâ‚€ two_ne_zero]
        simp only [Int.reduceNeg, neg_mul, Nat.cast_add, Nat.cast_mul, Nat.cast_ofNat,
          sub_add_add_cancel, neg_add_cancel_left, zpow_natCast]
  -- 6.3.12
  have hp'3 : dist_(p) (ğ’¬ p') o' < 2^N := by
    apply lt_of_le_of_lt (cdist_mono _) hlt3
    gcongr
    rw [div_le_iffâ‚€ (by positivity)]
    rw [mul_comm, â† mul_assoc]
    calc (D : â„) ^ ğ”° p
      _ = 1 * (D : â„) ^ ğ”° p := by rw [one_mul]
      _ â‰¤ 4 * 2 ^ (2 - 5 * (a : â„¤) ^ 2 - 2 * â†‘a) * D * D ^ ğ”° p := by
        have h4 : (4 : â„) = 2^(2 : â„¤) := by ring
        apply mul_le_mul _ (le_refl _) (by positivity) (by positivity)
        Â· have h12 : (1 : â„) â‰¤ 2 := one_le_two
          simp only [defaultD, Nat.cast_pow, Nat.cast_ofNat]
          rw [h4, â† zpow_natCast, â† zpow_addâ‚€ two_ne_zero, â† zpow_addâ‚€ two_ne_zero,
            â† zpow_zero (2 : â„)]
          rw [Nat.cast_mul, Nat.cast_ofNat, Nat.cast_pow]
          gcongr --uses h12
          have : (2 : â„)^a = 2^(a : â„¤) := by rw [@zpow_natCast]
          ring_nf
          nlinarith
      _ = (4 * 2 ^ (2 - 5 * (a : â„¤)  ^ 2 - 2 * â†‘a)) * (D * D ^ ğ”° p) := by ring
      _ â‰¤ 4 * 2 ^ (2 - 5 * (a : â„¤)  ^ 2 - 2 * â†‘a) * D ^ ğ”° p' := by
        have h1D : 1 â‰¤ (D : â„) := one_le_D
        nth_rewrite 1 [mul_le_mul_left (by positivity), â† zpow_one (D : â„),
          â† zpow_addâ‚€ (ne_of_gt (defaultD_pos _))]
        gcongr
        rw [add_comm]
        exact hs
  -- 6.3.13 (and 6.3.3.)
  have h34 : (3 : â„) < 4 := by linarith
  calc dist_(p) o' (ğ’¬ p)
    _ = dist_(p) (ğ’¬ p) o' := by rw [dist_comm]
    _ â‰¤ dist_(p) (ğ’¬ p) (ğ’¬ p') + dist_(p) (ğ’¬ p') o' := dist_triangle _ _ _
    _ < 2^(N + 1) + 2^N := add_lt_add_of_le_of_lt hp'2 hp'3
    _ < 2^(N + 2) := by ring_nf; gcongr -- uses h34
  -- 6.3.14 -- Not needed


-- Def 6.3.15
def ğ”„_aux (ğ”„ : Finset (ğ”“ X)) (Ï‘ : Î˜ X) (N : â„•) : Finset (ğ”“ X) :=
  {p âˆˆ ğ”„ | 1 + dist_(p) (ğ’¬ p) Ï‘ âˆˆ Icc (2^N) (2^(N+1))}

open Classical Metric

-- Lemma 6.3.2
lemma stack_density (ğ”„ : Finset (ğ”“ X)) (Ï‘ : Î˜ X) (N : â„•) (L : Grid X) :
    âˆ‘ (p âˆˆ {p âˆˆ (ğ”„_aux ğ”„ Ï‘ N) | ğ“˜ p = L}), volume (E p âˆ© G) â‰¤
      2^(a * (N + 5)) * densâ‚ (ğ”„ : Set (ğ”“ X)) * volume (L : Set X) := sorry

-- We prove inclusion 6.3.25 for every `p âˆˆ (ğ”„_aux ğ”„ Ï‘ N)` with `ğ”° p' < ğ”° p` such that
-- `(ğ“˜ p : Set X) âˆ© (ğ“˜ p') â‰  âˆ…`.
-- p' is ğ”­_Ï‘ in the blueprint
lemma Ep_inter_G_inter_Ip'_subset_E2 (ha : 4 â‰¤ a) {ğ”„ : Finset (ğ”“ X)} (Ï‘ : Î˜ X) (N : â„•)
    {p p' : ğ”“ X} (hpin : p âˆˆ (ğ”„_aux ğ”„ Ï‘ N)) (hp' : Ï‘ âˆˆ Î© p') (hs : ğ”° p' < ğ”° p)
    (hğ“˜ : ((ğ“˜ p' : Set X) âˆ© (ğ“˜ p)).Nonempty) :
    E p âˆ© G âˆ© â†‘(ğ“˜ p') âŠ† Eâ‚‚ (2^(N + 3)) p' := by
  have hle : ğ“˜ p' â‰¤ ğ“˜ p := âŸ¨Or.resolve_right (fundamental_dyadic (le_of_lt hs))
    (not_disjoint_iff_nonempty_inter.mpr hğ“˜), le_of_lt hsâŸ©
  -- 6.3.22
  have hÏ‘aux : Ï‘ âˆˆ ball_(p') (ğ’¬ p') 1 := subset_cball hp'
  have hÏ‘in' : dist_(p') (ğ’¬ p') Ï‘ < ((2 : â„)^(N + 1)) := by
    have h12 : (1 : â„) < 2 := one_lt_two
    have h0N : 0 < N + 1 := Nat.zero_lt_succ N
    simp only [mem_ball'] at hÏ‘aux
    apply lt_trans hÏ‘aux
    nth_rewrite 1 [â† pow_zero 2]
    gcongr -- uses h12, h0N
  -- 6.3.23
  have hÏ‘in : dist_(p) (ğ’¬ p) Ï‘ < ((2 : â„)^(N + 1)) := by
    simp only [ğ”„_aux, Finset.mem_filter] at hpin
    exact lt_of_lt_of_le (lt_one_add (dist_(p) (ğ’¬ p) Ï‘)) hpin.2.2
  -- 6.3.24
  have hsmul_le : smul (2 ^ (N + 3)) p' â‰¤ smul (2 ^ (N + 3)) p :=
    tile_reach ha (le_of_lt hÏ‘in') (le_of_lt hÏ‘in) hle hs
  -- NOTE: TileLike.toSet is not a mono.
  -- 6.3.25
  have hss : E p âˆ© G âˆ© â†‘(ğ“˜ p') âŠ† Eâ‚‚ (2^(N + 3)) p' := by
    simp only [TileLike.le_def, smul_snd] at hsmul_le
    simp only [E, Eâ‚‚, TileLike.toSet, smul_fst, smul_snd, subset_inter_iff, inter_subset_right,
      true_and]
    constructor
    Â· rw [inter_assoc, inter_comm, inter_assoc]
      exact inter_subset_left
    Â· have h1N : (1 : â„) â‰¤ 2 ^ (N + 3) := by exact_mod_cast Nat.one_le_two_pow
      intro x hx
      apply mem_of_subset_of_mem (le_trans (le_trans subset_cball (ball_subset_ball h1N))
        hsmul_le.2) hx.1.1.2.1
  exact hss

-- Lemma 6.3.3
-- p' is ğ”­_Ï‘ in the blueprint
lemma local_antichain_density (ha : 4 â‰¤ a) {ğ”„ : Finset (ğ”“ X)}
    (hğ”„ : IsAntichain (Â·â‰¤Â·) (ğ”„ : Set (ğ”“ X))) (Ï‘ : Î˜ X) (N : â„•) {p' : ğ”“ X} (hp' : Ï‘ âˆˆ Î© p') :
    âˆ‘ (p âˆˆ {p âˆˆ (ğ”„_aux ğ”„ Ï‘ N) | ğ”° p' < ğ”° p}), volume (E p âˆ© G âˆ© ğ“˜ p') â‰¤
      volume (Eâ‚‚ (2 ^ (N + 3)) p') := by
  rw [â† MeasureTheory.measure_biUnion_finset _
    (fun _ _ â†¦  MeasurableSet.inter (measurableSet_E.inter measurableSet_G) coeGrid_measurable)]
  Â· apply measure_mono
    simp only [ Finset.mem_filter, iUnion_subset_iff, and_imp]
    intro p hp hs
    by_cases hğ“˜ : ((ğ“˜ p' : Set X) âˆ© â†‘(ğ“˜ p)).Nonempty
    Â· exact Ep_inter_G_inter_Ip'_subset_E2 ha Ï‘ N hp hp' hs hğ“˜
    Â· rw [not_nonempty_iff_eq_empty] at hğ“˜
      have hemp : (ğ“˜ p' : Set X) âˆ© E p = âˆ… :=
        eq_empty_of_subset_empty (hğ“˜ â–¸ inter_subset_inter_right _
          (sep_subset â†‘(ğ“˜ p) fun x â†¦ Q x âˆˆ Î© p âˆ§ ğ”° p âˆˆ Icc (Ïƒâ‚ x) (Ïƒâ‚‚ x)))
      rw [inter_comm, â† inter_assoc, hemp, empty_inter]
      exact empty_subset _
  Â· simp only [Finset.coe_filter]
    intro q hq q' hq' hqq'
    simp only [ğ”„_aux, Finset.mem_filter, mem_setOf_eq] at hq hq'
    have hE : E q âˆ© E q' = âŠ¥ := by
      rw [bot_eq_empty]
      contrapose! hqq'
      exact E_disjoint hğ”„ hq.1.1 hq'.1.1  hqq'
    simp only [disjoint_iff, eq_bot_iff]
    rw [â† hE]
    simp only [inf_eq_inter, le_eq_subset, subset_inter_iff]
    constructor
    Â· simp only [inter_assoc, inter_subset_left]
    Â· rw [inter_comm]
      simp only [inter_assoc, inter_subset_left]

def C_6_3_4 (a N : â„•) : â„â‰¥0 := 2^(101*a^3 + N*a)

-- Lemma 6.3.4
lemma global_antichain_density (ğ”„ : Finset (ğ”“ X)) (Ï‘ : Î˜ X) (N : â„•) :
    âˆ‘ (p âˆˆ ğ”„_aux ğ”„ Ï‘ N), volume (E p âˆ© G) â‰¤
      (C_6_3_4 a N) * densâ‚ (ğ”„ : Set (ğ”“ X)) * volume (â‹ƒ (p âˆˆ ğ”„), (ğ“˜ p : Set X)) := sorry

-- p in Lemma 6.1.6
private def p (a : â„•) := 4 * a^2

def C_6_1_6 (a : â„•) : â„â‰¥0 := 2 ^ (104 * a)

-- Lemma 6.1.6
-- Note: p' is introduced in the statement in the blueprint but not used. There may be a typo.
lemma tile_count {ğ”„ ğ”„' : Finset (ğ”“ X)} (h_le : ğ”„' âŠ† ğ”„) (Ï‘ : Î˜ X) :
    eLpNorm (âˆ‘ ğ”­ âˆˆ ğ”„', (1 + dist_(ğ”­) (ğ’¬ ğ”­) Ï‘) ^ ((-1 : â„)/(2*a^2 + a^3)) â€¢
      ((E ğ”­).indicator 1) *  (G.indicator (1 : X â†’ â„))) (p a) volume â‰¤
      (C_6_1_6 a) * densâ‚ (ğ”„ : Set (ğ”“ X)) ^ ((1 : â„)/(p a)) *
        (volume (â‹ƒ (p âˆˆ ğ”„), (ğ“˜ p : Set X))) ^ ((1 : â„)/(p a)) := by
  sorry

end Antichain
