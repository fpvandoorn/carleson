import Carleson.ToMathlib.CoverByBalls
import Mathlib.Data.Real.StarOrdered
import Mathlib.MeasureTheory.Measure.Lebesgue.VolumeOfBalls
import Mathlib.Order.CompletePartialOrder

open MeasureTheory Measure NNReal ENNReal Metric Filter Topology TopologicalSpace
noncomputable section

section Doubling

/-- The blow-up factor of repeatedly increasing the size of balls. -/
def As (A : ‚Ñù‚â•0) (s : ‚Ñù) : ‚Ñù‚â•0 := A ^ ‚åàReal.logb 2 s‚åâ‚Çä

lemma le_pow_natCeil_logb {b x : ‚Ñù} (hb : 1 < b) (hx : 0 < x) :
    x ‚â§ b ^ ‚åàReal.logb b x‚åâ‚Çä := by
  calc
    x = b ^ Real.logb b x := by rw [Real.rpow_logb (by linarith) hb.ne' hx]
    _ ‚â§ b ^ ‚åàReal.logb b x‚åâ‚Çä := by
      rw [‚Üê Real.rpow_natCast]
      gcongr
      ¬∑ exact hb.le
      apply Nat.le_ceil

end Doubling

namespace MeasureTheory

/-- A doubling measure is a measure on a metric space with the condition that doubling
the radius of a ball only increases the volume by a constant factor, independent of the ball. -/
class Measure.IsDoubling {X : Type*} [MeasurableSpace X] [PseudoMetricSpace X]
    (Œº : Measure X) (A : outParam ‚Ñù‚â•0) : Prop where
  measure_ball_two_le_same : ‚àÄ (x : X) r, Œº (ball x (2 * r)) ‚â§ A * Œº (ball x r)
export IsDoubling (measure_ball_two_le_same)

section PseudoMetric

variable {X : Type*} [PseudoMetricSpace X]

lemma ball_subset_ball_of_le {x x' : X} {r r' : ‚Ñù}
    (hr : dist x x' + r' ‚â§ r) : ball x' r' ‚äÜ ball x r := by
  rw [dist_comm, add_comm] at hr
  exact ball_subset_ball' hr

lemma dist_lt_of_not_disjoint_ball {x x' : X} {r r' : ‚Ñù} (hd : ¬¨Disjoint (ball x r) (ball x' r')) :
    dist x x' < r + r' := by
  obtain ‚ü®y, dy‚ÇÅ, dy‚ÇÇ‚ü© := Set.not_disjoint_iff.mp hd
  rw [mem_ball] at dy‚ÇÅ dy‚ÇÇ
  calc
    _ ‚â§ dist y x + dist y x' := dist_triangle_left ..
    _ < _ := by gcongr

lemma eq_zero_of_isDoubling_zero [MeasurableSpace X] (Œº : Measure X) [hŒº : Œº.IsDoubling 0] :
    Œº = 0 := by
  rcases isEmpty_or_nonempty X with hX | ‚ü®‚ü®x‚ü©‚ü©
  ¬∑ exact eq_zero_of_isEmpty Œº
  have M (r : ‚Ñù) : Œº (ball x r) = 0 := by
    have := hŒº.measure_ball_two_le_same x (r / 2)
    simp only [ENNReal.coe_zero, zero_mul, nonpos_iff_eq_zero] at this
    convert this
    ring
  rw [‚Üê measure_univ_eq_zero, ‚Üê iUnion_ball_nat x]
  exact measure_iUnion_null_iff.mpr fun i ‚Ü¶ M ‚Üëi

variable {A : ‚Ñù‚â•0} [MeasurableSpace X] {Œº : Measure X} [Œº.IsDoubling A]

variable (Œº) in
lemma eq_zero_of_isDoubling_lt_one [ProperSpace X] [IsFiniteMeasureOnCompacts Œº] (hA : A < 1) :
    Œº = 0 := by
  rcases isEmpty_or_nonempty X with hX | ‚ü®‚ü®x‚ü©‚ü©
  ¬∑ simp [eq_zero_of_isEmpty Œº]
  have M (r : ‚Ñù) (hr : 0 ‚â§ r) : Œº (ball x r) = 0 := by
    have I : Œº (ball x r) ‚â§ A * Œº (ball x r) := calc
      _ = Œº (ball x (2 * (r / 2))) := by
        have : 2 * (r / 2) = r := by ring
        simp [this]
      _ ‚â§ A * Œº (ball x (r / 2)) := by
        apply measure_ball_two_le_same (Œº := Œº)
      _ ‚â§ A * Œº (ball x r) := by gcongr; linarith
    by_contra H
    have : Œº (ball x r) < 1 * Œº (ball x r) := by
      apply I.trans_lt (ENNReal.mul_lt_mul_right' H measure_ball_lt_top.ne (mod_cast hA))
    simp at this
  rw [‚Üê measure_univ_eq_zero, ‚Üê iUnion_ball_nat x]
  exact measure_iUnion_null_iff.mpr fun i ‚Ü¶ M ‚Üëi (Nat.cast_nonneg' i)

lemma IsDoubling.mono {A'} (h : A ‚â§ A') : IsDoubling Œº A' where
  measure_ball_two_le_same := by
    intro x r
    calc Œº (Metric.ball x (2 * r))
      _ ‚â§ A * Œº (Metric.ball x r) := measure_ball_two_le_same _ _
      _ ‚â§ A' * Œº (Metric.ball x r) := by gcongr

lemma measure_ball_two_le_same_iterate (x : X) (r : ‚Ñù) (n : ‚Ñï) :
    Œº (ball x ((2 ^ n) * r)) ‚â§ A ^ n * Œº (ball x r) := by
  induction n with
  | zero => simp
  | succ m ih =>
      simp_rw [add_comm m 1, pow_add, pow_one, mul_assoc]
      exact le_trans (measure_ball_two_le_same x _) (mul_le_mul_left' ih A)

variable [ProperSpace X] [IsFiniteMeasureOnCompacts Œº]

lemma measure_real_ball_two_le_same (x : X) (r : ‚Ñù) :
    Œº.real (ball x (2 * r)) ‚â§ A * Œº.real (ball x r) := by
  simp_rw [Measure.real, ‚Üê ENNReal.coe_toReal, ‚Üê toReal_mul]
  gcongr
  ¬∑ exact ENNReal.mul_ne_top coe_ne_top measure_ball_lt_top.ne
  ¬∑ exact measure_ball_two_le_same x r

lemma measure_real_ball_two_le_same_iterate (x : X) (r : ‚Ñù) (n : ‚Ñï) :
    Œº.real (ball x ((2 ^ n) * r)) ‚â§ A ^ n * Œº.real (ball x r) := by
  induction n with
  | zero => simp
  | succ m ih =>
      simp_rw [add_comm m 1, pow_add, pow_one, mul_assoc]
      exact le_trans (measure_real_ball_two_le_same x _) (by gcongr)

lemma measure_real_ball_pos [Œº.IsOpenPosMeasure] (x : X) {r : ‚Ñù} (hr : 0 < r) :
    0 < Œº.real (ball x r) :=
  toReal_pos ((measure_ball_pos Œº x hr).ne.symm) (measure_ball_lt_top.ne)

variable (Œº) in
lemma one_le_A [Nonempty X] [Œº.IsOpenPosMeasure] : 1 ‚â§ A := by
  obtain ‚ü®x‚ü© := ‚ÄπNonempty X‚Ä∫
  have : 0 < Œº.real (ball x 1) := measure_real_ball_pos x (by linarith)
  apply le_of_mul_le_mul_right _ this
  simp only [val_eq_coe, NNReal.coe_one, one_mul]
  calc
    Œº.real (ball x 1)
      ‚â§ Œº.real (ball x (2 * 1)) := by
        rw [mul_one]
        exact ENNReal.toReal_mono (measure_ball_lt_top.ne)
          (Œº.mono (ball_subset_ball (by linarith)))
    _ ‚â§ A * Œº.real (ball x 1) := measure_real_ball_two_le_same x 1

variable (Œº) in
lemma A_pos [Nonempty X] [Œº.IsOpenPosMeasure] : 0 < A := by
  calc
    0 < 1 := by norm_num
    _ ‚â§ A := one_le_A Œº

variable (Œº) in
lemma A_pos' [Nonempty X] [Œº.IsOpenPosMeasure] : 0 < (A : ‚Ñù‚â•0‚àû) := by
  rw [ENNReal.coe_pos]
  exact A_pos Œº

-- lemma measure_ball_two_le_same' (x:X) (r:‚Ñù): Œº (ball x (2 * r)) ‚â§ A * Œº (ball x r) := by
--   have hv1 : Œº (ball x (2 * r)) < ‚ä§ := measure_ball_lt_top
--   have hv2 : Œº (ball x r) < ‚ä§ := measure_ball_lt_top
--   rw [‚Üê ENNReal.ofReal_toReal hv1.ne, ‚Üê ENNReal.ofReal_toReal hv2.ne, ‚Üê ENNReal.ofReal_toReal
--     (coe_ne_top : (A : ‚Ñù‚â•0‚àû) ‚â† ‚ä§), ‚Üê ENNReal.ofReal_mul (by exact toReal_nonneg)]
--   exact ENNReal.ofReal_le_ofReal (measure_ball_two_le_same x r)

lemma measure_ball_four_le_same (x : X) (r : ‚Ñù) :
    Œº.real (ball x (4 * r)) ‚â§ A ^ 2 * Œº.real (ball x r) := by
  calc Œº.real (ball x (4 * r))
      = Œº.real (ball x (2 * (2 * r))) := by ring_nf
    _ ‚â§ A * Œº.real (ball x (2 * r)) := measure_real_ball_two_le_same ..
    _ ‚â§ A * (A * Œº.real (ball x r)) := mul_le_mul_of_nonneg_left
      (measure_real_ball_two_le_same ..) (zero_le_coe)
    _ = A ^ 2 * Œº.real (ball x r) := by ring_nf

lemma measure_ball_ne_top (x : X) (r : ‚Ñù) : Œº (ball x r) ‚â† ‚àû := measure_ball_lt_top.ne

lemma measure_ball_four_le_same' (x : X) (r : ‚Ñù) :
    Œº (ball x (4 * r)) ‚â§ A ^ 2 * Œº (ball x r) := by
  have hfactor : (A ^ 2 : ‚Ñù‚â•0‚àû) ‚â† ‚ä§ := ne_of_beq_false rfl
  rw [‚Üê ENNReal.ofReal_toReal (measure_ball_ne_top x (4 * r)),
    ‚Üê ENNReal.ofReal_toReal (measure_ball_ne_top x r), ‚Üê ENNReal.ofReal_toReal hfactor,
    ‚Üê ENNReal.ofReal_mul]
  ¬∑ exact ENNReal.ofReal_le_ofReal <| measure_ball_four_le_same x r
  ¬∑ simp

attribute [aesop (rule_sets := [finiteness]) safe apply] measure_ball_ne_top

variable (Œº) in
lemma As_pos [Nonempty X] [Œº.IsOpenPosMeasure] (s : ‚Ñù) : 0 < As A s :=
  pow_pos (A_pos Œº) ‚åàReal.logb 2 s‚åâ‚Çä

variable (Œº) in
lemma As_pos' [Nonempty X] [Œº.IsOpenPosMeasure] (s : ‚Ñù) : 0 < (As A s : ‚Ñù‚â•0‚àû) := by
  rw [ENNReal.coe_pos]; exact As_pos Œº s

/- Proof sketch: First do for powers of 2 by induction, then use monotonicity. -/
omit [ProperSpace X] [IsFiniteMeasureOnCompacts Œº] in
lemma measure_ball_le_same' (x : X) {r s r' : ‚Ñù} (hsp : 0 < s) (hs : r' ‚â§ s * r) :
    Œº (ball x r') ‚â§ As A s * Œº (ball x r) := by
  /-If the large ball is empty, then they all are-/
  if hr: r < 0 then
    have hr' : r' < 0 := by
      calc r' ‚â§ s * r := hs
      _ < 0 := mul_neg_of_pos_of_neg hsp hr
    rw [ball_eq_empty.mpr hr.le,ball_eq_empty.mpr hr'.le]
    simp only [measure_empty, mul_zero, le_refl]
  else
  push_neg at hr
  /- Show inclusion in larger ball -/
  have haux : s * r ‚â§ 2 ^ ‚åàReal.logb 2 s‚åâ‚Çä * r := by
    gcongr
    apply le_pow_natCeil_logb (by norm_num) hsp
  have h1 : ball x r' ‚äÜ ball x (2 ^ ‚åàReal.logb 2 s‚åâ‚Çä * r) :=
    ball_subset_ball <| hs.trans haux
  /- Apply result for power of two to slightly larger ball -/
  calc Œº (ball x r')
      ‚â§ Œº (ball x (2 ^ ‚åàReal.logb 2 s‚åâ‚Çä * r)) := by gcongr
    _ ‚â§ As A s * Œº (ball x r) := measure_ball_two_le_same_iterate x r _

lemma measure_ball_le_same (x : X) {r s r' : ‚Ñù} (hsp : 0 < s) (hs : r' ‚â§ s * r) :
    Œº.real (ball x r') ‚â§ As A s * Œº.real (ball x r) := by
  have hz := measure_ball_le_same' (Œº := Œº) x hsp hs
  have hbr': Œº (ball x r') ‚â† ‚ä§ := measure_ball_ne_top x r'
  have hbr: Œº (ball x r) ‚â† ‚ä§ := measure_ball_ne_top x r
  have hAs : (As A s: ‚Ñù‚â•0‚àû) ‚â† ‚ä§ := coe_ne_top
  rw [‚Üê ENNReal.ofReal_toReal hbr,‚Üê ENNReal.ofReal_toReal hbr',
    ‚Üê ENNReal.ofReal_toReal hAs, ‚Üê ENNReal.ofReal_mul] at hz
  ¬∑ simp only [coe_toReal] at hz
    rw [‚Üê ENNReal.ofReal_le_ofReal_iff]
    ¬∑ exact hz
    positivity
  ¬∑ simp only [coe_toReal, zero_le_coe]

/-- Version of `measure_ball_le_same` without ceiling function. -/
lemma measure_ball_le_same'' (x : X) {r t : ‚Ñù} (ht : 0 < t) (h't : t ‚â§ 1) :
    Œº.real (ball x r) ‚â§ A * t ^ (- Real.logb 2 A) * Œº.real (ball x (t * r)) := by
  rcases lt_or_le A 1 with hA | hA
  ¬∑ simp [eq_zero_of_isDoubling_lt_one Œº hA]
  have : r = t‚Åª¬π * (t * r) := (eq_inv_mul_iff_mul_eq‚ÇÄ ht.ne').mpr rfl
  apply (measure_ball_le_same x (inv_pos_of_pos ht) this.le).trans
  gcongr
  simp only [As, Real.logb_inv, NNReal.coe_pow]
  have : t = 2 ^ (Real.logb 2 t) := by rw [Real.rpow_logb (by norm_num) (by norm_num) ht]
  conv_rhs => rw [this]
  have : (A : ‚Ñù) = 2 ^ (Real.logb 2 (A : ‚Ñù)) := by
    rw [Real.rpow_logb (by norm_num) (by norm_num)]
    apply zero_lt_one.trans_le hA
  nth_rewrite 1 2 [this]
  rw [‚Üê Real.rpow_mul zero_le_two, ‚Üê Real.rpow_natCast, ‚Üê Real.rpow_mul zero_le_two,
    ‚Üê Real.rpow_add zero_lt_two]
  apply (Real.rpow_le_rpow_left_iff one_lt_two).2
  have : (‚åà-Real.logb 2 t‚åâ‚Çä : ‚Ñù) < -Real.logb 2 t + 1 := by
    apply Nat.ceil_lt_add_one
    simp only [Left.nonneg_neg_iff]
    rw [Real.logb_nonpos_iff one_lt_two ht]
    exact h't
  calc
  Real.logb 2 ‚ÜëA * ‚Üë‚åà-Real.logb 2 t‚åâ‚Çä
  _ ‚â§ Real.logb 2 ‚ÜëA * (-Real.logb 2 t + 1) := by
    gcongr
    exact Real.logb_nonneg one_lt_two hA
  _ = _ := by ring

omit [ProperSpace X] [IsFiniteMeasureOnCompacts Œº] in
lemma measure_ball_le_of_dist_le' {x x' : X} {r r' s : ‚Ñù} (hs : 0 < s)
    (h : dist x x' + r' ‚â§ s * r) :
    Œº (ball x' r') ‚â§ As A s * Œº (ball x r) := by
  calc
    Œº (ball x' r')
      ‚â§ Œº (ball x (dist x x' + r')) := by gcongr; exact ball_subset_ball_of_le le_rfl
    _ ‚â§ As A s * Œº (ball x r) := measure_ball_le_same' x hs h

lemma measureNNReal_ball_le_of_dist_le' {x x' : X} {r r' s : ‚Ñù} (hs : 0 < s)
    (h : dist x x' + r' ‚â§ s * r) :
    (Œº (ball x' r')).toNNReal ‚â§ As A s * (Œº (ball x r)).toNNReal := by
  simp only [‚Üê ENNReal.coe_le_coe, coe_mul, ENNReal.coe_toNNReal
    (measure_ball_ne_top x r), ENNReal.coe_toNNReal (measure_ball_ne_top x' r')]
  exact measure_ball_le_of_dist_le' hs h

section

variable {x x': X} {r r' s d : ‚Ñù} (hs : 0 < s)

-- #check (@measure_ball_le_of_dist_le X A _ _ x' x r (2 * r) s s hs hs)

end
/-
-- def Ai (A : ‚Ñù‚â•0) (s : ‚Ñù) : ‚Ñù‚â•0 := As A s -- maybe wrong

-- lemma measure_ball_le_of_subset {x' x : X} {r r' s : ‚Ñù}
--     (hs : r' ‚â§ s * r) (hr : ball x' r ‚äÜ ball x r') :
--     Œº.real (ball x (2 * r)) ‚â§ Ai A s * Œº.real (ball x' r) := by sorry

-- def Ai2 (A : ‚Ñù‚â•0) : ‚Ñù‚â•0 := Ai A 2

-- lemma measure_ball_two_le_of_subset {x' x : X} {r : ‚Ñù} (hr : ball x' r ‚äÜ ball x (2 * r)) :
--     Œº.real (ball x (2 * r)) ‚â§ Ai2 A * Œº.real (ball x' r) :=
--   measure_ball_le_of_subset le_rfl hr

-- def Np (A : ‚Ñù‚â•0) (s : ‚Ñù) : ‚Ñï := ‚åäAs A (s * A + 2‚Åª¬π)‚åã‚Çä -- probably wrong

-- /- Proof sketch: take a ball of radius `r / (2 * A)` around each point in `s`.
-- These are disjoint, and are subsets of `ball x (r * (2 * A + 2‚Åª¬π))`. -/
-- lemma card_le_of_le_dist (x : X) {r r' s : ‚Ñù} (P : Set X) (hs : r' ‚â§ s * r) (hP : P ‚äÜ ball x r')
--   (h2P : ‚àÄ x y, x ‚àà P ‚Üí y ‚àà P ‚Üí x ‚â† y ‚Üí r ‚â§ dist x y) : P.Finite ‚àß Nat.card P ‚â§ Np A s := by sorry

-- /- Proof sketch: take any maximal set `s` of points that are at least distance `r` apart.
-- By the previous lemma, you only need a bounded number of points.
-- -/
-- lemma ballsCoverBalls {r r' s : ‚Ñù} (hs : r' ‚â§ s * r) : BallsCoverBalls X r' r (Np A s) := by
--   sorry

-- /- [Stein, 1.1.3(iv)] -/
-- lemma continuous_measure_ball_inter {U : Set X} (hU : IsOpen U) {Œ¥} (hŒ¥ : 0 < Œ¥) :
--   Continuous fun x ‚Ü¶ Œº.real (ball x Œ¥ ‚à© U) := sorry

-- /- [Stein, 1.1.4] -/
-- lemma continuous_average {E} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {f : X ‚Üí E}
--     (hf : LocallyIntegrable f Œº) {Œ¥ : ‚Ñù} (hŒ¥ : 0 < Œ¥) :
--     Continuous (fun x ‚Ü¶ ‚®ç y in ball x Œ¥, f y ‚àÇŒº) :=
--   sorry

-- /- [Stein, 1.3.1], cor -/
-- lemma tendsto_average_zero {E} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {f : X ‚Üí E}
--     (hf : LocallyIntegrable f Œº) {x : X} :
--     Tendsto (fun Œ¥ ‚Ü¶ ‚®ç y in ball x Œ¥, f y ‚àÇŒº) (ùìù[>] 0) (ùìù (f x)) :=
--   sorry
-/

end PseudoMetric

section Metric

variable {X : Type*} {A : ‚Ñù‚â•0} [MetricSpace X] [MeasurableSpace X]
  {Œº : Measure X} [Œº.IsDoubling A] [IsFiniteMeasureOnCompacts Œº] [ProperSpace X]
  [Nonempty X] [IsOpenPosMeasure Œº] -- only needed sometimes

instance : IsUnifLocDoublingMeasure (Œº : Measure X) where
  exists_measure_closedBall_le_mul'' := by
    use A^2, Set.univ, by simp only [univ_mem]
    simp only [mem_principal]
    use Set.univ
    simp only [Set.subset_univ, Set.inter_self, true_and]
    ext r
    simp only [ENNReal.coe_pow, Set.mem_setOf_eq, Set.mem_univ, iff_true]
    intro x
    letI : Nonempty X := ‚ü®x‚ü©
    if hr : r ‚â§ 0 then
      have cball_eq : closedBall x (2 * r) = closedBall x r:= by
        if hr' : r < 0 then
          have : 2 * r < 0 := by linarith
          rw [closedBall_eq_empty.mpr hr',closedBall_eq_empty.mpr this]
        else
          push_neg at hr'
          have : r = 0 := le_antisymm hr hr'
          rw [this]
          simp only [mul_zero, closedBall_zero]
      rw [cball_eq]
      nth_rw 1 [‚Üê one_mul (Œº (closedBall x r))]
      gcongr
      have : 1 ‚â§ (A:‚Ñù‚â•0‚àû) := by rw [one_le_coe_iff]; exact one_le_A Œº
      rw [‚Üê one_mul 1, pow_two]
      gcongr
    else
    calc
      Œº (closedBall x (2 * r))
        ‚â§ Œº (ball x (2 * (2 * r))) := Œº.mono (closedBall_subset_ball (by linarith))
      _ ‚â§ A * Œº (ball x (2 * r)) := measure_ball_two_le_same x (2 * r)
      _ ‚â§ A * (A * Œº (ball x r)) := mul_le_mul_of_nonneg_left
        (measure_ball_two_le_same x r) (zero_le _)
      _ = ‚Üë(A ^ 2) * Œº (ball x r) := by simp only [pow_two, coe_mul,mul_assoc]
      _ ‚â§ ‚Üë(A ^ 2) * Œº (closedBall x r) := mul_le_mul_of_nonneg_left
        (Œº.mono ball_subset_closedBall) (zero_le ((A ^ 2 : ‚Ñù‚â•0) : ‚Ñù‚â•0‚àû))
end Metric

section Normed

open Module ENNReal

lemma Subsingleton.ball_eq {Œ±} [PseudoMetricSpace Œ±] [Subsingleton Œ±] {x : Œ±} {r : ‚Ñù} :
    ball x r = if r > 0 then {x} else ‚àÖ := by
  ext y; cases Subsingleton.elim x y; simp

instance InnerProductSpace.IsDoubling {E : Type*} [NormedAddCommGroup E]
    [InnerProductSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E] [FiniteDimensional ‚Ñù E] :
    IsDoubling (volume : Measure E) (2 ^ finrank ‚Ñù E) where
  measure_ball_two_le_same x r := by
    obtain hE|hE := subsingleton_or_nontrivial E
    ¬∑ simp_rw [Subsingleton.ball_eq, finrank_zero_of_subsingleton]; simp
    simp_rw [InnerProductSpace.volume_ball, ofReal_mul zero_le_two, ‚Üê ENNReal.rpow_natCast,
      ENNReal.mul_rpow_of_ne_top ofReal_ne_top ofReal_ne_top, ENNReal.rpow_natCast, mul_assoc]
    simp

end Normed

/- # Instances of spaces of homogeneous type -/

/-- A metric space with a measure and a doubling condition.
This is called a "doubling metric measure space" in the blueprint.
`A` will usually be `2 ^ a`.

This class is not Mathlib-ready code, and results moved to Mathlib should be reformulated using
a (locally) doubling measure and more minimal other classes.

Note (F): we currently assume that the space is proper, which we should probably add to the
blueprint.
Remark: `IsUnifLocDoublingMeasure` which is a weaker notion in Mathlib. -/

class DoublingMeasure (X : Type*) (A : outParam ‚Ñù‚â•0) [PseudoMetricSpace X] extends
  MeasureSpace X, ProperSpace X, BorelSpace X,
  Regular (volume : Measure X), IsOpenPosMeasure (volume : Measure X),
  IsDoubling (volume : Measure X) A

variable {X : Type*} {A : ‚Ñù‚â•0} [PseudoMetricSpace X] [DoublingMeasure X A]

-- the following classes hold
-- #synth ProperSpace X
-- #synth LocallyCompactSpace X
-- #synth CompleteSpace X
-- #synth SigmaCompactSpace X
-- #synth SigmaFinite (volume : Measure X)
-- #synth SecondCountableTopology X
-- #synth SeparableSpace X


section MetricSpace
variable {Y : Type*} [MetricSpace Y] [DoublingMeasure Y A]
-- Moreover, the following classes hold if we assume that `Y` is a metric space
-- #synth T4Space Y
-- #synth PolishSpace Y
-- #synth MeasurableSingletonClass Y
end MetricSpace


/-- Monotonicity of doubling measure metric spaces in `A`. -/
@[reducible]
def DoublingMeasure.mono {A'} (h : A ‚â§ A') : DoublingMeasure X A' where
  toIsDoubling := IsDoubling.mono h
  __ := ‚ÄπDoublingMeasure X A‚Ä∫

open Module
/- Preferably we prove that in this form. -/
instance InnerProductSpace.DoublingMeasure
    {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E]
    [MeasurableSpace E] [BorelSpace E] [FiniteDimensional ‚Ñù E] :
    DoublingMeasure E (2 ^ finrank ‚Ñù E) where

/- todo: ‚Ñù^n with nonstandard metric: `dist x y = ‚àë i, |x i - y i| ^ Œ± i` for `Œ± i > 0` -/
end MeasureTheory
