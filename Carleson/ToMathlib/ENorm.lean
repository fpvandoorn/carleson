import Mathlib.Analysis.NormedSpace.IndicatorFunction
import Mathlib.MeasureTheory.Function.EssSup
import Mathlib.MeasureTheory.Function.AEEqFun
import Mathlib.MeasureTheory.Function.SpecialFunctions.Basic

-- these imports are only needed in the `tmp` section
import Mathlib.Topology.MetricSpace.Pseudo.Defs
import Mathlib.MeasureTheory.Covering.Vitali

noncomputable section

open ENNReal NNReal MeasureTheory Function Set

/-- Auxiliary class, endowing a type `Î±` with a function `enorm : Î± â†’ â„â‰¥0âˆ` with notation `â€–xâ€–â‚‘`. -/
@[notation_class]
class ENorm (E : Type*) where
  /-- the `â„â‰¥0âˆ`-valued norm function. -/
  enorm : E â†’ â„â‰¥0âˆ

export ENorm (enorm)

@[inherit_doc]
notation "â€–" e "â€–â‚‘" => enorm e

/-- An enormed monoid is an additive monoid endowed with an enorm. -/
class ENormedAddMonoid (E : Type*) extends ENorm E, AddMonoid E, TopologicalSpace E where
  enorm_zero : âˆ€ x : E, enorm x = 0 â†” x = 0
  enorm_neg : âˆ€ x y : E, x + y = 0 â†’ â€–xâ€–â‚‘ = â€–yâ€–â‚‘ -- this is a silly way to write this
  enorm_triangle : âˆ€ x y : E, â€–x + yâ€–â‚‘ â‰¤ â€–xâ€–â‚‘ + â€–yâ€–â‚‘
  -- the topology is somehow defined by the enorm.

/-- An enormed monoid is an additive monoid endowed with an enorm. -/
class ENormedAddCommMonoid (E : Type*) extends ENormedAddMonoid E, AddCommMonoid E where
  -- the topology is somehow defined by the enorm.

variable {Î± Î±' E Eâ‚ Eâ‚‚ F : Type*} [ENorm F]

instance : ENormedAddCommMonoid â„â‰¥0âˆ where
  enorm := id
  enorm_zero := by simp
  enorm_neg := by simp
  enorm_triangle := by simp
  add_comm := by simp [add_comm]

instance [NNNorm E] : ENorm E where
  enorm := (â€–Â·â€–â‚Š : E â†’ â„â‰¥0âˆ)

lemma enorm_eq_nnnorm [NNNorm E] {x : E} : â€–xâ€–â‚‘ = â€–xâ€–â‚Š := rfl

instance [NormedAddGroup E] : ENormedAddMonoid E where
  enorm_zero := by simp [enorm_eq_nnnorm]
  enorm_neg := by
    simp (config := {contextual := true}) [â† eq_neg_iff_add_eq_zero, enorm_eq_nnnorm]
  enorm_triangle := by simp [enorm_eq_nnnorm, â† ENNReal.coe_add, nnnorm_add_le]

instance [NormedAddCommGroup E] : ENormedAddCommMonoid E where
  add_comm := by simp [add_comm]

namespace MeasureTheory

/-- `(âˆ« â€–f aâ€–^q âˆ‚Î¼) ^ (1/q)`, which is a seminorm on the space of measurable functions for which
this quantity is finite -/
def eLpNorm' {_ : MeasurableSpace Î±} (f : Î± â†’ F) (q : â„) (Î¼ : Measure Î±) : â„â‰¥0âˆ :=
  (âˆ«â» a, â€–f aâ€–â‚‘ ^ q âˆ‚Î¼) ^ (1 / q)

/-- seminorm for `â„’âˆ`, equal to the essential supremum of `â€–fâ€–`. -/
def eLpNormEssSup {_ : MeasurableSpace Î±} (f : Î± â†’ F) (Î¼ : Measure Î±) :=
  essSup (fun x => â€–f xâ€–â‚‘) Î¼

/-- `â„’p` seminorm, equal to `0` for `p=0`, to `(âˆ« â€–f aâ€–^p âˆ‚Î¼) ^ (1/p)` for `0 < p < âˆ` and to
`essSup â€–fâ€– Î¼` for `p = âˆ`. -/
def eLpNorm {_ : MeasurableSpace Î±}
    (f : Î± â†’ F) (p : â„â‰¥0âˆ) (Î¼ : Measure Î± := by volume_tac) : â„â‰¥0âˆ :=
  if p = 0 then 0 else if p = âˆ then eLpNormEssSup f Î¼ else eLpNorm' f (ENNReal.toReal p) Î¼

/-- The property that `f : Î± â†’ E` is a.e. strongly measurable and `(âˆ« â€–f aâ€–^p âˆ‚Î¼)^(1/p)` is finite
if `p < âˆ`, or `essSup f < âˆ` if `p = âˆ`. -/
def Memâ„’p [TopologicalSpace E] [ENorm E] {_ : MeasurableSpace Î±} (f : Î± â†’ E) (p : â„â‰¥0âˆ)
    (Î¼ : Measure Î± := by volume_tac) : Prop :=
  AEStronglyMeasurable f Î¼ âˆ§ eLpNorm f p Î¼ < âˆ

variable [MeasurableSpace Î±] [MeasurableSpace Î±']

/-! # The distribution function `d_f` -/

/-- The distribution function of a function `f`.
Note that unlike the notes, we also define this for `t = âˆ`.
Note: we also want to use this for functions with codomain `â„â‰¥0âˆ`, but for those we just write
`Î¼ { x | t < f x }` -/
def distribution [ENorm E] (f : Î± â†’ E) (t : â„â‰¥0âˆ) (Î¼ : Measure Î±) : â„â‰¥0âˆ :=
  Î¼ { x | t < â€–f xâ€–â‚‘ }

/-- The weak L^p norm of a function, for `p < âˆ` -/
def wnorm' [ENorm E] (f : Î± â†’ E) (p : â„) (Î¼ : Measure Î±) : â„â‰¥0âˆ :=
  â¨† t : â„â‰¥0, t * distribution f t Î¼ ^ (p : â„)â»Â¹

/-- The weak L^p norm of a function. -/
def wnorm [ENorm E] (f : Î± â†’ E) (p : â„â‰¥0âˆ) (Î¼ : Measure Î±) : â„â‰¥0âˆ :=
  if p = âˆ then eLpNormEssSup f Î¼ else wnorm' f (ENNReal.toReal p) Î¼

lemma wnorm'_zero [ENorm E] (f : Î± â†’ E) (Î¼ : Measure Î±) : wnorm' f 0 Î¼ = âˆ := by
  simp only [wnorm, zero_ne_top, â†“reduceIte, wnorm', zero_toReal, GroupWithZero.inv_zero,
    ENNReal.rpow_zero, mul_one, iSup_eq_top]
  refine fun b hb â†¦ âŸ¨b.toNNReal + 1, ?_âŸ©
  rw [coe_add, ENNReal.coe_one, coe_toNNReal hb.ne_top]
  exact lt_add_right hb.ne_top one_ne_zero

lemma wnorm_zero [ENorm E] (f : Î± â†’ E) (Î¼ : Measure Î±) : wnorm f 0 Î¼ = âˆ := by
  simp [wnorm, wnorm'_zero]

/-- A function is in weak-L^p if it is (strongly a.e.)-measurable and has finite weak L^p norm. -/
def MemWâ„’p [TopologicalSpace E] [ENorm E] (f : Î± â†’ E) (p : â„â‰¥0âˆ) (Î¼ : Measure Î±) : Prop :=
  AEStronglyMeasurable f Î¼ âˆ§ wnorm f p Î¼ < âˆ

lemma MemWâ„’p_zero [TopologicalSpace E] [ENorm E] (f : Î± â†’ E) (Î¼ : Measure Î±) : Â¬ MemWâ„’p f 0 Î¼ := by
  simp [MemWâ„’p, wnorm_zero]

variable [ENorm Eâ‚] [ENorm Eâ‚‚] [TopologicalSpace Eâ‚] [TopologicalSpace Eâ‚‚]

/-- An operator has weak type `(p, q)` if it is bounded as a map from L^p to weak-L^q.
`HasWeakType T p p' Î¼ Î½ c` means that `T` has weak type `(p, p')` w.r.t. measures `Î¼`, `Î½`
and constant `c`.  -/
def HasWeakType (T : (Î± â†’ Eâ‚) â†’ (Î±' â†’ Eâ‚‚)) (p p' : â„â‰¥0âˆ) (Î¼ : Measure Î±) (Î½ : Measure Î±')
    (c : â„â‰¥0) : Prop :=
  âˆ€ f : Î± â†’ Eâ‚, Memâ„’p f p Î¼ â†’ AEStronglyMeasurable (T f) Î½ âˆ§ wnorm (T f) p' Î½ â‰¤ c * eLpNorm f p Î¼

/-- An operator has strong type `(p, q)` if it is bounded as an operator on `L^p â†’ L^q`.
`HasStrongType T p p' Î¼ Î½ c` means that `T` has strong type `(p, p')` w.r.t. measures `Î¼`, `Î½`
and constant `c`.  -/
def HasStrongType {E E' Î± Î±' : Type*} [NormedAddCommGroup E] [NormedAddCommGroup E']
    {_x : MeasurableSpace Î±} {_x' : MeasurableSpace Î±'} (T : (Î± â†’ E) â†’ (Î±' â†’ E'))
    (p p' : â„â‰¥0âˆ) (Î¼ : Measure Î±) (Î½ : Measure Î±') (c : â„â‰¥0) : Prop :=
  âˆ€ f : Î± â†’ E, Memâ„’p f p Î¼ â†’ AEStronglyMeasurable (T f) Î½ âˆ§ eLpNorm (T f) p' Î½ â‰¤ c * eLpNorm f p Î¼

/-- A weaker version of `HasStrongType`. This is the same as `HasStrongType` if `T` is continuous
w.r.t. the L^2 norm, but weaker in general. -/
def HasBoundedStrongType {E E' Î± Î±' : Type*} [NormedAddCommGroup E] [NormedAddCommGroup E']
    {_x : MeasurableSpace Î±} {_x' : MeasurableSpace Î±'} (T : (Î± â†’ E) â†’ (Î±' â†’ E'))
    (p p' : â„â‰¥0âˆ) (Î¼ : Measure Î±) (Î½ : Measure Î±') (c : â„â‰¥0) : Prop :=
  âˆ€ f : Î± â†’ E, Memâ„’p f p Î¼ â†’ eLpNorm f âˆ Î¼ < âˆ â†’ Î¼ (support f) < âˆ â†’
  AEStronglyMeasurable (T f) Î½ âˆ§ eLpNorm (T f) p' Î½ â‰¤ c * eLpNorm f p Î¼

/- Mathlib PR: https://github.com/leanprover-community/mathlib4/pull/18704. -/
lemma _root_.ENNReal.inv_div {a b : â„â‰¥0âˆ} (h1 : b â‰  âˆ âˆ¨ a â‰  âˆ) (h2 : b â‰  0 âˆ¨ a â‰  0) :
    (a / b)â»Â¹ = b / a := by
  rw [â† ENNReal.inv_ne_zero] at h1
  rw [â† ENNReal.inv_ne_top] at h2
  rw [ENNReal.div_eq_inv_mul, ENNReal.div_eq_inv_mul, ENNReal.mul_inv h1 h2, mul_comm, inv_inv]

/-- If a function `f : Î± â†’ ENNReal` is `MemWâ„’p`, then its norm is almost everywhere finite.-/
theorem MemWâ„’p.ae_ne_top [TopologicalSpace E] [ENorm E] {f : Î± â†’ E} {p : â„â‰¥0âˆ} {Î¼ : Measure Î±}
    (hf : MemWâ„’p f p Î¼) : âˆ€áµ x âˆ‚Î¼, â€–f xâ€–â‚‘ â‰  âˆ := by
  by_cases hp_inf : p = âˆ
  Â· rw [hp_inf] at hf
    simp_rw [â† lt_top_iff_ne_top]
    exact ae_lt_of_essSup_lt hf.2
  by_cases hp_zero : p = 0
  Â· exact (MemWâ„’p_zero _ _ <| hp_zero â–¸ hf).elim
  set A := {x | â€–f xâ€–â‚‘ = âˆ} with hA
  unfold MemWâ„’p wnorm wnorm' at hf
  simp only [hp_inf] at hf
  rw [Filter.eventually_iff, mem_ae_iff]
  simp only [ne_eq, compl_def, mem_setOf_eq, Decidable.not_not, â† hA]
  have hp_toReal_zero := toReal_ne_zero.mpr âŸ¨hp_zero, hp_infâŸ©
  have h1 (t : â„â‰¥0) : Î¼ A â‰¤ distribution f t Î¼ := by
    refine Î¼.mono ?_
    simp_all only [setOf_subset_setOf, coe_lt_top, implies_true, A]
  set C := â¨† t : â„â‰¥0, t * distribution f t Î¼ ^ p.toRealâ»Â¹
  by_cases hC_zero : C = 0
  Â· simp only [ENNReal.iSup_eq_zero, mul_eq_zero, ENNReal.rpow_eq_zero_iff, inv_neg'', C] at hC_zero
    specialize hC_zero 1
    simp only [one_ne_zero, ENNReal.coe_one, toReal_nonneg.not_lt, and_false, or_false,
      false_or] at hC_zero
    exact measure_mono_null (setOf_subset_setOf.mpr fun x hx => hx â–¸ one_lt_top) hC_zero.1
  by_contra h
  have h2 : C < âˆ := by aesop
  have h3 (t : â„â‰¥0) : distribution f t Î¼ â‰¤ (C / t) ^ p.toReal := by
    rw [â† rpow_inv_rpow hp_toReal_zero (distribution ..)]
    refine rpow_le_rpow ?_ toReal_nonneg
    rw [ENNReal.le_div_iff_mul_le (Or.inr hC_zero) (Or.inl coe_ne_top), mul_comm]
    exact le_iSup_iff.mpr fun _ a â†¦ a t
  have h4 (t : â„â‰¥0) : Î¼ A â‰¤ (C / t) ^ p.toReal := (h1 t).trans (h3 t)
  have h5 : Î¼ A â‰¤ Î¼ A / 2 := by
    convert h4 (C * (2 / Î¼ A) ^ p.toRealâ»Â¹).toNNReal
    rw [coe_toNNReal ?_]
    swap
    Â· refine mul_ne_top h2.ne_top (rpow_ne_top_of_nonneg (inv_nonneg.mpr toReal_nonneg) ?_)
      simp [div_eq_top, h]
    nth_rw 1 [â† mul_one C]
    rw [ENNReal.mul_div_mul_left _ _ hC_zero h2.ne_top, div_rpow_of_nonneg _ _ toReal_nonneg,
      ENNReal.rpow_inv_rpow hp_toReal_zero, ENNReal.one_rpow, one_div,
        ENNReal.inv_div (Or.inr two_ne_top) (Or.inr (NeZero.ne' 2).symm)]
  have h6 : Î¼ A = 0 := by
    convert (fun hh â†¦ ENNReal.half_lt_self hh (ne_top_of_le_ne_top (rpow_ne_top_of_nonneg
      toReal_nonneg ((div_one C).symm â–¸ h2.ne_top)) (h4 1))).mt h5.not_lt
    tauto
  exact h h6

-- Maybe this statement is not needed, we can just use the previous one.
/-- If a function `f : Î± â†’ ENNReal` is `MemWâ„’p`, then it is almost everywhere finite.-/
theorem MemWâ„’p.ae_ne_top' {f : Î± â†’ ENNReal} {p : â„â‰¥0âˆ} {Î¼ : Measure Î±}
    (hf : MemWâ„’p f p Î¼) : âˆ€áµ x âˆ‚Î¼, f x â‰  âˆ := hf.ae_ne_top

/- Add temporary section. -/
section tmp

/- Copy-pasted all this code here temporarily to be able to prove `HasWeakType.MB_one'` while avoiding the import conflicts, the problem is that this file redefines some objects that are imported in the files that define `IsDoubling`, `MB` ecc. When these definitions will replace the old ones this can be fixed
-/

open Metric Vitali MeasureTheory Measure

/-- A doubling measure is a measure on a metric space with the condition doubling
the radius of a ball only increases the volume by a constant factor, independent of the ball. -/
class Measure.IsDoubling {X : Type*} [MeasurableSpace X] [PseudoMetricSpace X]
    (Î¼ : Measure X) (A : outParam â„â‰¥0) : Prop where
  measure_ball_two_le_same : âˆ€ (x : X) r, Î¼ (ball x (2 * r)) â‰¤ A * Î¼ (ball x r)

variable {X E : Type*} {A : â„â‰¥0} [MetricSpace X] [MeasurableSpace X]
  {Î¼ : Measure X} [Î¼.IsDoubling A] [NormedAddCommGroup E]
  {f : X â†’ E} {x : X} {Î¹ : Type*} {ğ“‘ : Set Î¹} {c : Î¹ â†’ X} {r : Î¹ â†’ â„}

section laverage

variable (Î¼ : Measure Î±)

/-- Average value of an `â„â‰¥0âˆ`-valued function `f` w.r.t. a measure `Î¼`, denoted `â¨â» x, f x âˆ‚Î¼`.

It is equal to `(Î¼ univ)â»Â¹ * âˆ«â» x, f x âˆ‚Î¼`, so it takes value zero if `Î¼` is an infinite measure. If
`Î¼` is a probability measure, then the average of any function is equal to its integral.

For the average on a set, use `â¨â» x in s, f x âˆ‚Î¼`, defined as `â¨â» x, f x âˆ‚(Î¼.restrict s)`. For the
average w.r.t. the volume, one can omit `âˆ‚volume`. -/
noncomputable def laverage (f : Î± â†’ â„â‰¥0âˆ) := âˆ«â» x, f x âˆ‚(Î¼ univ)â»Â¹ â€¢ Î¼

/-- Average value of an `â„â‰¥0âˆ`-valued function `f` w.r.t. a measure `Î¼`.

It is equal to `(Î¼ univ)â»Â¹ * âˆ«â» x, f x âˆ‚Î¼`, so it takes value zero if `Î¼` is an infinite measure. If
`Î¼` is a probability measure, then the average of any function is equal to its integral.

For the average on a set, use `â¨â» x in s, f x âˆ‚Î¼`, defined as `â¨â» x, f x âˆ‚(Î¼.restrict s)`. For the
average w.r.t. the volume, one can omit `âˆ‚volume`. -/
notation3 "â¨â» "(...)", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => laverage Î¼ r

/-- Average value of an `â„â‰¥0âˆ`-valued function `f` w.r.t. a measure `Î¼` on a set `s`.

It is equal to `(Î¼ s)â»Â¹ * âˆ«â» x, f x âˆ‚Î¼`, so it takes value zero if `s` has infinite measure. If `s`
has measure `1`, then the average of any function is equal to its integral.

For the average w.r.t. the volume, one can omit `âˆ‚volume`. -/
notation3 "â¨â» "(...)" in "s", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => laverage (Measure.restrict Î¼ s) r

theorem laverage_eq' (f : Î± â†’ â„â‰¥0âˆ) : â¨â» x, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚(Î¼ univ)â»Â¹ â€¢ Î¼ := rfl

theorem laverage_eq (f : Î± â†’ â„â‰¥0âˆ) : â¨â» x, f x âˆ‚Î¼ = (âˆ«â» x, f x âˆ‚Î¼) / Î¼ univ := by
  rw [laverage_eq', lintegral_smul_measure, ENNReal.div_eq_inv_mul]

theorem setLaverage_eq (f : Î± â†’ â„â‰¥0âˆ) (s : Set Î±) :
    â¨â» x in s, f x âˆ‚Î¼ = (âˆ«â» x in s, f x âˆ‚Î¼) / Î¼ s := by rw [laverage_eq, restrict_apply_univ]

end laverage

/-- The Hardy-Littlewood maximal function w.r.t. a collection of balls ğ“‘.
M_{ğ“‘, p} in the blueprint. -/
def maximalFunction (Î¼ : Measure X) (ğ“‘ : Set Î¹) (c : Î¹ â†’ X) (r : Î¹ â†’ â„)
  (p : â„) (u : X â†’ E) (x : X) : â„â‰¥0âˆ :=
  (â¨† i âˆˆ ğ“‘, (ball (c i) (r i)).indicator (x := x)
  fun _ â†¦ â¨â» y in ball (c i) (r i), â€–u yâ€–â‚Š ^ p âˆ‚Î¼) ^ pâ»Â¹

/-- The Hardy-Littlewood maximal function w.r.t. a collection of balls ğ“‘ with exponent 1.
M_ğ“‘ in the blueprint. -/
abbrev MB (Î¼ : Measure X) (ğ“‘ : Set Î¹) (c : Î¹ â†’ X) (r : Î¹ â†’ â„) (u : X â†’ E) (x : X) : â„â‰¥0âˆ :=
  maximalFunction Î¼ ğ“‘ c r 1 u x

-- todo: move
-- slightly more general than the Mathlib version
-- the extra conclusion says that if there is a nonnegative radius, then we can choose `r b` to be
-- larger than `r a` (up to a constant)
theorem exists_disjoint_subfamily_covering_enlargement_closedBall' {Î±} [MetricSpace Î±] (t : Set Î¹)
    (x : Î¹ â†’ Î±) (r : Î¹ â†’ â„) (R : â„) (hr : âˆ€ a âˆˆ t, r a â‰¤ R) (Ï„ : â„) (hÏ„ : 3 < Ï„) :
    âˆƒ u âŠ† t,
      (u.PairwiseDisjoint fun a => closedBall (x a) (r a)) âˆ§
        âˆ€ a âˆˆ t, âˆƒ b âˆˆ u, closedBall (x a) (r a) âŠ† closedBall (x b) (Ï„ * r b) âˆ§
        (âˆ€ u âˆˆ t, 0 â‰¤ r u â†’ r a â‰¤ (Ï„ - 1) / 2 * r b) := by
  rcases eq_empty_or_nonempty t with (rfl | _)
  Â· exact âŸ¨âˆ…, Subset.refl _, pairwiseDisjoint_empty, by simpâŸ©
  by_cases ht : âˆ€ a âˆˆ t, r a < 0
  Â· refine âŸ¨t, .rfl, fun a ha b _ _ â†¦ by
      simp only [Function.onFun, closedBall_eq_empty.2 (ht a ha), empty_disjoint],
      fun a ha => âŸ¨a, ha, by simp only [closedBall_eq_empty.2 (ht a ha), empty_subset],
      fun u hut hu â†¦ (ht u hut).not_le hu |>.elimâŸ©âŸ©
  push_neg at ht
  let t' := { a âˆˆ t | 0 â‰¤ r a }
  have h2Ï„ : 1 < (Ï„ - 1) / 2 := by linarith
  rcases exists_disjoint_subfamily_covering_enlargment (fun a => closedBall (x a) (r a)) t' r
      ((Ï„ - 1) / 2) h2Ï„ (fun a ha => ha.2) R (fun a ha => hr a ha.1) fun a ha =>
      âŸ¨x a, mem_closedBall_self ha.2âŸ© with
    âŸ¨u, ut', u_disj, huâŸ©
  have A : âˆ€ a âˆˆ t', âˆƒ b âˆˆ u, closedBall (x a) (r a) âŠ† closedBall (x b) (Ï„ * r b) âˆ§
    âˆ€ u âˆˆ t, 0 â‰¤ r u â†’ r a â‰¤ (Ï„ - 1) / 2 * r b := by
    intro a ha
    rcases hu a ha with âŸ¨b, bu, hb, rbâŸ©
    refine âŸ¨b, bu, ?_âŸ©
    have : dist (x a) (x b) â‰¤ r a + r b := dist_le_add_of_nonempty_closedBall_inter_closedBall hb
    exact âŸ¨closedBall_subset_closedBall' <| by linarith, fun _ _ _ â†¦ rbâŸ©
  refine âŸ¨u, ut'.trans fun a ha => ha.1, u_disj, fun a ha => ?_âŸ©
  rcases le_or_lt 0 (r a) with (h'a | h'a)
  Â· exact A a âŸ¨ha, h'aâŸ©
  Â· rcases ht with âŸ¨b, rbâŸ©
    rcases A b âŸ¨rb.1, rb.2âŸ© with âŸ¨c, cu, _, hcâŸ©
    refine âŸ¨c, cu, by simp only [closedBall_eq_empty.2 h'a, empty_subset], fun _ _ _ â†¦ ?_âŸ©
    have : 0 â‰¤ r c := nonneg_of_mul_nonneg_right (rb.2.trans <| hc b rb.1 rb.2) (by positivity)
    exact h'a.le.trans <| by positivity

-- move to Vitali
theorem Vitali.exists_disjoint_subfamily_covering_enlargement_ball {Î±} [MetricSpace Î±] (t : Set Î¹)
    (x : Î¹ â†’ Î±) (r : Î¹ â†’ â„) (R : â„) (hr : âˆ€ a âˆˆ t, r a â‰¤ R) (Ï„ : â„) (hÏ„ : 3 < Ï„) :
    âˆƒ u âŠ† t,
      (u.PairwiseDisjoint fun a => ball (x a) (r a)) âˆ§
        âˆ€ a âˆˆ t, âˆƒ b âˆˆ u, ball (x a) (r a) âŠ† ball (x b) (Ï„ * r b) := by
  obtain âŸ¨Ïƒ, hÏƒ, hÏƒÏ„âŸ© := exists_between hÏ„
  obtain âŸ¨u, hut, hux, huâŸ© :=
    exists_disjoint_subfamily_covering_enlargement_closedBall' t x r R hr Ïƒ hÏƒ
  refine âŸ¨u, hut, fun i hi j hj hij â†¦ ?_, fun a ha => ?_âŸ©
  Â· exact (hux hi hj hij).mono ball_subset_closedBall ball_subset_closedBall
  obtain âŸ¨b, hbu, hbâŸ© := hu a ha
  refine âŸ¨b, hbu, ?_âŸ©
  obtain h2a|h2a := le_or_lt (r a) 0
  Â· simp_rw [ball_eq_empty.mpr h2a, empty_subset]
  refine ball_subset_closedBall.trans hb.1 |>.trans <| closedBall_subset_ball ?_
  gcongr
  apply pos_of_mul_pos_right <| h2a.trans_le <| hb.2 a ha h2a.le
  linarith

/- NOTE: This was changed to use `â„â‰¥0âˆ` rather than `â„â‰¥0` because that was more convenient for the
proof of `first_exception'` in ExceptionalSet.lean. But everything involved there is finite, so
you can prove this with `â„â‰¥0` and deal with casting between `â„â‰¥0` and `â„â‰¥0âˆ` there, if that turns
out to be easier. -/
theorem _root_.Set.Countable.measure_biUnion_le_lintegral [OpensMeasurableSpace X] (hğ“‘ : ğ“‘.Countable)
    (l : â„â‰¥0âˆ) (u : X â†’ â„â‰¥0âˆ) (R : â„) (hR : âˆ€ a âˆˆ ğ“‘, r a â‰¤ R)
    (h2u : âˆ€ i âˆˆ ğ“‘, l * Î¼ (ball (c i) (r i)) â‰¤ âˆ«â» x in ball (c i) (r i), u x âˆ‚Î¼) :
    l * Î¼ (â‹ƒ i âˆˆ ğ“‘, ball (c i) (r i)) â‰¤ A ^ 2 * âˆ«â» x, u x âˆ‚Î¼  := by
  obtain âŸ¨B, hBğ“‘, hB, h2BâŸ© := Vitali.exists_disjoint_subfamily_covering_enlargement_ball
    ğ“‘ c r R hR (2 ^ 2) (by norm_num)
  have : Countable B := hğ“‘.mono hBğ“‘
  have disj := fun i j hij â†¦
    hB (Subtype.coe_prop i) (Subtype.coe_prop j) (Subtype.coe_ne_coe.mpr hij)
  calc
    l * Î¼ (â‹ƒ i âˆˆ ğ“‘, ball (c i) (r i)) â‰¤ l * Î¼ (â‹ƒ i âˆˆ B, ball (c i) (2 ^ 2 * r i)) := by
          refine l.mul_left_mono (Î¼.mono fun x hx â†¦ ?_)
          simp only [mem_iUnion, mem_ball, exists_prop] at hx
          rcases hx with âŸ¨i, iğ“‘, hiâŸ©
          obtain âŸ¨b, bB, hbâŸ© := h2B i iğ“‘
          refine mem_iUnionâ‚‚.mpr âŸ¨b, bB, hb <| mem_ball.mpr hiâŸ©
    _ â‰¤ l * âˆ‘' i : B, Î¼ (ball (c i) (2 ^ 2 * r i)) :=
          l.mul_left_mono <| measure_biUnion_le Î¼ (hğ“‘.mono hBğ“‘) fun i â†¦ ball (c i) (2 ^ 2 * r i)
    _ â‰¤ l * âˆ‘' i : B, A ^ 2 * Î¼ (ball (c i) (r i)) := by
          refine l.mul_left_mono <| ENNReal.tsum_le_tsum (fun i â†¦ ?_)
          rw [sq, sq, mul_assoc, mul_assoc]
          apply (Measure.IsDoubling.measure_ball_two_le_same (c i) (2 * r i)).trans
          exact ENNReal.mul_left_mono (Measure.IsDoubling.measure_ball_two_le_same (c i) (r i))
    _ = A ^ 2 * âˆ‘' i : B, l * Î¼ (ball (c i) (r i)) := by
          rw [ENNReal.tsum_mul_left, ENNReal.tsum_mul_left, â† mul_assoc, â† mul_assoc, mul_comm l]
    _ â‰¤ A ^ 2 * âˆ‘' i : B, âˆ«â» x in ball (c i) (r i), u x âˆ‚Î¼ := by
          gcongr; exact h2u _ (hBğ“‘ (Subtype.coe_prop _))
    _ = A ^ 2 * âˆ«â» x in â‹ƒ i âˆˆ B, ball (c i) (r i), u x âˆ‚Î¼ := by
          congr; simpa using (lintegral_iUnion (fun i â†¦ measurableSet_ball) disj u).symm
    _ â‰¤ A ^ 2 * âˆ«â» x, u x âˆ‚Î¼ := by
          gcongr; exact setLIntegral_le_lintegral (â‹ƒ i âˆˆ B, ball (c i) (r i)) u

protected theorem AEStronglyMeasurable.maximalFunction [BorelSpace X] {p : â„}
    {u : X â†’ E} (hğ“‘ : ğ“‘.Countable) : AEStronglyMeasurable (maximalFunction Î¼ ğ“‘ c r p u) Î¼ :=
  (AEMeasurable.biSup ğ“‘ hğ“‘ fun _ _ â†¦ aemeasurable_const.indicator measurableSet_ball).pow
    aemeasurable_const |>.aestronglyMeasurable

protected theorem HasWeakType.MB_one' [BorelSpace X] (hğ“‘ : ğ“‘.Countable)
    {R : â„} (hR : âˆ€ i âˆˆ ğ“‘, r i â‰¤ R) :
    HasWeakType (fun (u : X â†’ E) (x : X) â†¦ MB Î¼ ğ“‘ c r u x) 1 1 Î¼ Î¼ (A ^ 2) := by
  intro f _
  use AEStronglyMeasurable.maximalFunction hğ“‘
  let Bâ‚— (â„“ : â„â‰¥0âˆ) := { i âˆˆ ğ“‘ | âˆ«â» y in (ball (c i) (r i)), â€–f yâ€–â‚Š âˆ‚Î¼ â‰¥ â„“ * Î¼ (ball (c i) (r i)) }
  simp only [wnorm, one_ne_top, wnorm', one_toReal, inv_one, ENNReal.rpow_one, reduceIte,
    ENNReal.coe_pow, eLpNorm, one_ne_zero, eLpNorm', ne_eq, not_false_eq_true, div_self,
    iSup_le_iff]
  intro t
  by_cases ht : t = 0
  Â· simp [ht]
  have hBâ‚— : (Bâ‚— t).Countable := hğ“‘.mono (fun i hi â†¦ mem_of_mem_inter_left hi)
  refine le_trans ?_ (hBâ‚—.measure_biUnion_le_lintegral (c := c) (r := r) (l := t)
    (u := fun x â†¦ â€–f xâ€–â‚Š) (R := R) ?_ ?_)
  Â· refine mul_left_mono <| Î¼.mono (fun x hx â†¦ mem_iUnionâ‚‚.mpr ?_)
    -- We need a ball in `Bâ‚— t` containing `x`. Since `MB Î¼ ğ“‘ c r f x` is large, such a ball exists
    simp only [mem_setOf_eq] at hx
    -- replace hx := lt_of_lt_of_le hx coe_toNNReal_le_self
    simp only [MB, maximalFunction, ENNReal.rpow_one, inv_one] at hx
    obtain âŸ¨i, htâŸ© := lt_iSup_iff.mp hx
    replace hx : x âˆˆ ball (c i) (r i) :=
      by_contradiction <| fun h â†¦ not_lt_of_ge (zero_le t) (ENNReal.coe_lt_coe.mp <| by simp [h] at ht)
    refine âŸ¨i, ?_, hxâŸ©
    -- It remains only to confirm that the chosen ball is actually in `Bâ‚— t`
    simp only [ge_iff_le, mem_setOf_eq, Bâ‚—]
    have hi : i âˆˆ ğ“‘ :=
      by_contradiction <| fun h â†¦ not_lt_of_ge (zero_le t) (ENNReal.coe_lt_coe.mp <| by simp [h] at ht)
    exact âŸ¨hi, mul_le_of_le_div <| le_of_lt (by simpa [setLaverage_eq, hi, hx] using ht)âŸ©
  Â· exact fun i hi â†¦ hR i (mem_of_mem_inter_left hi)
  Â· exact fun i hi â†¦ hi.2.trans (setLIntegral_mono' measurableSet_ball fun x _ â†¦ by simp)

end tmp

end MeasureTheory
