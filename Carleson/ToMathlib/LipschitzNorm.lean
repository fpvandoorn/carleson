import Mathlib.Analysis.RCLike.Basic

open Metric Function ENNReal
open scoped NNReal

/-!
# Inhomogeneous Lipschitz norm

This file defines the Lipschitz norm, which probably in some form should end up in Mathlib.
Lemmas about this norm that are proven in Carleson are collected here.

-/

noncomputable section

section Def

variable {ğ•œ X : Type*} [_root_.RCLike ğ•œ] [PseudoMetricSpace X]

/-- The inhomogeneous Lipschitz norm on a ball. -/
def iLipENorm {ğ•œ} [NormedField ğ•œ] (Ï• : X â†’ ğ•œ) (xâ‚€ : X) (R : â„) : â„â‰¥0âˆ :=
  (â¨† x âˆˆ ball xâ‚€ R, â€–Ï• xâ€–â‚‘) +
  ENNReal.ofReal R * â¨† (x âˆˆ ball xâ‚€ R) (y âˆˆ ball xâ‚€ R) (_ : x â‰  y), â€–Ï• x - Ï• yâ€–â‚‘ / edist x y

/-- The `NNReal` version of the inhomogeneous Lipschitz norm on a ball, `iLipENorm`. -/
def iLipNNNorm {ğ•œ} [NormedField ğ•œ] (Ï• : X â†’ ğ•œ) (xâ‚€ : X) (R : â„) : â„â‰¥0 :=
  (iLipENorm Ï• xâ‚€ R).toNNReal

end Def

section iLipENorm

variable {ğ•œ X : Type*} {x z : X} {R : â„} {C C' : â„â‰¥0} {Ï• : X â†’ ğ•œ}
variable [MetricSpace X] [NormedField ğ•œ]

lemma iLipENorm_le_add (h : âˆ€ x âˆˆ ball z R, â€–Ï• xâ€– â‰¤ C)
    (h' : âˆ€ x âˆˆ ball z R, âˆ€ x' âˆˆ ball z R, x â‰  x' â†’ â€–Ï• x - Ï• x'â€– â‰¤ C' * dist x x' / R) :
    iLipENorm Ï• z R â‰¤ C + C' := by
  apply add_le_add
  Â· simp only [iSup_le_iff, enorm_le_coe]
    exact h
  Â· apply ENNReal.mul_le_of_le_div'
    simp only [ne_eq, iSup_le_iff]
    intro x hx x' hx' hne
    have hR : 0 < R := pos_of_mem_ball hx
    have W := h' x hx x' hx' hne
    rw [ENNReal.div_le_iff (by simpa only [ne_eq, edist_eq_zero] using hne) (edist_ne_top x x')]
    convert ENNReal.ofReal_le_ofReal W
    Â· exact (ofReal_norm_eq_enorm (Ï• x - Ï• x')).symm
    Â· rw [ENNReal.ofReal_div_of_pos hR, ENNReal.ofReal_mul NNReal.zero_le_coe, edist_dist,
        ENNReal.mul_div_right_comm, ENNReal.ofReal_coe_nnreal]

lemma iLipENorm_le (h : âˆ€ x âˆˆ ball z R, â€–Ï• xâ€– â‰¤ 2â»Â¹ * C)
    (h' : âˆ€ x âˆˆ ball z R, âˆ€ x' âˆˆ ball z R, x â‰  x' â†’ â€–Ï• x - Ï• x'â€– â‰¤ 2â»Â¹ * C * dist x x' / R) :
    iLipENorm Ï• z R â‰¤ C := by
  apply (iLipENorm_le_add (C := 2â»Â¹ * C) (C' := 2â»Â¹ * C) h h').trans_eq
  simp [â† add_mul, ENNReal.inv_two_add_inv_two]

lemma enorm_le_iLipENorm_of_mem (hx : x âˆˆ ball z R) :
    â€–Ï• xâ€–â‚‘ â‰¤ iLipENorm Ï• z R := by
  apply le_trans _ le_self_add
  simp only [le_iSup_iff, iSup_le_iff]
  tauto

lemma LipschitzOnWith.of_iLipENorm_ne_top (hÏ• : iLipENorm Ï• z R â‰  âŠ¤) :
    LipschitzOnWith (iLipNNNorm Ï• z R / R.toNNReal) Ï• (ball z R) := by
  intro x hx y hy
  have hR : 0 < R := by
    simp only [mem_ball] at hx
    apply dist_nonneg.trans_lt hx
  rcases eq_or_ne x y with rfl | hne
  Â· simp
  have : (ENNReal.ofReal R) * (â€–Ï• x - Ï• yâ€–â‚‘ / edist x y) â‰¤ iLipENorm Ï• z R := calc
      (ENNReal.ofReal R) * (â€–Ï• x - Ï• yâ€–â‚‘ / (edist x y))
    _ â‰¤ (ENNReal.ofReal R) *
        â¨† (x âˆˆ ball z R) (y âˆˆ ball z R) (_ : x â‰  y), (â€–Ï• x - Ï• yâ€–â‚‘ / edist x y) := by
      gcongr
      simp only [ne_eq, le_iSup_iff, iSup_le_iff]
      tauto
    _ â‰¤ _ := le_add_self
  rw [edist_eq_enorm_sub, ENNReal.coe_div (by simp [hR]), iLipNNNorm, coe_toNNReal hÏ•]
  rw [â† ENNReal.div_le_iff_le_mul]; rotate_left
  Â· have : edist x y â‰  0 := by simp [hne]
    simp [this]
  Â· simp [edist_ne_top]
  rw [ENNReal.le_div_iff_mul_le]; rotate_left
  Â· simp [hR]
  Â· simp
  convert this using 1
  simp only [ENNReal.ofReal, mul_comm]

lemma continuous_of_iLipENorm_ne_top {z : X} {R : â„}
    {Ï• : X â†’ ğ•œ} (hÏ• : tsupport Ï• âŠ† ball z R) (h'Ï• : iLipENorm Ï• z R â‰  âŠ¤) :
    Continuous Ï• :=
  (LipschitzOnWith.of_iLipENorm_ne_top h'Ï•).continuousOn.continuous_of_tsupport_subset
    isOpen_ball hÏ•

section iLipNNNorm

lemma norm_le_iLipNNNorm_of_mem (hÏ• : iLipENorm Ï• z R â‰  âŠ¤) (hx : x âˆˆ ball z R) :
    â€–Ï• xâ€– â‰¤ iLipNNNorm Ï• z R :=
  (ENNReal.toReal_le_toReal (by simp) hÏ•).2 (enorm_le_iLipENorm_of_mem hx)

lemma norm_le_iLipNNNorm_of_subset (hÏ• : iLipENorm Ï• z R â‰  âŠ¤) (h : support Ï• âŠ† ball z R) :
    â€–Ï• xâ€– â‰¤ iLipNNNorm Ï• z R := by
  by_cases hx : x âˆˆ ball z R
  Â· apply norm_le_iLipNNNorm_of_mem hÏ• hx
  Â· have : x âˆ‰ support Ï• := fun a â†¦ hx (h a)
    simp [notMem_support.mp this]

end iLipNNNorm

end iLipENorm
