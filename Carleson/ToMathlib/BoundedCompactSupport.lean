/-
Copyright (c) 2024 Joris Roos. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joris Roos, S√©bastien Gou√´zel
-/

import Carleson.ToMathlib.BoundedFiniteSupport

/-!

EXPERIMENTAL

# Bounded compactly supported functions

The purpose of this file is to provide helper lemmas to streamline proofs that
functions are (essentially) bounded, compactly supported and measurable.

Most functions we need to deal with are of this class.
This can be a useful way to streamline proofs of `L^p` membership,
in particular integrability.

Todo: make `Mathlib.Tactic.FunProp` work for this

This can be expanded as needed

-/

open Bornology Function Set HasCompactSupport
open scoped ENNReal

namespace MeasureTheory

section

variable {X E : Type*} [TopologicalSpace X] [MeasurableSpace X] {Œº ŒΩ : Measure X} {f : X ‚Üí E}

-- move near the MeasurePreserving/mono_measure analogues
variable {Œ± : Type*} {m0 : MeasurableSpace Œ±} {Œº ŒΩ : Measure Œ±}
  {Œµ : Type*} [TopologicalSpace Œµ] [ContinuousENorm Œµ] {f : Œ± ‚Üí Œµ} in
theorem eLpNorm_mono_ac (hŒºŒΩ : ŒΩ ‚â™ Œº) : eLpNorm f ‚àû ŒΩ ‚â§ eLpNorm f ‚àû Œº := by
  simp_rw [eLpNorm_exponent_top, MeasureTheory.eLpNormEssSup_mono_measure _ hŒºŒΩ]

variable {Œ± : Type*} {m0 : MeasurableSpace Œ±} {Œº ŒΩ : Measure Œ±}
  {Œµ : Type*} [TopologicalSpace Œµ] [ContinuousENorm Œµ] {f : Œ± ‚Üí Œµ} in
theorem MemLp.mono_ac (hf : MemLp f ‚àû Œº) (hŒºŒΩ : ŒΩ ‚â™ Œº) :
    MemLp f ‚àû ŒΩ :=
‚ü®hf.1.mono_ac hŒºŒΩ, eLpNorm_mono_ac hŒºŒΩ |>.trans_lt hf.2‚ü©

variable {Œ± : Type*} {m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}
  {Œµ : Type*} [TopologicalSpace Œµ] [ContinuousENorm Œµ]
  {Œ≤ : Type*} {mŒ≤ : MeasurableSpace Œ≤} {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œµ} {ŒΩ : Measure Œ≤} in
theorem MemLp.comp_quasiMeasurePreserving
    (hg : MemLp g ‚àû ŒΩ) (hf : Measure.QuasiMeasurePreserving f Œº ŒΩ) : MemLp (g ‚àò f) ‚àû Œº :=
  .comp_of_map (hg.mono_ac hf.absolutelyContinuous) hf.aemeasurable

-- maybe don't upstream
variable {Œ± : Type*} {m0 : MeasurableSpace Œ±} {Œº ŒΩ : Measure Œ±}
  {E : Type*} [NormedAddCommGroup E] {f : Œ± ‚Üí E} in
theorem MemLp.ae_norm_le (hf : MemLp f ‚àû Œº) : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ (eLpNorm f ‚ä§ Œº).toReal := by
  filter_upwards [ae_le_eLpNormEssSup (f := f)] with x hx
  simp_rw [‚Üê toReal_enorm]
  apply ENNReal.toReal_mono hf.2.ne
  simp [hx]

variable [TopologicalSpace E] [ENorm E] [Zero E] in
/- currently we assume that the functions are a.e.-bounded, since that plays better with mathlib.
Since it might be nicer to work with suprema instead of essential suprema, we need to prove
everywhere-boundedness in one place. -/
/-- Bounded compactly supported measurable functions -/
structure BoundedCompactSupport (f : X ‚Üí E) (Œº : Measure X := by volume_tac) :
    Prop where
  memLp_top : MemLp f ‚àû Œº
  hasCompactSupport : HasCompactSupport f

namespace BoundedCompactSupport

section General

variable [TopologicalSpace E] [ENorm E] [Zero E]

theorem aestronglyMeasurable (hf : BoundedCompactSupport f Œº) : AEStronglyMeasurable f Œº :=
  hf.memLp_top.aestronglyMeasurable

theorem boundedFiniteSupport [IsFiniteMeasureOnCompacts Œº] (hf : BoundedCompactSupport f Œº) :
    BoundedFiniteSupport f Œº where
  memLp_top := hf.memLp_top
  measure_support_lt :=
    measure_mono (subset_tsupport _) |>.trans_lt hf.hasCompactSupport.isCompact.measure_lt_top

end General

section ContinuousENorm

variable [TopologicalSpace E] [ContinuousENorm E] [Zero E]

theorem mono_ac (hf : BoundedCompactSupport f Œº) (h : ŒΩ ‚â™ Œº) :
    BoundedCompactSupport f ŒΩ where
  memLp_top := hf.memLp_top.mono_ac h
  hasCompactSupport := hf.hasCompactSupport

theorem mono_measure (hf : BoundedCompactSupport f Œº) (h : ŒΩ ‚â§ Œº) : BoundedCompactSupport f ŒΩ :=
  hf.mono_ac h.absolutelyContinuous

theorem restrict {s : Set X} (hf : BoundedCompactSupport f Œº) :
    BoundedCompactSupport f (Œº.restrict s) :=
  hf.mono_measure Measure.restrict_le_self


end ContinuousENorm


section  ENormedAddCommMonoid
variable [TopologicalSpace E] [ENormedAddCommMonoid E]

/-- Bounded compactly supported functions are in all `L·µñ` spaces. -/
theorem memLp [IsFiniteMeasureOnCompacts Œº] (hf : BoundedCompactSupport f Œº) (p : ‚Ñù‚â•0‚àû) :
    MemLp f p Œº :=
  hf.boundedFiniteSupport.memLp p

end ENormedAddCommMonoid

section NormedAddCommGroup

variable [NormedAddCommGroup E]

-- todo: prove more results for ENorm-classes (awaiting-mathlib)

/-- Bounded compactly supported functions are integrable. -/
theorem integrable [IsFiniteMeasureOnCompacts Œº] (hf : BoundedCompactSupport f Œº) :
    Integrable f Œº :=
  hf.boundedFiniteSupport.integrable

protected theorem zero : BoundedCompactSupport (fun (_ : X) ‚Ü¶ (0 : E)) Œº where
  memLp_top := memLp_top_const 0
  hasCompactSupport := HasCompactSupport.zero

theorem enorm (hf : BoundedCompactSupport f Œº) : BoundedCompactSupport (‚Äñf ¬∑‚Äñ‚Çë) Œº where
  memLp_top := hf.memLp_top.enorm
  hasCompactSupport := hasCompactSupport_comp_left enorm_eq_zero |>.mpr hf.hasCompactSupport

-- preferably use `enorm`
theorem norm (hf : BoundedCompactSupport f Œº) : BoundedCompactSupport (‚Äñf ¬∑‚Äñ) Œº where
  memLp_top := hf.memLp_top.norm
  hasCompactSupport := hasCompactSupport_comp_left norm_eq_zero |>.mpr hf.hasCompactSupport

protected theorem neg (hf : BoundedCompactSupport f Œº) : BoundedCompactSupport (- f) Œº where
  memLp_top := hf.memLp_top.neg
  hasCompactSupport := hf.hasCompactSupport.neg'

variable {ùïú : Type*} [RCLike ùïú] {g : X ‚Üí E}

protected theorem add (hf : BoundedCompactSupport f Œº) (hg : BoundedCompactSupport g Œº) :
    BoundedCompactSupport (f + g) Œº where
  memLp_top := hf.memLp_top.add hg.memLp_top
  hasCompactSupport := hf.hasCompactSupport.add hg.hasCompactSupport

protected theorem sub (hf : BoundedCompactSupport f Œº) (hg : BoundedCompactSupport g Œº) :
    BoundedCompactSupport (f - g) Œº := by
  rw [sub_eq_add_neg]
  exact hf.add hg.neg

section Mul

variable {f g : X ‚Üí ùïú}

theorem mul_bdd_right (hf : BoundedCompactSupport f Œº) (hg : MemLp g ‚àû Œº) :
    BoundedCompactSupport (f * g) Œº where
  memLp_top := hg.mul hf.memLp_top
  hasCompactSupport := hf.hasCompactSupport.mul_right

theorem mul_bdd_left (hf : BoundedCompactSupport f Œº) (hg : MemLp g ‚àû Œº) :
    BoundedCompactSupport (g * f) Œº := by
  rw [mul_comm]; exact mul_bdd_right hf hg

theorem mul (hf : BoundedCompactSupport f Œº) (hg : BoundedCompactSupport g Œº) :
    BoundedCompactSupport (f * g) Œº :=
  hf.mul_bdd_right hg.memLp_top

-- doesn't use compact support but is convenient to have here
theorem integrable_mul (hf : BoundedCompactSupport f Œº) (hg : Integrable g Œº) :
    Integrable (f * g) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at hg ‚ä¢
  exact hg.mul hf.memLp_top

-- todo: extract 3-4 lemmas from this proof
theorem conj (hf : BoundedCompactSupport f Œº) : BoundedCompactSupport (star f) Œº where
  memLp_top := by
    refine ‚ü®RCLike.continuous_conj.comp_aestronglyMeasurable hf.aestronglyMeasurable, ?_‚ü©
    simp_rw [star]
    rw [MeasureTheory.eLpNorm_congr_enorm_ae (g := f)]
    ¬∑ exact hf.memLp_top.eLpNorm_lt_top
    simp
  hasCompactSupport := by
    simp_rw [star]
    exact (hasCompactSupport_comp_left (by simp)).2 hf.hasCompactSupport

theorem const_mul (hf : BoundedCompactSupport f Œº) (c : ùïú) :
    BoundedCompactSupport (fun x ‚Ü¶ c * f x) Œº :=
  hf.mul_bdd_left <| memLp_top_const _

theorem mul_const (hf : BoundedCompactSupport f Œº) (c : ùïú) :
    BoundedCompactSupport (fun x ‚Ü¶ f x * c) Œº :=
  hf.mul_bdd_right <| memLp_top_const _

end Mul

/-- If `‚Äñf‚Äñ` is bounded by `g` and `g` is bounded compactly supported, then so is `f`. -/
theorem mono {g : X ‚Üí ‚Ñù‚â•0‚àû} (hg : BoundedCompactSupport g Œº) (hf : AEStronglyMeasurable f Œº)
    (hfg : ‚àÄ x, ‚Äñf x‚Äñ‚Çë ‚â§ g x) : BoundedCompactSupport f Œº where
  memLp_top := ‚ü®hf, eLpNorm_mono_enorm hfg |>.trans_lt hg.memLp_top.eLpNorm_lt_top‚ü©
  hasCompactSupport := by
    refine hg.hasCompactSupport.mono ?_
    by_contra h
    simp only [support_subset_iff, ne_eq, mem_support, not_forall, Classical.not_imp,
      Decidable.not_not] at h
    obtain ‚ü®x, hfx, hgx‚ü© := h
    specialize hfg x
    simp_rw [hgx, nonpos_iff_eq_zero, enorm_eq_zero, hfx] at hfg

-- use `mono` preferably
theorem mono_norm {g : X ‚Üí ‚Ñù} (hg : BoundedCompactSupport g Œº) (hf : AEStronglyMeasurable f Œº)
    (hfg : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ g x) : BoundedCompactSupport f Œº where
  memLp_top := ‚ü®hf, eLpNorm_mono_real hfg |>.trans_lt hg.memLp_top.eLpNorm_lt_top‚ü©
  hasCompactSupport := by
    refine hg.hasCompactSupport.mono ?_
    by_contra h
    simp only [support_subset_iff, ne_eq, mem_support, not_forall, Classical.not_imp,
      Decidable.not_not] at h
    obtain ‚ü®x, hfx, hgx‚ü© := h
    specialize hfg x
    simp_rw [hgx, norm_le_zero_iff, hfx] at hfg

theorem toComplex {f : X ‚Üí ‚Ñù} (hf : BoundedCompactSupport f Œº) :
    BoundedCompactSupport (fun x ‚Ü¶ (f x : ‚ÑÇ)) Œº :=
  mono (g := (‚Äñf ¬∑‚Äñ‚Çë)) hf.enorm
    (Complex.continuous_ofReal.comp_aestronglyMeasurable hf.aestronglyMeasurable)
    (by simp [‚Üê ofReal_norm])

section Sum

variable {Œπ : Type*} {s : Finset Œπ} {F : Œπ ‚Üí X ‚Üí E}

/-- A finite sum of bounded compactly supported functions is bounded compactly supported. -/
theorem finset_sum
    (hF : ‚àÄ i ‚àà s, BoundedCompactSupport (F i) Œº) :
    BoundedCompactSupport (fun x ‚Ü¶ ‚àë i ‚àà s, F i x) Œº := by
  classical
  induction s using Finset.induction with
  | empty => simp [BoundedCompactSupport.zero]
  | @insert j s hjs IH =>
    simp_rw [Finset.sum_insert hjs]
    apply BoundedCompactSupport.add
    ¬∑ exact hF _ (Finset.mem_insert_self j s)
    ¬∑ exact IH (fun i hi ‚Ü¶ hF _ (Finset.mem_insert_of_mem hi))

end Sum

section Prod

variable {Y : Type*} [MeasureSpace Y] {f : X ‚Üí ùïú} {g : Y ‚Üí ùïú} {ŒΩ : Measure Y}
variable [TopologicalSpace Y]
variable [R1Space (X √ó Y)]

/-- An elementary tensor of bounded compactly supported functions is
  bounded compactly supported. -/
theorem prod_mul (hf : BoundedCompactSupport f Œº) (hg : BoundedCompactSupport g ŒΩ) :
    BoundedCompactSupport (uncurry fun x y ‚Ü¶ f x * g y) (Œº.prod ŒΩ) where
  memLp_top := by
    -- todo: maybe separate out as lemmas
    have h2f : MemLp (fun z : X √ó Y ‚Ü¶ f z.1) ‚àû (Œº.prod ŒΩ) :=
      hf.memLp_top.comp_quasiMeasurePreserving Measure.quasiMeasurePreserving_fst
    have h2g : MemLp (fun z : X √ó Y ‚Ü¶ g z.2) ‚àû (Œº.prod ŒΩ) :=
      hg.memLp_top.comp_quasiMeasurePreserving Measure.quasiMeasurePreserving_snd
    -- todo: reorder arguments of `mul`
    exact h2g.mul (r := ‚àû) h2f
  hasCompactSupport := by
    -- todo: separate out as lemmas
    apply HasCompactSupport.intro <| hf.hasCompactSupport.prod hg.hasCompactSupport
    intro ‚ü®x,y‚ü© hxy
    simp only [uncurry_apply_pair, mul_eq_zero]
    simp only [mem_prod, not_and] at hxy
    by_cases hx : x ‚àà tsupport f
    ¬∑ exact Or.inr (image_eq_zero_of_notMem_tsupport (hxy hx))
    ¬∑ exact Or.inl (image_eq_zero_of_notMem_tsupport hx)

variable [R1Space X] in
theorem indicator_of_isCompact_closure {f : X ‚Üí E} (hf : MemLp f ‚àû Œº)
    {s : Set X} (h's : IsCompact (closure s)) (hs : MeasurableSet s) :
    BoundedCompactSupport (s.indicator f) Œº where
  memLp_top := hf.indicator hs
  hasCompactSupport := by
    apply HasCompactSupport.intro h's
    exact fun x hx ‚Ü¶ by simp [notMem_of_notMem_closure hx]

protected theorem indicator {f : X ‚Üí E} (hf : BoundedCompactSupport f Œº) {s : Set X}
    (hs : MeasurableSet s) : BoundedCompactSupport (s.indicator f) Œº where
  memLp_top := hf.memLp_top.indicator hs
  hasCompactSupport := hf.hasCompactSupport.mono (by simp)

variable {F : X √ó Y ‚Üí E}

-- -- prove when needed
-- theorem swap (hF : BoundedCompactSupport f Œº) : BoundedCompactSupport (F ‚àò Prod.swap) where
--   memLp_top := sorry
--   stronglyMeasurable := sorry
--   hasCompactSupport := sorry

variable {F : X √ó Y ‚Üí E}

-- ---- adapt and prove below when needed
-- theorem prod_left (hF : BoundedCompactSupport f Œº) :
--     ‚àÄ y, BoundedCompactSupport (fun x ‚Ü¶ F (x, y)) := fun y ‚Ü¶ {
--   memLp_top := by
--     rcases isBounded_range_iff_forall_norm_le.1 hF.isBounded with ‚ü®C, hC‚ü©
--     apply isBounded_range_iff_forall_norm_le.2 ‚ü®C, fun x ‚Ü¶ ?_‚ü©
--     exact hC (x, y)
--   stronglyMeasurable := hF.stronglyMeasurable.comp_measurable measurable_prodMk_right
--   hasCompactSupport := sorry
--   -- by
--   --   apply HasCompactSupport.intro
--   --   sorry
-- }


-- theorem prod_right_ae (hF : BoundedCompactSupport f Œº) :
--     ‚àÄ·µê x, BoundedCompactSupport (fun y ‚Ü¶ F (x, y)) := hF.swap.prod_left_ae

-- theorem integral_prod_left (hF : BoundedCompactSupport f Œº) :
--     BoundedCompactSupport (fun x ‚Ü¶ ‚à´ y, F (x, y)) := sorry
-- --   have := hF.integrable.integrable_prod_left

-- theorem integral_prod_right (hF : BoundedCompactSupport f Œº) :
--     BoundedCompactSupport (fun y ‚Ü¶ ‚à´ x, F (x, y)) := hF.swap.integral_prod_left

end Prod

end NormedAddCommGroup


end BoundedCompactSupport

end

section Metric
namespace BoundedCompactSupport

variable {X E : Type*} [MetricSpace X] [MeasurableSpace X] {Œº : Measure X}
  [TopologicalSpace E] [ENorm E] [Zero E]
  {f : X ‚Üí E}

theorem isBoundedTSupport (hf : BoundedCompactSupport f Œº) : IsBounded (tsupport f) :=
  hf.hasCompactSupport.isBounded

theorem isBoundedSupport (hf : BoundedCompactSupport f Œº) : IsBounded (support f) :=
  hf.isBoundedTSupport.subset <| subset_tsupport f


end BoundedCompactSupport

end Metric

end MeasureTheory
