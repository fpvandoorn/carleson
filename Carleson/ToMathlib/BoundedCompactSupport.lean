/-
Copyright (c) 2024 Joris Roos. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joris Roos, SÃ©bastien GouÃ«zel
-/
import Carleson.ToMathlib.BoundedFiniteSupport
import Carleson.ToMathlib.Misc

/-!

EXPERIMENTAL

# Bounded compactly supported functions

The purpose of this file is to provide helper lemmas to streamline proofs that
functions are (essentially) bounded, compactly supported and measurable.

Most functions we need to deal with are of this class.
This can be a useful way to streamline proofs of `L^p` membership,
in particular integrability.

Todo: make `Mathlib.Tactic.FunProp` work for this

This can be expanded as needed

Upstreaming status: should be upstreamed, but need to clarify design questions first
- want to tag this with fun_prop first and make it work smoothly (see #499)
- this is similar to `BoundedFiniteSupport` (and in almost all cases, implies the latter)
  TODO: are there any advantages of using this and not BoundedFiniteSupport? for instance, is
  there any operation which preserves `BoundedCompactSupport` but not `BoundedFiniteSupport`?
  Decide if we want both classes in mathlib or just one of them. If the latter, rewrite all of
  Carleson/ToMathlib to use that one class.

-/

open Bornology Function Set HasCompactSupport
open scoped ENNReal

namespace MeasureTheory

section

variable {X E : Type*} [TopologicalSpace X] [MeasurableSpace X] {Î¼ Î½ : Measure X} {f : X â†’ E}

-- move near the MeasurePreserving/mono_measure analogues
variable {Î± : Type*} {m0 : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±}
  {Îµ : Type*} [TopologicalSpace Îµ] [ContinuousENorm Îµ] {f : Î± â†’ Îµ} in
theorem eLpNorm_mono_ac (hÎ¼Î½ : Î½ â‰ª Î¼) : eLpNorm f âˆ Î½ â‰¤ eLpNorm f âˆ Î¼ := by
  simp_rw [eLpNorm_exponent_top, MeasureTheory.eLpNormEssSup_mono_measure _ hÎ¼Î½]

variable {Î± : Type*} {m0 : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±}
  {Îµ : Type*} [TopologicalSpace Îµ] [ContinuousENorm Îµ] {f : Î± â†’ Îµ} in
theorem MemLp.mono_ac (hf : MemLp f âˆ Î¼) (hÎ¼Î½ : Î½ â‰ª Î¼) :
    MemLp f âˆ Î½ :=
âŸ¨hf.1.mono_ac hÎ¼Î½, eLpNorm_mono_ac hÎ¼Î½ |>.trans_lt hf.2âŸ©

variable {Î± : Type*} {m0 : MeasurableSpace Î±} {Î¼ : Measure Î±}
  {Îµ : Type*} [TopologicalSpace Îµ] [ContinuousENorm Îµ]
  {Î² : Type*} {mÎ² : MeasurableSpace Î²} {f : Î± â†’ Î²} {g : Î² â†’ Îµ} {Î½ : Measure Î²} in
theorem MemLp.comp_quasiMeasurePreserving
    (hg : MemLp g âˆ Î½) (hf : Measure.QuasiMeasurePreserving f Î¼ Î½) : MemLp (g âˆ˜ f) âˆ Î¼ :=
  .comp_of_map (hg.mono_ac hf.absolutelyContinuous) hf.aemeasurable

-- maybe don't upstream
variable {Î± : Type*} {m0 : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±}
  {E : Type*} [NormedAddCommGroup E] {f : Î± â†’ E} in
theorem MemLp.ae_norm_le (hf : MemLp f âˆ Î¼) : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ (eLpNorm f âŠ¤ Î¼).toReal := by
  filter_upwards [ae_le_eLpNormEssSup (f := f)] with x hx
  simp_rw [â† toReal_enorm]
  apply ENNReal.toReal_mono hf.2.ne
  simp [hx]

variable [TopologicalSpace E] [ENorm E] [Zero E] in
/- currently we assume that the functions are a.e.-bounded, since that plays better with mathlib.
Since it might be nicer to work with suprema instead of essential suprema, we need to prove
everywhere-boundedness in one place. -/
/-- Bounded compactly supported measurable functions -/
structure BoundedCompactSupport (f : X â†’ E) (Î¼ : Measure X := by volume_tac) :
    Prop where
  memLp_top : MemLp f âˆ Î¼
  hasCompactSupport : HasCompactSupport f

namespace BoundedCompactSupport

section General

open Bornology in
lemma _root_.isBounded_range_iff_forall_norm_le {Î± Î²} [SeminormedAddCommGroup Î±] {f : Î² â†’ Î±} :
    IsBounded (range f) â†” âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C := by convert isBounded_iff_forall_norm_le; simp

variable [TopologicalSpace E] [ENorm E] [Zero E]

theorem aestronglyMeasurable (hf : BoundedCompactSupport f Î¼) : AEStronglyMeasurable f Î¼ :=
  hf.memLp_top.aestronglyMeasurable

theorem boundedFiniteSupport [IsFiniteMeasureOnCompacts Î¼] (hf : BoundedCompactSupport f Î¼) :
    BoundedFiniteSupport f Î¼ where
  memLp_top := hf.memLp_top
  measure_support_lt :=
    measure_mono (subset_tsupport _) |>.trans_lt hf.hasCompactSupport.isCompact.measure_lt_top

end General

section ContinuousENorm

variable [TopologicalSpace E] [ContinuousENorm E] [Zero E]

theorem mono_ac (hf : BoundedCompactSupport f Î¼) (h : Î½ â‰ª Î¼) :
    BoundedCompactSupport f Î½ where
  memLp_top := hf.memLp_top.mono_ac h
  hasCompactSupport := hf.hasCompactSupport

theorem mono_measure (hf : BoundedCompactSupport f Î¼) (h : Î½ â‰¤ Î¼) : BoundedCompactSupport f Î½ :=
  hf.mono_ac h.absolutelyContinuous

theorem restrict {s : Set X} (hf : BoundedCompactSupport f Î¼) :
    BoundedCompactSupport f (Î¼.restrict s) :=
  hf.mono_measure Measure.restrict_le_self


end ContinuousENorm


section  ENormedAddCommMonoid
variable [TopologicalSpace E] [ENormedAddCommMonoid E]

/-- Bounded compactly supported functions are in all `Láµ–` spaces. -/
theorem memLp [IsFiniteMeasureOnCompacts Î¼] (hf : BoundedCompactSupport f Î¼) (p : â„â‰¥0âˆ) :
    MemLp f p Î¼ :=
  hf.boundedFiniteSupport.memLp p

end ENormedAddCommMonoid

section NormedAddCommGroup

variable [NormedAddCommGroup E]

-- todo: prove more results for ENorm-classes (awaiting-mathlib)

/-- Bounded compactly supported functions are integrable. -/
theorem integrable [IsFiniteMeasureOnCompacts Î¼] (hf : BoundedCompactSupport f Î¼) :
    Integrable f Î¼ :=
  hf.boundedFiniteSupport.integrable

protected theorem zero : BoundedCompactSupport (fun (_ : X) â†¦ (0 : E)) Î¼ where
  memLp_top := memLp_top_const 0
  hasCompactSupport := HasCompactSupport.zero

theorem enorm (hf : BoundedCompactSupport f Î¼) : BoundedCompactSupport (â€–f Â·â€–â‚‘) Î¼ where
  memLp_top := hf.memLp_top.enorm
  hasCompactSupport := hasCompactSupport_comp_left enorm_eq_zero |>.mpr hf.hasCompactSupport

-- preferably use `enorm`
theorem norm (hf : BoundedCompactSupport f Î¼) : BoundedCompactSupport (â€–f Â·â€–) Î¼ where
  memLp_top := hf.memLp_top.norm
  hasCompactSupport := hasCompactSupport_comp_left norm_eq_zero |>.mpr hf.hasCompactSupport

theorem comp_left_norm {F} [NormedAddCommGroup F] {g : E â†’ F} (hf : BoundedCompactSupport f Î¼)
    (hg : g 0 = 0) (hg1 : Continuous g) (hg2 : (âˆ€ (a : E), â€–g aâ€– = â€–aâ€–)) :
 BoundedCompactSupport (g âˆ˜ f) Î¼ := by
  refine âŸ¨?_, hf.hasCompactSupport.comp_left hgâŸ©
  rw [â† memLp_norm_iff]
  Â· simp_rw [Function.comp_apply, hg2, memLp_norm_iff hf.aestronglyMeasurable, hf.memLp_top]
  exact hg1.comp_aestronglyMeasurable hf.aestronglyMeasurable

protected theorem neg (hf : BoundedCompactSupport f Î¼) : BoundedCompactSupport (- f) Î¼ where
  memLp_top := hf.memLp_top.neg
  hasCompactSupport := hf.hasCompactSupport.neg

variable {ğ•œ : Type*} [RCLike ğ•œ] {g : X â†’ E}

protected theorem add (hf : BoundedCompactSupport f Î¼) (hg : BoundedCompactSupport g Î¼) :
    BoundedCompactSupport (f + g) Î¼ where
  memLp_top := hf.memLp_top.add hg.memLp_top
  hasCompactSupport := hf.hasCompactSupport.add hg.hasCompactSupport

protected theorem sub (hf : BoundedCompactSupport f Î¼) (hg : BoundedCompactSupport g Î¼) :
    BoundedCompactSupport (f - g) Î¼ := by
  rw [sub_eq_add_neg]
  exact hf.add hg.neg

section Mul

variable {f g : X â†’ ğ•œ}

theorem mul_bdd_right (hf : BoundedCompactSupport f Î¼) (hg : MemLp g âˆ Î¼) :
    BoundedCompactSupport (f * g) Î¼ where
  memLp_top := hg.mul hf.memLp_top
  hasCompactSupport := hf.hasCompactSupport.mul_right

theorem mul_bdd_left (hf : BoundedCompactSupport f Î¼) (hg : MemLp g âˆ Î¼) :
    BoundedCompactSupport (g * f) Î¼ := by
  rw [mul_comm]; exact mul_bdd_right hf hg

theorem mul (hf : BoundedCompactSupport f Î¼) (hg : BoundedCompactSupport g Î¼) :
    BoundedCompactSupport (f * g) Î¼ :=
  hf.mul_bdd_right hg.memLp_top

-- doesn't use compact support but is convenient to have here
theorem integrable_mul (hf : BoundedCompactSupport f Î¼) (hg : Integrable g Î¼) :
    Integrable (f * g) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hg âŠ¢
  exact hg.mul hf.memLp_top

-- todo: extract 3-4 lemmas from this proof
theorem conj (hf : BoundedCompactSupport f Î¼) : BoundedCompactSupport (star f) Î¼ where
  memLp_top := by
    refine âŸ¨RCLike.continuous_conj.comp_aestronglyMeasurable hf.aestronglyMeasurable, ?_âŸ©
    simp_rw [star]
    rw [MeasureTheory.eLpNorm_congr_enorm_ae (g := f)]
    Â· exact hf.memLp_top.eLpNorm_lt_top
    simp
  hasCompactSupport := by
    simp_rw [star]
    exact (hasCompactSupport_comp_left (by simp)).2 hf.hasCompactSupport

theorem const_mul (hf : BoundedCompactSupport f Î¼) (c : ğ•œ) :
    BoundedCompactSupport (fun x â†¦ c * f x) Î¼ :=
  hf.mul_bdd_left <| memLp_top_const _

theorem mul_const (hf : BoundedCompactSupport f Î¼) (c : ğ•œ) :
    BoundedCompactSupport (fun x â†¦ f x * c) Î¼ :=
  hf.mul_bdd_right <| memLp_top_const _

end Mul

/-- If `â€–fâ€–` is bounded by `g` and `g` is bounded compactly supported, then so is `f`. -/
theorem mono {g : X â†’ â„â‰¥0âˆ} (hg : BoundedCompactSupport g Î¼) (hf : AEStronglyMeasurable f Î¼)
    (hfg : âˆ€ x, â€–f xâ€–â‚‘ â‰¤ g x) : BoundedCompactSupport f Î¼ where
  memLp_top := âŸ¨hf, eLpNorm_mono_enorm hfg |>.trans_lt hg.memLp_top.eLpNorm_lt_topâŸ©
  hasCompactSupport := by
    refine hg.hasCompactSupport.mono ?_
    by_contra h
    simp only [support_subset_iff, ne_eq, mem_support, not_forall, Decidable.not_not] at h
    obtain âŸ¨x, hfx, hgxâŸ© := h
    specialize hfg x
    simp_rw [hgx, nonpos_iff_eq_zero, enorm_eq_zero, hfx] at hfg

-- use `mono` preferably
theorem mono_norm {g : X â†’ â„} (hg : BoundedCompactSupport g Î¼) (hf : AEStronglyMeasurable f Î¼)
    (hfg : âˆ€ x, â€–f xâ€– â‰¤ g x) : BoundedCompactSupport f Î¼ where
  memLp_top := âŸ¨hf, eLpNorm_mono_real hfg |>.trans_lt hg.memLp_top.eLpNorm_lt_topâŸ©
  hasCompactSupport := by
    refine hg.hasCompactSupport.mono ?_
    by_contra h
    simp only [support_subset_iff, ne_eq, mem_support, not_forall, Decidable.not_not] at h
    obtain âŸ¨x, hfx, hgxâŸ© := h
    specialize hfg x
    simp_rw [hgx, norm_le_zero_iff, hfx] at hfg

theorem toComplex {f : X â†’ â„} (hf : BoundedCompactSupport f Î¼) :
    BoundedCompactSupport (fun x â†¦ (f x : â„‚)) Î¼ :=
  mono (g := (â€–f Â·â€–â‚‘)) hf.enorm
    (Complex.continuous_ofReal.comp_aestronglyMeasurable hf.aestronglyMeasurable)
    (by simp [â† ofReal_norm])

section Sum

variable {Î¹ : Type*} {s : Finset Î¹} {F : Î¹ â†’ X â†’ E}

/-- A finite sum of bounded compactly supported functions is bounded compactly supported. -/
theorem finset_sum
    (hF : âˆ€ i âˆˆ s, BoundedCompactSupport (F i) Î¼) :
    BoundedCompactSupport (fun x â†¦ âˆ‘ i âˆˆ s, F i x) Î¼ := by
  classical
  induction s using Finset.induction with
  | empty => simp [BoundedCompactSupport.zero]
  | @insert j s hjs IH =>
    simp_rw [Finset.sum_insert hjs]
    apply BoundedCompactSupport.add
    Â· exact hF _ (Finset.mem_insert_self j s)
    Â· exact IH (fun i hi â†¦ hF _ (Finset.mem_insert_of_mem hi))

open Classical ComplexConjugate in
/-- Used in Proposition 2.0.4. -/
lemma sq_eLpNorm_le_sums [IsFiniteMeasureOnCompacts Î¼] {f : Î¹ â†’ X â†’ â„‚}
    (hf : âˆ€ i, BoundedCompactSupport (f i) Î¼) {s : Finset Î¹} :
    eLpNorm (âˆ‘ i âˆˆ s, f i Â·) 2 Î¼ ^ 2 â‰¤
    âˆ‘ i âˆˆ s, eLpNorm (f i) 2 Î¼ ^ 2 +
    âˆ‘ i âˆˆ s, âˆ‘ j âˆˆ s with i â‰  j, â€–âˆ« x, f i x * conj (f j x) âˆ‚Î¼â€–â‚‘ := by
  have lâ‚ {i : Î¹} : Integrable (fun x â†¦ f i x * conj (f i x)) Î¼ :=
    ((hf i).mul (hf i).conj).integrable
  have lâ‚‚ {i j : Î¹} : Integrable (fun x â†¦ f i x * conj (f j x)) Î¼ :=
    ((hf i).mul (hf j).conj).integrable
  calc
    _ = â€–âˆ« x, (âˆ‘ i âˆˆ s, f i x) * conj (âˆ‘ j âˆˆ s, f j x) âˆ‚Î¼â€–â‚‘ :=
      eLpNorm_two_eq_enorm_integral_mul_conj (memLp (finset_sum fun i _ â†¦ hf i) 2)
    _ = â€–âˆ« x, (âˆ‘ i âˆˆ s, f i x * conj (f i x) +
        âˆ‘ i âˆˆ s, âˆ‘ j âˆˆ s with i â‰  j, f i x * conj (f j x)) âˆ‚Î¼â€–â‚‘ := by
      congr! 3 with x; rw [Finset.sum_range_mul_conj_sum_range]
    _ = â€–(âˆ‘ i âˆˆ s, âˆ« x, f i x * conj (f i x) âˆ‚Î¼) +
        âˆ‘ i âˆˆ s, âˆ‘ j âˆˆ s with i â‰  j, âˆ« x, f i x * conj (f j x) âˆ‚Î¼â€–â‚‘ := by
      rw [integral_add]; rotate_left
      Â· exact integrable_finset_sum _ fun i mi â†¦ lâ‚
      Â· refine integrable_finset_sum _ fun i mi â†¦ integrable_finset_sum _ fun j mj â†¦ lâ‚‚
      congr
      Â· exact integral_finset_sum _ fun i mi â†¦ lâ‚
      Â· rw [integral_finset_sum _ fun i mi â†¦ integrable_finset_sum _ fun j mj â†¦ lâ‚‚]
        congr! with i mi; rw [integral_finset_sum _ fun j mj â†¦ lâ‚‚]
    _ â‰¤ â€–âˆ‘ i âˆˆ s, âˆ« x, f i x * conj (f i x) âˆ‚Î¼â€–â‚‘ +
        â€–âˆ‘ i âˆˆ s, âˆ‘ j âˆˆ s with i â‰  j, âˆ« x, f i x * conj (f j x) âˆ‚Î¼â€–â‚‘ := enorm_add_le _ _
    _ â‰¤ âˆ‘ i âˆˆ s, â€–âˆ« x, f i x * conj (f i x) âˆ‚Î¼â€–â‚‘ +
        âˆ‘ i âˆˆ s, â€–âˆ‘ j âˆˆ s with i â‰  j, âˆ« x, f i x * conj (f j x) âˆ‚Î¼â€–â‚‘ := by
      gcongr <;> exact enorm_sum_le _ _
    _ â‰¤ _ := by
      conv_rhs => enter [1, 2, i]; rw [eLpNorm_two_eq_enorm_integral_mul_conj ((hf i).memLp 2)]
      gcongr; exact enorm_sum_le _ _

end Sum

section Prod

variable {Y : Type*} [MeasureSpace Y] {f : X â†’ ğ•œ} {g : Y â†’ ğ•œ} {Î½ : Measure Y}
variable [TopologicalSpace Y]
variable [R1Space (X Ã— Y)]

/-- An elementary tensor of bounded compactly supported functions is
  bounded compactly supported. -/
theorem prod_mul (hf : BoundedCompactSupport f Î¼) (hg : BoundedCompactSupport g Î½) :
    BoundedCompactSupport (uncurry fun x y â†¦ f x * g y) (Î¼.prod Î½) where
  memLp_top := by
    -- todo: maybe separate out as lemmas
    have h2f : MemLp (fun z : X Ã— Y â†¦ f z.1) âˆ (Î¼.prod Î½) :=
      hf.memLp_top.comp_quasiMeasurePreserving Measure.quasiMeasurePreserving_fst
    have h2g : MemLp (fun z : X Ã— Y â†¦ g z.2) âˆ (Î¼.prod Î½) :=
      hg.memLp_top.comp_quasiMeasurePreserving Measure.quasiMeasurePreserving_snd
    -- todo: reorder arguments of `mul`
    exact h2g.mul (r := âˆ) h2f
  hasCompactSupport := by
    -- todo: separate out as lemmas
    apply HasCompactSupport.intro <| hf.hasCompactSupport.prod hg.hasCompactSupport
    intro âŸ¨x, yâŸ© hxy
    simp only [uncurry_apply_pair, mul_eq_zero]
    simp only [mem_prod, not_and] at hxy
    by_cases hx : x âˆˆ tsupport f
    Â· exact Or.inr (image_eq_zero_of_notMem_tsupport (hxy hx))
    Â· exact Or.inl (image_eq_zero_of_notMem_tsupport hx)

variable [R1Space X] in
theorem indicator_of_isCompact_closure {f : X â†’ E} (hf : MemLp f âˆ Î¼)
    {s : Set X} (h's : IsCompact (closure s)) (hs : MeasurableSet s) :
    BoundedCompactSupport (s.indicator f) Î¼ where
  memLp_top := hf.indicator hs
  hasCompactSupport := by
    apply HasCompactSupport.intro h's
    exact fun x hx â†¦ by simp [notMem_of_notMem_closure hx]

protected theorem indicator {f : X â†’ E} (hf : BoundedCompactSupport f Î¼) {s : Set X}
    (hs : MeasurableSet s) : BoundedCompactSupport (s.indicator f) Î¼ where
  memLp_top := hf.memLp_top.indicator hs
  hasCompactSupport := hf.hasCompactSupport.mono (by simp)

variable {F : X Ã— Y â†’ E}

-- -- prove when needed
-- theorem swap (hF : BoundedCompactSupport f Î¼) : BoundedCompactSupport (F âˆ˜ Prod.swap)

variable {F : X Ã— Y â†’ E}

-- ---- adapt and prove below when needed
-- theorem prod_left (hF : BoundedCompactSupport f Î¼) :
--     âˆ€ y, BoundedCompactSupport (fun x â†¦ F (x, y)) := fun y â†¦ {
--   memLp_top := by
--     rcases isBounded_range_iff_forall_norm_le.1 hF.isBounded with âŸ¨C, hCâŸ©
--     apply isBounded_range_iff_forall_norm_le.2 âŸ¨C, fun x â†¦ ?_âŸ©
--     exact hC (x, y)
--   stronglyMeasurable := hF.stronglyMeasurable.comp_measurable measurable_prodMk_right
--   hasCompactSupport :=
--   -- by
--   --   apply HasCompactSupport.intro
-- }


-- theorem prod_right_ae (hF : BoundedCompactSupport f Î¼) :
--     âˆ€áµ x, BoundedCompactSupport (fun y â†¦ F (x, y)) := hF.swap.prod_left_ae

-- theorem integral_prod_left (hF : BoundedCompactSupport f Î¼) :
--     BoundedCompactSupport (fun x â†¦ âˆ« y, F (x, y)) :=
-- --   have := hF.integrable.integrable_prod_left

-- theorem integral_prod_right (hF : BoundedCompactSupport f Î¼) :
--     BoundedCompactSupport (fun y â†¦ âˆ« x, F (x, y)) := hF.swap.integral_prod_left

end Prod

end NormedAddCommGroup


end BoundedCompactSupport

end

section Metric
namespace BoundedCompactSupport

variable {X E : Type*} [MetricSpace X] [MeasurableSpace X] {Î¼ : Measure X}
  [TopologicalSpace E] [ENorm E] [Zero E]
  {f : X â†’ E}

theorem isBoundedTSupport (hf : BoundedCompactSupport f Î¼) : IsBounded (tsupport f) :=
  hf.hasCompactSupport.isBounded

theorem isBoundedSupport (hf : BoundedCompactSupport f Î¼) : IsBounded (support f) :=
  hf.isBoundedTSupport.subset <| subset_tsupport f


end BoundedCompactSupport

end Metric

end MeasureTheory

section

open Bornology ENNReal MeasureTheory Set

variable {X ğ•œ E F : Type*} [MeasurableSpace X] [MetricSpace X]
variable [NormedAddCommGroup E]
variable {Y W : Type*} [MeasurableSpace Y] [TopologicalSpace Y]
variable [MeasurableSpace W] [TopologicalSpace W] {Î¼ : Measure W}
variable {f : X â†’ ğ•œ} {Î½ : Measure X} [RCLike ğ•œ]

lemma BoundedCompactSupport.mul_bdd_right'' (hf : BoundedCompactSupport f Î½) {e : W â†’ X}
    {g : W â†’ ğ•œ} (he : Continuous e) (he1 : Measure.QuasiMeasurePreserving e Î¼ Î½)
    (hg : AEStronglyMeasurable g Î¼)
    (hg1 : âˆ€ K : Set X, IsCompact K -> IsCompact (e â»Â¹' K âˆ© tsupport g))
    (hg2 : âˆ€ (A : Set X) (_hA : IsBounded A), IsBounded (g '' (e â»Â¹' A))) :
    BoundedCompactSupport (fun x â†¦ f (e x) * g x) Î¼ where
  memLp_top := by
    have := hf.memLp_top.ae_norm_le
    set B := (eLpNorm f âŠ¤ Î½).toReal
    obtain âŸ¨C, hCâŸ© := isBounded_iff_forall_norm_le.1
      (hg2 (tsupport f) hf.hasCompactSupport.isBounded)
    apply memLp_top_of_bound (C := max 0 B * max 0 C)
    Â· exact (hf.mono_ac he1.absolutelyContinuous |>.aestronglyMeasurable.comp_measurable
        he1.measurable).mul hg
    filter_upwards [he1.ae this] with z hB
    rw [norm_mul]
    by_cases hz : z âˆˆ e â»Â¹' tsupport f
    Â· gcongr
      Â· exact hB.trans (le_max_right ..)
      refine hC _ ?_ |>.trans (le_max_right ..)
      exact mem_image_of_mem g hz
    Â· simp only [image_eq_zero_of_notMem_tsupport hz, norm_zero, zero_mul,
        mul_nonneg (le_max_left 0 B) (le_max_left 0 C)]
  hasCompactSupport := by
    refine IsCompact.of_isClosed_subset (hg1 _ hf.hasCompactSupport)
      (isClosed_tsupport fun x â†¦ f (e x) * g x) ?_
    apply subset_inter ?_ tsupport_mul_subset_right
    apply subset_trans (tsupport_mul_subset_left)
    rw [tsupport, ((isClosed_tsupport f).preimage he ).closure_subset_iff]
    exact fun _ hx â†¦ subset_closure hx

lemma BoundedCompactSupport.mul_bdd_left' (hf : BoundedCompactSupport f Î½) {e : W â†’ X} {g : W â†’ ğ•œ}
    (he : Continuous e) (he1 : Measure.QuasiMeasurePreserving e Î¼ Î½)
    (hg : AEStronglyMeasurable g Î¼)
    (hg1 : âˆ€ K : Set X, IsCompact K -> IsCompact (e â»Â¹' K âˆ© tsupport g))
    (hg2 : âˆ€ (A : Set X) (_hA : IsBounded A), IsBounded (g '' (e â»Â¹' A))) :
    BoundedCompactSupport (fun x â†¦ g x * f (e x)) Î¼:= by
  simp_rw [mul_comm]; exact mul_bdd_right'' hf he he1 hg hg1 hg2

end
