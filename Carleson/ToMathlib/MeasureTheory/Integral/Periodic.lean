import Mathlib.MeasureTheory.Integral.Periodic
import Mathlib.Analysis.Convolution
import Carleson.ToMathlib.Topology.Instances.AddCircle

open Set Function MeasureTheory MeasureTheory.Measure TopologicalSpace AddSubgroup intervalIntegral

open scoped MeasureTheory NNReal ENNReal

open scoped Convolution

namespace AddCircle

variable {B : Type*}

section AE

variable {p a : ‚Ñù} [hp : Fact (0 < p)] (f : ‚Ñù ‚Üí B)

instance noAtoms_volume : NoAtoms (volume : Measure (AddCircle p)) where
  measure_singleton x := by simpa [hp.out.le] using AddCircle.volume_closedBall p (x := x) 0

theorem liftIoc_eq_liftIco_of_ne {x : AddCircle p} (x_ne_a : x ‚â† a) :
    liftIoc p a f x = liftIco p a f x := by
  let b := QuotientAddGroup.equivIcoMod hp.out a x
  have x_eq_b : x = ‚Üëb := (QuotientAddGroup.equivIcoMod hp.out a).apply_eq_iff_eq_symm_apply.mp rfl
  have hb := mem_Ico.mp (Subtype.coe_prop b)
  rw [x_eq_b, liftIco_coe_apply (Subtype.coe_prop b)]
  exact liftIoc_coe_apply ‚ü®lt_of_le_of_ne hb.1 (x_ne_a <| ¬∑ ‚ñ∏ x_eq_b), hb.2.le‚ü©

theorem liftIoc_ae_eq_liftIco : liftIoc p a f =·∂†[ae volume] liftIco p a f :=
  Filter.Eventually.mono (by simp [Filter.Eventually, ae]) (fun _ ‚Ü¶ liftIoc_eq_liftIco_of_ne f)

end AE

section Measurability

variable (p a : ‚Ñù) [hp : Fact (0 < p)] {f : ‚Ñù ‚Üí B}

theorem liftIoc_aestronglyMeasurable [TopologicalSpace B] (hf : AEStronglyMeasurable f) :
    AEStronglyMeasurable (liftIoc p a f) := by
  have h : Measurable (equivIoc p a) := (AddCircle.measurableEquivIoc p a).measurable_toFun
  refine AEStronglyMeasurable.comp_measurable ?_ measurable_subtype_coe |>.comp_measurable h
  convert hf.restrict (s := Ioc a (a + p))
  rw [‚Üê (AddCircle.measurePreserving_mk p a).map_eq]
  rw [Measure.map_map measurable_subtype_coe h, Measure.map_map (measurable_subtype_coe.comp h)]
  ¬∑ exact (Measure.map_congr <| Filter.Eventually.mono (self_mem_ae_restrict measurableSet_Ioc) <|
      fun x hx ‚Ü¶ AddCircle.liftIoc_coe_apply hx).trans Measure.map_id
  ¬∑ exact fun _ ‚Ü¶ id

theorem liftIco_aestronglyMeasurable [TopologicalSpace B] (hf : AEStronglyMeasurable f) :
    AEStronglyMeasurable (liftIco p a f) :=
  (liftIoc_aestronglyMeasurable p a hf).congr (liftIoc_ae_eq_liftIco f)

theorem liftIoc_aemeasurable [MeasurableSpace B] (hf : AEMeasurable f) :
    AEMeasurable (liftIoc p a f) := by
  have h : Measurable (equivIoc p a) := (AddCircle.measurableEquivIoc p a).measurable_toFun
  refine AEMeasurable.comp_measurable ?_ measurable_subtype_coe |>.comp_measurable h
  convert hf.restrict (s := Ioc a (a + p))
  rw [‚Üê (AddCircle.measurePreserving_mk p a).map_eq]
  rw [Measure.map_map measurable_subtype_coe h, Measure.map_map (measurable_subtype_coe.comp h)]
  ¬∑ exact (Measure.map_congr <| Filter.Eventually.mono (self_mem_ae_restrict measurableSet_Ioc) <|
      fun x hx ‚Ü¶ AddCircle.liftIoc_coe_apply (f := id (Œ± := ‚Ñù)) hx).trans Measure.map_id
  ¬∑ exact fun _ ‚Ü¶ id

theorem liftIco_aemeasurable [MeasurableSpace B] (hf : AEMeasurable f) :
    AEMeasurable (liftIco p a f) :=
  (liftIoc_aemeasurable p a hf).congr (liftIoc_ae_eq_liftIco f)

end Measurability

section Convolution

variable {ùïú : Type*} {E : Type*} {E' : Type*} {F : Type*} [NormedAddCommGroup E]
  [NormedAddCommGroup E'] [NormedAddCommGroup F] [NontriviallyNormedField ùïú] [NormedSpace ùïú E]
  [NormedSpace ùïú E'] [NormedSpace ùïú F] [NormedSpace ‚Ñù F] (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
  {f : ‚Ñù ‚Üí E} {g : ‚Ñù ‚Üí E'}

variable {T : ‚Ñù} [hT : Fact (0 < T)] (a : ‚Ñù)

theorem convolution_liftIco (hf : f.Periodic T) (hg : g.Periodic T) :
    liftIco T a f ‚ãÜ[L] liftIco T a g = liftIco T a fun x ‚Ü¶ ‚à´ y in a..a+T, L (f y) (g (x - y)) := by
  refine funext (fun q ‚Ü¶ QuotientAddGroup.induction_on q (fun x ‚Ü¶ ?_))
  have : Periodic (fun x ‚Ü¶ ‚à´ y in a..a+T, L (f y) (g (x-y))) T := by
    intro; refine integral_congr (fun _ _ ‚Ü¶ ?_); rw [add_sub_right_comm, hg]
  rw [convolution, ‚Üê AddCircle.intervalIntegral_preimage T a, liftIco_coe_apply_of_periodic a this]
  refine integral_congr (fun y _ ‚Ü¶ ?_)
  rw [AddCircle.liftIco_coe_apply_of_periodic a hf, ‚Üê AddCircle.liftIco_coe_apply_of_periodic a hg]
  rfl

theorem convolution_liftIoc (hf : f.Periodic T) (hg : g.Periodic T) :
    liftIoc T a f ‚ãÜ[L] liftIoc T a g = liftIoc T a fun x ‚Ü¶ ‚à´ y in a..a+T, L (f y) (g (x - y)) := by
  have : Periodic (fun x ‚Ü¶ ‚à´ y in a..a+T, L (f y) (g (x-y))) T := by
    intro; refine integral_congr (fun _ _ ‚Ü¶ ?_); rw [add_sub_right_comm, hg]
  rw [‚Üê liftIco_eq_liftIoc a a hf, ‚Üê liftIco_eq_liftIoc a a hg, ‚Üê liftIco_eq_liftIoc a a this]
  exact convolution_liftIco L a hf hg

end Convolution

section eLpNorm

variable {ùïú B : Type*} [NormedAddCommGroup B]

variable (T : ‚Ñù) [hT : Fact (0 < T)] (a a' : ‚Ñù) {f : ‚Ñù ‚Üí B} (hf : AEStronglyMeasurable f)
include hf

/-- The norm of the lift of a function `f` is equal to the norm of `f` on that period. -/
theorem eLpNorm_liftIoc (p : ‚Ñù‚â•0‚àû) :
    eLpNorm (AddCircle.liftIoc T a f) p = eLpNorm ((Set.Ioc a (a + T)).indicator f) p := by
  set I := Ioc a (a + T)
  have : I.indicator f = I.indicator (liftIoc T a f ‚àò QuotientAddGroup.mk) := by
    ext x
    by_cases hx : x ‚àà I
    ¬∑ simpa [hx] using (liftIoc_coe_apply hx).symm
    ¬∑ simp [hx]
  rw [this, eLpNorm_indicator_eq_eLpNorm_restrict measurableSet_Ioc]
  refine (eLpNorm_comp_measurePreserving (liftIoc_aestronglyMeasurable T a hf) ?_).symm
  exact AddCircle.measurePreserving_mk T a

/-- The norm of the lift of a function `f` is equal to the norm of `f` on that period. -/
theorem eLpNorm_liftIco (p : ‚Ñù‚â•0‚àû) :
    eLpNorm (AddCircle.liftIco T a f) p = eLpNorm ((Set.Ico a (a + T)).indicator f) p := by
  rw [eLpNorm_congr_ae (liftIoc_ae_eq_liftIco f).symm, eLpNorm_liftIoc T a hf,
    eLpNorm_indicator_eq_eLpNorm_restrict measurableSet_Ico,
    eLpNorm_indicator_eq_eLpNorm_restrict measurableSet_Ioc, restrict_Ico_eq_restrict_Ioc]

/-- The norm of the lift of a periodic function `f` is equal to the norm of `f` on any period. -/
theorem eLpNorm_liftIoc_of_periodic (hfT : Periodic f T) (p : ‚Ñù‚â•0‚àû) :
    eLpNorm (AddCircle.liftIoc T a f) p = eLpNorm ((Set.Ioc a' (a' + T)).indicator f) p := by
  rw [liftIoc_eq_liftIoc a a' hfT, eLpNorm_liftIoc T a' hf p]

/-- The norm of the lift of a periodic function `f` is equal to the norm of `f` on any period. -/
theorem eLpNorm_liftIco_of_periodic (hfT : Periodic f T) (p : ‚Ñù‚â•0‚àû) :
    eLpNorm (AddCircle.liftIco T a f) p = eLpNorm ((Set.Ico a' (a' + T)).indicator f) p := by
  rw [liftIco_eq_liftIco a a' hfT, eLpNorm_liftIco T a' hf p]

end eLpNorm

end AddCircle
