import Carleson.ToMathlib.BoundedFiniteSupport
import Carleson.ToMathlib.Misc
import Mathlib.Analysis.SpecialFunctions.ImproperIntegrals
import Mathlib.Analysis.SpecialFunctions.Pow.Integral

noncomputable section

open NNReal ENNReal NormedSpace MeasureTheory Set Filter Topology Function

namespace MeasureTheory

variable {Œ± Œ±' Œµ Œµ‚ÇÅ Œµ‚ÇÇ Œµ‚ÇÉ ùïú E E‚ÇÅ E‚ÇÇ E‚ÇÉ : Type*} {m : MeasurableSpace Œ±} {m : MeasurableSpace Œ±'}
  {p p' q : ‚Ñù‚â•0‚àû} {c : ‚Ñù‚â•0‚àû}
  {Œº : Measure Œ±} {ŒΩ : Measure Œ±'} [NontriviallyNormedField ùïú]
  {t s x y : ‚Ñù‚â•0‚àû} {T : (Œ± ‚Üí Œµ‚ÇÅ) ‚Üí (Œ±' ‚Üí Œµ‚ÇÇ)}

section ENorm

variable [ENorm Œµ] {f g g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œµ}

/- Proofs for this file can be found in
Folland, Real Analysis. Modern Techniques and Their Applications, section 6.3. -/

/-! # The distribution function `d_f` -/

/-- The distribution function of a function `f`.
Todo: rename to something more Mathlib-appropriate. -/
def distribution (f : Œ± ‚Üí Œµ) (t : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) : ‚Ñù‚â•0‚àû :=
  Œº { x | t < ‚Äñf x‚Äñ‚Çë }

@[simp]
lemma distibution_top (f : Œ± ‚Üí Œµ) (Œº : Measure Œ±) : distribution f ‚àû Œº = 0 := by simp [distribution]

@[gcongr]
lemma distribution_mono_right (h : t ‚â§ s) : distribution f s Œº ‚â§ distribution f t Œº :=
  measure_mono fun _ a ‚Ü¶ lt_of_le_of_lt h a

lemma distribution_mono_right' : (Antitone (fun t ‚Ü¶ distribution f t Œº)) :=
  fun _ _ h ‚Ü¶ distribution_mono_right h

@[measurability, fun_prop]
lemma distribution_measurable‚ÇÄ : Measurable (fun t ‚Ü¶ distribution f t Œº) :=
  Antitone.measurable (distribution_mono_right' (f := f) (Œº := Œº))

@[measurability, fun_prop]
lemma distribution_measurable {g : Œ±' ‚Üí ‚Ñù‚â•0‚àû} (hg : Measurable g) :
    Measurable (fun y : Œ±' ‚Ü¶ distribution f (g y) Œº) := by
  fun_prop

lemma distribution_toReal_le {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} :
    distribution (ENNReal.toReal ‚àò f) t Œº ‚â§ distribution f t Œº := by
  simp_rw [distribution]
  apply measure_mono
  simp_rw [comp_apply, enorm_eq_self, setOf_subset_setOf]
  exact fun x hx ‚Ü¶ hx.trans_le enorm_toReal_le

lemma distribution_toReal_eq {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : ‚àÄ·µê x ‚àÇŒº, f x ‚â† ‚àû) :
    distribution (ENNReal.toReal ‚àò f) t Œº = distribution f t Œº := by
  refine measure_congr (.set_eq ?_)
  filter_upwards [hf] with x hx
  simp [hx]

lemma distribution_add_le_of_enorm {A : ‚Ñù‚â•0‚àû}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ A * (‚Äñg‚ÇÅ x‚Äñ‚Çë + ‚Äñg‚ÇÇ x‚Äñ‚Çë)) :
    distribution f (A * (t + s)) Œº ‚â§ distribution g‚ÇÅ t Œº + distribution g‚ÇÇ s Œº := by
  unfold distribution
  have h‚ÇÅ : Œº ({x | A * (t + s) < ‚Äñf x‚Äñ‚Çë} \
      ({x | t < ‚Äñg‚ÇÅ x‚Äñ‚Çë} ‚à™ {x | s < ‚Äñg‚ÇÇ x‚Äñ‚Çë})) = 0 := by
    apply measure_mono_null ?_ h
    intro x
    simp only [mem_diff, mem_setOf_eq, mem_union, not_or, not_lt, mem_compl_iff, not_le, and_imp]
    refine fun h‚ÇÅ h‚ÇÇ h‚ÇÉ ‚Ü¶ lt_of_le_of_lt ?_ h‚ÇÅ
    gcongr
  calc
    Œº {x | A * (t + s) < ‚Äñf x‚Äñ‚Çë}
      ‚â§ Œº ({x | t < ‚Äñg‚ÇÅ x‚Äñ‚Çë} ‚à™ {x | s < ‚Äñg‚ÇÇ x‚Äñ‚Çë}) := measure_mono_ae' h‚ÇÅ
    _ ‚â§ Œº {x | t < ‚Äñg‚ÇÅ x‚Äñ‚Çë} + Œº {x | s < ‚Äñg‚ÇÇ x‚Äñ‚Çë} := measure_union_le _ _

lemma approx_above_superset (t‚ÇÄ : ‚Ñù‚â•0‚àû) :
    ‚ãÉ n, (fun n : ‚Ñï ‚Ü¶ {x | t‚ÇÄ + (‚Üën)‚Åª¬π < ‚Äñf x‚Äñ‚Çë}) n = {x | t‚ÇÄ < ‚Äñf x‚Äñ‚Çë} := by
  ext y
  constructor <;> intro h
  ¬∑ obtain ‚ü®n, wn‚ü© := exists_exists_eq_and.mp h
    calc
      t‚ÇÄ ‚â§ t‚ÇÄ + (‚Üën)‚Åª¬π := le_self_add
      _  < ‚Äñf y‚Äñ‚Çë      := wn
  ¬∑ have h‚ÇÅ : Iio (‚Äñf y‚Äñ‚Çë - t‚ÇÄ) ‚àà ùìù 0 := Iio_mem_nhds (tsub_pos_of_lt h)
    have h‚ÇÇ := ENNReal.tendsto_inv_nat_nhds_zero h‚ÇÅ
    simp only [mem_map, mem_atTop_sets, mem_preimage, mem_Iio] at h‚ÇÇ
    rcases h‚ÇÇ with ‚ü®n, wn‚ü©
    simpa using ‚ü®n, lt_tsub_iff_left.mp (wn n (Nat.le_refl n))‚ü©

lemma tendsto_measure_iUnion_distribution (t‚ÇÄ : ‚Ñù‚â•0‚àû) :
    Filter.Tendsto (‚áëŒº ‚àò (fun n : ‚Ñï ‚Ü¶ {x | t‚ÇÄ + (‚Üën)‚Åª¬π < ‚Äñf x‚Äñ‚Çë}))
      Filter.atTop (nhds (Œº ({x | t‚ÇÄ < ‚Äñf x‚Äñ‚Çë}))) := by
  rw [‚Üê approx_above_superset]
  refine tendsto_measure_iUnion_atTop fun a b h x h‚ÇÅ ‚Ü¶ ?_
  calc
    _ ‚â§ t‚ÇÄ + (‚Üëa)‚Åª¬π := by gcongr
    _ < _ := h‚ÇÅ

lemma select_neighborhood_distribution (t‚ÇÄ : ‚Ñù‚â•0‚àû) (l : ‚Ñù‚â•0‚àû)
    (hu : l < distribution f t‚ÇÄ Œº) :
    ‚àÉ n : ‚Ñï, l < distribution f (t‚ÇÄ + (‚Üën)‚Åª¬π) Œº := by
  have h := (tendsto_measure_iUnion_distribution t‚ÇÄ) (Ioi_mem_nhds hu)
  simp only [mem_map, mem_atTop_sets, mem_preimage, comp_apply, mem_Ioi] at h
  rcases h with ‚ü®n, wn‚ü©
  exact ‚ü®n, wn n (Nat.le_refl n)‚ü©

lemma continuousWithinAt_distribution (t‚ÇÄ : ‚Ñù‚â•0‚àû) :
    ContinuousWithinAt (distribution f ¬∑ Œº) (Ioi t‚ÇÄ) t‚ÇÄ := by
  rcases (eq_top_or_lt_top t‚ÇÄ) with t‚ÇÄtop | t‚ÇÄnottop
  ¬∑ rw [t‚ÇÄtop]
    apply continuousWithinAt_of_notMem_closure
    simp
  ¬∑ unfold ContinuousWithinAt
    rcases (eq_top_or_lt_top (distribution f t‚ÇÄ Œº)) with db_top | db_not_top
    -- Case: distribution f t‚ÇÄ Œº = ‚ä§
    ¬∑ simp only [db_top, ENNReal.tendsto_nhds_top_iff_nnreal]
      intro b
      have h‚ÇÄ : ‚àÉ n : ‚Ñï, ‚Üëb < distribution f (t‚ÇÄ + (‚Üën)‚Åª¬π) Œº :=
        select_neighborhood_distribution _ _ (db_top ‚ñ∏ coe_lt_top)
      rcases h‚ÇÄ with ‚ü®n, wn‚ü©
      refine eventually_mem_set.mpr (mem_inf_iff_superset.mpr ‚ü®Iio (t‚ÇÄ + (‚Üën)‚Åª¬π), ?_, ?_‚ü©)
      ¬∑ exact Iio_mem_nhds (lt_add_right t‚ÇÄnottop.ne_top
          (ENNReal.inv_ne_zero.mpr (ENNReal.natCast_ne_top n)))
      ¬∑ exact ‚ü®Ioi t‚ÇÄ, by simp, fun z h‚ÇÅ ‚Ü¶ wn.trans_le (distribution_mono_right (le_of_lt h‚ÇÅ.1))‚ü©
    -- Case: distribution f t‚ÇÄ Œº < ‚ä§
    ¬∑ refine (ENNReal.tendsto_nhds db_not_top.ne_top).mpr fun Œµ Œµ_gt_0 ‚Ü¶
        eventually_mem_set.mpr (mem_inf_iff_superset.mpr ?_)
      rcases eq_zero_or_pos (distribution f t‚ÇÄ Œº) with db_zero | db_not_zero
      -- Case: distribution f t‚ÇÄ Œº = 0
      ¬∑ use Ico 0 (t‚ÇÄ + 1)
        constructor
        ¬∑ refine IsOpen.mem_nhds isOpen_Ico_zero ?_
          simp [lt_add_right t‚ÇÄnottop.ne_top one_ne_zero]
        ¬∑ use Ioi t‚ÇÄ
          refine ‚ü®by simp, fun z hz ‚Ü¶ ?_‚ü©
          rw [db_zero]
          simp only [zero_le, tsub_eq_zero_of_le, zero_add]
          have h‚ÇÇ : distribution f z Œº ‚â§ distribution f t‚ÇÄ Œº :=
            distribution_mono_right (le_of_lt hz.2)
          rw [db_zero] at h‚ÇÇ
          change Icc 0 Œµ (distribution f z Œº)
          rw [nonpos_iff_eq_zero.mp h‚ÇÇ]
          exact ‚ü®zero_le 0, zero_le Œµ‚ü©
      -- Case: 0 < distribution f t‚ÇÄ Œº
      ¬∑ obtain ‚ü®n, wn‚ü© :=
          select_neighborhood_distribution t‚ÇÄ _ (ENNReal.sub_lt_self db_not_top.ne_top
              (ne_of_lt db_not_zero).symm (ne_of_lt Œµ_gt_0).symm)
        use Iio (t‚ÇÄ + (‚Üën)‚Åª¬π)
        constructor
        ¬∑ exact Iio_mem_nhds (lt_add_right t‚ÇÄnottop.ne_top
            (ENNReal.inv_ne_zero.mpr (ENNReal.natCast_ne_top n)))
        ¬∑ refine ‚ü®Ioi t‚ÇÄ, by simp, fun z h ‚Ü¶ ‚ü®?_, ?_‚ü©‚ü©
          ¬∑ calc
              distribution f t‚ÇÄ Œº - Œµ
                ‚â§ distribution f (t‚ÇÄ + (‚Üën)‚Åª¬π) Œº := le_of_lt wn
              _ ‚â§ distribution f z Œº             := distribution_mono_right (le_of_lt h.1)
          ¬∑ calc
              distribution f z Œº
                ‚â§ distribution f t‚ÇÄ Œº := distribution_mono_right (le_of_lt h.2)
              _ ‚â§ distribution f t‚ÇÄ Œº + Œµ := le_self_add

-- TODO: investigate generalising this to enorm classes
-- This requires adding enorm versions of NormOneClass and NormMulClass
lemma distribution_pow (Œµ : Type*) [SeminormedRing Œµ] [NormOneClass Œµ] [NormMulClass Œµ] (f : Œ± ‚Üí Œµ)
    (t : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) {n : ‚Ñï} (hn : n ‚â† 0) :
    distribution (f ^ n) (t ^ n) Œº = distribution f t Œº := by
  simp_rw [distribution, Pi.pow_apply]
  refine congrArg Œº <| ext fun x ‚Ü¶ ‚ü®fun hx ‚Ü¶ ?_, fun hx ‚Ü¶ ?_‚ü©
  ¬∑ rw [mem_setOf_eq, enorm_pow (f x) n] at hx; simpa using lt_of_pow_lt_pow_left' n hx
  ¬∑ rw [mem_setOf_eq, enorm_pow (f x) n]; exact ENNReal.pow_right_strictMono hn hx

/-- The weak L^p norm of a function, for `p < ‚àû` -/
def wnorm' (f : Œ± ‚Üí Œµ) (p : ‚Ñù) (Œº : Measure Œ±) : ‚Ñù‚â•0‚àû :=
  ‚®Ü t : ‚Ñù‚â•0, t * distribution f t Œº ^ (p : ‚Ñù)‚Åª¬π

lemma wnorm'_zero (f : Œ± ‚Üí Œµ) (Œº : Measure Œ±) : wnorm' f 0 Œº = ‚àû := by
  simp only [wnorm', GroupWithZero.inv_zero, ENNReal.rpow_zero, mul_one, iSup_eq_top]
  refine fun b hb ‚Ü¶ ‚ü®b.toNNReal + 1, ?_‚ü©
  rw [coe_add, ENNReal.coe_one, coe_toNNReal hb.ne_top]
  exact lt_add_right hb.ne_top one_ne_zero

lemma wnorm'_toReal_le {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} {p : ‚Ñù} (hp : 0 ‚â§ p) :
    wnorm' (ENNReal.toReal ‚àò f) p Œº ‚â§ wnorm' f p Œº := by
  refine iSup_mono fun x ‚Ü¶ ?_
  gcongr
  exact distribution_toReal_le

lemma wnorm'_toReal_eq {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} {p : ‚Ñù} (hf : ‚àÄ·µê x ‚àÇŒº, f x ‚â† ‚àû) :
    wnorm' (ENNReal.toReal ‚àò f) p Œº = wnorm' f p Œº := by
  simp_rw [wnorm', distribution_toReal_eq hf]

/-- The weak L^p norm of a function. -/
def wnorm (f : Œ± ‚Üí Œµ) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) : ‚Ñù‚â•0‚àû :=
  if p = ‚àû then eLpNormEssSup f Œº else wnorm' f (ENNReal.toReal p) Œº

@[simp]
lemma wnorm_zero : wnorm f 0 Œº = ‚àû := by
  simp [wnorm, wnorm'_zero]

@[simp]
lemma wnorm_top : wnorm f ‚ä§ Œº = eLpNormEssSup f Œº := by simp [wnorm]

lemma wnorm_ne_top (h : p ‚â† ‚ä§) : wnorm f p Œº = wnorm' f p.toReal Œº := by simp [wnorm, h]

lemma wnorm_coe {p : ‚Ñù‚â•0} : wnorm f p Œº = wnorm' f p Œº := by simp [wnorm]

lemma wnorm_ofReal {p : ‚Ñù} (hp : 0 ‚â§ p) : wnorm f (.ofReal p) Œº = wnorm' f p Œº := by
  simp [wnorm, hp]

lemma wnorm_toReal_le {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} {p : ‚Ñù‚â•0‚àû} :
    wnorm (ENNReal.toReal ‚àò f) p Œº ‚â§ wnorm f p Œº := by
  induction p
  ¬∑ simp [eLpNormEssSup_toReal_le]
  exact wnorm'_toReal_le toReal_nonneg

lemma wnorm_toReal_eq {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} {p : ‚Ñù‚â•0‚àû} (hf : ‚àÄ·µê x ‚àÇŒº, f x ‚â† ‚àû) :
    wnorm (ENNReal.toReal ‚àò f) p Œº = wnorm f p Œº := by
  simp_rw [wnorm, eLpNormEssSup_toReal_eq hf, wnorm'_toReal_eq hf]

/-
--TODO: move
theorem ENNReal.exists_pos_lt_mul {a : ‚Ñù‚â•0‚àû} (h : 0 < a)
    (b : ‚Ñù‚â•0‚àû) : ‚àÉ c, 0 < c ‚àß b < c * a := by
  sorry

lemma wnorm_eq_top {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} {p : ‚Ñù‚â•0‚àû} (ae_finite : distribution f ‚ä§ Œº ‚â† 0) :
    wnorm f p Œº = ‚ä§ := by
  unfold wnorm
  split_ifs
  ¬∑ sorry
  unfold wnorm'
  --rw [eq_top_iff]
  --apply iSup_mul_le
  rw [‚Üê ENNReal.bot_eq_zero, ‚Üê bot_lt_iff_ne_bot] at ae_finite
  rw [iSup_eq_top]
  intro b hb
  rcases ENNReal.exists_pos_lt_mul ae_finite b with ‚ü®i, i_pos, hi‚ü©
  use i.toNNReal
  rw [coe_toNNReal]

  --constructor
  --¬∑ apply distribution_mono_right _ ae_finite
  --sorry
-/

/-- A function is in weak-L^p if it is (strongly a.e.)-measurable and has finite weak L^p norm. -/
def MemWLp [TopologicalSpace Œµ] (f : Œ± ‚Üí Œµ) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) : Prop :=
  AEStronglyMeasurable f Œº ‚àß wnorm f p Œº < ‚àû

lemma MemWLp_zero [TopologicalSpace Œµ] : ¬¨ MemWLp f 0 Œº := by
  simp [MemWLp, wnorm_zero]

lemma MemWLp.aeStronglyMeasurable [TopologicalSpace Œµ] (hf : MemWLp f p Œº) : AEStronglyMeasurable f Œº := hf.1

lemma MemWLp.wnorm_lt_top [TopologicalSpace Œµ] (hf : MemWLp f p Œº) : wnorm f p Œº < ‚ä§ := hf.2

lemma MemWLp.ennreal_toReal {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : MemWLp f p Œº) :
    MemWLp (ENNReal.toReal ‚àò f) p Œº :=
  ‚ü®hf.aeStronglyMeasurable.ennreal_toReal, wnorm_toReal_le.trans_lt hf.2‚ü©

/-- If a function `f` is `MemWLp`, then its norm is almost everywhere finite. -/
theorem MemWLp.ae_ne_top [TopologicalSpace Œµ] (hf : MemWLp f p Œº) :
    ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â† ‚àû := by
  by_cases hp_inf : p = ‚àû
  ¬∑ rw [hp_inf] at hf
    simp_rw [‚Üê lt_top_iff_ne_top]
    exact ae_lt_of_essSup_lt hf.2
  by_cases hp_zero : p = 0
  ¬∑ exact (MemWLp_zero <| hp_zero ‚ñ∏ hf).elim
  set A := {x | ‚Äñf x‚Äñ‚Çë = ‚àû} with hA
  simp only [MemWLp, wnorm, wnorm', hp_inf] at hf
  rw [Filter.eventually_iff, mem_ae_iff]
  simp only [ne_eq, compl_def, mem_setOf_eq, Decidable.not_not, ‚Üê hA]
  have hp_toReal_zero := toReal_ne_zero.mpr ‚ü®hp_zero, hp_inf‚ü©
  have h1 (t : ‚Ñù‚â•0) : Œº A ‚â§ distribution f t Œº := by
    refine Œº.mono ?_
    simp_all only [setOf_subset_setOf, coe_lt_top, implies_true, A]
  set C := ‚®Ü t : ‚Ñù‚â•0, t * distribution f t Œº ^ p.toReal‚Åª¬π
  by_cases hC_zero : C = 0
  ¬∑ simp only [ENNReal.iSup_eq_zero, mul_eq_zero, ENNReal.rpow_eq_zero_iff, inv_neg'', C] at hC_zero
    specialize hC_zero 1
    simp only [one_ne_zero, ENNReal.coe_one, toReal_nonneg.not_gt, and_false, or_false,
      false_or] at hC_zero
    exact measure_mono_null (setOf_subset_setOf.mpr fun x hx => hx ‚ñ∏ one_lt_top) hC_zero.1
  by_contra h
  have h2 : C < ‚àû := by aesop
  have h3 (t : ‚Ñù‚â•0) : distribution f t Œº ‚â§ (C / t) ^ p.toReal := by
    rw [‚Üê rpow_inv_rpow hp_toReal_zero (distribution ..)]
    refine rpow_le_rpow ?_ toReal_nonneg
    rw [ENNReal.le_div_iff_mul_le (Or.inr hC_zero) (Or.inl coe_ne_top), mul_comm]
    exact le_iSup_iff.mpr fun _ a ‚Ü¶ a t
  have h4 (t : ‚Ñù‚â•0) : Œº A ‚â§ (C / t) ^ p.toReal := (h1 t).trans (h3 t)
  have h5 : Œº A ‚â§ Œº A / 2 := by
    convert h4 (C * (2 / Œº A) ^ p.toReal‚Åª¬π).toNNReal
    rw [coe_toNNReal ?_]
    swap
    ¬∑ refine mul_ne_top h2.ne_top (rpow_ne_top_of_nonneg (inv_nonneg.mpr toReal_nonneg) ?_)
      simp [div_eq_top, h]
    nth_rw 1 [‚Üê mul_one C]
    rw [ENNReal.mul_div_mul_left _ _ hC_zero h2.ne_top, div_rpow_of_nonneg _ _ toReal_nonneg,
      ENNReal.rpow_inv_rpow hp_toReal_zero, ENNReal.one_rpow, one_div,
        ENNReal.inv_div (Or.inr ofNat_ne_top) (Or.inr (NeZero.ne' 2).symm)]
  have h6 : Œº A = 0 := by
    convert (fun hh ‚Ü¶ ENNReal.half_lt_self hh (ne_top_of_le_ne_top (rpow_ne_top_of_nonneg
      toReal_nonneg ((div_one C).symm ‚ñ∏ h2.ne_top)) (h4 1))).mt h5.not_gt
    tauto
  exact h h6

end ENorm

section ContinuousENorm

variable [TopologicalSpace Œµ] [ContinuousENorm Œµ] {f : Œ± ‚Üí Œµ}


lemma distribution_le [MeasurableSpace Œµ] [OpensMeasurableSpace Œµ]
    {c : ‚Ñù‚â•0‚àû} (hc : c ‚â† 0) {Œº : Measure Œ±} (hf : AEMeasurable f Œº) :
    distribution f c Œº ‚â§ c‚Åª¬π * (‚à´‚Åª y, ‚Äñf y‚Äñ‚Çë ‚àÇŒº) := by
  by_cases hc_top : c = ‚ä§
  ¬∑ simp [hc_top]
  apply (mul_le_iff_le_inv hc hc_top).mp
  simp_rw [distribution, ‚Üê setLIntegral_one, ‚Üê lintegral_const_mul' _ _ hc_top, mul_one]
  refine le_trans (lintegral_mono_ae ?_) (setLIntegral_le_lintegral _ _)
  simp only [Filter.Eventually, ae, mem_ofCountableUnion]
  rw [Measure.restrict_apply‚ÇÄ']
  ¬∑ convert measure_empty (Œº := Œº); ext; simpa using le_of_lt
  ¬∑ exact hf.enorm.nullMeasurableSet_preimage measurableSet_Ioi

lemma wnorm'_le_eLpNorm' (hf : AEStronglyMeasurable f Œº) {p : ‚Ñù} (p0 : 0 < p) :
    wnorm' f p Œº ‚â§ eLpNorm' f p Œº := by
  refine iSup_le (fun t ‚Ü¶ ?_)
  simp_rw [distribution, eLpNorm']
  have p0' : 0 ‚â§ 1 / p := (div_pos one_pos p0).le
  have set_eq : {x | ofNNReal t < ‚Äñf x‚Äñ‚Çë} = {x | ofNNReal t ^ p < ‚Äñf x‚Äñ‚Çë ^ p} := by
    simp [ENNReal.rpow_lt_rpow_iff p0]
  have : ofNNReal t = (ofNNReal t ^ p) ^ (1 / p) := by simp [p0.ne.symm]
  nth_rewrite 1 [inv_eq_one_div p, this, ‚Üê mul_rpow_of_nonneg _ _ p0', set_eq]
  refine rpow_le_rpow ?_ p0'
  refine le_trans ?_ <| mul_meas_ge_le_lintegral‚ÇÄ (hf.enorm.pow_const p) (ofNNReal t ^ p)
  gcongr
  exact fun x ‚Ü¶ le_of_lt x

lemma distribution_lt_top (hf : MemLp f p Œº) (p_pos : 0 < p) (p_ne_top : p ‚â† ‚àû)
    {t : ‚Ñù‚â•0} (ht : 0 < t) :
    distribution f t Œº < ‚àû := by
  have := wnorm'_le_eLpNorm' hf.1 (toReal_pos p_pos.ne.symm p_ne_top)
  rw [‚Üê eLpNorm_eq_eLpNorm' p_pos.ne.symm p_ne_top] at this
  have := this.trans_lt hf.2
  rw [wnorm'] at this
  rw [iSup_lt_iff] at this
  rcases this with ‚ü®b,b_lt_top, h‚ü©
  have := (h t).trans_lt b_lt_top
  rw [mul_lt_top_iff] at this
  rcases this with ‚ü®t_lt_top, h‚ü©| (t_zero| h)
  ¬∑ rwa [rpow_lt_top_iff_of_pos] at h
    simp only [inv_pos]
    exact toReal_pos p_pos.ne.symm p_ne_top
  ¬∑ rw [ENNReal.coe_eq_zero] at t_zero
    exfalso
    exact ht.ne.symm t_zero
  ¬∑ rw [ENNReal.rpow_eq_zero_iff_of_pos (by simp only [inv_pos]; exact toReal_pos p_pos.ne.symm p_ne_top)] at h
    rw [h]
    simp only [zero_lt_top]


lemma wnorm_le_eLpNorm (hf : AEStronglyMeasurable f Œº) {p : ‚Ñù‚â•0‚àû} (hp : 0 < p) :
    wnorm f p Œº ‚â§ eLpNorm f p Œº := by
  by_cases h : p = ‚ä§
  ¬∑ simp [h, wnorm, eLpNorm]
  ¬∑ have p0 : p ‚â† 0 := hp.ne.symm
    simpa [h, wnorm, eLpNorm, p0] using wnorm'_le_eLpNorm' hf (toReal_pos p0 h)

lemma MemLp.memWLp (hp : 0 < p) (hf : MemLp f p Œº) : MemWLp f p Œº :=
  ‚ü®hf.1, wnorm_le_eLpNorm hf.1 hp |>.trans_lt hf.2‚ü©

end ContinuousENorm

section Defs

variable [ENorm Œµ‚ÇÅ] [ENorm Œµ‚ÇÇ] [TopologicalSpace Œµ‚ÇÅ] [TopologicalSpace Œµ‚ÇÇ]
/- Todo: define `MeasureTheory.WLp` as a subgroup, similar to `MeasureTheory.Lp` -/

/-- An operator has weak type `(p, q)` if it is bounded as a map from `L^p` to weak `L^q`.
`HasWeakType T p p' Œº ŒΩ c` means that `T` has weak type `(p, p')` w.r.t. measures `Œº`, `ŒΩ`
and constant `c`. -/
def HasWeakType (T : (Œ± ‚Üí Œµ‚ÇÅ) ‚Üí (Œ±' ‚Üí Œµ‚ÇÇ)) (p p' : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) (ŒΩ : Measure Œ±')
    (c : ‚Ñù‚â•0‚àû) : Prop :=
  ‚àÄ f : Œ± ‚Üí Œµ‚ÇÅ, MemLp f p Œº ‚Üí AEStronglyMeasurable (T f) ŒΩ ‚àß wnorm (T f) p' ŒΩ ‚â§ c * eLpNorm f p Œº

/-- A weaker version of `HasWeakType`. -/
def HasBoundedWeakType {Œ± Œ±' : Type*} [Zero Œµ‚ÇÅ]
    {_x : MeasurableSpace Œ±} {_x' : MeasurableSpace Œ±'} (T : (Œ± ‚Üí Œµ‚ÇÅ) ‚Üí (Œ±' ‚Üí Œµ‚ÇÇ))
    (p p' : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) (ŒΩ : Measure Œ±') (c : ‚Ñù‚â•0‚àû) : Prop :=
  ‚àÄ f : Œ± ‚Üí Œµ‚ÇÅ, BoundedFiniteSupport f Œº ‚Üí
  AEStronglyMeasurable (T f) ŒΩ ‚àß wnorm (T f) p' ŒΩ ‚â§ c * eLpNorm f p Œº

/-- An operator has strong type `(p, q)` if it is bounded as an operator on `L^p ‚Üí L^q`.
`HasStrongType T p p' Œº ŒΩ c` means that `T` has strong type (p, p') w.r.t. measures `Œº`, `ŒΩ`
and constant `c`. -/
def HasStrongType {Œ± Œ±' : Type*}
    {_x : MeasurableSpace Œ±} {_x' : MeasurableSpace Œ±'} (T : (Œ± ‚Üí Œµ‚ÇÅ) ‚Üí (Œ±' ‚Üí Œµ‚ÇÇ))
    (p p' : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) (ŒΩ : Measure Œ±') (c : ‚Ñù‚â•0‚àû) : Prop :=
  ‚àÄ f : Œ± ‚Üí Œµ‚ÇÅ, MemLp f p Œº ‚Üí AEStronglyMeasurable (T f) ŒΩ ‚àß eLpNorm (T f) p' ŒΩ ‚â§ c * eLpNorm f p Œº

/-- A weaker version of `HasStrongType`. This is the same as `HasStrongType` if `T` is continuous
w.r.t. the L^2 norm, but weaker in general. -/
def HasBoundedStrongType {Œ± Œ±' : Type*} [Zero Œµ‚ÇÅ]
    {_x : MeasurableSpace Œ±} {_x' : MeasurableSpace Œ±'} (T : (Œ± ‚Üí Œµ‚ÇÅ) ‚Üí (Œ±' ‚Üí Œµ‚ÇÇ))
    (p p' : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) (ŒΩ : Measure Œ±') (c : ‚Ñù‚â•0‚àû) : Prop :=
  ‚àÄ f : Œ± ‚Üí Œµ‚ÇÅ, BoundedFiniteSupport f Œº ‚Üí
  AEStronglyMeasurable (T f) ŒΩ ‚àß eLpNorm (T f) p' ŒΩ ‚â§ c * eLpNorm f p Œº

end Defs

/-! ### Lemmas about `HasWeakType` -/

section HasWeakType

variable [TopologicalSpace Œµ‚ÇÅ] [ContinuousENorm Œµ‚ÇÅ] [TopologicalSpace Œµ‚ÇÇ] [ContinuousENorm Œµ‚ÇÇ]
    {f‚ÇÅ : Œ± ‚Üí Œµ‚ÇÅ}

lemma HasWeakType.memWLp (h : HasWeakType T p p' Œº ŒΩ c) (hf‚ÇÅ : MemLp f‚ÇÅ p Œº)
    (hc : c < ‚ä§ := by finiteness) : MemWLp (T f‚ÇÅ) p' ŒΩ :=
  ‚ü®(h f‚ÇÅ hf‚ÇÅ).1, h f‚ÇÅ hf‚ÇÅ |>.2.trans_lt <| mul_lt_top hc hf‚ÇÅ.2‚ü©

lemma HasWeakType.toReal {T : (Œ± ‚Üí Œµ‚ÇÅ) ‚Üí (Œ±' ‚Üí ‚Ñù‚â•0‚àû)} (h : HasWeakType T p p' Œº ŒΩ c) :
    HasWeakType (T ¬∑ ¬∑ |>.toReal) p p' Œº ŒΩ c :=
  fun f hf ‚Ü¶ ‚ü®(h f hf).1.ennreal_toReal, wnorm_toReal_le.trans (h f hf).2 ‚ü©

-- unused, probably delete
open Classical in
lemma toReal_ofReal_preimage' {s : Set ‚Ñù‚â•0‚àû} : ENNReal.toReal ‚Åª¬π' (ENNReal.ofReal ‚Åª¬π' s) =
    if ‚àû ‚àà s ‚Üî 0 ‚àà s then s else if 0 ‚àà s then s ‚à™ {‚àû} else s \ {‚àû} := by
  split_ifs <;> ext (_|_) <;> simp_all

-- move
open Classical in
lemma toReal_ofReal_preimage {s : Set ‚Ñù‚â•0‚àû} : letI t := ENNReal.toReal ‚Åª¬π' (ENNReal.ofReal ‚Åª¬π' s)
  s = if ‚àû ‚àà s ‚Üî 0 ‚àà s then t else if 0 ‚àà s then t \ {‚àû} else t ‚à™ {‚àû} := by
  split_ifs <;> ext (_|_) <;> simp_all

-- move
lemma aestronglyMeasurable_ennreal_toReal_iff {f : Œ± ‚Üí ‚Ñù‚â•0‚àû}
    (hf : NullMeasurableSet (f ‚Åª¬π' {‚àû}) Œº) :
    AEStronglyMeasurable (ENNReal.toReal ‚àò f) Œº ‚Üî AEStronglyMeasurable f Œº := by
  refine ‚ü®fun h ‚Ü¶ AEMeasurable.aestronglyMeasurable (NullMeasurable.aemeasurable fun s hs ‚Ü¶ ?_),
    fun h ‚Ü¶ h.ennreal_toReal‚ü©
  have := h.aemeasurable.nullMeasurable (hs.preimage measurable_ofReal)
  simp_rw [preimage_comp] at this
  rw [toReal_ofReal_preimage (s := s)]
  split_ifs
  ¬∑ exact this
  ¬∑ simp_rw [preimage_diff]
    exact this.diff hf
  ¬∑ simp_rw [preimage_union]
    exact this.union hf

lemma hasWeakType_toReal_iff {T : (Œ± ‚Üí Œµ‚ÇÅ) ‚Üí (Œ±' ‚Üí ‚Ñù‚â•0‚àû)}
    (hT : ‚àÄ f, MemLp f p Œº ‚Üí ‚àÄ·µê x ‚àÇŒΩ, T f x ‚â† ‚ä§) :
    HasWeakType (T ¬∑ ¬∑ |>.toReal) p p' Œº ŒΩ c ‚Üî HasWeakType T p p' Œº ŒΩ c := by
  refine ‚ü®fun h ‚Ü¶ fun f hf ‚Ü¶ ?_, (¬∑.toReal)‚ü©
  obtain ‚ü®h1, h2‚ü© := h f hf
  refine ‚ü®?_, by rwa [‚Üê wnorm_toReal_eq (hT f hf)]‚ü©
  rwa [‚Üê aestronglyMeasurable_ennreal_toReal_iff]
  refine .of_null <| measure_zero_iff_ae_notMem.mpr ?_
  filter_upwards [hT f hf] with x hx
  simp [hx]

-- lemma comp_left [MeasurableSpace Œµ‚ÇÇ] {ŒΩ' : Measure Œµ‚ÇÇ} {f : Œµ‚ÇÇ ‚Üí Œµ‚ÇÉ} (h : HasWeakType T p p' Œº ŒΩ c)
--     (hf : MemLp f p' ŒΩ') :
--     HasWeakType (f ‚àò T ¬∑) p p' Œº ŒΩ c := by
--   intro u hu
--   refine ‚ü®h u hu |>.1.comp_measurable hf.1, ?_‚ü©

end HasWeakType

/-! ### Lemmas about `HasBoundedWeakType` -/

section HasBoundedWeakType

variable [TopologicalSpace Œµ‚ÇÅ] [ENormedAddMonoid Œµ‚ÇÅ] [TopologicalSpace Œµ‚ÇÇ] [ENorm Œµ‚ÇÇ]
    {f‚ÇÅ : Œ± ‚Üí Œµ‚ÇÅ}

lemma HasBoundedWeakType.memWLp (h : HasBoundedWeakType T p p' Œº ŒΩ c)
    (hf‚ÇÅ : BoundedFiniteSupport f‚ÇÅ Œº) (hc : c < ‚ä§ := by finiteness) :
    MemWLp (T f‚ÇÅ) p' ŒΩ :=
  ‚ü®(h f‚ÇÅ hf‚ÇÅ).1, h f‚ÇÅ hf‚ÇÅ |>.2.trans_lt <| mul_lt_top hc (hf‚ÇÅ.memLp p).2‚ü©

lemma HasWeakType.hasBoundedWeakType (h : HasWeakType T p p' Œº ŒΩ c) :
    HasBoundedWeakType T p p' Œº ŒΩ c :=
  fun f hf ‚Ü¶ h f (hf.memLp _)

end HasBoundedWeakType

/-! ### Lemmas about `HasStrongType` -/

section HasStrongType

variable [TopologicalSpace Œµ‚ÇÅ] [ContinuousENorm Œµ‚ÇÅ] [TopologicalSpace Œµ‚ÇÇ] [ContinuousENorm Œµ‚ÇÇ]
    {f‚ÇÅ : Œ± ‚Üí Œµ‚ÇÅ}

lemma HasStrongType.memLp (h : HasStrongType T p p' Œº ŒΩ c) (hf‚ÇÅ : MemLp f‚ÇÅ p Œº)
    (hc : c < ‚ä§ := by finiteness) : MemLp (T f‚ÇÅ) p' ŒΩ :=
  ‚ü®(h f‚ÇÅ hf‚ÇÅ).1, h f‚ÇÅ hf‚ÇÅ |>.2.trans_lt <| mul_lt_top hc hf‚ÇÅ.2‚ü©

lemma HasStrongType.hasWeakType (hp' : 0 < p')
    (h : HasStrongType T p p' Œº ŒΩ c) : HasWeakType T p p' Œº ŒΩ c :=
  fun f hf ‚Ü¶ ‚ü®(h f hf).1, wnorm_le_eLpNorm (h f hf).1 hp' |>.trans (h f hf).2‚ü©

lemma HasStrongType.toReal {T : (Œ± ‚Üí Œµ‚ÇÅ) ‚Üí (Œ±' ‚Üí ‚Ñù‚â•0‚àû)} (h : HasStrongType T p p' Œº ŒΩ c) :
    HasStrongType (T ¬∑ ¬∑ |>.toReal) p p' Œº ŒΩ c :=
  fun f hf ‚Ü¶ ‚ü®(h f hf).1.ennreal_toReal, eLpNorm_toReal_le.trans (h f hf).2 ‚ü©

lemma hasStrongType_toReal_iff {T : (Œ± ‚Üí Œµ‚ÇÅ) ‚Üí (Œ±' ‚Üí ‚Ñù‚â•0‚àû)}
    (hT : ‚àÄ f, MemLp f p Œº ‚Üí ‚àÄ·µê x ‚àÇŒΩ, T f x ‚â† ‚ä§) :
    HasStrongType (T ¬∑ ¬∑ |>.toReal) p p' Œº ŒΩ c ‚Üî HasStrongType T p p' Œº ŒΩ c := by
  refine ‚ü®fun h ‚Ü¶ fun f hf ‚Ü¶ ?_, (¬∑.toReal)‚ü©
  obtain ‚ü®h1, h2‚ü© := h f hf
  refine ‚ü®?_, by rwa [‚Üê eLpNorm_toReal_eq (hT f hf)]‚ü©
  rwa [‚Üê aestronglyMeasurable_ennreal_toReal_iff]
  refine .of_null <| measure_zero_iff_ae_notMem.mpr ?_
  filter_upwards [hT f hf] with x hx
  simp [hx]

end HasStrongType

/-! ### Lemmas about `HasBoundedStrongType` -/

section HasBoundedStrongType

variable [TopologicalSpace Œµ‚ÇÅ] [ENormedAddMonoid Œµ‚ÇÅ] [TopologicalSpace Œµ‚ÇÇ] [ContinuousENorm Œµ‚ÇÇ]
    {f‚ÇÅ : Œ± ‚Üí Œµ‚ÇÅ}

lemma HasBoundedStrongType.memLp (h : HasBoundedStrongType T p p' Œº ŒΩ c)
    (hf‚ÇÅ : BoundedFiniteSupport f‚ÇÅ Œº) (hc : c < ‚ä§ := by finiteness) :
    MemLp (T f‚ÇÅ) p' ŒΩ :=
  ‚ü®(h f‚ÇÅ hf‚ÇÅ).1, h f‚ÇÅ hf‚ÇÅ |>.2.trans_lt <| mul_lt_top hc (hf‚ÇÅ.memLp _).2‚ü©

lemma HasStrongType.hasBoundedStrongType (h : HasStrongType T p p' Œº ŒΩ c) :
    HasBoundedStrongType T p p' Œº ŒΩ c :=
  fun f hf ‚Ü¶ h f (hf.memLp _)

lemma HasBoundedStrongType.hasBoundedWeakType (hp' : 0 < p')
    (h : HasBoundedStrongType T p p' Œº ŒΩ c) :
    HasBoundedWeakType T p p' Œº ŒΩ c :=
  fun f hf ‚Ü¶
    ‚ü®(h f hf).1, wnorm_le_eLpNorm (h f hf).1 hp' |>.trans (h f hf).2‚ü©

lemma HasBoundedStrongType.const_smul {T : (Œ± ‚Üí Œµ‚ÇÅ) ‚Üí Œ±' ‚Üí ‚Ñù‚â•0‚àû}
    (h : HasBoundedStrongType T p p' Œº ŒΩ c) (r : ‚Ñù‚â•0) :
    HasBoundedStrongType (r ‚Ä¢ T) p p' Œº ŒΩ (r ‚Ä¢ c) := by
  intro f hf
  rw [Pi.smul_apply, MeasureTheory.eLpNorm_const_smul']
  exact ‚ü®(h f hf).1.const_smul _, le_of_le_of_eq (mul_le_mul_left' (h f hf).2 ‚Äñr‚Äñ‚Çë) (by simp; rfl)‚ü©

end HasBoundedStrongType

section distribution

variable {f g : Œ± ‚Üí Œµ}

@[gcongr]
lemma distribution_mono_left [ENorm Œµ] (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ ‚Äñg x‚Äñ‚Çë) :
    distribution f t Œº ‚â§ distribution g t Œº := by
  have h‚ÇÄ : {x | t < ‚Äñf x‚Äñ‚Çë} \ {x | t < ‚Äñg x‚Äñ‚Çë} ‚äÜ {x | ¬¨‚Äñf x‚Äñ‚Çë ‚â§ ‚Äñg x‚Äñ‚Çë} := fun x ‚Ü¶ by
    simp_rw [mem_diff, mem_setOf_eq, not_lt, not_le, and_imp]
    intro i‚ÇÅ i‚ÇÇ; simpa using i‚ÇÇ.trans_lt i‚ÇÅ
  calc
    _ ‚â§ Œº ({x | t < ‚Äñf x‚Äñ‚Çë} ‚à© {x | t < ‚Äñg x‚Äñ‚Çë})
      + Œº ({x | t < ‚Äñf x‚Äñ‚Çë} \ {x | t < ‚Äñg x‚Äñ‚Çë}) := measure_le_inter_add_diff Œº _ _
    _ = Œº ({x | t < ‚Äñf x‚Äñ‚Çë} ‚à© {x | t < ‚Äñg x‚Äñ‚Çë}) := by rw [measure_mono_null h‚ÇÄ h, add_zero]
    _ ‚â§ _ := by apply measure_mono; simp

@[gcongr]
lemma distribution_mono [ENorm Œµ] (h‚ÇÅ : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ ‚Äñg x‚Äñ‚Çë) (h‚ÇÇ : t ‚â§ s) :
    distribution f s Œº ‚â§ distribution g t Œº :=
  (distribution_mono_left h‚ÇÅ).trans (distribution_mono_right h‚ÇÇ)

lemma distribution_snormEssSup [ENorm Œµ] : distribution f (eLpNormEssSup f Œº) Œº = 0 :=
  meas_essSup_lt -- meas_eLpNormEssSup_lt

lemma distribution_add_le' [ENorm Œµ] {A : ‚Ñù‚â•0‚àû} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œµ}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ A * (‚Äñg‚ÇÅ x‚Äñ‚Çë + ‚Äñg‚ÇÇ x‚Äñ‚Çë)) :
    distribution f (A * (t + s)) Œº ‚â§ distribution g‚ÇÅ t Œº + distribution g‚ÇÇ s Œº := by
  apply distribution_add_le_of_enorm
  simp [h]

lemma distribution_add_le [TopologicalSpace Œµ] [ENormedAddMonoid Œµ] {f g : Œ± ‚Üí Œµ} :
    distribution (f + g) (t + s) Œº ‚â§ distribution f t Œº + distribution g s Œº :=
  calc
    _ ‚â§ Œº ({x | t < ‚Äñf x‚Äñ‚Çë} ‚à™ {x | s < ‚Äñg x‚Äñ‚Çë}) := by
      refine measure_mono fun x h ‚Ü¶ ?_
      simp only [mem_union, mem_setOf_eq, Pi.add_apply] at h ‚ä¢
      contrapose! h
      exact (ENormedAddMonoid.enorm_add_le _ _).trans (add_le_add h.1 h.2)
    _ ‚â§ _ := measure_union_le _ _

--TODO: make this an iff?
lemma distribution_zero [TopologicalSpace Œµ] [ENormedAddMonoid Œµ] {f : Œ± ‚Üí Œµ} (h : f =·µê[Œº] 0) :
    distribution f t Œº = 0 := by
  unfold distribution
  rw[‚Üê le_zero_iff]
  calc _
    _ ‚â§ Œº {x | 0 < ‚Äñf x‚Äñ‚Çë} := by
      apply measure_mono
      intro x hx
      simp only [Set.mem_setOf_eq] at hx
      exact pos_of_gt hx
    _ = Œº {x | ‚Äñf x‚Äñ‚Çë ‚â† 0} := by
      congr
      ext x
      simp
    _ = 0 := by
      refine ae_iff.mp ?_
      change enorm ‚àò f =·∂†[ae Œº] 0
      unfold Filter.EventuallyEq
      simpa only [comp_apply, Pi.zero_apply, enorm_eq_zero]


lemma distribution_indicator_const [TopologicalSpace Œµ] [ENormedAddMonoid Œµ] {s : Set Œ±} {a : Œµ} :
    distribution (s.indicator (Function.const Œ± a)) t Œº = (Set.Iio ‚Äña‚Äñ‚Çë).indicator (fun _ ‚Ü¶ Œº s) t := by
  unfold distribution indicator
  split_ifs with h
  ¬∑ simp only [const_apply]
    congr
    ext x
    simp only [mem_setOf_eq]
    rw [apply_ite enorm]
    rw [enorm_zero]
    constructor
    ¬∑ split_ifs with h'
      ¬∑ intro _
        exact h'
      ¬∑ intro h''
        exfalso
        exact ENNReal.not_lt_zero h''
    ¬∑ intro hx
      rwa [ite_cond_eq_true]
      simpa only [eq_iff_iff, iff_true]
  ¬∑ convert measure_empty (Œº := Œº)
    apply eq_empty_of_subset_empty
    intro x
    simp only [const_apply, mem_setOf_eq, mem_empty_iff_false, imp_false, not_lt]
    split_ifs
    ¬∑ simp only [mem_Iio, not_lt] at h
      exact h
    ¬∑ simp

--TODO: Can the measurability condition be weakened?
lemma distribution_add [TopologicalSpace Œµ] [ENormedAddMonoid Œµ] {f g : Œ± ‚Üí Œµ}
  (h : Disjoint (Function.support f) (Function.support g)) (hg : StronglyMeasurable g) :
    distribution (f + g) t Œº = distribution f t Œº + distribution g t Œº := by
  unfold distribution
  rw [‚Üê measure_union]
  ¬∑ congr
    ext x
    simp only [Pi.add_apply, mem_setOf_eq, mem_union]
    rcases (@or_not (x ‚àà support f)) with hxf | hxf
    ¬∑ have := disjoint_left.mp h hxf
      simp only [mem_support, ne_eq, not_not] at this
      rw [this]
      simp
    ¬∑ simp only [mem_support, ne_eq, not_not] at hxf
      rw [hxf]
      simp
  ¬∑ apply disjoint_of_subset _ _ h
    ¬∑ intro x
      simp only [mem_setOf_eq, mem_support, ne_eq]
      intro h'
      rw [‚Üê enorm_eq_zero, ‚Üê ENNReal.bot_eq_zero]
      exact LT.lt.ne_bot h'
    ¬∑ intro x
      simp only [mem_setOf_eq, mem_support, ne_eq]
      intro h'
      rw [‚Üê enorm_eq_zero, ‚Üê ENNReal.bot_eq_zero]
      exact LT.lt.ne_bot h'
  ¬∑ exact measurableSet_lt measurable_const (StronglyMeasurable.enorm hg)

lemma distribution_indicator_add_of_support_subset [TopologicalSpace Œµ] [ENormedAddMonoid Œµ]
  (enorm_add : ‚àÄ a b : Œµ, ‚Äña + b‚Äñ‚Çë = ‚Äña‚Äñ‚Çë + ‚Äñb‚Äñ‚Çë) --TODO: new type class for this property?
  {f : Œ± ‚Üí Œµ} {c : Œµ} (hc : ‚Äñc‚Äñ‚Çë ‚â† ‚ä§) {s : Set Œ±}
  (hfs : Function.support f ‚äÜ s) :
    distribution (f + s.indicator (Function.const Œ± c)) t Œº = if t < ‚Äñc‚Äñ‚Çë then Œº s else distribution f (t - ‚Äñc‚Äñ‚Çë) Œº := by
  unfold distribution
  split_ifs with ht
  ¬∑ congr with x
    simp only [Pi.add_apply, mem_setOf_eq]
    constructor
    ¬∑ intro h
      contrapose! h
      have : x ‚àâ support f := by exact fun a ‚Ü¶ h (hfs a)
      simp only [mem_support, ne_eq, not_not] at this
      rw [this]
      unfold indicator
      simp [h]
    ¬∑ intro h
      unfold indicator
      rw [enorm_add, add_comm]
      split_ifs
      apply lt_add_of_lt_of_nonneg _ (zero_le _)
      simpa [h]
  ¬∑ push_neg at ht
    congr with x
    simp only [Pi.add_apply, mem_setOf_eq]
    rw [enorm_add, ENNReal.sub_lt_iff_lt_right hc ht]
    constructor
    ¬∑ intro h
      apply h.trans_le
      gcongr
      unfold indicator
      split_ifs <;> simp
    ¬∑ intro h
      apply h.trans_le
      gcongr
      unfold indicator
      split_ifs with hxs
      ¬∑ simp
      exfalso
      have : x ‚àâ support f := by exact fun a ‚Ü¶ hxs (hfs a)
      simp only [mem_support, ne_eq, not_not] at this
      rw [this, enorm_zero, zero_add] at h
      exact (lt_self_iff_false _).mp (h.trans_le ht)

/- ENNReal version of the previous lemma -/
lemma distribution_indicator_add_of_support_subset_ennreal {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} {c : ‚Ñù‚â•0‚àû} (hc : c ‚â† ‚ä§)
  {s : Set Œ±} (hfs : Function.support f ‚äÜ s) :
    distribution (f + s.indicator (Function.const Œ± c)) t Œº
      = if t < c then Œº s else distribution f (t - c) Œº := by
  convert distribution_indicator_add_of_support_subset (by simp) (by simpa) hfs

/- NNReal version of the previous lemma -/
lemma distribution_indicator_add_of_support_subset_nnreal {f : Œ± ‚Üí ‚Ñù‚â•0} {c : ‚Ñù‚â•0}
  {s : Set Œ±} (hfs : Function.support f ‚äÜ s) :
    distribution (f + s.indicator (Function.const Œ± c)) t Œº
      = if t < c then Œº s else distribution f (t - c) Œº := by
  convert distribution_indicator_add_of_support_subset (by simp) (by simp) hfs


end distribution

variable {f g : Œ± ‚Üí Œµ}

section

variable {Œµ Œµ' : Type*} [TopologicalSpace Œµ] [ENorm Œµ]
variable [TopologicalSpace Œµ'] [ENormedSpace Œµ']

-- TODO: this lemma and its primed version could be unified using a `NormedSemifield` typeclass
-- (which includes NNReal and normed fields like ‚Ñù and ‚ÑÇ), i.e. assuming ùïú is a normed semifield.
-- Investigate if this is worthwhile when upstreaming this to mathlib.
lemma distribution_smul_left {f : Œ± ‚Üí Œµ'} {c : ‚Ñù‚â•0} (hc : c ‚â† 0) :
    distribution (c ‚Ä¢ f) t Œº = distribution f (t / ‚Äñc‚Äñ‚Çë) Œº := by
  have h‚ÇÄ : ‚Äñc‚Äñ‚Çë ‚â† 0 := by
    have : ‚Äñc‚Äñ‚Çë = ‚Äñ(c : ‚Ñù‚â•0‚àû)‚Äñ‚Çë := rfl
    rw [this, enorm_ne_zero]
    exact ENNReal.coe_ne_zero.mpr hc
  unfold distribution
  congr with x
  simp only [Pi.smul_apply]
  rw [‚Üê @ENNReal.mul_lt_mul_right (t / ‚Äñc‚Äñ‚Çë) _ (‚Äñc‚Äñ‚Çë) h‚ÇÄ coe_ne_top,
    enorm_smul_eq_mul (c := c) _, ENNReal.div_mul_cancel h‚ÇÄ coe_ne_top, mul_comm]

variable [NormedAddCommGroup E] [MulActionWithZero ùïú E] [NormSMulClass ùïú E]
  {E' : Type*} [NormedAddCommGroup E'] [MulActionWithZero ùïú E'] [NormSMulClass ùïú E']

lemma distribution_smul_left' {f : Œ± ‚Üí E} {c : ùïú} (hc : c ‚â† 0) :
    distribution (c ‚Ä¢ f) t Œº = distribution f (t / ‚Äñc‚Äñ‚Çë) Œº := by
  have h‚ÇÄ : ‚Äñc‚Äñ‚Çë ‚â† 0 := enorm_ne_zero.mpr hc
  unfold distribution
  congr with x
  simp only [Pi.smul_apply]
  rw [‚Üê @ENNReal.mul_lt_mul_right (t / ‚Äñc‚Äñ‚Çë) _ (‚Äñc‚Äñ‚Çë) h‚ÇÄ coe_ne_top,
    enorm_smul _, mul_comm, ENNReal.div_mul_cancel h‚ÇÄ coe_ne_top]

lemma HasStrongType.const_smul [ContinuousConstSMul ‚Ñù‚â•0 Œµ']
    {T : (Œ± ‚Üí Œµ) ‚Üí (Œ±' ‚Üí Œµ')} {c : ‚Ñù‚â•0‚àû} (h : HasStrongType T p p' Œº ŒΩ c) (k : ‚Ñù‚â•0) :
    HasStrongType (k ‚Ä¢ T) p p' Œº ŒΩ (‚Äñk‚Äñ‚Çë * c) := by
  refine fun f hf ‚Ü¶
    ‚ü®AEStronglyMeasurable.const_smul (h f hf).1 k, eLpNorm_const_nnreal_smul_le.trans ?_‚ü©
  rw [mul_assoc]
  gcongr
  exact (h f hf).2

-- TODO: do we want to unify this lemma with its unprimed version, perhaps using an
-- `ENormedSemiring` class?
variable {ùïú E' : Type*} [NormedRing ùïú] [NormedAddCommGroup E'] [MulActionWithZero ùïú E'] [IsBoundedSMul ùïú E'] in
lemma HasStrongType.const_smul'
    {T : (Œ± ‚Üí Œµ) ‚Üí (Œ±' ‚Üí E')} {c : ‚Ñù‚â•0‚àû} (h : HasStrongType T p p' Œº ŒΩ c) (k : ùïú) :
    HasStrongType (k ‚Ä¢ T) p p' Œº ŒΩ (‚Äñk‚Äñ‚Çë * c) := by
  refine fun f hf ‚Ü¶ ‚ü®AEStronglyMeasurable.const_smul (h f hf).1 k, eLpNorm_const_smul_le.trans ?_‚ü©
  rw [mul_assoc]
  gcongr
  exact (h f hf).2

lemma HasStrongType.const_mul
    {T : (Œ± ‚Üí Œµ) ‚Üí (Œ±' ‚Üí ‚Ñù‚â•0‚àû)} {c : ‚Ñù‚â•0‚àû} (h : HasStrongType T p p' Œº ŒΩ c) (e : ‚Ñù‚â•0) :
    HasStrongType (fun f x ‚Ü¶ e * T f x) p p' Œº ŒΩ (‚Äñe‚Äñ‚Çë * c) :=
  h.const_smul e

-- TODO: do we want to unify this lemma with its unprimed version, perhaps using an
-- `ENormedSemiring` class?
variable {E' : Type*} [NormedRing E'] in
lemma HasStrongType.const_mul'
    {T : (Œ± ‚Üí Œµ) ‚Üí (Œ±' ‚Üí E')} {c : ‚Ñù‚â•0‚àû} (h : HasStrongType T p p' Œº ŒΩ c) (e : E') :
    HasStrongType (fun f x ‚Ü¶ e * T f x) p p' Œº ŒΩ (‚Äñe‚Äñ‚Çë * c) :=
  h.const_smul' e

lemma wnorm_const_smul_le (hp : p ‚â† 0) {f : Œ± ‚Üí Œµ'} (k : ‚Ñù‚â•0) :
    wnorm (k ‚Ä¢ f) p Œº ‚â§ ‚Äñk‚Äñ‚Çë * wnorm f p Œº := by
  by_cases ptop : p = ‚ä§
  ¬∑ simp only [ptop, wnorm_top]
    apply eLpNormEssSup_const_nnreal_smul_le
  simp only [wnorm, ptop, ‚ÜìreduceIte, wnorm', iSup_le_iff]
  by_cases k_zero : k = 0
  ¬∑ simp only [distribution, k_zero, Pi.smul_apply, zero_smul, enorm_zero, not_lt_zero',
    setOf_false, measure_empty, zero_mul, nonpos_iff_eq_zero, mul_eq_zero, ENNReal.coe_eq_zero,
    ENNReal.rpow_eq_zero_iff, inv_pos, true_and, zero_ne_top, inv_neg'', false_and, or_false]
    intro _
    right
    exact toReal_pos hp ptop
  simp only [distribution_smul_left k_zero]
  intro t
  rw [ENNReal.mul_iSup]
  have : t * distribution f (t / ‚Äñk‚Äñ‚Çë) Œº ^ p.toReal‚Åª¬π =
      ‚Äñk‚Äñ‚Çë * ((t / ‚Äñk‚Äñ‚Çë) * distribution f (t / ‚Äñk‚Äñ‚Çë) Œº ^ p.toReal‚Åª¬π) := by
    nth_rewrite 1 [‚Üê mul_div_cancel‚ÇÄ t k_zero]
    simp only [coe_mul, mul_assoc]
    congr
    exact coe_div k_zero
  rw [this]
  apply le_iSup_of_le (‚Üët / ‚Üë‚Äñk‚Äñ‚Çä)
  apply le_of_eq
  congr <;> exact (coe_div k_zero).symm

lemma wnorm_const_smul_le' [IsBoundedSMul ùïú E] (hp : p ‚â† 0) {f : Œ± ‚Üí E} (k : ùïú) :
    wnorm (k ‚Ä¢ f) p Œº ‚â§ ‚Äñk‚Äñ‚Çë * wnorm f p Œº := by
  by_cases ptop : p = ‚ä§
  ¬∑ simp only [ptop, wnorm_top]
    apply eLpNormEssSup_const_smul_le
  simp only [wnorm, ptop, ‚ÜìreduceIte, wnorm', iSup_le_iff]
  by_cases k_zero : k = 0
  ¬∑ simp only [distribution, k_zero, Pi.smul_apply, zero_smul, enorm_zero, not_lt_zero', setOf_false,
      measure_empty, coe_lt_enorm, zero_mul, nonpos_iff_eq_zero, mul_eq_zero, ENNReal.coe_eq_zero,
      ENNReal.rpow_eq_zero_iff, inv_pos, true_and, zero_ne_top, inv_neg'', false_and, or_false]
    intro _
    right
    exact toReal_pos hp ptop
  simp only [distribution_smul_left' k_zero]
  intro t
  rw [ENNReal.mul_iSup]
  have knorm_ne_zero : ‚Äñk‚Äñ‚Çä ‚â† 0 := nnnorm_ne_zero_iff.mpr k_zero
  have : t * distribution f (t / ‚Äñk‚Äñ‚Çë) Œº ^ p.toReal‚Åª¬π =
      ‚Äñk‚Äñ‚Çë * ((t / ‚Äñk‚Äñ‚Çë) * distribution f (t / ‚Äñk‚Äñ‚Çë) Œº ^ p.toReal‚Åª¬π) := by
    nth_rewrite 1 [‚Üê mul_div_cancel‚ÇÄ t knorm_ne_zero]
    simp only [coe_mul, mul_assoc]
    congr
    exact coe_div knorm_ne_zero
  erw [this]
  apply le_iSup_of_le (‚Üët / ‚Üë‚Äñk‚Äñ‚Çä)
  apply le_of_eq
  congr <;> exact (coe_div knorm_ne_zero).symm

lemma HasWeakType.const_smul [ContinuousConstSMul ‚Ñù‚â•0 Œµ']
    {T : (Œ± ‚Üí Œµ) ‚Üí (Œ±' ‚Üí Œµ')} (hp' : p' ‚â† 0) {c : ‚Ñù‚â•0‚àû} (h : HasWeakType T p p' Œº ŒΩ c) (k : ‚Ñù‚â•0) :
    HasWeakType (k ‚Ä¢ T) p p' Œº ŒΩ (k * c) := by
  intro f hf
  refine ‚ü®(h f hf).1.const_smul k, ?_‚ü©
  calc wnorm ((k ‚Ä¢ T) f) p' ŒΩ
    _ ‚â§ k * wnorm (T f) p' ŒΩ := by simpa using wnorm_const_smul_le hp' _
    _ ‚â§ k * (c * eLpNorm f p Œº) := by
      gcongr
      apply (h f hf).2
    _ = (k * c) * eLpNorm f p Œº := by rw [mul_assoc]

-- TODO: do we want to unify this lemma with its unprimed version, perhaps using an
-- `ENormedSemiring` class?
lemma HasWeakType.const_smul' [IsBoundedSMul ùïú E'] {T : (Œ± ‚Üí Œµ) ‚Üí (Œ±' ‚Üí E')} (hp' : p' ‚â† 0)
    {c : ‚Ñù‚â•0‚àû} (h : HasWeakType T p p' Œº ŒΩ c) (k : ùïú) :
    HasWeakType (k ‚Ä¢ T) p p' Œº ŒΩ (‚Äñk‚Äñ‚Çë * c) := by
  intro f hf
  refine ‚ü®aestronglyMeasurable_const.smul (h f hf).1, ?_‚ü©
  calc wnorm ((k ‚Ä¢ T) f) p' ŒΩ
    _ ‚â§ ‚Äñk‚Äñ‚Çë * wnorm (T f) p' ŒΩ := by simp [wnorm_const_smul_le' hp']
    _ ‚â§ ‚Äñk‚Äñ‚Çë * (c * eLpNorm f p Œº) := by
      gcongr
      apply (h f hf).2
    _ = (‚Äñk‚Äñ‚Çë * c) * eLpNorm f p Œº := by rw [mul_assoc]

lemma HasWeakType.const_mul {T : (Œ± ‚Üí Œµ) ‚Üí (Œ±' ‚Üí ‚Ñù‚â•0‚àû)} (hp' : p' ‚â† 0)
    {c : ‚Ñù‚â•0‚àû} (h : HasWeakType T p p' Œº ŒΩ c) (e : ‚Ñù‚â•0) :
    HasWeakType (fun f x ‚Ü¶ e * T f x) p p' Œº ŒΩ (e * c) :=
  h.const_smul hp' e

-- TODO: do we want to unify this lemma with its unprimed version, perhaps using an
-- `ENormedSemiring` class?
lemma HasWeakType.const_mul' {T : (Œ± ‚Üí Œµ) ‚Üí (Œ±' ‚Üí ùïú)} (hp' : p' ‚â† 0)
    {c : ‚Ñù‚â•0‚àû} (h : HasWeakType T p p' Œº ŒΩ c) (e : ùïú) :
    HasWeakType (fun f x ‚Ü¶ e * T f x) p p' Œº ŒΩ (‚Äñe‚Äñ‚Çë * c) :=
  h.const_smul' hp' e

end

section NormedGroup

variable [NormedAddCommGroup E‚ÇÅ] [NormedSpace ùïú E‚ÇÅ] [NormedAddCommGroup E‚ÇÇ] [NormedSpace ùïú E‚ÇÇ]
  [NormedAddCommGroup E‚ÇÉ] [NormedSpace ùïú E‚ÇÉ]

lemma _root_.ContinuousLinearMap.distribution_le {f : Œ± ‚Üí E‚ÇÅ} {g : Œ± ‚Üí E‚ÇÇ} (L : E‚ÇÅ ‚ÜíL[ùïú] E‚ÇÇ ‚ÜíL[ùïú] E‚ÇÉ) :
    distribution (fun x ‚Ü¶ L (f x) (g x)) (‚ÄñL‚Äñ‚Çë * t * s) Œº ‚â§
    distribution f t Œº + distribution g s Œº := by
  have h‚ÇÄ : {x | ‚ÄñL‚Äñ‚Çë * t * s < ‚Äñ(fun x ‚Ü¶ (L (f x)) (g x)) x‚Äñ‚Çë} ‚äÜ
      {x | t < ‚Äñf x‚Äñ‚Çë} ‚à™ {x | s < ‚Äñg x‚Äñ‚Çë} := fun z hz ‚Ü¶ by
    simp only [mem_union, mem_setOf_eq] at hz ‚ä¢
    contrapose! hz
    calc
      ‚Äñ(L (f z)) (g z)‚Äñ‚Çë ‚â§ ‚ÄñL‚Äñ‚Çë * ‚Äñf z‚Äñ‚Çë * ‚Äñg z‚Äñ‚Çë := by calc
          _ ‚â§ ‚ÄñL (f z)‚Äñ‚Çë * ‚Äñg z‚Äñ‚Çë := ContinuousLinearMap.le_opENorm (L (f z)) (g z)
          _ ‚â§ ‚ÄñL‚Äñ‚Çë * ‚Äñf z‚Äñ‚Çë * ‚Äñg z‚Äñ‚Çë :=
            mul_le_mul' (ContinuousLinearMap.le_opENorm L (f z)) (by rfl)
      _ ‚â§ _ := mul_le_mul' (mul_le_mul_left' hz.1 ‚ÄñL‚Äñ‚Çë) hz.2
  calc
    _ ‚â§ Œº ({x | t < ‚Äñf x‚Äñ‚Çë} ‚à™ {x | s < ‚Äñg x‚Äñ‚Çë}) := measure_mono h‚ÇÄ
    _ ‚â§ _ := measure_union_le _ _

end NormedGroup

section Layercake

variable [TopologicalSpace Œµ] [ContinuousENorm Œµ]

/-- The layer-cake theorem, or Cavalieri's principle for functions into a space with a continuous
enorm. -/
lemma lintegral_norm_pow_eq_distribution {f : Œ± ‚Üí Œµ} (hf : AEStronglyMeasurable f Œº) {p : ‚Ñù} (hp : 0 < p) :
    ‚à´‚Åª x, ‚Äñf x‚Äñ‚Çë ^ p ‚àÇŒº =
    ‚à´‚Åª t in Ioi (0 : ‚Ñù), ENNReal.ofReal (p * t ^ (p - 1)) * distribution f (.ofReal t) Œº := by
  have := lintegral_rpow_eq_lintegral_meas_lt_mul Œº (f := fun x ‚Ü¶ ENNReal.toReal ‚Äñf x‚Äñ‚Çë)
    (Eventually.of_forall fun x ‚Ü¶ ENNReal.toReal_nonneg) hf.enorm.ennreal_toReal hp
  simp only [mul_comm (Œº _), ne_eq, ofReal_ne_top, not_false_eq_true, ‚Üê lintegral_const_mul',
    ‚Üê mul_assoc, ofReal_mul, distribution, hp.le] at this ‚ä¢
  -- TODO: clean up this whole proof
  by_cases ae_finite : Œº {x | ‚Äñf x‚Äñ‚Çë = ‚àû} = 0
  ¬∑ -- main case
    convert this using 1
    ¬∑ apply lintegral_congr_ae
      rw [Filter.eventuallyEq_iff_exists_mem]
      use {x | ‚Äñf x‚Äñ‚Çë ‚â† ‚àû}
      rw [mem_ae_iff, compl_setOf]
      simp only [ne_eq, Decidable.not_not]
      use ae_finite
      intro x hx
      dsimp only
      rw [toReal_rpow, ofReal_toReal (rpow_ne_top_of_nonneg hp.le hx)]
    apply setLIntegral_congr_fun measurableSet_Ioi
    intro t ht
    dsimp only
    congr 1
    symm
    apply measure_eq_measure_of_null_diff
    ¬∑ intro x hx
      simp only [mem_setOf_eq] at *
      rwa [ofReal_lt_iff_lt_toReal ht.le]
      by_contra hfx
      rw [hfx] at hx
      simp only [toReal_top] at hx
      linarith [hx, mem_Ioi.mp ht]
    dsimp [sdiff, Set.diff]
    apply measure_mono_null _ ae_finite
    intro x hx
    dsimp only [mem_setOf_eq] at *
    by_contra hf_top
    rw [ofReal_lt_iff_lt_toReal ht.le hf_top] at hx
    exact hx.2 hx.1
  ¬∑ rw [lintegral_eq_top_of_measure_eq_top_ne_zero]
    ¬∑ symm
      push_neg at ae_finite
      rw [‚Üê zero_lt_iff] at ae_finite
      rw [eq_top_iff]
      calc
      _ = ENNReal.ofReal p *
          (‚à´‚Åª (t : ‚Ñù) in Ioi 0, ENNReal.ofReal (t ^ (p - 1))) * Œº {x | ‚Äñf x‚Äñ‚Çë = ‚àû} := by
        convert (top_mul ae_finite.ne').symm
        convert mul_top (ENNReal.ofReal_pos.mpr hp).ne'
        rw [‚Üê not_ne_iff, lintegral_ofReal_ne_top_iff_integrable]; rotate_left
        ¬∑ exact (measurable_id.pow_const (p - 1)).aestronglyMeasurable.restrict
        ¬∑ refine ae_restrict_of_forall_mem measurableSet_Ioi fun x mx ‚Ü¶ ?_
          simp_rw [Pi.zero_apply]; rw [mem_Ioi] at mx; positivity
        exact not_integrableOn_Ioi_rpow (p - 1)
      _ = ‚à´‚Åª (t : ‚Ñù) in Ioi 0, ENNReal.ofReal p * ENNReal.ofReal (t ^ (p - 1))
            * Œº {x | ‚Äñf x‚Äñ‚Çë = ‚àû} := by
        rw [lintegral_mul_const, lintegral_const_mul] <;> fun_prop
      _ ‚â§ ‚à´‚Åª (t : ‚Ñù) in Ioi 0, ENNReal.ofReal p * ENNReal.ofReal (t ^ (p - 1))
            * Œº {x | ENNReal.ofReal t < ‚Äñf x‚Äñ‚Çë} := by
        gcongr with t x
        intro hfx
        simp [hfx]
    ¬∑ fun_prop
    ¬∑ simp_rw [rpow_eq_top_iff_of_pos hp]
      exact ae_finite

/-- The layer-cake theorem, or Cavalieri's principle, written using `eLpNorm`. -/
lemma eLpNorm_pow_eq_distribution {f : Œ± ‚Üí Œµ} (hf : AEStronglyMeasurable f Œº) {p : ‚Ñù‚â•0} (hp : 0 < p) :
    eLpNorm f p Œº ^ (p : ‚Ñù) =
    ‚à´‚Åª t in Ioi (0 : ‚Ñù), p * ENNReal.ofReal (t ^ ((p : ‚Ñù) - 1)) * distribution f (.ofReal t) Œº := by
  have h2p : 0 < (p : ‚Ñù) := hp
  simp_rw [eLpNorm_nnreal_eq_eLpNorm' hp.ne', eLpNorm', one_div, ‚Üê ENNReal.rpow_mul,
    inv_mul_cancel‚ÇÄ h2p.ne', ENNReal.rpow_one, lintegral_norm_pow_eq_distribution hf h2p,
    ENNReal.ofReal_mul zero_le_coe, ofReal_coe_nnreal]

/-- The layer-cake theorem, or Cavalieri's principle, written using `eLpNorm`, without
    taking powers. -/
lemma eLpNorm_eq_distribution {f : Œ± ‚Üí Œµ} (hf : AEStronglyMeasurable f Œº) {p : ‚Ñù} (hp : 0 < p) :
    eLpNorm f (.ofReal p) Œº =
    (ENNReal.ofReal p  * ‚à´‚Åª t in Ioi (0 : ‚Ñù), distribution f (.ofReal t) Œº *
        ENNReal.ofReal (t ^ (p - 1)) ) ^ p‚Åª¬π := by
  unfold eLpNorm
  split_ifs with sgn_p sz_p
  ¬∑ exact False.elim (not_le_of_gt hp (ofReal_eq_zero.mp sgn_p))
  ¬∑ exact False.elim (coe_ne_top sz_p)
  ¬∑ unfold eLpNorm'
    rw [toReal_ofReal hp.le, one_div]
    congr 1
    rw [‚Üê lintegral_const_mul']
    swap; ¬∑ exact coe_ne_top
    rw [lintegral_norm_pow_eq_distribution hf hp]
    congr 1 with x; rw [ofReal_mul] <;> [ring; positivity]

lemma lintegral_pow_mul_distribution {f : Œ± ‚Üí Œµ} (hf : AEStronglyMeasurable f Œº) {p : ‚Ñù} (hp : -1 < p) :
    ‚à´‚Åª t in Ioi (0 : ‚Ñù), ENNReal.ofReal (t ^ p) * distribution f (.ofReal t) Œº =
    ENNReal.ofReal (p + 1)‚Åª¬π * ‚à´‚Åª x, ‚Äñf x‚Äñ‚Çë ^ (p + 1) ‚àÇŒº := by
  have h2p : 0 < p + 1 := by linarith
  have h3p : 0 ‚â§ p + 1 := by linarith
  have h4p : p + 1 ‚â† 0 := by linarith
  simp [*, lintegral_norm_pow_eq_distribution, ‚Üê lintegral_const_mul', ‚Üê ofReal_mul, ‚Üê mul_assoc]

end Layercake

end MeasureTheory
