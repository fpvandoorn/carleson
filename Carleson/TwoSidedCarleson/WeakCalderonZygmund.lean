import Carleson.TwoSidedCarleson.Basic
import Carleson.ToMathlib.HardyLittlewood

open MeasureTheory Set Bornology Function ENNReal Metric Filter Topology
open scoped NNReal

noncomputable section

variable {X : Type*} {a : ‚Ñï} [MetricSpace X] [DoublingMeasure X (defaultA a : ‚Ñï)]
variable {œÑ C r R : ‚Ñù} {q q' : ‚Ñù‚â•0}
variable {F G : Set X}
variable {K : X ‚Üí X ‚Üí ‚ÑÇ} {x x' : X} [IsTwoSidedKernel a K]
variable {f : X ‚Üí ‚ÑÇ} {Œ± : ‚Ñù‚â•0‚àû}

/-! ## Section 10.2 and Lemma 10.0.3

Question: -/

/-- The constant used in `nontangential_from_simple`.
I(F) think the constant needs to be fixed in the blueprint. -/
irreducible_def C10_2_1 (a : ‚Ñï) : ‚Ñù‚â•0 := 2 ^ (4 * a)

/-- Lemma 10.2.1, formulated differently.
The blueprint version is basically this after unfolding `HasBoundedWeakType`, `wnorm` and `wnorm'`.
-/
theorem maximal_theorem [Nonempty X] :
    HasBoundedWeakType (globalMaximalFunction volume 1 : (X ‚Üí ‚ÑÇ) ‚Üí X ‚Üí ‚Ñù‚â•0‚àû) 1 1 volume volume
      (C10_2_1 a) := by
  apply HasWeakType.hasBoundedWeakType
  have : C10_2_1 a = C_weakType_globalMaximalFunction (defaultA a) 1 1 := by
    unfold C_weakType_globalMaximalFunction C_weakType_maximalFunction
    split_ifs with h; swap; simp at h
    simp_rw [C10_2_1_def, defaultA, coe_pow, coe_ofNat, Nat.cast_pow, Nat.cast_ofNat,
        NNReal.coe_one, div_one, rpow_ofNat, pow_mul', ‚Üê npow_add,
        two_add_two_eq_four]; rfl
  rw [this]
  apply hasWeakType_globalMaximalFunction (Œº := volume) le_rfl le_rfl

/-- Lemma 10.2.2.
Should be an easy consequence of `VitaliFamily.ae_tendsto_average`. -/
theorem lebesgue_differentiation
    {f : X ‚Üí ‚ÑÇ} (hf : BoundedFiniteSupport f) :
    ‚àÄ·µê x ‚àÇvolume, ‚àÉ (c : ‚Ñï ‚Üí X) (r : ‚Ñï ‚Üí ‚Ñù),
    Tendsto (fun i ‚Ü¶ ‚®ç y in ball (c i) (r i), f y ‚àÇvolume) atTop (ùìù (f x)) ‚àß
    Tendsto r atTop (ùìù[>] 0) ‚àß
    ‚àÄ i, x ‚àà ball (c i) (r i) := by
  sorry

/-! Lemma 10.2.3 is in Mathlib: `Pairwise.countable_of_isOpen_disjoint`. -/

section Depth

/-- `Œ¥(x)` in the blueprint. The depth of a point `x` in a set `O` is the supremum of radii of
balls centred on `x` that are subsets of `O`. -/
def depth (O : Set X) (x : X) : ‚Ñù‚â•0‚àû :=
  ‚®Ü r : ‚Ñù‚â•0, ‚®Ü (_ : ball x r ‚äÜ O), r

variable {O : Set X} {x y : X}

lemma depth_lt_iff_not_disjoint {d : ‚Ñù} :
    depth O x < ENNReal.ofReal d ‚Üî ¬¨Disjoint (ball x d) O·∂ú where
  mp hd := by
    simp_rw [depth, iSup_lt_iff, iSup_le_iff] at hd; obtain ‚ü®d', ld', hd'‚ü© := hd
    have ns := (hd' d.toNNReal).mt; rw [not_le] at ns; specialize ns ld'
    rw [not_subset_iff_exists_mem_notMem] at ns; obtain ‚ü®y, my, ny‚ü© := ns
    have pd := (zero_le _).trans_lt ld'
    rw [ofReal_pos] at pd; replace pd := Real.coe_toNNReal d pd.le
    rw [pd] at my; exact not_disjoint_iff.mpr ‚ü®y, my, ny‚ü©
  mpr hd := by
    obtain ‚ü®y, my‚ÇÅ, my‚ÇÇ‚ü© := not_disjoint_iff.mp hd
    simp_rw [depth, iSup_lt_iff, iSup_le_iff]; use edist x y
    rw [mem_ball'] at my‚ÇÅ; simp_rw [edist_lt_ofReal, my‚ÇÅ, true_and]; intro d' sd; by_contra! h
    rw [‚Üê ofReal_coe_nnreal, edist_lt_ofReal, ‚Üê mem_ball'] at h
    exact my‚ÇÇ (sd h)

lemma le_depth_iff_subset {d : ‚Ñù} : ENNReal.ofReal d ‚â§ depth O x ‚Üî ball x d ‚äÜ O := by
  rw [‚Üê disjoint_compl_right_iff_subset, ‚Üê not_iff_not, not_le]
  exact depth_lt_iff_not_disjoint

/-- A point's depth in an open set `O` is positive iff it lies in `O`. -/
lemma depth_pos_iff_mem (hO : IsOpen O) : 0 < depth O x ‚Üî x ‚àà O := by
  constructor <;> intro h
  ¬∑ contrapose! h; simp_rw [le_zero_iff, depth, iSup_eq_zero]; intro d hd
    by_contra! dpos; apply absurd _ h; rw [coe_ne_zero, ‚Üê zero_lt_iff] at dpos
    exact hd (mem_ball_self dpos)
  ¬∑ obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© := Metric.mem_nhds_iff.mp (hO.mem_nhds h)
    lift Œµ to ‚Ñù‚â•0 using Œµpos.le
    calc
      _ < ofNNReal Œµ := mod_cast Œµpos
      _ ‚â§ _ := le_biSup _ hŒµ

lemma depth_eq_zero_iff_notMem (hO : IsOpen O) : depth O x = 0 ‚Üî x ‚àâ O := by
  have := (depth_pos_iff_mem hO (x := x)).not
  rwa [not_lt, le_zero_iff] at this

/-- A point has finite depth in `O` iff `O` is not the whole space. -/
lemma depth_lt_top_iff_ne_univ : depth O x < ‚ä§ ‚Üî O ‚â† univ := by
  constructor <;> intro h
  ¬∑ contrapose! h; simp_rw [top_le_iff, depth, iSup‚ÇÇ_eq_top, h, subset_univ, exists_const]
    intro r rlt; lift r to ‚Ñù‚â•0 using rlt.ne
    use r + 1; exact coe_lt_coe_of_lt (lt_add_one r)
  ¬∑ obtain ‚ü®p, np‚ü© := (ne_univ_iff_exists_notMem _).mp h
    calc
      _ ‚â§ edist x p := by
        refine iSup‚ÇÇ_le fun r hr ‚Ü¶ ?_
        contrapose! hr; rw [not_subset]; use p, ?_, np
        rw [coe_nnreal_eq, edist_dist, ofReal_lt_ofReal_iff_of_nonneg dist_nonneg] at hr
        rwa [mem_ball']
      _ < _ := edist_lt_top ..

/-- A "triangle inequality" for depths. -/
lemma depth_le_edist_add_depth : depth O x ‚â§ edist x y + depth O y := by
  refine iSup‚ÇÇ_le fun d sd ‚Ü¶ ?_; contrapose! sd
  rw [‚Üê lt_tsub_iff_left, coe_nnreal_eq, edist_dist, ‚Üê ENNReal.ofReal_sub _ dist_nonneg,
    depth_lt_iff_not_disjoint, not_disjoint_iff] at sd
  obtain ‚ü®z, mz‚ÇÅ, mz‚ÇÇ‚ü© := sd
  rw [mem_ball, lt_tsub_iff_left] at mz‚ÇÅ
  replace mz‚ÇÅ := mem_ball'.mpr <| (dist_triangle_right ..).trans_lt mz‚ÇÅ
  exact fun w ‚Ü¶ mz‚ÇÇ (w mz‚ÇÅ)

lemma depth_bound_1 (hO : O ‚â† univ)
    (h : ¬¨Disjoint (ball x ((depth O x).toReal / 6)) (ball y ((depth O y).toReal / 6))) :
    x ‚àà ball y (3 * ((depth O y).toReal / 6)) := by
  rw [mem_ball]
  have dnt {z : X} : depth O z ‚â† ‚ä§ := (depth_lt_top_iff_ne_univ.mpr hO).ne
  have pre : depth O x / 6 + 5 * depth O x / 6 ‚â§ depth O x / 6 + 7 * depth O y / 6 := by
    calc
      _ = depth O x := by
        rw [‚Üê ENNReal.add_div, ‚Üê one_add_mul, show (1 : ‚Ñù‚â•0‚àû) + 5 = 6 by norm_num, mul_comm]
        exact ENNReal.mul_div_cancel_right (by norm_num) (by norm_num)
      _ ‚â§ edist x y + depth O y := depth_le_edist_add_depth
      _ ‚â§ ENNReal.ofReal ((depth O x).toReal / 6 + (depth O y).toReal / 6) + depth O y := by
        rw [edist_dist]
        exact add_le_add_right (ofReal_le_ofReal (dist_lt_of_not_disjoint_ball h).le) _
      _ ‚â§ depth O x / 6 + depth O y / 6 + depth O y := by
        rw [ofReal_add (by positivity) (by positivity)]
        iterate 2 rw [ofReal_div_of_pos (by norm_num), ofReal_ofNat, ofReal_toReal dnt]
      _ = _ := by
        rw [show (7 : ‚Ñù‚â•0‚àû) = 1 + 6 by norm_num, one_add_mul, ENNReal.add_div, mul_comm,
          ENNReal.mul_div_cancel_right (by norm_num) (by norm_num), add_assoc]
  rw [ENNReal.add_le_add_iff_left (div_ne_top dnt (by norm_num)),
    ENNReal.div_le_iff (by norm_num) (by norm_num),
    ENNReal.div_mul_cancel (by norm_num) (by norm_num), mul_comm,
    ‚Üê ENNReal.le_div_iff_mul_le (.inl (by norm_num)) (.inl (by norm_num)),
    ENNReal.mul_div_right_comm] at pre
  calc
    _ < (depth O x).toReal / 6 + (depth O y).toReal / 6 := dist_lt_of_not_disjoint_ball h
    _ ‚â§ (7 / 5 * depth O y).toReal / 6 + (depth O y).toReal / 6 := by
      gcongr; exact mul_ne_top (by finiteness) dnt
    _ ‚â§ 2 * (depth O y).toReal / 6 + (depth O y).toReal / 6 := by
      nth_rw 3 [‚Üê toReal_ofNat]; rw [‚Üê toReal_mul]; gcongr
      ¬∑ exact mul_ne_top (by finiteness) dnt
      ¬∑ rw [ENNReal.div_le_iff_le_mul (.inl (by norm_num)) (.inl (by norm_num))]
        norm_num
    _ = _ := by rw [mul_div_assoc, ‚Üê add_one_mul, two_add_one_eq_three]

lemma depth_bound_2 (hO : O ‚â† univ) (h : x ‚àà ball y (3 * ((depth O y).toReal / 6))) :
    (depth O x).toReal / 6 + dist x y ‚â§ 8 * (depth O y).toReal / 6 := by
  rw [mem_ball] at h
  have ent : edist x y ‚â† ‚ä§ := by finiteness
  have dnt {z : X} : depth O z ‚â† ‚ä§ := (depth_lt_top_iff_ne_univ.mpr hO).ne
  calc
    _ ‚â§ (edist x y + depth O y).toReal / 6 + dist x y := by
      gcongr
      ¬∑ rw [ENNReal.add_ne_top]; exact ‚ü®ent, dnt‚ü©
      ¬∑ exact depth_le_edist_add_depth
    _ = (depth O y).toReal / 6 + (7 / 6) * dist x y := by
      rw [ENNReal.toReal_add ent dnt, ‚Üê dist_edist]; ring
    _ ‚â§ (depth O y).toReal / 6 + (7 / 6) * (3 * ((depth O y).toReal / 6)) := by gcongr
    _ = (9 / 2) * (depth O y).toReal / 6 := by ring
    _ ‚â§ _ := by gcongr; norm_num

lemma depth_bound_3 (hO : O ‚â† univ) (h : x ‚àà ball y (3 * ((depth O y).toReal / 6))) :
    (depth O y).toReal / 6 + dist y x ‚â§ 8 * (depth O x).toReal / 6 := by
  rw [mem_ball'] at h
  have dnt {z : X} : depth O z ‚â† ‚ä§ := (depth_lt_top_iff_ne_univ.mpr hO).ne
  have dnt2 : depth O y / 2 ‚â† ‚ä§ := ENNReal.div_ne_top dnt (by norm_num)
  have hti : depth O y ‚â§ 2 * depth O x := by
    rw [mul_comm, ‚Üê ENNReal.div_le_iff_le_mul (.inl (by norm_num)) (.inl (by norm_num)),
      ‚Üê ENNReal.add_le_add_iff_left dnt2, ENNReal.add_halves]
    calc
      _ ‚â§ edist y x + depth O x := depth_le_edist_add_depth
      _ ‚â§ _ := by
        gcongr; rw [edist_dist]; apply ofReal_le_of_le_toReal
        rw [toReal_div, toReal_ofNat]; linarith
  calc
    _ ‚â§ (2 * depth O x).toReal / 6 + 3 * ((depth O y).toReal / 6) := by
      gcongr; exact mul_ne_top ofNat_ne_top dnt
    _ ‚â§ (2 * depth O x).toReal / 6 + 3 * ((2 * depth O x).toReal / 6) := by
      gcongr; exact mul_ne_top ofNat_ne_top dnt
    _ = _ := by rw [toReal_mul, toReal_ofNat]; ring

lemma ball_covering_bounded_intersection
    (hO : IsOpen O ‚àß O ‚â† univ) {U : Set X} (countU : U.Countable)
    (pdU : U.PairwiseDisjoint fun c ‚Ü¶ ball c ((depth O c).toReal / 6)) {x : X} (mx : x ‚àà O) :
    {c ‚àà U | x ‚àà ball c (3 * ((depth O c).toReal / 6))}.encard ‚â§ (2 ^ (6 * a) : ‚Ñï) := by
  set V := {c ‚àà U | x ‚àà ball c (3 * ((depth O c).toReal / 6))}
  have vbpos : 0 < volume (ball x ((depth O x).toReal / 6)) := by
    refine measure_ball_pos volume x (div_pos ?_ (by norm_num))
    rw [toReal_pos_iff, depth_pos_iff_mem hO.1, depth_lt_top_iff_ne_univ]; exact ‚ü®mx, hO.2‚ü©
  have vbnt : volume (ball x ((depth O x).toReal / 6)) ‚â† ‚ä§ := by finiteness
  rw [‚Üê ENat.toENNReal_le, Nat.cast_pow, Nat.cast_ofNat, ENat.toENNReal_pow, ENat.toENNReal_ofNat,
    ‚Üê ENNReal.mul_le_mul_right vbpos.ne' vbnt]
  have Aeq : (2 : ‚Ñù‚â•0‚àû) ^ (3 * a) = defaultA a ^ 3 := by
    rw [defaultA, Nat.cast_pow, Nat.cast_ofNat, ‚Üê pow_mul, mul_comm]
  calc
    _ = ‚àë' c : V, volume (ball x ((depth O x).toReal / 6)) := (ENNReal.tsum_set_const _ _).symm
    _ ‚â§ ‚àë' c : V, volume (ball c.1 (8 * (depth O c.1).toReal / 6)) :=
      ENNReal.tsum_le_tsum fun ‚ü®u, mu, xu‚ü© ‚Ü¶
        measure_mono (ball_subset_ball' (depth_bound_2 hO.2 xu))
    _ ‚â§ 2 ^ (3 * a) * ‚àë' v : V, volume (ball v.1 ((depth O v.1).toReal / 6)) := by
      rw [‚Üê ENNReal.tsum_mul_left]; refine ENNReal.tsum_le_tsum fun ‚ü®u, mu, xu‚ü© ‚Ü¶ ?_
      rw [mul_div_assoc, show (8 : ‚Ñù) = 2 ^ 3 by norm_num, Aeq]
      apply measure_ball_two_le_same_iterate
    _ = 2 ^ (3 * a) * volume (‚ãÉ v : V, ball v.1 ((depth O v.1).toReal / 6)) := by
      have VsU : V ‚äÜ U := sep_subset ..
      haveI : Countable V := by rw [countable_coe_iff]; exact countU.mono VsU
      congr 1
      refine (measure_iUnion (fun ‚ü®v‚ÇÅ, mv‚ÇÅ‚ü© ‚ü®v‚ÇÇ, mv‚ÇÇ‚ü© hn ‚Ü¶ ?_) (fun _ ‚Ü¶ measurableSet_ball)).symm
      rw [ne_eq, Subtype.mk.injEq] at hn
      exact pdU (VsU mv‚ÇÅ) (VsU mv‚ÇÇ) hn
    _ ‚â§ 2 ^ (3 * a) * volume (ball x (8 * (depth O x).toReal / 6)) := by
      gcongr; exact iUnion_subset fun ‚ü®u, mu, xu‚ü© ‚Ü¶ ball_subset_ball' (depth_bound_3 hO.2 xu)
    _ ‚â§ _ := by
      rw [show 6 * a = 3 * a + 3 * a by omega, pow_add, mul_assoc]; gcongr
      rw [mul_div_assoc, show (8 : ‚Ñù) = 2 ^ 3 by norm_num, Aeq]
      apply measure_ball_two_le_same_iterate

/-- Lemma 10.2.4, but following the blueprint exactly (with a countable set of centres rather than
functions from `‚Ñï`). -/
lemma ball_covering' (hO : IsOpen O ‚àß O ‚â† univ) :
    ‚àÉ (U : Set X) (r : X ‚Üí ‚Ñù), U.Countable ‚àß (U.PairwiseDisjoint fun c ‚Ü¶ ball c (r c)) ‚àß
      ‚ãÉ c ‚àà U, ball c (3 * r c) = O ‚àß (‚àÄ c ‚àà U, ¬¨Disjoint (ball c (7 * r c)) O·∂ú) ‚àß
      ‚àÄ x ‚àà O, {c ‚àà U | x ‚àà ball c (3 * r c)}.encard ‚â§ (2 ^ (6 * a) : ‚Ñï) := by
  let W : Set (Set X) := {U | U ‚äÜ O ‚àß U.PairwiseDisjoint fun c ‚Ü¶ ball c ((depth O c).toReal / 6)}
  obtain ‚ü®U, maxU‚ü© : ‚àÉ U, Maximal (¬∑ ‚àà W) U := by
    refine zorn_subset _ fun U sU cU ‚Ü¶ ‚ü®‚ãÉ‚ÇÄ U, ?_, fun _ ‚Ü¶ subset_sUnion_of_mem‚ü©
    simp only [W, sUnion_subset_iff, mem_setOf_eq]
    exact ‚ü®fun u hu ‚Ü¶ (sU hu).1, (pairwiseDisjoint_sUnion cU.directedOn).2 fun u hu ‚Ü¶ (sU hu).2‚ü©
  have countU : U.Countable := by
    refine maxU.1.2.countable_of_isOpen (fun _ _ ‚Ü¶ isOpen_ball) (fun u mu ‚Ü¶ ?_)
    rw [nonempty_ball]; refine div_pos (toReal_pos ?_ ?_) (by norm_num)
    ¬∑ rw [‚Üê zero_lt_iff, depth_pos_iff_mem hO.1]; exact maxU.1.1 mu
    ¬∑ rw [‚Üê lt_top_iff_ne_top, depth_lt_top_iff_ne_univ]; exact hO.2
  refine ‚ü®U, fun c ‚Ü¶ (depth O c).toReal / 6, countU, maxU.1.2, ?_, fun c mc ‚Ü¶ ?_, fun x mx ‚Ü¶ ?_‚ü©
  ¬∑ refine subset_antisymm (fun x mx ‚Ü¶ ?_) (fun x mx ‚Ü¶ ?_)
    ¬∑ simp only [mem_iUnion‚ÇÇ] at mx; obtain ‚ü®c, mc, mx‚ü© := mx
      refine (le_depth_iff_subset.mp ?_) mx
      rw [‚Üê mul_comm_div, ENNReal.ofReal_mul (by norm_num),
        ENNReal.ofReal_toReal (depth_lt_top_iff_ne_univ.mpr hO.2).ne]
      nth_rw 2 [‚Üê one_mul (depth O _)]; gcongr; norm_num
    ¬∑ rw [mem_iUnion‚ÇÇ]
      by_cases mxU : x ‚àà U
      ¬∑ refine ‚ü®x, mxU, mem_ball_self ?_‚ü©
        simp only [Nat.ofNat_pos, mul_pos_iff_of_pos_left, div_pos_iff_of_pos_right, toReal_pos_iff]
        rw [depth_pos_iff_mem hO.1, depth_lt_top_iff_ne_univ]; exact ‚ü®mx, hO.2‚ü©
      obtain ‚ü®i, mi, hi‚ü© : ‚àÉ i ‚àà U,
          ¬¨Disjoint (ball x ((depth O x).toReal / 6)) (ball i ((depth O i).toReal / 6)) := by
        by_contra! h; apply absurd maxU; simp_rw [not_maximal_iff maxU.1]
        exact ‚ü®insert x U, ‚ü®insert_subset mx maxU.1.1, maxU.1.2.insert_of_notMem mxU h‚ü©,
          ssubset_insert mxU‚ü©
      use i, mi, depth_bound_1 hO.2 hi
  ¬∑ rw [‚Üê depth_lt_iff_not_disjoint, ‚Üê mul_comm_div, ENNReal.ofReal_mul (by norm_num),
      ENNReal.ofReal_toReal (depth_lt_top_iff_ne_univ.mpr hO.2).ne]
    nth_rw 1 [‚Üê one_mul (depth O _)]
    have dpos := (depth_pos_iff_mem hO.1).mpr (maxU.1.1 mc)
    have dlt := (depth_lt_top_iff_ne_univ (x := c)).mpr hO.2
    exact ENNReal.mul_lt_mul_right' dpos.ne' dlt.ne (by norm_num)
  ¬∑ exact ball_covering_bounded_intersection hO countU maxU.1.2 mx

/-- Lemma 10.2.4. -/
theorem ball_covering (ha : 4 ‚â§ a) {O : Set X} (hO : IsOpen O ‚àß O ‚â† univ) :
    ‚àÉ (c : ‚Ñï ‚Üí X) (r : ‚Ñï ‚Üí ‚Ñù), (univ.PairwiseDisjoint fun i ‚Ü¶ ball (c i) (r i)) ‚àß
      ‚ãÉ i, ball (c i) (3 * r i) = O ‚àß (‚àÄ i, 0 < r i ‚Üí ¬¨Disjoint (ball (c i) (7 * r i)) O·∂ú) ‚àß
      ‚àÄ x ‚àà O, {i | x ‚àà ball (c i) (3 * r i)}.encard ‚â§ (2 ^ (6 * a) : ‚Ñï) := by
  sorry

#exit

end Depth

variable [CompatibleFunctions ‚Ñù X (defaultA a)] [IsCancellative X (defaultœÑ a)]

/-! ### Lemma 10.2.5

Lemma 10.2.5 has an annoying case distinction between the case `E_Œ± ‚â† X` (general case) and
`E_Œ± = X` (finite case). It isn't easy to get rid of this case distinction.

In the formalization we state most properties of Lemma 10.2.5 twice, once for each case
(in some cases the condition is vacuous in the finite case, and then we omit it)

We could have tried harder to uniformize the cases, but in the finite case there is really only set
`B*_j`, and in the general case it is convenient to index `B*_j` by the natural numbers.
-/

/-- The property specifying whether we are in the "general case". -/
def GeneralCase (f : X ‚Üí ‚ÑÇ) (Œ± : ‚Ñù‚â•0‚àû) : Prop :=
  ‚àÉ x, Œ± ‚â• globalMaximalFunction (X := X) volume 1 f x

omit [IsCancellative X (defaultœÑ a)] in
/-- In the finite case, the volume of `X` is finite. -/
lemma volume_lt_of_not_GeneralCase (hf : BoundedFiniteSupport f) (h : ¬¨ GeneralCase f Œ±)
    (hŒ± : 0 < Œ±) : volume (univ : Set X) < ‚àû := by
  simp only [GeneralCase, not_exists, not_le] at h
  have ne_top : Œ± ‚â† ‚ä§ := have ‚ü®x‚ü© : Nonempty X := inferInstance; (h x).ne_top
  have ineq := (maximal_theorem f hf).2
  simp only [wnorm, one_ne_top, reduceIte, wnorm', distribution, toReal_one, inv_one, rpow_one,
    iSup_le_iff] at ineq
  rw [‚Üê eq_univ_iff_forall.mpr fun x ‚Ü¶ (coe_toNNReal ne_top) ‚ñ∏ h x]
  refine lt_top_of_mul_ne_top_right ?_ (coe_ne_zero.mpr (toNNReal_ne_zero.mpr ‚ü®hŒ±.ne.symm, ne_top‚ü©))
  refine (lt_of_le_of_lt (ineq Œ±.toNNReal) ?_).ne
  exact mul_lt_top (by norm_num) <| (BoundedFiniteSupport.memLp hf 1).eLpNorm_lt_top

omit [CompatibleFunctions ‚Ñù X (defaultA a)] [IsCancellative X (defaultœÑ a)] in
lemma isOpen_MB_preimage_Ioi (hX : GeneralCase f Œ±) :
    IsOpen (globalMaximalFunction (X := X) volume 1 f ‚Åª¬π' Ioi Œ±) ‚àß
    globalMaximalFunction (X := X) volume 1 f ‚Åª¬π' Ioi Œ± ‚â† univ :=
  have ‚ü®x, hx‚ü© := hX
  ‚ü®lowerSemiContinuous_globalMaximalFunction.isOpen_preimage _,
    (Set.ne_univ_iff_exists_notMem _).mpr ‚ü®x, by simpa using hx‚ü©‚ü©

/-- The center of B_j in the proof of Lemma 10.2.5 (general case). -/
def czCenter (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (i : ‚Ñï) : X :=
  ball_covering ha (isOpen_MB_preimage_Ioi hX) |>.choose i

/-- The radius of B_j in the proof of Lemma 10.2.5 (general case). -/
def czRadius (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (i : ‚Ñï) : ‚Ñù :=
  ball_covering ha (isOpen_MB_preimage_Ioi hX) |>.choose_spec.choose i

/-- The ball B_j in the proof of Lemma 10.2.5 (general case). -/
abbrev czBall (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (i : ‚Ñï) : Set X :=
  ball (czCenter ha hX i) (czRadius ha hX i)

/-- The ball B_j' introduced below Lemma 10.2.5 (general case). -/
abbrev czBall2 (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (i : ‚Ñï) : Set X :=
  ball (czCenter ha hX i) (2 * czRadius ha hX i)

/-- The ball B_j* in Lemma 10.2.5 (general case). -/
abbrev czBall3 (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (i : ‚Ñï) : Set X :=
  ball (czCenter ha hX i) (3 * czRadius ha hX i)

/-- The ball B_j** in the proof of Lemma 10.2.5 (general case). -/
abbrev czBall7 (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (i : ‚Ñï) : Set X :=
  ball (czCenter ha hX i) (7 * czRadius ha hX i)

lemma czBall_pairwiseDisjoint (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±} :
    univ.PairwiseDisjoint fun i ‚Ü¶ ball (czCenter ha hX i) (czRadius ha hX i) :=
  ball_covering ha (isOpen_MB_preimage_Ioi hX) |>.choose_spec.choose_spec.1

lemma iUnion_czBall3 (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±} :
    ‚ãÉ i, czBall3 ha hX i = globalMaximalFunction volume 1 f ‚Åª¬π' Ioi Œ± :=
  ball_covering ha (isOpen_MB_preimage_Ioi hX) |>.choose_spec.choose_spec.2.1

lemma not_disjoint_czBall7 (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±} {i : ‚Ñï} (hi : 0 < czRadius ha hX i) :
    ¬¨Disjoint (czBall7 ha hX i) (globalMaximalFunction volume 1 f ‚Åª¬π' Ioi Œ±)·∂ú :=
  ball_covering ha (isOpen_MB_preimage_Ioi hX) |>.choose_spec.choose_spec.2.2.1 i hi

/-- Part of Lemma 10.2.5 (general case). -/
lemma encard_czBall3_le (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±}
    {y : X} (hy : Œ± < globalMaximalFunction volume 1 f y) :
    {i | y ‚àà czBall3 ha hX i}.encard ‚â§ (2 ^ (6 * a) : ‚Ñï) :=
  ball_covering ha (isOpen_MB_preimage_Ioi hX) |>.choose_spec.choose_spec.2.2.2 y hy

lemma mem_czBall3_finite (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±} {y : X}
    (hy : Œ± < globalMaximalFunction volume 1 f y) :
    {i | y ‚àà czBall3 ha hX i}.Finite :=
  finite_of_encard_le_coe (encard_czBall3_le ha hy)

/-- `Q_i` in the proof of Lemma 10.2.5 (general case). -/
def czPartition (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (i : ‚Ñï) : Set X :=
  czBall3 ha hX i \ ((‚ãÉ j < i, czPartition ha hX j) ‚à™ ‚ãÉ j > i, czBall ha hX j)

lemma MeasurableSet.czPartition (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (i : ‚Ñï) :
    MeasurableSet (czPartition ha hX i) := by
  refine i.strong_induction_on (fun j hj ‚Ü¶ ?_)
  unfold _root_.czPartition
  apply measurableSet_ball.diff ‚àò (MeasurableSet.biUnion (to_countable _) hj).union
  exact MeasurableSet.biUnion (to_countable _) (fun _ _ ‚Ü¶ measurableSet_ball)

lemma czBall_subset_czPartition (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±} {i : ‚Ñï} :
    czBall ha hX i ‚äÜ czPartition ha hX i := by
  intro r hr
  rw [czPartition]
  refine mem_diff_of_mem ?_ ?_
  ¬∑ have : dist r (czCenter ha hX i) ‚â• 0 := dist_nonneg
    simp_all only [mem_ball]
    linarith
  ¬∑ rw [mem_ball] at hr
    simp only [mem_union, mem_iUnion, mem_ball, not_or, not_exists, not_lt]
    constructor
    ¬∑ unfold czPartition
      simp only [mem_diff, mem_ball, mem_union, mem_iUnion, not_or, not_and, not_forall, not_not]
      exact fun _ _ _ _ ‚Ü¶ by use i
    ¬∑ intro x hx
      have := pairwiseDisjoint_iff.mp <| czBall_pairwiseDisjoint ha (hX := hX)
      simp only [mem_univ, forall_const] at this
      have := (disjoint_or_nonempty_inter _ _).resolve_right <| (@this i x).mt (by omega)
      exact not_lt.mp <| mem_ball.mpr.mt <| disjoint_left.mp this hr

lemma czPartition_subset_czBall3 (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±} {i : ‚Ñï} :
    czPartition ha hX i ‚äÜ czBall3 ha hX i := by
  rw [czPartition]; exact diff_subset

lemma czPartition_pairwiseDisjoint (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±} :
    univ.PairwiseDisjoint fun i ‚Ü¶ czPartition ha hX i := by
  simp only [pairwiseDisjoint_iff, mem_univ, forall_const]
  intro i k hik
  have ‚ü®x, hxi, hxk‚ü© := inter_nonempty.mp hik
  have (t d) (hx : x ‚àà czPartition ha hX t) (hd : t < d) : x ‚àâ czPartition ha hX d := by
    have : czPartition ha hX t ‚äÜ ‚ãÉ j < d, czPartition ha hX j := subset_biUnion_of_mem hd
    rw [czPartition]
    exact notMem_diff_of_mem <| mem_union_left _ (this hx)
  have : _ ‚àß _ := ‚ü®this i k hxi |>.mt (¬∑ hxk), this k i hxk |>.mt (¬∑ hxi)‚ü©
  omega

lemma czPartition_pairwiseDisjoint' (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±}
    {x : X} {i j : ‚Ñï} (hi : x ‚àà czPartition ha hX i) (hj : x ‚àà czPartition ha hX j) :
    i = j := by
  have := czPartition_pairwiseDisjoint ha (hX := hX)
  apply pairwiseDisjoint_iff.mp this (mem_univ i) (mem_univ j)
  exact inter_nonempty.mp <| .intro x ‚ü®hi, hj‚ü©

lemma iUnion_czPartition (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±} :
    ‚ãÉ i, czPartition ha hX i = globalMaximalFunction volume 1 f ‚Åª¬π' Ioi Œ± := by
  rw [‚Üê iUnion_czBall3 ha (hX := hX)]
  refine ext fun x ‚Ü¶ ‚ü®fun hx ‚Ü¶ ?_, fun hx ‚Ü¶ ?_‚ü©
  ¬∑ have : ‚ãÉ i, czPartition ha hX i ‚äÜ ‚ãÉ i, czBall3 ha hX i := fun y hy ‚Ü¶
      have ‚ü®r, hr‚ü© := mem_iUnion.mp hy
      mem_iUnion_of_mem r (czPartition_subset_czBall3 ha hr)
    exact this hx
  ¬∑ rw [mem_iUnion] at hx ‚ä¢
    by_contra! hp
    obtain ‚ü®g, hg‚ü© := hx
    have ‚ü®t, ht‚ü© : ‚àÉ i, x ‚àà (‚ãÉ j < i, czPartition ha hX j) ‚à™ ‚ãÉ j > i, czBall ha hX j := by
      by_contra! hb
      absurd hp g
      rw [czPartition, mem_diff]
      exact ‚ü®hg, hb g‚ü©
    have : ‚ãÉ j > t, czBall ha hX j ‚äÜ ‚ãÉ j > t, czPartition ha hX j :=
      iUnion‚ÇÇ_mono fun i j ‚Ü¶ czBall_subset_czPartition ha (i := i)
    have := (mem_or_mem_of_mem_union ht).imp_right (this ¬∑)
    simp_all

open scoped Classical in
variable (f) in
/-- The function `g` in Lemma 10.2.5. (both cases) -/
def czApproximation (ha : 4 ‚â§ a) (Œ± : ‚Ñù‚â•0‚àû) (x : X) : ‚ÑÇ :=
  if hX : GeneralCase f Œ± then
    if hx : ‚àÉ j, x ‚àà czPartition ha hX j then ‚®ç y in czPartition ha hX hx.choose, f y else f x
  else ‚®ç y, f y

lemma czApproximation_def_of_mem (ha : 4 ‚â§ a) {hX : GeneralCase f Œ±} {x : X}
    {i : ‚Ñï} (hx : x ‚àà czPartition ha hX i) :
    czApproximation f ha Œ± x = ‚®ç y in czPartition ha hX i, f y := by
  have : ‚àÉ i, x ‚àà czPartition ha hX i := ‚ü®i, hx‚ü©
  simp [czApproximation, hX, this, czPartition_pairwiseDisjoint' ha this.choose_spec hx]

lemma czApproximation_def_of_notMem (ha : 4 ‚â§ a) {x : X} (hX : GeneralCase f Œ±)
    (hx : x ‚àâ globalMaximalFunction volume 1 f ‚Åª¬π' Ioi Œ±) :
    czApproximation f ha Œ± x = f x := by
  rw [‚Üê iUnion_czPartition ha (hX := hX), mem_iUnion, not_exists] at hx
  simp only [czApproximation, hX, ‚ÜìreduceDIte, hx, exists_const]

lemma czApproximation_def_of_volume_lt (ha : 4 ‚â§ a) {x : X}
    (hX : ¬¨ GeneralCase f Œ±) : czApproximation f ha Œ± x = ‚®ç y, f y := by
  simp [czApproximation, hX]

/-- The function `b_i` in Lemma 10.2.5 (general case). -/
def czRemainder' (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (i : ‚Ñï) (x : X) : ‚ÑÇ :=
  (czPartition ha hX i).indicator (f - czApproximation f ha Œ±) x

variable (f) in
/-- The function `b = ‚àë‚±º b‚±º` introduced below Lemma 10.2.5.
In the finite case, we also use this as the function `b = b‚ÇÅ` in Lemma 10.2.5.
We choose a more convenient definition, but prove in `tsum_czRemainder'` that this is the same. -/
def czRemainder (ha : 4 ‚â§ a) (Œ± : ‚Ñù‚â•0‚àû) (x : X) : ‚ÑÇ :=
  f x - czApproximation f ha Œ± x

/-- Part of Lemma 10.2.5, this is essentially (10.2.16) (both cases). -/
def tsum_czRemainder' (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (x : X) :
    ‚àë·∂† i, czRemainder' ha hX i x = czRemainder f ha Œ± x := by
  simp only [czRemainder', czRemainder]
  by_cases hx : ‚àÉ j, x ‚àà czPartition ha hX j
  ¬∑ have ‚ü®j, hj‚ü© := hx
    rw [finsum_eq_single _ j, indicator_of_mem hj]
    ¬∑ rfl
    ¬∑ refine fun i hi ‚Ü¶ indicator_of_notMem ?_ _
      exact (czPartition_pairwiseDisjoint ha (mem_univ i) (mem_univ j) hi).notMem_of_mem_right hj
  ¬∑ simp only [czApproximation, hX, reduceDIte, hx, sub_self]
    exact finsum_eq_zero_of_forall_eq_zero fun i ‚Ü¶ indicator_of_notMem (fun hi ‚Ü¶ hx ‚ü®i, hi‚ü©) _

open scoped Classical in
/-- Part of Lemma 10.2.5 (both cases). -/
lemma aemeasurable_czApproximation (ha : 4 ‚â§ a) {hf : AEMeasurable f} :
    AEMeasurable (czApproximation f ha Œ±) := by
  by_cases hX : GeneralCase f Œ±; swap
  ¬∑ unfold czApproximation; simp [hX]
  let czA (x : X) := -- Measurable version of `czApproximation f ha Œ±`
    if hx : ‚àÉ j, x ‚àà czPartition ha hX j then ‚®ç y in czPartition ha hX hx.choose, f y else hf.mk f x
  refine ‚ü®czA, fun T hT ‚Ü¶ ?_, hf.ae_eq_mk.mono fun x h ‚Ü¶ by simp [czApproximation, czA, hX, h]‚ü©
  let S := {x : X | ‚àÉ j, x ‚àà czPartition ha hX j}·∂ú ‚à© (hf.mk f) ‚Åª¬π' T
  have : czA ‚Åª¬π' T = S ‚à™ ‚ãÉ‚ÇÄ (czPartition ha hX '' {i | ‚®ç y in czPartition ha hX i, f y ‚àà T}) := by
    refine subset_antisymm (fun x h ‚Ü¶ ?_) (fun x h ‚Ü¶ ?_)
    ¬∑ by_cases hx : ‚àÉ j, x ‚àà czPartition ha hX j
      ¬∑ refine Or.inr ‚ü®czPartition ha hX hx.choose, ‚ü®mem_image_of_mem _ ?_, hx.choose_spec‚ü©‚ü©
        simpa [czA, hx] using h
      ¬∑ exact Or.inl ‚ü®hx, by simpa [czA, hx, hX] using h‚ü©
    ¬∑ cases h with
      | inl h => simpa [czA, mem_setOf_eq ‚ñ∏ mem_setOf_eq ‚ñ∏ h.1] using h.2
      | inr h => obtain ‚ü®_, ‚ü®‚ü®i, ‚ü®hi, rfl‚ü©‚ü©, hxi‚ü©‚ü© := h
                 have hx : ‚àÉ j, x ‚àà czPartition ha hX j := ‚ü®i, hxi‚ü©
                 simpa [czA, hx, czPartition_pairwiseDisjoint' ha hx.choose_spec hxi] using hi
  rw [this]
  have := Measurable.exists (MeasurableSet.czPartition ha hX ¬∑ |>.mem)
  apply MeasurableSet.union (by measurability) ‚àò MeasurableSet.sUnion ((to_countable _).image _)
  simp [MeasurableSet.czPartition ha hX]

/-- Part of Lemma 10.2.5, equation (10.2.16) (both cases).
This is true by definition, the work lies in `tsum_czRemainder'` -/
lemma czApproximation_add_czRemainder (ha : 4 ‚â§ a) {x : X} :
    czApproximation f ha Œ± x + czRemainder f ha Œ± x = f x := by
  simp [czApproximation, czRemainder]

/-- Part of Lemma 10.2.5, equation (10.2.17) (both cases). -/
lemma norm_czApproximation_le (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} (hŒ± : ‚®ç‚Åª x, ‚Äñf x‚Äñ‚Çë < Œ±) :
    ‚àÄ·µê x, ‚ÄñczApproximation f ha Œ± x‚Äñ‚Çë ‚â§ 2 ^ (3 * a) * Œ± := by
  sorry

/-- Equation (10.2.17) specialized to the general case. -/
lemma norm_czApproximation_le_infinite (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f}
    (hX : GeneralCase f Œ±) (hŒ± : 0 < Œ±) :
    ‚àÄ·µê x, ‚ÄñczApproximation f ha Œ± x‚Äñ‚Çë ‚â§ 2 ^ (3 * a) * Œ± := by
  sorry

/-- Part of Lemma 10.2.5, equation (10.2.18) (both cases). -/
lemma eLpNorm_czApproximation_le (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} (hŒ± : 0 < Œ±) :
    eLpNorm (czApproximation f ha Œ±) 1 volume ‚â§ eLpNorm f 1 volume := by
  sorry

/-- Part of Lemma 10.2.5, equation (10.2.19) (general case). -/
lemma support_czRemainder'_subset (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} {hX : GeneralCase f Œ±} (hŒ± : 0 < Œ±)
    {i : ‚Ñï} :
    support (czRemainder' ha hX i) ‚äÜ czBall3 ha hX i := by
  sorry

/-- Part of Lemma 10.2.5, equation (10.2.20) (general case). -/
lemma integral_czRemainder' (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} {hX : GeneralCase f Œ±} (hŒ± : 0 < Œ±)
    {i : ‚Ñï} :
    ‚à´ x, czRemainder' ha hX i x = 0 := by
  sorry

/-- Part of Lemma 10.2.5, equation (10.2.20) (finite case). -/
lemma integral_czRemainder (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} (hX : ¬¨ GeneralCase f Œ±) (hŒ± : 0 < Œ±) :
    ‚à´ x, czRemainder f ha Œ± x = 0 := by
  sorry

/-- Part of Lemma 10.2.5, equation (10.2.21) (general case). -/
lemma eLpNorm_czRemainder'_le (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} {hX : GeneralCase f Œ±}
    (hŒ± : 0 < Œ±) {i : ‚Ñï} :
    eLpNorm (czRemainder' ha hX i) 1 volume ‚â§ 2 ^ (2 * a + 1) * Œ± * volume (czBall3 ha hX i) := by
  sorry

/-- Part of Lemma 10.2.5, equation (10.2.21) (finite case). -/
lemma eLpNorm_czRemainder_le (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} (hX : ¬¨ GeneralCase f Œ±)
    (hŒ± : 0 < Œ±) :
    eLpNorm (czRemainder f ha Œ±) 1 volume ‚â§ 2 ^ (2 * a + 1) * Œ± * volume (univ : Set X) := by
  sorry

/-- Part of Lemma 10.2.5, equation (10.2.22) (general case). -/
lemma tsum_volume_czBall3_le (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} (hX : GeneralCase f Œ±)
    (hŒ± : 0 < Œ±) :
    ‚àë' i, volume (czBall3 ha hX i) ‚â§ 2 ^ (4 * a) / Œ± * eLpNorm f 1 volume := by
  sorry

/-- Part of Lemma 10.2.5, equation (10.2.22) (finite case). -/
lemma volume_univ_le (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} (hX : ¬¨ GeneralCase f Œ±)
    (hŒ± : 0 < Œ±) :
    volume (univ : Set X) ‚â§ 2 ^ (4 * a) / Œ± * eLpNorm f 1 volume := by
  sorry

/-- Part of Lemma 10.2.5, equation (10.2.23) (general case). -/
lemma tsum_eLpNorm_czRemainder'_le (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} (hX : GeneralCase f Œ±)
    (hŒ± : 0 < Œ±) :
    ‚àë' i, eLpNorm (czRemainder' ha hX i) 1 volume ‚â§ 2 * eLpNorm f 1 volume := by
  sorry

/-- Part of Lemma 10.2.5, equation (10.2.23) (finite case). -/
lemma tsum_eLpNorm_czRemainder_le (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} (hX : ¬¨ GeneralCase f Œ±)
    (hŒ± : 0 < Œ±) :
    eLpNorm (czRemainder f ha Œ±) 1 volume ‚â§ 2 * eLpNorm f 1 volume := by
  sorry

/- ### Lemmas 10.2.6 - 10.2.9 -/

/-- The constant `c` introduced below Lemma 10.2.5. -/
irreducible_def c10_0_3 (a : ‚Ñï) : ‚Ñù‚â•0 := (2 ^ (a ^ 3 + 12 * a + 4))‚Åª¬π

open scoped Classical in
variable (f) in
/-- The set `Œ©` introduced below Lemma 10.2.5. -/
def Œ© (ha : 4 ‚â§ a) (Œ± : ‚Ñù‚â•0‚àû) : Set X :=
  if hX : GeneralCase f Œ± then ‚ãÉ i, czBall2 ha hX i else univ

/-- The constant used in `estimate_good`. -/
irreducible_def C10_2_6 (a : ‚Ñï) : ‚Ñù‚â•0 := 2 ^ (2 * a ^ 3 + 3 * a + 2) * c10_0_3 a

/-- Lemma 10.2.6 -/
lemma estimate_good (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f} (hŒ± : ‚®ç‚Åª x, ‚Äñf x‚Äñ‚Çë / c10_0_3 a < Œ±) :
    distribution (czOperator K r (czApproximation f ha Œ±)) (Œ± / 2) volume ‚â§
    C10_2_6 a / Œ± * eLpNorm f 1 volume := by
  sorry

/-- The constant used in `czOperatorBound`. -/
irreducible_def C10_2_7 (a : ‚Ñï) : ‚Ñù‚â•0 := 2 ^ (a ^ 3 + 2 * a + 1) * c10_0_3 a

/-- The function `F` defined in Lemma 10.2.7. -/
def czOperatorBound (ha : 4 ‚â§ a) (hX : GeneralCase f Œ±) (x : X) : ‚Ñù‚â•0‚àû :=
  C10_2_7 a * Œ± * ‚àë' i, ((czRadius ha hX i).toNNReal / nndist x (czCenter ha hX i)) ^ (a : ‚Ñù)‚Åª¬π *
    volume (czBall3 ha hX i) / volume (ball x (dist x (czCenter ha hX i)))

/-- Lemma 10.2.7.
Note that `hx` implies `hX`, but we keep the superfluous hypothesis to shorten the statement. -/
lemma estimate_bad_partial (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f}
    (hŒ± : ‚®ç‚Åª x, ‚Äñf x‚Äñ‚Çë / c10_0_3 a < Œ±)
    (hx : x ‚àà (Œ© f ha Œ±)·∂ú) (hX : GeneralCase f Œ±) :
    ‚ÄñczOperator K r (czRemainder f ha Œ±) x‚Äñ‚Çë ‚â§ 3 * czOperatorBound ha hX x + Œ± / 8 := by
  sorry

/-- The constant used in `distribution_czOperatorBound`. -/
irreducible_def C10_2_8 (a : ‚Ñï) : ‚Ñù‚â•0 := 2 ^ (a ^ 3 + 9 * a + 4)

/-- Lemma 10.2.8 -/
lemma distribution_czOperatorBound (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f}
    (hŒ± : ‚®ç‚Åª x, ‚Äñf x‚Äñ‚Çë / c10_0_3 a < Œ±) (hX : GeneralCase f Œ±) :
    volume ((Œ© f ha Œ±)·∂ú ‚à© czOperatorBound ha hX ‚Åª¬π' Ioi (Œ± / 8)) ‚â§
    C10_2_8 a / Œ± * eLpNorm f 1 volume := by
  sorry

/-- The constant used in `estimate_bad`. -/
irreducible_def C10_2_9 (a : ‚Ñï) : ‚Ñù‚â•0 := 2 ^ (5 * a) / c10_0_3 a + 2 ^ (a ^ 3 + 9 * a + 4)

/-- Lemma 10.2.9 -/
-- In the proof, case on `GeneralCase f Œ±`, noting in the finite case that `Œ© = univ`
lemma estimate_bad (ha : 4 ‚â§ a) {hf : BoundedFiniteSupport f}
    (hŒ± : ‚®ç‚Åª x, ‚Äñf x‚Äñ‚Çë / c10_0_3 a < Œ±) (hX : GeneralCase f Œ±) :
    distribution (czOperator K r (czRemainder f ha Œ±)) (Œ± / 2) volume ‚â§
    C10_2_9 a / Œ± * eLpNorm f 1 volume := by
  sorry


/- ### Lemmas 10.0.3 -/

/-- The constant used in `czoperator_weak_1_1`. -/
irreducible_def C10_0_3 (a : ‚Ñï) : ‚Ñù‚â•0 := 2 ^ (a ^ 3 + 19 * a)

/-- Lemma 10.0.3, formulated differently.
The blueprint version is basically this after unfolding `HasBoundedWeakType`, `wnorm` and `wnorm'`.
-/
-- in proof: do cases on `Œ± ‚â§ ‚®ç‚Åª x, ‚Äñf x‚Äñ‚Çë / c10_0_3 a`.
-- If true, use the argument directly below (10.2.37)
theorem czoperator_weak_1_1 (ha : 4 ‚â§ a) (hr : 0 < r)
    (hT : HasBoundedStrongType (czOperator K r) 2 2 volume volume (C_Ts a)) :
    HasBoundedWeakType (czOperator K r) 1 1 volume volume (C10_0_3 a) := by
  sorry


end
