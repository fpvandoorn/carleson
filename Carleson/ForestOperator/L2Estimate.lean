import Carleson.ForestOperator.PointwiseEstimate

open ShortVariables TileStructure
variable {X : Type*} {a : â„•} {q : â„} {K : X â†’ X â†’ â„‚} {Ïƒâ‚ Ïƒâ‚‚ : X â†’ â„¤} {F G : Set X}
  [MetricSpace X] [ProofData a q K Ïƒâ‚ Ïƒâ‚‚ F G] [TileStructure Q D Îº S o]
  {n j j' : â„•} {t : Forest X n} {u uâ‚ uâ‚‚ p : ğ”“ X} {x x' : X} {ğ”– : Set (ğ”“ X)}
  {f fâ‚ fâ‚‚ g gâ‚ gâ‚‚ : X â†’ â„‚} {I J J' L : Grid X}
variable {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E']

noncomputable section

open Set MeasureTheory Metric Function Complex Bornology TileStructure Classical Filter
open scoped NNReal ENNReal ComplexConjugate

namespace TileStructure.Forest

/-! ## Section 7.2 and Lemma 7.2.1 -/

/-- The constant used in `nontangential_operator_bound`.
Has value `2 ^ (103 * a ^ 3)` in the blueprint. -/
-- Todo: define this recursively in terms of previous constants
irreducible_def C7_2_2 (a : â„•) : â„â‰¥0 := 2 ^ (103 * (a : â„) ^ 3)

/-- Lemma 7.2.2. -/
lemma nontangential_operator_bound
    (hf : BoundedCompactSupport f)
    (Î¸ : Î˜ X) :
    eLpNorm (nontangentialMaximalFunction Î¸ f Â·) 2 volume â‰¤ (C7_2_2 a) * eLpNorm f 2 volume := by
  suffices âˆ€ x, (fun x â†¦ nontangentialMaximalFunction Î¸ f x) x â‰¤ nontangentialOperator K f x +
      2 ^ (102 * (a : â„) ^ 3) * MB volume ğ“‘ cğ“‘ rğ“‘ f x by
    calc eLpNorm (fun x â†¦ nontangentialMaximalFunction Î¸ f x) 2 volume
      _ â‰¤ eLpNorm (fun x â†¦ nontangentialOperator K f x +
            2 ^ (102 * (a : â„) ^ 3) * MB volume ğ“‘ cğ“‘ rğ“‘ f x) 2 volume := by
        simp only [eLpNorm, OfNat.ofNat_ne_zero, reduceIte, ENNReal.ofNat_ne_top, eLpNorm']
        gcongr with x
        exact this x
      _ â‰¤ eLpNorm (nontangentialOperator K f) 2 volume +
            eLpNorm ((2 : â„â‰¥0âˆ) ^ (102 * (a : â„) ^ 3) * MB volume ğ“‘ cğ“‘ rğ“‘ f Â·) 2 volume := by
        simp only [eLpNorm, OfNat.ofNat_ne_zero, reduceIte, ENNReal.ofNat_ne_top, eLpNorm',
          enorm_eq_self, ENNReal.toReal_ofNat, ENNReal.rpow_ofNat]
        have m1 : AEMeasurable (nontangentialOperator K f) := by
          have âŸ¨hâ‚, hâ‚‚âŸ© := hasBoundedStrongType_Tstar f (hf.memâ„’p 2) hf.memâ„’p_top.eLpNorm_lt_top
            hf.isBoundedSupport.measure_lt_top
          suffices ENNReal.ofReal âˆ˜ (nontangentialOperator K f Â· |>.toReal) =á¶ [ae volume]
              nontangentialOperator K f from hâ‚.aemeasurable.ennreal_ofReal.congr this
          suffices âˆ€áµ x, nontangentialOperator K f x â‰  âŠ¤ from this.mono (fun x hx â†¦ by simp [hx])

          sorry
        have m2 : AEMeasurable (2 ^ (102 * (a : â„) ^ 3) * MB volume ğ“‘ cğ“‘ rğ“‘ f Â·) := by sorry
        simpa using ENNReal.lintegral_Lp_add_le m1 m2 one_le_two
      _ â‰¤ _ := by sorry
    --refine le_trans (ENNReal.lintegral_Lp_add_le ?_ ?_ ?_) ?_

  sorry

/-- The set of cubes in Lemma 7.2.4. -/
def kissing (I : Grid X) : Finset (Grid X) :=
  {J | s J = s I âˆ§ Â¬Disjoint (ball (c I) (16 * D ^ s I)) (ball (c J) (16 * D ^ s J))}

lemma subset_of_kissing (h : J âˆˆ kissing I) :
    ball (c J) (D ^ s J / 4) âŠ† ball (c I) (33 * D ^ s I) := by
  simp_rw [kissing, Finset.mem_filter, Finset.mem_univ, true_and] at h
  obtain âŸ¨x, xI, xJâŸ© := not_disjoint_iff.mp h.2
  apply ball_subset_ball'
  calc
    _ â‰¤ D ^ s J / 4 + dist (c J) x + dist x (c I) := by
      rw [add_assoc]; exact add_le_add_left (dist_triangle ..) _
    _ â‰¤ D ^ s J / 4 + 16 * D ^ s J + 16 * D ^ s I := by
      gcongr
      Â· exact (mem_ball'.mp xJ).le
      Â· exact (mem_ball.mp xI).le
    _ â‰¤ _ := by
      rw [h.1, div_eq_mul_inv, mul_comm _ 4â»Â¹, â† distrib_three_right]
      gcongr
      norm_num

lemma volume_le_of_kissing (h : J âˆˆ kissing I) :
    volume (ball (c I) (33 * D ^ s I)) â‰¤ 2 ^ (9 * a) * volume (ball (c J) (D ^ s J / 4)) := by
  simp_rw [kissing, Finset.mem_filter, Finset.mem_univ, true_and] at h
  obtain âŸ¨x, xI, xJâŸ© := not_disjoint_iff.mp h.2
  have : ball (c I) (33 * D ^ s I) âŠ† ball (c J) (128 * D ^ s J) := by
    apply ball_subset_ball'
    calc
      _ â‰¤ 33 * D ^ s I + dist (c I) x + dist x (c J) := by
        rw [add_assoc]; exact add_le_add_left (dist_triangle ..) _
      _ â‰¤ 33 * D ^ s I + 16 * D ^ s I + 16 * D ^ s J := by
        gcongr
        Â· exact (mem_ball'.mp xI).le
        Â· exact (mem_ball.mp xJ).le
      _ â‰¤ _ := by
        rw [h.1, â† distrib_three_right]
        gcongr; norm_num
  have double := measure_ball_le_pow_two' (Î¼ := volume) (x := c J) (r := D ^ s J / 4) (n := 9)
  have A9 : (defaultA a : â„â‰¥0) ^ 9 = (2 : â„â‰¥0âˆ) ^ (9 * a) := by
    simp only [defaultA]; norm_cast; ring
  rw [show (2 : â„) ^ 9 * (D ^ s J / 4) = 128 * D ^ s J by ring, A9] at double
  exact (measure_mono this).trans double

lemma pairwiseDisjoint_of_kissing :
    (kissing I).toSet.PairwiseDisjoint fun i â†¦ ball (c i) (D ^ s i / 4) := fun j mj k mk hn â†¦ by
  apply disjoint_of_subset ball_subset_Grid ball_subset_Grid
  simp_rw [Finset.mem_coe, kissing, Finset.mem_filter] at mj mk
  exact (eq_or_disjoint (mj.2.1.trans mk.2.1.symm)).resolve_left hn

/-- Lemma 7.2.4. -/
lemma boundary_overlap (I : Grid X) : (kissing I).card â‰¤ 2 ^ (9 * a) := by
  have key : (kissing I).card * volume (ball (c I) (33 * D ^ s I)) â‰¤
      2 ^ (9 * a) * volume (ball (c I) (33 * D ^ s I)) := by
    calc
      _ = âˆ‘ _ âˆˆ kissing I, volume (ball (c I) (33 * D ^ s I)) := by
        rw [Finset.sum_const, nsmul_eq_mul]
      _ â‰¤ âˆ‘ J âˆˆ kissing I, 2 ^ (9 * a) * volume (ball (c J) (D ^ s J / 4)) :=
        Finset.sum_le_sum fun _ â†¦ volume_le_of_kissing
      _ = 2 ^ (9 * a) * volume (â‹ƒ J âˆˆ kissing I, ball (c J) (D ^ s J / 4)) := by
        rw [â† Finset.mul_sum]; congr
        exact (measure_biUnion_finset pairwiseDisjoint_of_kissing fun _ _ â†¦ measurableSet_ball).symm
      _ â‰¤ _ := by gcongr; exact iUnionâ‚‚_subset fun _ â†¦ subset_of_kissing
  have vn0 : volume (ball (c I) (33 * D ^ s I)) â‰  0 := by
    refine (measure_ball_pos volume _ ?_).ne'; simp only [defaultD]; positivity
  rw [ENNReal.mul_le_mul_right vn0 (measure_ball_ne_top _ _)] at key; norm_cast at key

irreducible_def C7_2_3 (a : â„•) : â„â‰¥0 := 2 ^ (12 * (a : â„))

/-- Lemma 7.2.3. -/
lemma boundary_operator_bound
    (hf : BoundedCompactSupport f) (hu : u âˆˆ t) :
    eLpNorm (boundaryOperator t u f) 2 volume â‰¤ (C7_2_3 a) * eLpNorm f 2 volume := by
  sorry

/-- The constant used in `tree_projection_estimate`.
Originally had value `2 ^ (104 * a ^ 3)` in the blueprint, but that seems to be a mistake. -/
-- Todo: define this recursively in terms of previous constants
irreducible_def C7_2_1 (a : â„•) : â„â‰¥0 := 2 ^ (152 * (a : â„) ^ 3)

-- Auxiliary function used in the proof of Lemma 7.2.1
private def eIğ’¬u_mul (u : ğ”“ X) (f : X â†’ â„‚) : X â†’ â„‚ := fun y â†¦ exp (.I * ğ’¬ u y) * f y

private lemma boundedCompactSupport_eIğ’¬u_mul (u : ğ”“ X) {f : X â†’ â„‚} (hf : BoundedCompactSupport f) :
    BoundedCompactSupport (eIğ’¬u_mul u f) := by
  apply hf.mul_bdd_left
  Â· refine isBounded_iff_forall_norm_le.mpr âŸ¨1, fun _ h â†¦ ?_âŸ©
    obtain âŸ¨_, rflâŸ© := mem_range.mp h
    rw [mul_comm, norm_exp_ofReal_mul_I]
  Â· apply measurable_exp.stronglyMeasurable.comp_measurable
    exact (measurable_ofReal.comp' (map_continuous (ğ’¬ u)).measurable).const_mul I

private lemma norm_eIğ’¬u_mul_eq (u : ğ”“ X) (f : X â†’ â„‚) (x : X) : â€–eIğ’¬u_mul u f xâ€– = â€–f xâ€– := by
  simp [eIğ’¬u_mul, mul_comm I]

-- The bound for `carlesonSum` from `pointwise_tree_estimate` (Lemma 7.1.3)
variable (t) (u) (f) in
private def cS_bound (x' : X) :=
    C7_1_3 a * (MB volume ğ“‘ cğ“‘ rğ“‘ (approxOnCube (ğ“™ (t u)) (â€–f Â·â€–)) x' +
    t.boundaryOperator u (approxOnCube (ğ“™ (t u)) (â€–f Â·â€–)) x') +
    nontangentialMaximalFunction (ğ’¬ u) (approxOnCube (ğ“™ (t u)) (eIğ’¬u_mul u f)) x'

private lemma aeMeasurable_cS_bound : AEMeasurable (cS_bound t u f) := by
  refine AEMeasurable.add ?_ MeasureTheory.Measurable.nontangentialMaximalFunction.aemeasurable
  apply ((AEStronglyMeasurable.maximalFunction (to_countable ğ“‘)).aemeasurable.add ?_).const_mul
  exact MeasureTheory.Measurable.boundaryOperator.aemeasurable

lemma _root_.CMB_defaultA_two_eq {a : â„•} : CMB (defaultA a) 2 = 2 ^ (a + (3 / 2 : â„)) := by
  suffices (2 : â„â‰¥0) * 2 ^ (2 : â„)â»Â¹ * (ENNReal.ofReal |2 - 1|â»Â¹).toNNReal ^ (2 : â„)â»Â¹ *
      ((2 ^ a) ^ (2 : â„)) ^ (2 : â„)â»Â¹ = 2 ^ (a + 3 / (2 : â„)) by
    simpa [CMB, C_realInterpolation, C_realInterpolation_ENNReal]
  rw [â† NNReal.rpow_mul, show (3 / 2 : â„) = 1 + (1 / 2 : â„) by norm_num]
  repeat rw [NNReal.rpow_add two_ne_zero]
  norm_num
  ring

-- The natural constant for Lemma 7.2.1 is â‰¤ the simpler constant `C7_2_1` we use instead.
private lemma le_C7_2_1 {a : â„•} (ha : 4 â‰¤ a) :
    C7_1_3 a * CMB (defaultA a) 2 + C7_1_3 a * C7_2_3 a + C7_2_2 a â‰¤ (C7_2_1 a : â„â‰¥0âˆ) := calc
  _ â‰¤ (3 : â„•) â€¢ (2 : â„â‰¥0âˆ) ^ (151 * (a : â„) ^ 3 + 12 * a) := by
    rw [three'_nsmul]
    gcongr
    Â· rw [C7_1_3_eq_C7_1_6 ha, C7_1_6_def, CMB_defaultA_two_eq, â† ENNReal.coe_mul,
        â† NNReal.rpow_add two_ne_zero, ENNReal.coe_rpow_of_ne_zero two_ne_zero, ENNReal.coe_ofNat]
      apply ENNReal.rpow_le_rpow_of_exponent_le one_le_two ?_
      linarith [show 4 â‰¤ (a : â„) by exact_mod_cast ha]
    Â· rw [C7_1_3_eq_C7_1_6 ha, C7_2_3_def, C7_1_6_def]
      norm_cast
      exact le_of_eq (pow_add _ _ _).symm
    Â· rw [C7_2_2_def]
      norm_cast
      exact pow_right_monoâ‚€ one_le_two <| (Nat.mul_le_mul_right _ (by norm_num)).trans le_self_add
  _ = 3 * 2 ^ (12 * (a : â„)) * (2 : â„â‰¥0âˆ) ^ (151 * (a : â„) ^ 3) := by
    rw [add_comm, ENNReal.rpow_add _ _ two_ne_zero ENNReal.two_ne_top]; ring
  _ â‰¤ (2 : â„â‰¥0âˆ) ^ ((a : â„) ^ 3) * (2 : â„â‰¥0âˆ) ^ (151 * (a : â„) ^ 3) := by
    apply mul_right_mono
    norm_cast
    calc
      3 * 2 ^ (12 * a) â‰¤ 2 ^ 2 * 2 ^ (12 * a) := by gcongr; norm_num
      _                = 2 ^ (2 + 12 * a)     := by rw [pow_add]
      _                â‰¤ 2 ^ (a ^ 3)          := pow_le_pow_rightâ‚€ one_le_two <| calc
                          2 + 12 * a â‰¤ a + 12 * a  := by apply add_le_add_right; linarith
                          _          = 13 * a      := by ring
                          _          â‰¤ a ^ 2 * a   := by rw [mul_le_mul_right] <;> nlinarith
                          _          = a ^ 3       := rfl
  _ = _ := by
    rw [C7_2_1_def, â† ENNReal.rpow_add _ _ two_ne_zero ENNReal.two_ne_top]
    norm_cast
    ring

-- Main estimate used in the proof of `tree_projection_estimate`
private lemma eLpNorm_two_cS_bound_le (hu : u âˆˆ t) : eLpNorm (cS_bound t u f) 2 volume â‰¤
    C7_2_1 a * eLpNorm (approxOnCube (ğ“™ (t u)) (â€–f Â·â€–)) 2 volume := by
  let Î¼ := volume (Î± := X)
  let aOC := (approxOnCube (ğ“™ (t u)) (â€–f Â·â€–))
  let gâ‚ := MB Î¼ ğ“‘ cğ“‘ rğ“‘ aOC
  let gâ‚‚ := t.boundaryOperator u (approxOnCube (ğ“™ (t u)) (â€–f Â·â€–))
  let gâ‚ƒ := nontangentialMaximalFunction (ğ’¬ u) (approxOnCube (ğ“™ (t u)) (eIğ’¬u_mul u f))
  have mâ‚ : AEMeasurable gâ‚ :=
    (MeasureTheory.AEStronglyMeasurable.maximalFunction (to_countable ğ“‘)).aemeasurable
  have mâ‚‚ : AEMeasurable gâ‚‚ := MeasureTheory.Measurable.boundaryOperator.aemeasurable
  calc eLpNorm (cS_bound t u f) 2 Î¼
    _ = eLpNorm (C7_1_3 a â€¢ (gâ‚ + gâ‚‚) + gâ‚ƒ) 2 Î¼ := rfl
    _ â‰¤ eLpNorm (C7_1_3 a â€¢ (gâ‚ + gâ‚‚)) 2 Î¼ + eLpNorm gâ‚ƒ 2 Î¼ := by
      simpa [eLpNorm, eLpNorm'] using
        ENNReal.lintegral_Lp_add_le ((mâ‚.add mâ‚‚).const_smul (C7_1_3 a)) (hp1 := one_le_two)
          MeasureTheory.Measurable.nontangentialMaximalFunction.aemeasurable
    _ = C7_1_3 a â€¢ eLpNorm (gâ‚ + gâ‚‚) 2 Î¼ + eLpNorm gâ‚ƒ 2 Î¼ := by
      congr
      simpa [eLpNorm, eLpNorm'] using ENNReal.lintegral_Lp_smul (mâ‚.add mâ‚‚) two_pos (C7_1_3 a)
    _ â‰¤ C7_1_3 a â€¢ (eLpNorm gâ‚ 2 Î¼ + eLpNorm gâ‚‚ 2 Î¼) + eLpNorm gâ‚ƒ 2 Î¼ := by
      gcongr
      simpa [eLpNorm, eLpNorm'] using ENNReal.lintegral_Lp_add_le mâ‚ mâ‚‚ one_le_two
    _ â‰¤ C7_1_3 a â€¢ ((CMB (defaultA a) 2) * eLpNorm aOC 2 Î¼ + (C7_2_3 a) * eLpNorm aOC 2 Î¼) +
          (C7_2_2 a) * eLpNorm aOC 2 Î¼ := by
      gcongr
      Â· have : HasStrongType (fun (u : X â†’ â„) (x : X) â†¦ MB Î¼ ğ“‘ cğ“‘ rğ“‘ u x |>.toReal) 2 2 Î¼ Î¼ _ :=
          hasStrongType_MB_finite ğ“‘_finite one_lt_two
        convert (this aOC <| boundedCompactSupport_approxOnCube.memâ„’p 2).2 using 1
        congr
        ext
        rw [ENNReal.nnorm_toReal]
        refine ENNReal.coe_toNNReal (ne_of_lt ?_) |>.symm
        exact lt_of_le_of_lt MB_le_eLpNormEssSup (boundedCompactSupport_approxOnCube.memâ„’p âŠ¤).2
      Â· apply le_of_le_of_eq <| boundary_operator_bound boundedCompactSupport_approxOnCube hu
        simp [eLpNorm, eLpNorm', aOC, approxOnCube_ofReal, enorm_eq_nnnorm, Î¼]
      Â· apply le_trans <| nontangential_operator_bound boundedCompactSupport_approxOnCube (ğ’¬ u)
        refine mul_le_mul_left' (eLpNorm_mono (fun x â†¦ ?_)) _
        apply le_of_le_of_eq norm_approxOnCube_le_approxOnCube_norm
        rw [Real.norm_of_nonneg <| approxOnCube_nonneg (fun _ â†¦ norm_nonneg _)]
        simp_rw [norm_eIğ’¬u_mul_eq]
    _ = (C7_1_3 a * CMB (defaultA a) 2 + C7_1_3 a * C7_2_3 a + C7_2_2 a) * eLpNorm aOC 2 Î¼ := by
      rw [ENNReal.smul_def, smul_eq_mul]; ring
    _ â‰¤ _ := mul_le_mul_right' (le_C7_2_1 (four_le_a X)) _

/-- Lemma 7.2.1. -/
lemma tree_projection_estimate
    (hf : BoundedCompactSupport f) (hg : BoundedCompactSupport g) (hu : u âˆˆ t) :
    â€–âˆ« x, conj (g x) * carlesonSum (t u) f xâ€–â‚Š â‰¤
    C7_2_1 a * eLpNorm (approxOnCube (ğ“™ (t u)) (â€–f Â·â€–)) 2 volume *
    eLpNorm (approxOnCube (ğ“› (t u)) (â€–g Â·â€–)) 2 volume := by
  set aOC := approxOnCube (ğ“› (t u)) (â€–g Â·â€–)
  let eaOC (x : X) := ENNReal.ofReal (aOC x)
  have aOC_nonneg {x : X} : 0 â‰¤ aOC x := approxOnCube_nonneg (fun _ â†¦ norm_nonneg _)
  calc ENNReal.ofNNReal â€–âˆ« x, conj (g x) * carlesonSum (t u) f xâ€–â‚Š
    _ â‰¤ âˆ«â» x, â€–conj (g x) * carlesonSum (t u) f xâ€–â‚‘ := ennnorm_integral_le_lintegral_ennnorm _
    _ = âˆ«â» x in (â‹ƒ p âˆˆ t u, ğ“˜ p), â€–g xâ€–â‚‘ * â€–carlesonSum (t u) f xâ€–â‚‘ := by
      rw [â† lintegral_indicator]; swap
      Â· exact MeasurableSet.biUnion (t u).to_countable (fun _ _ â†¦ coeGrid_measurable)
      simp_rw [enorm_eq_nnnorm, nnnorm_mul, ENNReal.coe_mul, RCLike.nnnorm_conj]
      refine lintegral_congr (fun x â†¦ ?_)
      by_cases hx : x âˆˆ â‹ƒ p âˆˆ t u, ğ“˜ p
      Â· rw [indicator_of_mem hx]
      Â· simp [indicator_of_not_mem hx, nmem_support.mp (hx <| support_carlesonSum_subset Â·)]
    _ â‰¤ âˆ«â» x in (â‹ƒ L âˆˆ ğ“› (t u), (L : Set X)), â€–g xâ€–â‚‘ * â€–carlesonSum (t u) f xâ€–â‚‘ := by
      rw [biUnion_ğ“›]
      refine lintegral_mono_set (fun x hx â†¦ ?_)
      have âŸ¨p, hpâŸ© : âˆƒ p âˆˆ t u, x âˆˆ ğ“˜ p := by simpa using hx
      apply mem_iUnion.mpr âŸ¨ğ“˜ p, hp.2âŸ©
    _ = âˆ‘ L in ğ“› (t u), âˆ«â» x in L, â€–g xâ€–â‚‘ * â€–carlesonSum (t u) f xâ€–â‚‘ := by
      simp only [â† mem_toFinset]
      refine lintegral_biUnion_finset ?_ (fun _ _ â†¦ coeGrid_measurable) _
      rw [coe_toFinset]
      exact pairwiseDisjoint_ğ“›
    _ â‰¤ âˆ‘ L in ğ“› (t u), âˆ«â» x in L, â€–g xâ€–â‚‘ * (â¨… x' âˆˆ L, â€–cS_bound t u f x'â€–â‚‘) := by
      gcongr âˆ‘ L in ğ“› (t u), ?_ with L hL
      refine setLIntegral_mono (Measurable.mul ?_ measurable_const) (fun x hx â†¦ ?_)
      Â· exact measurable_coe_nnreal_ennreal_iff.mpr hg.stronglyMeasurable.measurable.nnnorm
      Â· gcongr
        refine le_iInfâ‚‚ (fun x' hx' â†¦ ?_)
        simp only [mem_toFinset] at hL
        convert pointwise_tree_estimate hu hL hx hx' (boundedCompactSupport_eIğ’¬u_mul u hf) using 1
        Â· congr
          simp_rw [mul_neg, eIğ’¬u_mul, â† mul_assoc, â† exp_add, neg_add_cancel, exp_zero, one_mul]
        Â· simp only [cS_bound, enorm_eq_self, norm_eIğ’¬u_mul_eq u f]
    _ = âˆ‘ L in ğ“› (t u), âˆ«â» x in L, eaOC x * (â¨… x' âˆˆ L, â€–cS_bound t u f x'â€–â‚‘) := by
      refine Finset.sum_congr rfl (fun L hL â†¦ ?_)
      rw [lintegral_mul_const, lintegral_mul_const]; rotate_left
      Â· exact ENNReal.measurable_ofReal.comp (stronglyMeasurable_approxOnCube _ _).measurable
      Â· exact hg.stronglyMeasurable.measurable.ennnorm
      simp_rw [eaOC, enorm_eq_nnnorm]
      simp_rw [lintegral_coe_eq_integral (â€–g Â·â€–â‚Š) hg.integrable.norm.restrict, coe_nnnorm]
      rw [integral_eq_lintegral_approxOnCube pairwiseDisjoint_ğ“› (mem_toFinset.mp hL) hg]
      simp_rw [â† Real.ennnorm_eq_ofReal aOC_nonneg, approxOnCube_ofReal, nnnorm_real, aOC]
    _ â‰¤ âˆ‘ L in ğ“› (t u), âˆ«â» x in L, eaOC x * â€–cS_bound t u f xâ€–â‚‘ :=
      Finset.sum_le_sum fun L hL â†¦
        setLIntegral_mono' coeGrid_measurable (fun x hx â†¦ mul_left_mono (biInf_le _ hx))
    _ = âˆ«â» x in (â‹ƒ L âˆˆ ğ“› (t u), (L : Set X)), eaOC x * â€–cS_bound t u f xâ€–â‚‘ := by
      rw [â† lintegral_biUnion_finset (hm := fun _ _ â†¦ coeGrid_measurable)]
      Â· simp only [mem_toFinset]
      Â· simpa only [coe_toFinset] using pairwiseDisjoint_ğ“› (ğ”– := t u)
    _ â‰¤ âˆ«â» (x : X), eaOC x * â€–cS_bound t u f xâ€–â‚‘ := by
      nth_rewrite 2 [â† setLIntegral_univ]
      exact lintegral_mono_set (fun _ _ â†¦ trivial)
    _ â‰¤ eLpNorm eaOC 2 volume * eLpNorm (cS_bound t u f) 2 volume := by
      have isConj : Real.IsConjExponent 2 2 := by constructor <;> norm_num
      have : AEMeasurable eaOC := (stronglyMeasurable_approxOnCube _ _).aemeasurable.ennreal_ofReal
      convert ENNReal.lintegral_mul_le_Lp_mul_Lq volume isConj this aeMeasurable_cS_bound <;>
        simp [eLpNorm, eLpNorm']
    _ = eLpNorm (cS_bound t u f) 2 volume * eLpNorm aOC 2 volume := by
      rw [mul_comm]; congr; ext; exact (Real.ennnorm_eq_ofReal aOC_nonneg).symm
    _ â‰¤ _ := mul_right_mono (eLpNorm_two_cS_bound_le hu)

end TileStructure.Forest
