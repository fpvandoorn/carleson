import Carleson.Calculations
import Carleson.ForestOperator.LargeSeparation.Defs
import Carleson.ToMathlib.Data.NNReal

open ShortVariables TileStructure
variable {X : Type*} {a : â„•} {q : â„} {K : X â†’ X â†’ â„‚} {Ïƒâ‚ Ïƒâ‚‚ : X â†’ â„¤} {F G : Set X}
  [MetricSpace X] [ProofData a q K Ïƒâ‚ Ïƒâ‚‚ F G] [TileStructure Q D Îº S o]
  {n j j' : â„•} {t : Forest X n} {u uâ‚ uâ‚‚ p : ğ”“ X} {x x' : X} {ğ”– : Set (ğ”“ X)}
  {f fâ‚ fâ‚‚ g gâ‚ gâ‚‚ : X â†’ â„‚} {I J J' L : Grid X}
variable {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E']

noncomputable section

open Set MeasureTheory Metric Function Complex Bornology TileStructure Filter
open scoped NNReal ENNReal ComplexConjugate

namespace TileStructure.Forest

/-! ## Section 7.5.1 (Lipschitz partition of unity, Lemmas 7.5.1 to 7.5.3 -/

/-- The definition of Ï‡-tilde, defined in the proof of Lemma 7.5.2 -/
def Ï‡tilde (J : Grid X) (uâ‚ : ğ”“ X) : X â†’ â„â‰¥0 :=
  (ğ“˜ uâ‚ : Set X).indicator fun x â†¦ (8 - D ^ (- s J) * dist x (c J)).toNNReal

lemma Ï‡tilde_pos_iff : 0 < Ï‡tilde J uâ‚ x â†” x âˆˆ ğ“˜ uâ‚ âˆ§ x âˆˆ ball (c J) (8 * D ^ s J) := by
  have := @one_le_D a; rw [Ï‡tilde]
  by_cases h : x âˆˆ ğ“˜ uâ‚
  Â· rw [indicator_of_mem h, Real.toNNReal_pos, sub_pos, zpow_neg, inv_mul_lt_iffâ‚€' (by positivity)]
    simp [h]
  Â· rw [indicator_of_not_mem h]; simp [h]

lemma Ï‡tilde_le_eight : Ï‡tilde J uâ‚ x â‰¤ 8 := by
  unfold Ï‡tilde; apply indicator_le fun _ _ â†¦ ?_
  rw [Real.toNNReal_le_iff_le_coe, NNReal.coe_ofNat, sub_le_self_iff]
  positivity

lemma four_lt_Ï‡tilde (xJ : x âˆˆ J) (xu : x âˆˆ ğ“˜ uâ‚) : 4 < Ï‡tilde J uâ‚ x := by
  have := @one_le_D a
  simp_rw [Ï‡tilde, indicator_of_mem xu, Real.ofNat_lt_toNNReal, lt_sub_comm, Nat.cast_ofNat]
  rw [show (8 : â„) - 4 = 4 by norm_num, zpow_neg, inv_mul_lt_iffâ‚€' (by positivity)]
  exact mem_ball.mp (Grid_subset_ball xJ)

lemma dist_Ï‡tilde_le (mx : x âˆˆ ğ“˜ uâ‚) (mx' : x' âˆˆ ğ“˜ uâ‚) :
    dist (Ï‡tilde J uâ‚ x) (Ï‡tilde J uâ‚ x') â‰¤ dist x x' / D ^ s J :=
  calc
    _ â‰¤ â€–(8 - D ^ (-s J) * dist x (c J)) - (8 - D ^ (-s J) * dist x' (c J))â€– := by
      rw [Ï‡tilde, indicator_of_mem mx, indicator_of_mem mx']
      change â€–(_ - _ : â„)â€– â‰¤ _
      simp_rw [NNReal.val_eq_coe, Real.coe_toNNReal']
      exact norm_sup_sub_sup_le_norm ..
    _ = â€–dist x' (c J) - dist x (c J)â€– / D ^ (s J) := by
      rw [sub_sub_sub_cancel_left, â† mul_sub, zpow_neg, â† div_eq_inv_mul, norm_div]; simp
    _ â‰¤ _ := by gcongr; rw [Real.norm_eq_abs, dist_comm x x']; exact abs_dist_sub_le ..

variable (t uâ‚ uâ‚‚) in
open scoped Classical in
/-- The definition of Ï‡, defined in the proof of Lemma 7.5.2 -/
def Ï‡ (J : Grid X) (x : X) : â„â‰¥0 :=
  Ï‡tilde J uâ‚ x / âˆ‘ J' âˆˆ ğ“™â‚… t uâ‚ uâ‚‚, Ï‡tilde J' uâ‚ x

-- /-- The definition of `B`, defined in (7.5.1) -/
-- protected def _root_.Grid.ball (I : Grid X) : Set X := ball (c I) (8 * D ^ s I)

/-! ### Subsection 7.5.1 and Lemma 7.5.2 -/

/-- Part of Lemma 7.5.1. -/
lemma union_ğ“™â‚… (huâ‚ : uâ‚ âˆˆ t) (huâ‚‚ : uâ‚‚ âˆˆ t) (hu : uâ‚ â‰  uâ‚‚) (h2u : ğ“˜ uâ‚ â‰¤ ğ“˜ uâ‚‚) :
    â‹ƒ J âˆˆ ğ“™â‚… t uâ‚ uâ‚‚, (J : Set X) = ğ“˜ uâ‚ := by
  apply Set.Subset.antisymm
  Â· intros x hx
    simp only [mem_iUnion] at hx
    rcases hx with âŸ¨cube, âŸ¨_, intervalâŸ©, hâŸ©
    exact Set.mem_of_mem_of_subset h interval.left
  Â· intros x hx
    have existsCube : x âˆˆ â‹ƒ J âˆˆ ğ“™ (t.ğ”–â‚€ uâ‚ uâ‚‚), (J : Set X) := ğ“˜_subset_iUnion_ğ“™_ğ”–â‚€ hx
    simp only [mem_iUnion, exists_prop] at existsCube
    rcases existsCube with âŸ¨cube, cube_in_ğ“™, xInCubeâŸ©
    simp only [mem_iUnion, exists_prop]
    have notDisjoint := Set.not_disjoint_iff.mpr âŸ¨x, xInCube, hxâŸ©
    have cubeInğ“™â‚€ : cube âˆˆ ğ“™â‚€ (t.ğ”–â‚€ uâ‚ uâ‚‚) := mem_of_mem_inter_left cube_in_ğ“™
    simp only [mem_setOf_eq] at cubeInğ“™â‚€
    cases cubeInğ“™â‚€ with
    | inl west =>
      refine âŸ¨cube, ?_, xInCubeâŸ©
      unfold ğ“™â‚…
      rw [inter_def, mem_setOf_eq]
      refine âŸ¨cube_in_ğ“™, ?_âŸ©
      simp only [mem_Iic, Grid.le_def]
      have smaller := calc s cube
        _ = -S := west
        _ â‰¤ s (ğ“˜ uâ‚) := (mem_Icc.mp (scale_mem_Icc (i := ğ“˜ uâ‚))).left
      refine âŸ¨?_, smallerâŸ©
      cases GridStructure.fundamental_dyadic' smaller with
      | inl subset => exact subset
      | inr disjoint => exact False.elim (notDisjoint disjoint)
    | inr east =>
      obtain âŸ¨p, belongsâŸ© := t.nonempty' huâ‚
      by_contra! contr
      have white := calc (ğ“˜ p : Set X)
        _ âŠ† ğ“˜ uâ‚ := (ğ“˜_le_ğ“˜ t huâ‚ belongs).1
        _ âŠ† cube := by
          apply subset_of_nmem_Iic_of_not_disjoint cube
          Â· have notIn : cube âˆ‰ t.ğ“™â‚… uâ‚ uâ‚‚ := fun a â†¦ contr cube a xInCube
            rw [ğ“™â‚…, inter_def, Set.mem_setOf_eq, not_and_or] at notIn
            exact Or.resolve_left notIn (Set.not_not_mem.mpr cube_in_ğ“™)
          Â· exact notDisjoint
        _ âŠ† ball (c cube) (4 * D ^ s cube) := by
          exact Grid_subset_ball (i := cube)
        _ âŠ† ball (c cube) (100 * D ^ (s cube + 1)) := by
          intro y xy
          rw [ball, mem_setOf_eq] at xy âŠ¢
          exact gt_trans (calculation_16 (X := X) (s := s cube)) xy
      have black : Â¬â†‘(ğ“˜ p) âŠ† ball (c cube) (100 * D ^ (s cube + 1)) := by
        have in_ğ”–â‚€ := ğ”—_subset_ğ”–â‚€ (huâ‚ := huâ‚) (huâ‚‚ := huâ‚‚) (hu := hu) (h2u := h2u)
        rw [subset_def] at in_ğ”–â‚€
        exact east p (in_ğ”–â‚€ p belongs)
      contradiction

/-- Part of Lemma 7.5.1. -/
lemma pairwiseDisjoint_ğ“™â‚… : (ğ“™â‚… t uâ‚ uâ‚‚).PairwiseDisjoint (fun I â†¦ (I : Set X)) := by
  have ss : (ğ“™ (t.ğ”–â‚€ uâ‚ uâ‚‚) âˆ© Iic (ğ“˜ uâ‚)) âŠ† ğ“™ (t.ğ”–â‚€ uâ‚ uâ‚‚) := inter_subset_left
  exact PairwiseDisjoint.subset (pairwiseDisjoint_ğ“™ (ğ”– := ğ”–â‚€ t uâ‚ uâ‚‚)) ss

open scoped Classical in
lemma four_lt_sum_Ï‡tilde
    (huâ‚ : uâ‚ âˆˆ t) (huâ‚‚ : uâ‚‚ âˆˆ t) (hu : uâ‚ â‰  uâ‚‚) (h2u : ğ“˜ uâ‚ â‰¤ ğ“˜ uâ‚‚) (hx : x âˆˆ ğ“˜ uâ‚) :
    4 < âˆ‘ J' âˆˆ ğ“™â‚… t uâ‚ uâ‚‚, Ï‡tilde J' uâ‚ x := by
  have hxâ‚€ := hx
  rw [Grid.mem_def, â† union_ğ“™â‚… huâ‚ huâ‚‚ hu h2u, mem_iUnionâ‚‚] at hx; obtain âŸ¨J, mJ, mxâŸ© := hx
  refine (four_lt_Ï‡tilde mx hxâ‚€).trans_le ?_
  apply Finset.single_le_sum (f := fun J â†¦ Ï‡tilde J uâ‚ x) (fun _ _ â†¦ zero_le _)
  rwa [mem_toFinset]

/-- Lemma 7.5.3 (stated somewhat differently). -/
lemma moderate_scale_change (hJ : J âˆˆ ğ“™â‚… t uâ‚ uâ‚‚) (hJ' : J' âˆˆ ğ“™â‚… t uâ‚ uâ‚‚)
    (hd : Â¬Disjoint (ball (c J) (8 * D ^ s J)) (ball (c J') (8 * D ^ s J'))) :
    s J - 1 â‰¤ s J' := by
  by_contra! hs
  have fa : âˆ€ p âˆˆ t.ğ”–â‚€ uâ‚ uâ‚‚, Â¬â†‘(ğ“˜ p) âŠ† ball (c J) (100 * D ^ (s J + 1)) :=
    hJ.1.1.resolve_left (by linarith [(scale_mem_Icc (i := J')).1])
  apply absurd fa; push_neg
  obtain âŸ¨J'', sJ'', lJ''âŸ© : âˆƒ J'', s J'' = s J' + 1 âˆ§ J' â‰¤ J'' := by
    refine Grid.exists_supercube (s J' + 1) âŸ¨by omega, ?_âŸ©
    rw [lt_sub_iff_add_lt] at hs; exact hs.le.trans scale_mem_Icc.2
  obtain âŸ¨p, mp, spâŸ© : âˆƒ p âˆˆ t.ğ”–â‚€ uâ‚ uâ‚‚, â†‘(ğ“˜ p) âŠ† ball (c J'') (100 * D ^ (s J' + 1 + 1)) := by
    have : J'' âˆ‰ ğ“™â‚€ (t.ğ”–â‚€ uâ‚ uâ‚‚) := bigger_than_ğ“™_is_not_in_ğ“™â‚€ lJ'' (by linarith) hJ'.1
    rw [ğ“™â‚€, mem_setOf_eq, sJ''] at this; push_neg at this; exact this.2
  use p, mp, sp.trans (ball_subset_ball' ?_)
  calc
    _ â‰¤ 100 * D ^ (s J' + 1 + 1) + (dist (c J'') (c J') + dist (c J) (c J')) :=
      add_le_add_left (dist_triangle_right ..) _
    _ â‰¤ 100 * D ^ (s J' + 1 + 1) + (4 * D ^ s J'' + 8 * D ^ s J + 8 * D ^ s J') := by
      rw [add_assoc (4 * _)]; gcongr
      Â· exact (mem_ball'.mp (Grid_subset_ball (lJ''.1 Grid.c_mem_Grid))).le
      Â· exact (dist_lt_of_not_disjoint_ball hd).le
    _ â‰¤ 100 * D ^ s J + (4 * D ^ s J + 8 * D ^ s J + 8 * D ^ s J) := by
      gcongr; exacts [one_le_D, by omega, one_le_D, by omega, one_le_D, by omega]
    _ â‰¤ _ := by
      rw [â† add_mul, â† add_mul, â† add_mul, zpow_add_oneâ‚€ (by simp), mul_comm _ (D : â„), â† mul_assoc]
      gcongr; trans 100 * 4
      Â· norm_num
      Â· gcongr; exact four_le_realD X

open scoped Classical in
/-- Part of Lemma 7.5.2. -/
lemma sum_Ï‡ (huâ‚ : uâ‚ âˆˆ t) (huâ‚‚ : uâ‚‚ âˆˆ t) (hu : uâ‚ â‰  uâ‚‚) (h2u : ğ“˜ uâ‚ â‰¤ ğ“˜ uâ‚‚) (x : X) :
    âˆ‘ J âˆˆ ğ“™â‚… t uâ‚ uâ‚‚, Ï‡ t uâ‚ uâ‚‚ J x = (ğ“˜ uâ‚ : Set X).indicator 1 x := by
  simp_rw [Ï‡, â† Finset.sum_div, NNReal.div_self_eq_ite, indicator, Pi.one_apply]
  refine if_congr ?_ rfl rfl
  simp_rw [NNReal.finset_sum_pos_iff, mem_toFinset, Ï‡tilde_pos_iff]
  conv_lhs => enter [1, J]; rw [and_rotate]
  rw [exists_and_left, Grid.mem_def, and_iff_left_iff_imp, â† union_ğ“™â‚… huâ‚ huâ‚‚ hu h2u]; intro mx
  rw [mem_iUnionâ‚‚] at mx; obtain âŸ¨J, mJ, hJâŸ© := mx
  refine âŸ¨J, Grid_subset_ball.trans (ball_subset_ball ?_) hJ, mJâŸ©
  change (4 : â„) * D ^ s J â‰¤ _; gcongr; norm_num

/-- Part of Lemma 7.5.2. -/
lemma Ï‡_le_indicator (hJ : J âˆˆ ğ“™â‚… t uâ‚ uâ‚‚) :
    Ï‡ t uâ‚ uâ‚‚ J x â‰¤ (ball (c J) (8 * D ^ s J)).indicator 1 x := by
  classical
  simp_rw [Ï‡, indicator, Pi.one_apply]
  split_ifs with h
  Â· apply NNReal.div_le_of_le_mul; rw [one_mul]
    apply Finset.single_le_sum (f := fun J â†¦ Ï‡tilde J uâ‚ x) (fun _ _ â†¦ zero_le _)
    rwa [mem_toFinset]
  Â· have : Ï‡tilde J uâ‚ x = 0 := by
      contrapose! h; rw [â† zero_lt_iff, Ï‡tilde_pos_iff] at h; exact h.2
    simp [this]

/-- The constant used in `dist_Ï‡_le`. Has value `2 ^ (226 * a ^ 3)` in the blueprint. -/
-- Todo: define this recursively in terms of previous constants
irreducible_def C7_5_2 (a : â„•) : â„â‰¥0 := 2 ^ (226 * (a : â„) ^ 3)

lemma one_le_C7_5_2 : 1 â‰¤ C7_5_2 a := by
  rw [C7_5_2]; exact_mod_cast Nat.one_le_two_pow

open scoped Classical in
lemma quarter_add_two_mul_D_mul_card_le (hJ : J âˆˆ ğ“™â‚… t uâ‚ uâ‚‚) :
    1 / 4 + 2 * (D : â„) * {J' âˆˆ (ğ“™â‚… t uâ‚ uâ‚‚).toFinset |
      Â¬Disjoint (ball (c J) (8 * D ^ s J)) (ball (c J') (8 * D ^ s J'))}.card â‰¤ C7_5_2 a := by
  set V := {J' âˆˆ (ğ“™â‚… t uâ‚ uâ‚‚).toFinset |
      Â¬Disjoint (ball (c J) (8 * D ^ s J)) (ball (c J') (8 * D ^ s J'))}
  suffices V.card â‰¤ 2 ^ (200 * a ^ 3 + 7 * a) by
    calc
      _ â‰¤ 1 / 4 + 2 * (D : â„) * 2 ^ (200 * a ^ 3 + 7 * a) := by gcongr; norm_cast
      _ â‰¤ 2 ^ (100 * a ^ 2 + 1 + (200 * a ^ 3 + 7 * a)) +
          2 ^ (100 * a ^ 2 + 1 + (200 * a ^ 3 + 7 * a)) := by
        rw [defaultD, Nat.cast_pow, Nat.cast_ofNat, â† pow_succ', â† pow_add]
        gcongr; trans 1
        Â· norm_num
        Â· norm_cast; exact Nat.one_le_two_pow
      _ â‰¤ _ := by
        rw [â† two_mul, â† pow_succ', C7_5_2,
          show 100 * a ^ 2 + 1 + (200 * a ^ 3 + 7 * a) + 1 =
            200 * a ^ 3 + 100 * a ^ 2 + 7 * a + 2 by ring]
        norm_cast; apply pow_le_pow_right' one_le_two
        calc
          _ = 200 * a ^ 3 + 25 * 4 * a ^ 2 + 7 * a + 2 := by norm_num
          _ â‰¤ 200 * a ^ 3 + 25 * a * a ^ 2 + 7 * a + a := by gcongr <;> linarith [four_le_a X]
          _ = 225 * a ^ 3 + 4 * 2 * a := by ring
          _ â‰¤ 225 * a ^ 3 + a * a * a := by gcongr <;> linarith [four_le_a X]
          _ = _ := by ring
  have dbl : âˆ€ J' âˆˆ V, volume (ball (c J) (9 * D ^ (s J + 1))) â‰¤
      2 ^ (200 * a ^ 3 + 7 * a) * volume (ball (c J') (D ^ s J' / 4)) := fun J' mJ' â†¦ by
    simp_rw [V, Finset.mem_filter, mem_toFinset] at mJ'
    have hs := moderate_scale_change hJ mJ'.1 mJ'.2
    rw [disjoint_comm] at mJ'
    have hs' := moderate_scale_change mJ'.1 hJ mJ'.2
    calc
      _ â‰¤ 2 ^ (200 * a ^ 3) * volume (ball (c J') (18 * D ^ s J')) := by
        have db : dist (c J') (c J) + 9 * D ^ (s J + 1) â‰¤ D ^ 2 * (18 * D ^ s J') :=
          calc
            _ â‰¤ 8 * (D : â„) ^ s J' + 8 * D ^ s J + 9 * D ^ (s J + 1) := by
              gcongr; exact (dist_lt_of_not_disjoint_ball mJ'.2).le
            _ â‰¤ 8 * (D : â„) ^ (s J + 1) + D ^ (s J + 1) + 9 * D ^ (s J + 1) := by
              gcongr; exacts [one_le_D, by omega, by
                rw [zpow_add_oneâ‚€ (by simp), mul_comm 8]; gcongr; exact eight_le_realD X]
            _ â‰¤ _ := by
              rw [â† add_one_mul, â† add_mul, â† mul_assoc, â† mul_rotate, â† zpow_natCast,
                â† zpow_addâ‚€ (by simp), mul_comm _ 18, show (8 : â„) + 1 + 9 = 18 by norm_num]
              gcongr 18 * (D : â„) ^ ?_; exacts [one_le_D, by omega]
        convert measure_ball_le_of_dist_le' (Î¼ := volume) (by simp) db
        simp_rw [As, defaultA, defaultD, Nat.cast_pow, Nat.cast_ofNat, â† pow_mul, Real.logb_pow,
          Real.logb_self_eq_one one_lt_two, mul_one, Nat.ceil_natCast, ENNReal.coe_pow,
          ENNReal.coe_ofNat]
        ring
      _ â‰¤ 2 ^ (200 * a ^ 3 + 7 * a) * volume (ball (c J') (18 / 128 * D ^ s J')) := by
        nth_rw 1 [show (18 : â„) * D ^ s J' = 2 ^ 7 * (18 / 128 * D ^ s J') by ring]
        rw [pow_add, mul_assoc]; gcongr
        convert measure_ball_two_le_same_iterate (Î¼ := volume) (c J') _ 7 using 2
        unfold defaultA; norm_cast; rw [â† pow_mul']
      _ â‰¤ _ := by rw [div_eq_inv_mul _ 4]; gcongr; norm_num
  replace dbl : V.card * volume (ball (c J) (9 * D ^ (s J + 1))) â‰¤
      2 ^ (200 * a ^ 3 + 7 * a) * volume (ball (c J) (9 * D ^ (s J + 1))) := by
    calc
      _ â‰¤ 2 ^ (200 * a ^ 3 + 7 * a) * âˆ‘ J' âˆˆ V, volume (ball (c J') (D ^ s J' / 4)) := by
        rw [Finset.mul_sum, â† nsmul_eq_mul, â† Finset.sum_const]; exact Finset.sum_le_sum dbl
      _ = 2 ^ (200 * a ^ 3 + 7 * a) * volume (â‹ƒ J' âˆˆ V, ball (c J') (D ^ s J' / 4)) := by
        congr; refine (measure_biUnion_finset ?_ fun _ _ â†¦ measurableSet_ball).symm
        have Vs : V âŠ† (t.ğ“™â‚… uâ‚ uâ‚‚).toFinset := Finset.filter_subset ..
        rw [subset_toFinset] at Vs
        exact (pairwiseDisjoint_ğ“™â‚….subset Vs).mono fun _ â†¦ ball_subset_Grid
      _ â‰¤ _ := by
        gcongr; refine iUnionâ‚‚_subset fun J' mJ' â†¦ ball_subset_ball' ?_
        simp_rw [V, Finset.mem_filter, mem_toFinset] at mJ'
        have hs := moderate_scale_change hJ mJ'.1 mJ'.2
        rw [disjoint_comm] at mJ'
        have hs' := moderate_scale_change mJ'.1 hJ mJ'.2
        calc
          _ â‰¤ (D : â„) ^ s J' / 4 + (8 * D ^ s J' + 8 * D ^ s J) := by
            gcongr; exact (dist_lt_of_not_disjoint_ball mJ'.2).le
          _ â‰¤ (D : â„) ^ (s J + 1) / 4 + (8 * D ^ (s J + 1) + (8 * D / 25) * D ^ s J) := by
            have : (8 : â„) â‰¤ 8 * D / 25 := by
              rw [le_div_iffâ‚€ (by norm_num)]; gcongr; exact twentyfive_le_realD X
            gcongr; exacts [one_le_D, by omega, one_le_D, by omega]
          _ â‰¤ _ := by
            rw [show 8 * (D : â„) / 25 * D ^ s J = 8 / 25 * (D ^ s J * D) by ring,
              â† zpow_add_oneâ‚€ (by simp), â† add_mul, div_eq_inv_mul, â† add_mul]
            gcongr; norm_num
  have vpos : 0 < volume (ball (c J) (9 * â†‘D ^ (s J + 1))) := by
    apply measure_ball_pos volume; unfold defaultD; positivity
  rw [ENNReal.mul_le_mul_right vpos.ne' measure_ball_lt_top.ne] at dbl
  exact_mod_cast dbl

/-- Part of Lemma 7.5.2. -/
lemma dist_Ï‡_le (huâ‚ : uâ‚ âˆˆ t) (huâ‚‚ : uâ‚‚ âˆˆ t) (hu : uâ‚ â‰  uâ‚‚)
    (h2u : ğ“˜ uâ‚ â‰¤ ğ“˜ uâ‚‚) (hJ : J âˆˆ ğ“™â‚… t uâ‚ uâ‚‚) (mx : x âˆˆ ğ“˜ uâ‚) (mx' : x' âˆˆ ğ“˜ uâ‚) :
    dist (Ï‡ t uâ‚ uâ‚‚ J x) (Ï‡ t uâ‚ uâ‚‚ J x') â‰¤ C7_5_2 a * dist x x' / D ^ s J := by
  classical
  by_cases hxx : x âˆ‰ ball (c J) (8 * D ^ s J) âˆ§ x' âˆ‰ ball (c J) (8 * D ^ s J)
  Â· have nâ‚ := Ï‡_le_indicator hJ (x := x)
    rw [indicator_of_not_mem hxx.1, nonpos_iff_eq_zero] at nâ‚
    have nâ‚‚ := Ï‡_le_indicator hJ (x := x')
    rw [indicator_of_not_mem hxx.2, nonpos_iff_eq_zero] at nâ‚‚
    rw [nâ‚, nâ‚‚, dist_self]; positivity
  rw [not_and_or, not_not_mem, not_not_mem] at hxx
  wlog hx : x âˆˆ ball (c J) (8 * D ^ s J) generalizing x x'
  Â· rw [or_comm] at hxx; specialize this mx' mx hxx (hxx.resolve_right hx)
    rwa [dist_comm, dist_comm x' x] at this
  clear hxx
  let ctx := Ï‡tilde J uâ‚ x
  let ctx' := Ï‡tilde J uâ‚ x'
  let ax := âˆ‘ J' âˆˆ ğ“™â‚… t uâ‚ uâ‚‚, Ï‡tilde J' uâ‚ x
  let ax' := âˆ‘ J' âˆˆ ğ“™â‚… t uâ‚ uâ‚‚, Ï‡tilde J' uâ‚ x'
  have ax4 : 4 â‰¤ ax := (four_lt_sum_Ï‡tilde huâ‚ huâ‚‚ hu h2u mx).le
  have ax'4 : 4 â‰¤ ax' := (four_lt_sum_Ï‡tilde huâ‚ huâ‚‚ hu h2u mx').le
  have nax : (ax : â„) â‰  0 := by exact_mod_cast (zero_lt_four.trans_le ax4).ne'
  have nax' : (ax' : â„) â‰  0 := by exact_mod_cast (zero_lt_four.trans_le ax'4).ne'
  have part1 : dist (Ï‡ t uâ‚ uâ‚‚ J x) (Ï‡ t uâ‚ uâ‚‚ J x') â‰¤
      (dist x x' / D ^ s J + ctx' * dist ax ax') / 4 := by
    calc
      _ = â€–(ctx - ctx' : â„) / ax - ctx' * (ax - ax') / (ax * ax')â€– := by
        rw [mul_sub, sub_div, sub_div, mul_div_mul_right _ _ nax', mul_comm (ax : â„) ax',
          mul_div_mul_right _ _ nax, â† sub_add, sub_right_comm, sub_add_cancel]; rfl
      _ â‰¤ dist ctx ctx' / ax + ctx' * dist ax ax' / (ax * ax') := by
        change _ â‰¤ â€–(ctx - ctx' : â„)â€– / ax + ctx' * â€–(ax - ax' : â„)â€– / (ax * ax')
        conv_rhs => enter [1]; rw [â† NNReal.norm_eq ax]
        conv_rhs =>
          enter [2]; rw [â† NNReal.norm_eq ctx']
          enter [2]; rw [â† NNReal.norm_eq ax, â† NNReal.norm_eq ax']
        rw [â† norm_mul, â† norm_mul, â† norm_div, â† norm_div]
        exact nnnorm_sub_le ..
      _ â‰¤ dist ctx ctx' / 4 + ctx' * dist ax ax' / (1 * 4) := by
        gcongr <;> norm_cast; exact le_trans (by norm_num) ax4
      _ â‰¤ _ := by
        rw [one_mul, â† add_div]; gcongr; exact dist_Ï‡tilde_le mx mx'
  apply part1.trans
  by_cases hx' : x' âˆ‰ ball (c J) (8 * D ^ s J)
  Â· have : ctx' = 0 := by
      simp_rw [ctx', â† not_ne_iff, â† zero_lt_iff, Ï‡tilde_pos_iff]; tauto
    rw [this, NNReal.coe_zero, zero_mul, add_zero, div_eq_inv_mul _ 4, mul_div_assoc]; gcongr
    rw [show 4â»Â¹ = (2 : â„) ^ (-2 : â„) by norm_num, C7_5_2, NNReal.coe_rpow, NNReal.coe_ofNat,
      Real.rpow_le_rpow_left_iff one_lt_two]
    norm_cast; omega
  rw [not_not_mem] at hx'
  calc
    _ â‰¤ (dist x x' / D ^ s J +
        8 * âˆ‘ J' âˆˆ ğ“™â‚… t uâ‚ uâ‚‚, dist (Ï‡tilde J' uâ‚ x) (Ï‡tilde J' uâ‚ x')) / 4 := by
      gcongr
      Â· exact Ï‡tilde_le_eight
      Â· have := dist_sum_sum_le (ğ“™â‚… t uâ‚ uâ‚‚).toFinset
          (fun J' â†¦ (Ï‡tilde J' uâ‚ x : â„)) (fun J' â†¦ Ï‡tilde J' uâ‚ x')
        exact_mod_cast this
    _ = (dist x x' / D ^ s J +
        8 * âˆ‘ J' âˆˆ ğ“™â‚… t uâ‚ uâ‚‚ with
          Â¬Disjoint (ball (c J) (8 * D ^ s J)) (ball (c J') (8 * D ^ s J')),
            dist (Ï‡tilde J' uâ‚ x) (Ï‡tilde J' uâ‚ x')) / 4 := by
      congr 3; refine (Finset.sum_filter_of_ne fun J' mJ' hd â†¦ ?_).symm; contrapose! hd
      have hâ‚ : Ï‡tilde J' uâ‚ x = 0 := by
        have := disjoint_left.mp hd hx
        rw [â† not_ne_iff, â† zero_lt_iff, Ï‡tilde_pos_iff]; tauto
      have hâ‚‚ : Ï‡tilde J' uâ‚ x' = 0 := by
        have := disjoint_left.mp hd hx'
        rw [â† not_ne_iff, â† zero_lt_iff, Ï‡tilde_pos_iff]; tauto
      rw [hâ‚, hâ‚‚, dist_self]
    _ â‰¤ (dist x x' / D ^ s J +
        8 * âˆ‘ J' âˆˆ ğ“™â‚… t uâ‚ uâ‚‚ with
          Â¬Disjoint (ball (c J) (8 * D ^ s J)) (ball (c J') (8 * D ^ s J')),
            dist x x' * D ^ (1 - s J)) / 4 := by
      gcongr with J' mJ'; trans dist x x' / D ^ (s J')
      Â· exact dist_Ï‡tilde_le mx mx'
      Â· rw [div_eq_mul_inv, â† zpow_neg]; gcongr
        Â· exact one_le_D
        Â· rw [Finset.mem_filter, mem_toFinset] at mJ'
          rw [neg_le, neg_sub]; exact moderate_scale_change hJ mJ'.1 mJ'.2
    _ = dist x x' / (D : â„) ^ s J *
        (1 / 4 + 2 * D * {J' âˆˆ (ğ“™â‚… t uâ‚ uâ‚‚).toFinset |
          Â¬Disjoint (ball (c J) (8 * D ^ s J)) (ball (c J') (8 * D ^ s J'))}.card) := by
      rw [Finset.sum_const, nsmul_eq_mul, zpow_subâ‚€ (by simp), zpow_one,
        show 8 * (_ * (dist x x' * (D / D ^ s J))) = dist x x' / D ^ s J * (2 * D * _) * 4 by ring,
        add_div, mul_div_cancel_rightâ‚€ _ four_ne_zero, div_eq_mul_one_div, â† mul_add]
    _ â‰¤ _ := by rw [mul_comm, mul_div_assoc]; gcongr; exact quarter_add_two_mul_D_mul_card_le hJ

end TileStructure.Forest
