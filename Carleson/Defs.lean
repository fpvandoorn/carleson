import Carleson.ToMathlib.Annulus
import Carleson.ToMathlib.DoublingMeasure
import Carleson.ToMathlib.WeakType
import Mathlib.Analysis.CStarAlgebra.Classes
import Mathlib.Analysis.Fourier.AddCircle

open MeasureTheory Measure Metric Complex Set Function ENNReal
open scoped NNReal

noncomputable section

/-! # Main statements of the Carleson project

This file contains the statements of the main theorems from the Carleson formalization project:
Theorem 1.0.1 (classical Carleson), Theorem 1.0.2 (metric space Carleson) and Theorem 1.0.3
(linearised metric Carleson), as well as the definitions required to state these results.

## Main definitions

- `MeasureTheory.DoublingMeasure`: A metric space with a measure with some nice propreties,
including a doubling condition. This is called a "doubling metric measure space" in the blueprint.
- `FunctionDistances`: class stating that continuous functions have distances associated to
every ball.
- `IsOneSidedKernel K` states that `K` is a one-sided Calderon-Zygmund kernel.
- `KernelProofData`: Data common through most of chapters 2-7. These contain the minimal axioms
for `kernel-summand`'s proof.
- `ClassicalCarleson`: statement of Carleson's theorem asserting a.e. convergence of the partial
Fourier sums for continous functions (Theorem 1.0.1 in the blueprint).
- `MetricSpaceCarleson`: statement of Theorem 1.0.2 from the blueprint.
- `LinearizedMetricCarleson`: statement of Theorem 1.0.3 from the blueprint.

-/

section DoublingMeasure

universe u

/- # Doubling metric measure spaces -/

/-- A metric space with a measure with some nice propreties, including a doubling condition.
This is called a "doubling metric measure space" in the blueprint.
`A` will usually be `2 ^ a`. -/
class MeasureTheory.DoublingMeasure (X : Type*) (A : outParam â„â‰¥0) [PseudoMetricSpace X] extends
    CompleteSpace X, LocallyCompactSpace X,
    MeasureSpace X, BorelSpace X,
    IsLocallyFiniteMeasure (volume : Measure X),
    IsDoubling (volume : Measure X) A, NeZero (volume : Measure X) where

variable {ğ•œ X : Type*} {A : â„•} [_root_.RCLike ğ•œ] [PseudoMetricSpace X]

/-- The local oscillation of two functions w.r.t. a set `E`. This is `d_E` in the blueprint. -/
def localOscillation (E : Set X) (f g : C(X, ğ•œ)) : â„â‰¥0âˆ :=
  â¨† z âˆˆ E Ã—Ë¢ E, ENNReal.ofReal â€–f z.1 - g z.1 - f z.2 + g z.2â€–

/-- A class stating that continuous functions have distances associated to every ball.
We use a separate type to conveniently index these functions. -/
class FunctionDistances (ğ•œ : outParam Type*) (X : Type u)
    [NormedField ğ•œ] [TopologicalSpace X] where
  /-- A type of continuous functions from `X` to `ğ•œ`. -/
  Î˜ : Type u
  /-- The coercion map from `Î˜` to `C(X, ğ•œ)`. -/
  coeÎ˜ : Î˜ â†’ C(X, ğ•œ)
  /-- Injectivity of the coercion map from `Î˜` to `C(X, ğ•œ)`. -/
  coeÎ˜_injective {f g : Î˜} (h : âˆ€ x, coeÎ˜ f x = coeÎ˜ g x) : f = g
  /-- For each `_x : X` and `_r : â„`, a `PseudoMetricSpace Î˜`. -/
  metric : âˆ€ (_x : X) (_r : â„), PseudoMetricSpace Î˜

export FunctionDistances (Î˜ coeÎ˜)

section FunctionDistances

variable [FunctionDistances ğ•œ X]

instance : Coe (Î˜ X) C(X, ğ•œ) := âŸ¨FunctionDistances.coeÎ˜âŸ©
instance : FunLike (Î˜ X) X ğ•œ where
  coe := fun f â†¦ (f : C(X, ğ•œ))
  coe_injective' _ _ hfg := FunctionDistances.coeÎ˜_injective fun x â†¦ congrFun hfg x

set_option linter.unusedVariables false in
/-- Class used to endow `Î˜ X` with a pseudometric space structure. -/
@[nolint unusedArguments]
def WithFunctionDistance (x : X) (r : â„) := Î˜ X

instance {x : X} {r : â„} [d : FunctionDistances ğ•œ X] :
    PseudoMetricSpace (WithFunctionDistance x r) := d.metric x r

end FunctionDistances

/-- The real-valued distance function on `WithFunctionDistance x r`. -/
notation3 "dist_{" x ", " r "}" => @dist (WithFunctionDistance x r) _
/-- The `â„â‰¥0âˆ`-valued distance function on `WithFunctionDistance x r`. Preferred over `nndist`. -/
notation3 "edist_{" x ", " r "}" => @edist (WithFunctionDistance x r) _

/-- A set `Î˜` of (continuous) functions is compatible. `A` will usually be `2 ^ a`. -/
class CompatibleFunctions (ğ•œ : outParam Type*) (X : Type u) (A : outParam â„•)
  [RCLike ğ•œ] [PseudoMetricSpace X] extends FunctionDistances ğ•œ X where
  eq_zero : âˆƒ o : X, âˆ€ f : Î˜, coeÎ˜ f o = 0
  /-- The distance is bounded below by the local oscillation. (1.0.7) -/
  localOscillation_le_cdist {x : X} {r : â„} {f g : Î˜} :
    localOscillation (ball x r) (coeÎ˜ f) (coeÎ˜ g) â‰¤ ENNReal.ofReal (dist_{x, r} f g)
  /-- The distance is monotone in the ball. (1.0.9) -/
  cdist_mono {xâ‚ xâ‚‚ : X} {râ‚ râ‚‚ : â„} {f g : Î˜}
    (h : ball xâ‚ râ‚ âŠ† ball xâ‚‚ râ‚‚) : dist_{xâ‚, râ‚} f g â‰¤ dist_{xâ‚‚, râ‚‚} f g
  /-- The distance of a ball with large radius is bounded above. (1.0.8) -/
  cdist_le {xâ‚ xâ‚‚ : X} {r : â„} {f g : Î˜} (h : dist xâ‚ xâ‚‚ < 2 * r) :
    dist_{xâ‚‚, 2 * r} f g â‰¤ A * dist_{xâ‚, r} f g
  /-- The distance of a ball with large radius is bounded below. (1.0.10) -/
  le_cdist {xâ‚ xâ‚‚ : X} {r : â„} {f g : Î˜} (h1 : ball xâ‚ r âŠ† ball xâ‚‚ (A * r)) :
    /-(h2 : A * r â‰¤ Metric.diam (univ : Set X))-/
    2 * dist_{xâ‚, r} f g â‰¤ dist_{xâ‚‚, A * r} f g
  /-- Every ball of radius `2R` can be covered by `A` balls of radius `R`. (1.0.11) -/
  allBallsCoverBalls {x : X} {r : â„} :
    AllBallsCoverBalls (WithFunctionDistance x r) 2 A

/-- The inhomogeneous Lipschitz norm on a ball. -/
def iLipENorm {ğ•œ} [NormedField ğ•œ] (Ï• : X â†’ ğ•œ) (xâ‚€ : X) (R : â„) : â„â‰¥0âˆ :=
  (â¨† x âˆˆ ball xâ‚€ R, â€–Ï• xâ€–â‚‘) +
  ENNReal.ofReal R * â¨† (x âˆˆ ball xâ‚€ R) (y âˆˆ ball xâ‚€ R) (_ : x â‰  y), â€–Ï• x - Ï• yâ€–â‚‘ / edist x y

variable [hXA : DoublingMeasure X A]

variable (X) in
/-- Î˜ is Ï„-cancellative. `Ï„` will usually be `1 / a` -/
class IsCancellative (Ï„ : â„) [CompatibleFunctions â„ X A] : Prop where
  /- We register a definition with strong assumptions, which makes them easier to prove.
  However, `enorm_integral_exp_le` removes them for easier application. -/
  enorm_integral_exp_le' {x : X} {r : â„} {Ï• : X â†’ â„‚} (hr : 0 < r) (h1 : iLipENorm Ï• x r â‰  âˆ)
    (h2 : support Ï• âŠ† ball x r) {f g : Î˜ X} :
    â€–âˆ« x, exp (I * (f x - g x)) * Ï• xâ€–â‚‘ â‰¤
    (A : â„â‰¥0âˆ) * volume (ball x r) * iLipENorm Ï• x r * (1 + edist_{x, r} f g) ^ (- Ï„)

/-- The "volume function" `V`. Preferably use `vol` instead. -/
protected def Real.vol {X : Type*} [PseudoMetricSpace X] [MeasureSpace X] (x y : X) : â„ :=
  volume.real (ball x (dist x y))

/-- `R_Q(Î¸, x)` defined in (1.0.20). -/
def upperRadius [FunctionDistances â„ X] (Q : X â†’ Î˜ X) (Î¸ : Î˜ X) (x : X) : â„â‰¥0âˆ :=
  â¨† (r : â„) (_ : dist_{x, r} Î¸ (Q x) < 1), ENNReal.ofReal r

/-- The linearized maximally truncated nontangential Calderonâ€“Zygmund operator `T_Q^Î¸`. -/
def linearizedNontangentialOperator [FunctionDistances â„ X] (Q : X â†’ Î˜ X) (Î¸ : Î˜ X)
    (K : X â†’ X â†’ â„‚) (f : X â†’ â„‚) (x : X) : â„â‰¥0âˆ :=
  â¨† (Râ‚‚ : â„) (Râ‚ âˆˆ Ioo 0 Râ‚‚) (x' âˆˆ ball x Râ‚),
  â€–âˆ« y in EAnnulus.oo x' (ENNReal.ofReal Râ‚) (min (ENNReal.ofReal Râ‚‚) (upperRadius Q Î¸ x')),
    K x' y * f yâ€–â‚‘

/-- The maximally truncated nontangential Calderonâ€“Zygmund operator `T_*`. -/
def nontangentialOperator (K : X â†’ X â†’ â„‚) (f : X â†’ â„‚) (x : X) : â„â‰¥0âˆ :=
  â¨† (Râ‚‚ : â„) (Râ‚ âˆˆ Ioo 0 Râ‚‚) (x' âˆˆ ball x Râ‚), â€–âˆ« y in Annulus.oo x' Râ‚ Râ‚‚, K x' y * f yâ€–â‚‘

/-- The integrand in the (linearized) Carleson operator.
This is `G` in Lemma 3.0.1. -/
def carlesonOperatorIntegrand [FunctionDistances â„ X] (K : X â†’ X â†’ â„‚)
    (Î¸ : Î˜ X) (Râ‚ Râ‚‚ : â„) (f : X â†’ â„‚) (x : X) : â„‚ :=
  âˆ« y in Annulus.oo x Râ‚ Râ‚‚, K x y * f y * exp (I * Î¸ y)

/-- The linearized generalized Carleson operator `T_Q`, taking values in `â„â‰¥0âˆ`.
Use `ENNReal.toReal` to get the corresponding real number. -/
def linearizedCarlesonOperator [FunctionDistances â„ X] (Q : X â†’ Î˜ X) (K : X â†’ X â†’ â„‚)
    (f : X â†’ â„‚) (x : X) : â„â‰¥0âˆ :=
  â¨† (Râ‚ : â„) (Râ‚‚ : â„) (_ : 0 < Râ‚) (_ : Râ‚ < Râ‚‚), â€–carlesonOperatorIntegrand K (Q x) Râ‚ Râ‚‚ f xâ€–â‚‘

/-- The generalized Carleson operator `T`, taking values in `â„â‰¥0âˆ`.
Use `ENNReal.toReal` to get the corresponding real number. -/
def carlesonOperator [FunctionDistances â„ X] (K : X â†’ X â†’ â„‚) (f : X â†’ â„‚) (x : X) : â„â‰¥0âˆ :=
  â¨† (Î¸ : Î˜ X), linearizedCarlesonOperator (fun _ â†¦ Î¸) K f x

end DoublingMeasure

/-- The main constant in the blueprint, driving all the construction, is `D = 2 ^ (100 * a ^ 2)`.
It turns out that the proof is robust, and works for other values of `100`, giving better constants
in the end. We will formalize it using a parameter `ğ•”` (that we fix equal to `100` to follow
the blueprint) and having `D = 2 ^ (ğ•” * a ^ 2)`. We register two lemmas `seven_le_c` and
`c_le_100` and will never unfold `ğ•”` from this point on. -/
irreducible_def ğ•” : â„• := 100

/-- This is usually the value of the argument `A` in `DoublingMeasure`
and `CompatibleFunctions` -/
@[simp] abbrev defaultA (a : â„•) : â„• := 2 ^ a
/-- `defaultÏ„` is the inverse of `a`. -/
@[simp] def defaultÏ„ (a : â„•) : â„ := aâ»Â¹

section Kernel

variable {X : Type*} {a : â„•} {K : X â†’ X â†’ â„‚} [PseudoMetricSpace X] [MeasureSpace X]
open Function

/-- The constant used twice in the definition of the Calderon-Zygmund kernel. -/
@[simp] def C_K (a : â„) : â„â‰¥0 := 2 ^ a ^ 3

/-- `K` is a one-sided Calderon-Zygmund kernel.
In the formalization `K x y` is defined everywhere, even for `x = y`. The assumptions on `K` show
that `K x x = 0`. -/
class IsOneSidedKernel (a : outParam â„•) (K : X â†’ X â†’ â„‚) : Prop where
  measurable_K : Measurable (uncurry K)
  norm_K_le_vol_inv (x y : X) : â€–K x yâ€– â‰¤ C_K a / Real.vol x y
  norm_K_sub_le {x y y' : X} (h : 2 * dist y y' â‰¤ dist x y) :
    â€–K x y - K x y'â€– â‰¤ (dist y y' / dist x y) ^ (a : â„)â»Â¹ * (C_K a / Real.vol x y)

end Kernel

/-- A constant used on the boundedness of `T_Q^Î¸` and `T_*`. We generally assume
`HasBoundedStrongType (linearizedNontangentialOperator Q Î¸ K Â· Â·) 2 2 volume volume (C_Ts a)`
throughout this formalization. -/
def C_Ts (a : â„•) : â„â‰¥0 := 2 ^ a ^ 3

/-- Data common through most of chapters 2-7.
These contain the minimal axioms for `kernel-summand`'s proof.
This is used in chapter 3 when we don't have all other fields from `ProofData`. -/
class KernelProofData {X : Type*} (a : outParam â„•) (K : outParam (X â†’ X â†’ â„‚))
    [PseudoMetricSpace X] where
  d : DoublingMeasure X (defaultA a)
  four_le_a : 4 â‰¤ a
  cf : CompatibleFunctions â„ X (defaultA a)
  hcz : IsOneSidedKernel a K

export KernelProofData (four_le_a)

attribute [instance] KernelProofData.d KernelProofData.cf KernelProofData.hcz

section statements

/- ## Main statements

This section contains the statements of the main theorems from the project: Theorem 1.0.1
(classical Carleson), Theorem 1.0.2 (metric space Carleson) and Theorem 1.0.3 (linearised metric
Carleson). -/

set_option linter.unusedVariables false

open Real

/-- The Náµ—Ê° partial Fourier sum of `f : â„ â†’ â„‚` for `N : â„•`. -/
def partialFourierSum (N : â„•) (f : â„ â†’ â„‚) (x : â„) : â„‚ := âˆ‘ n âˆˆ Finset.Icc (-(N : â„¤)) N,
    fourierCoeffOn Real.two_pi_pos f n * fourier n (x : AddCircle (2 * Ï€))

local notation "S_" => partialFourierSum

/-- Theorem 1.0.1: Carleson's theorem asserting a.e. convergence of the partial Fourier sums for
continous functions.
For the proof, see `classical_carleson` in the file `Carleson.Classical.ClassicalCarleson`. -/
def ClassicalCarleson : Prop :=
  âˆ€ {f : â„ â†’ â„‚} (cont_f : Continuous f) (periodic_f : f.Periodic (2 * Ï€)),
    âˆ€áµ x, Filter.Tendsto (S_ Â· f x) Filter.atTop (nhds (f x))

/-- The constant used from `R_truncation` to `metric_carleson`.
Has value `2 ^ (443 * a ^ 3)` in the blueprint. -/
def C1_0_2 (a : â„•) (q : â„â‰¥0) : â„â‰¥0 := 2 ^ ((3 * ğ•” + 18 + 5 * (ğ•” / 4)) * a ^ 3) / (q - 1) ^ 6

/-- Theorem 1.0.2.
For the proof, see `metric_carleson` in the file `Carleson.MetricCarleson.Main`. -/
def MetricSpaceCarleson : Prop :=
  âˆ€ {X : Type*} {a : â„•} [MetricSpace X] {q q' : â„â‰¥0} {F G : Set X} {K : X â†’ X â†’ â„‚}
    [KernelProofData a K] {f : X â†’ â„‚} [IsCancellative X (defaultÏ„ a)] (hq : q âˆˆ Ioc 1 2)
    (hqq' : q.HolderConjugate q') (mF : MeasurableSet F) (mG : MeasurableSet G)
    (mf : Measurable f) (nf : (â€–f Â·â€–) â‰¤ F.indicator 1)
    (hT : HasBoundedStrongType (nontangentialOperator K Â· Â·) 2 2 volume volume (C_Ts a)),
    âˆ«â» x in G, carlesonOperator K f x â‰¤ C1_0_2 a q * volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹

/-- Theorem 1.0.3.
For the proof, see `linearized_metric_carleson` in the file `Carleson.MetricCarleson.Linearized`. -/
def LinearizedMetricCarleson : Prop :=
  âˆ€ {X : Type*} {a : â„•} [MetricSpace X] {q q' : â„â‰¥0} {F G : Set X} {K : X â†’ X â†’ â„‚}
    [KernelProofData a K] {Q : SimpleFunc X (Î˜ X)} {f : X â†’ â„‚} [IsCancellative X (defaultÏ„ a)]
    (hq : q âˆˆ Ioc 1 2) (hqq' : q.HolderConjugate q') (mF : MeasurableSet F) (mG : MeasurableSet G)
    (mf : Measurable f) (nf : (â€–f Â·â€–) â‰¤ F.indicator 1)
    (hT : âˆ€ Î¸ : Î˜ X, HasBoundedStrongType (linearizedNontangentialOperator Q Î¸ K Â· Â·)
      2 2 volume volume (C_Ts a)),
    âˆ«â» x in G, linearizedCarlesonOperator Q K f x â‰¤
      C1_0_2 a q * volume G ^ (q' : â„)â»Â¹ * volume F ^ (q : â„)â»Â¹

end statements
