import Carleson.Discrete.ExceptionalSet
import Carleson.Discrete.ForestComplement
import Carleson.Discrete.ForestUnion

open MeasureTheory NNReal Set
open scoped ShortVariables
variable {X : Type*} {a : ‚Ñï} {q : ‚Ñù} {K : X ‚Üí X ‚Üí ‚ÑÇ} {œÉ‚ÇÅ œÉ‚ÇÇ : X ‚Üí ‚Ñ§} {F G : Set X}
  [MetricSpace X] [ProofData a q K œÉ‚ÇÅ œÉ‚ÇÇ F G] [TileStructure Q D Œ∫ S o]

/-! ## Proposition 2.0.2 -/

/-- The constant used in Proposition 2.0.2,
which has value `2 ^ (434 * a ^ 3) / (q - 1) ^ 5` in the blueprint. -/
noncomputable def C2_0_2 (a : ‚Ñï) (q : ‚Ñù‚â•0) : ‚Ñù‚â•0 :=
    2 ^ ((3 * CDN + 18 + 5 * (CDN / 4)) * a ^ 3) / (q - 1) ^ 5

lemma le_C2_0_2 (ha : 4 ‚â§ a) {q : ‚Ñù‚â•0} (hq : q ‚àà Ioc 1 2) :
    C5_1_2 a q + C5_1_3 a q ‚â§ C2_0_2 a q := by
  have h'q : 0 < q - 1 := tsub_pos_of_lt hq.1
  simp only [C5_1_2, C5_1_3, C2_0_2, ge_iff_le]
  have : (q - 1) ^ 4 ‚â• (q - 1) ^ 5 := by
    apply pow_le_pow_of_le_one h'q.le ?_ (by norm_num)
    rw [tsub_le_iff_left]
    convert hq.2
    norm_num
  grw [this]
  simp only [‚Üê add_div, ge_iff_le]
  gcongr
  apply (add_le_pow_two_add_cube ha le_rfl ?_).trans_eq (by ring)
  have : CDN/8 ‚â§ CDN/4 := by omega
  grw [this]
  ring_nf
  omega

omit [TileStructure Q D Œ∫ S o] in
lemma C2_0_2_pos : 0 < C2_0_2 a nnq := by
  apply (add_pos (C5_1_2_pos (X := X)) (C5_1_3_pos (X := X))).trans_le
  apply le_C2_0_2 (four_le_a X) (q_mem_Ioc X)

variable (X) in
theorem discrete_carleson :
    ‚àÉ G', MeasurableSet G' ‚àß 2 * volume G' ‚â§ volume G ‚àß
    ‚àÄ f : X ‚Üí ‚ÑÇ, Measurable f ‚Üí (‚àÄ x, ‚Äñf x‚Äñ ‚â§ F.indicator 1 x) ‚Üí
    ‚à´‚Åª x in G \ G', ‚ÄñcarlesonSum univ f x‚Äñ‚Çë ‚â§
    C2_0_2 a nnq * volume G ^ (1 - q‚Åª¬π) * volume F ^ q‚Åª¬π := by
  have exc := exceptional_set (X := X)
  rw [zpow_neg_one, ‚Üê ENNReal.div_eq_inv_mul] at exc
  use G', measurable_G', ENNReal.mul_le_of_le_div' exc; intro f measf hf
  classical
  calc
    _ ‚â§ ‚à´‚Åª x in G \ G', ‚ÄñcarlesonSum ùîì‚ÇÅ f x‚Äñ‚Çë + ‚ÄñcarlesonSum ùîì‚ÇÅ·∂ú f x‚Äñ‚Çë := by
      refine setLIntegral_mono (by fun_prop) fun x _ ‚Ü¶ ?_
      rw [carlesonSum, ‚Üê Finset.sum_filter_add_sum_filter_not _ (¬∑ ‚àà ùîì‚ÇÅ (X := X))]
      simp_rw [Finset.filter_filter, mem_univ, true_and, carlesonSum, mem_compl_iff]
      apply enorm_add_le
    _ = (‚à´‚Åª x in G \ G', ‚ÄñcarlesonSum ùîì‚ÇÅ f x‚Äñ‚Çë) + ‚à´‚Åª x in G \ G', ‚ÄñcarlesonSum ùîì‚ÇÅ·∂ú f x‚Äñ‚Çë :=
      lintegral_add_left (by fun_prop) _
    _ ‚â§ C5_1_2 a nnq * volume G ^ (1 - q‚Åª¬π) * volume F ^ q‚Åª¬π +
        C5_1_3 a nnq * volume G ^ (1 - q‚Åª¬π) * volume F ^ q‚Åª¬π :=
      add_le_add (forest_union hf measf) (forest_complement hf measf)
    _ ‚â§ _ := by
      simp_rw [mul_assoc, ‚Üê add_mul]
      gcongr
      norm_cast
      apply le_C2_0_2 (four_le_a X) (q_mem_Ioc X)
