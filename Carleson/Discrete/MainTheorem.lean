import Carleson.Discrete.ExceptionalSet
import Carleson.Discrete.ForestComplement
import Carleson.Discrete.ForestUnion

open MeasureTheory NNReal Set Classical
noncomputable section

open scoped ShortVariables
variable {X : Type*} {a : â„•} {q : â„} {K : X â†’ X â†’ â„‚} {Ïƒâ‚ Ïƒâ‚‚ : X â†’ â„¤} {F G : Set X}
  [MetricSpace X] [ProofData a q K Ïƒâ‚ Ïƒâ‚‚ F G] [TileStructure Q D Îº S o]

/-! ## Proposition 2.0.2 -/

/-- The constant used in Proposition 2.0.2,
which has value `2 ^ (440 * a ^ 3) / (q - 1) ^ 5` in the blueprint. -/
def C2_0_2 (a : â„) (q : â„â‰¥0) : â„â‰¥0 := C5_1_2 a q + C5_1_3 a q

lemma C2_0_2_pos : C2_0_2 a nnq > 0 := add_pos C5_1_2_pos C5_1_3_pos

variable (X) in
theorem discrete_carleson :
    âˆƒ G', MeasurableSet G' âˆ§ 2 * volume G' â‰¤ volume G âˆ§
    âˆ€ f : X â†’ â„‚, Measurable f â†’ (âˆ€ x, â€–f xâ€– â‰¤ F.indicator 1 x) â†’
    âˆ«â» x in G \ G', â€–carlesonSum univ f xâ€–â‚Š â‰¤
    C2_0_2 a nnq * volume G ^ (1 - qâ»Â¹) * volume F ^ qâ»Â¹ := by
  have exc := exceptional_set (X := X)
  rw [zpow_neg_one, â† ENNReal.div_eq_inv_mul] at exc
  use G', measurable_G', ENNReal.mul_le_of_le_div' exc; intro f measf hf
  calc
    _ â‰¤ âˆ«â» x in G \ G', â€–carlesonSum ğ”“â‚ f xâ€–â‚Š + â€–carlesonSum ğ”“â‚á¶œ f xâ€–â‚Š := by
      refine setLIntegral_mono ?_ fun x _ â†¦ ?_
      Â· exact ((measurable_carlesonSum measf).nnnorm.add
          (measurable_carlesonSum measf).nnnorm).coe_nnreal_ennreal
      Â· norm_cast
        rw [carlesonSum, â† Finset.sum_filter_add_sum_filter_not _ (Â· âˆˆ ğ”“â‚ (X := X))]
        simp_rw [Finset.filter_filter, mem_univ, true_and, carlesonSum, mem_compl_iff]
        exact nnnorm_add_le ..
    _ = (âˆ«â» x in G \ G', â€–carlesonSum ğ”“â‚ f xâ€–â‚Š) + âˆ«â» x in G \ G', â€–carlesonSum ğ”“â‚á¶œ f xâ€–â‚Š :=
      lintegral_add_left ((measurable_carlesonSum measf).nnnorm).coe_nnreal_ennreal _
    _ â‰¤ C5_1_2 a nnq * volume G ^ (1 - qâ»Â¹) * volume F ^ qâ»Â¹ +
        C5_1_3 a nnq * volume G ^ (1 - qâ»Â¹) * volume F ^ qâ»Â¹ :=
      add_le_add (forest_union hf) (forest_complement hf)
    _ = _ := by simp_rw [mul_assoc, â† add_mul]; congr
